[TOC]

# Tree ALV #

## 主程序 ##

```ABAP
REPORT ZHRB_029 NO STANDARD PAGE HEADING.

TYPES: ITEM_TABLE_TYPE LIKE STANDARD TABLE OF MTREEITM.

DATA: ALV_GRID_LEFT          TYPE REF TO CL_GUI_LIST_TREE,
      ALV_GRID_RIGHT         TYPE REF TO CL_GUI_ALV_GRID,
      ALV_CONTAINER          TYPE REF TO CL_GUI_DOCKING_CONTAINER,
      ALV_SPLITTER_CONTAINER TYPE REF TO CL_GUI_SPLITTER_CONTAINER,
      REF_CONTAINER_LEFT     TYPE REF TO CL_GUI_CONTAINER,
      REF_CONTAINER_RIGHT    TYPE REF TO CL_GUI_CONTAINER.

DATA: GT_ZHR_SBGXPZ     TYPE TABLE OF ZHR_SBGXPZ,
      GT_ZHR_SBGXPZ_TMP TYPE TABLE OF ZHR_SBGXPZ.

DATA: G_EVENT(30),
      G_NODE_KEY  TYPE TV_NODEKEY, "组织单位
      G_ITEM_NAME TYPE TV_ITMNAME.

DATA: GV_FLAG        TYPE CHAR1,   "控制按钮的点击顺序
      GV_ZTBDWLX     TYPE ZTBDWLX, "控制按钮的显示
      GV_CHANGE      TYPE CHAR1 VALUE 'X', "判断检查通过后是否再保存数据,默认'X'检查通过后清空,再次保存或更改数据重新赋值
      GV_OBJID_SUPER TYPE HROBJID,  "上层组织单位
      GV_STEXT_SUPER TYPE STEXT,    "上层组织单位描述
      GV_DATE_BEG    TYPE SY-DATUM,
      GV_DATE_END    TYPE SY-DATUM,
      GV_NAME        TYPE STRING,  "报表抬头文本
      GV_SBZT        TYPE CHAR1,   "当前组织单位的上报状态
      GV_STEXT       TYPE STEXT,   "当前组织单位的文本描述.
      GV_EDIT        TYPE CHAR1,   "当前组织单位的编辑权限
      GV_DWCJ        TYPE CHAR1,   "当前组织单位的单位层级
      GV_SUM         TYPE CHAR1.   "当前组织单位是否是汇总单位

DATA: GT_ZHR_JYLW LIKE TABLE OF ZHR_JYLW."报表校验例外表,此表中配置的报表不走校验

SELECTION-SCREEN BEGIN OF BLOCK B2 WITH FRAME TITLE TEXT-001.

  PARAMETERS:P_ZQLX   TYPE CHAR1 ,           "周期类型
             P_GJAHR  TYPE BSED-GJAHR,       "填报年度
             P_TBJD   TYPE CHAR2,            "填报季度
             P_TBYD   TYPE CHAR2,            "填报月度
             P_BT1    TYPE CHAR30,           "统计报表名称
             P_ZHRTJ  TYPE ZHR_BBXI-ZHRTJBB, "统计报表编号
             P_REPORT TYPE PROGRAM,          "程序名称
             P_TBNAME TYPE TABNAME.          "存储表


SELECTION-SCREEN END OF BLOCK B2.

CLASS CL_EVENT_HANDLE1 DEFINITION. "事件处理类定义
  PUBLIC SECTION.

    METHODS:
      HANDLE_TOOLBAR FOR EVENT TOOLBAR OF CL_GUI_ALV_GRID "初始化ALV工具栏对象事件，如增加按钮并设定属性
        IMPORTING
          E_OBJECT
          E_INTERACTIVE,

      HANDLE_USER_COMMAND FOR EVENT USER_COMMAND OF CL_GUI_ALV_GRID    "ALV工具栏按钮的点击事件
        IMPORTING
          E_UCOMM,

      HANDLE_ONF4 FOR EVENT ONF4 OF CL_GUI_ALV_GRID          "增加搜索帮助
        IMPORTING
          E_FIELDNAME "代表用户点击了ALV的哪个字段来触发搜索帮助，
          E_FIELDVALUE
          ES_ROW_NO  "代表了当前行信息，es_row_no-row_id就是ALV中内表记录的INDEX。
          ER_EVENT_DATA "当前用户对ALV进行了哪些编辑的信息
          ET_BAD_CELLS
          E_DISPLAY,

      HANDLE_MODIFY FOR EVENT DATA_CHANGED_FINISHED OF CL_GUI_ALV_GRID
        IMPORTING
          E_MODIFIED
          ET_GOOD_CELLS,

      HANDLE_DATA_CHANGED FOR EVENT DATA_CHANGED OF CL_GUI_ALV_GRID
        IMPORTING
          ER_DATA_CHANGED,

      HOTSPOT_CLICK FOR EVENT HOTSPOT_CLICK OF CL_GUI_ALV_GRID  "单击事件
        IMPORTING
          E_ROW_ID
          E_COLUMN_ID
          ES_ROW_NO,

      DOUBLE_CLICK FOR EVENT DOUBLE_CLICK OF CL_GUI_ALV_GRID "双击事件
        IMPORTING
          E_ROW
          E_COLUMN.

    METHODS:
      HANDLE_NODE_DOUBLE_CLICK FOR EVENT NODE_DOUBLE_CLICK OF CL_GUI_LIST_TREE
        IMPORTING
          NODE_KEY,

      HANDLE_EXPAND_NO_CHILDREN FOR EVENT EXPAND_NO_CHILDREN OF CL_GUI_LIST_TREE
        IMPORTING
          NODE_KEY,

      HANDLE_ITEM_DOUBLE_CLICK  FOR EVENT ITEM_DOUBLE_CLICK  OF CL_GUI_LIST_TREE
        IMPORTING
          NODE_KEY
          ITEM_NAME,

      HANDLE_BUTTON_CLICK FOR EVENT BUTTON_CLICK OF CL_GUI_LIST_TREE
        IMPORTING
          NODE_KEY
          ITEM_NAME,

      HANDLE_LINK_CLICK FOR EVENT LINK_CLICK OF CL_GUI_LIST_TREE
        IMPORTING
          NODE_KEY
          ITEM_NAME,

      HANDLE_CHECKBOX_CHANGE FOR EVENT CHECKBOX_CHANGE OF CL_GUI_LIST_TREE
        IMPORTING
          NODE_KEY
          ITEM_NAME
          CHECKED.


ENDCLASS.

DATA: L_CL_EVENT_HANDLE1 TYPE REF TO CL_EVENT_HANDLE1.

CLASS CL_EVENT_HANDLE1 IMPLEMENTATION."事件处理类实现部分

  METHOD HANDLE_TOOLBAR.

    PERFORM FRM_GET_ZTBDWLX USING G_NODE_KEY GV_ZTBDWLX.
    PERFORM FRM_SET_TOOLBAR USING E_OBJECT.

  ENDMETHOD.

  METHOD HANDLE_USER_COMMAND.

    PERFORM FRM_USER_COMMAND USING E_UCOMM.

  ENDMETHOD.

  METHOD HANDLE_ONF4.
    PERFORM F4_HELP_VALUE IN PROGRAM (P_REPORT) IF FOUND USING E_FIELDNAME ES_ROW_NO ER_EVENT_DATA.
    ER_EVENT_DATA->M_EVENT_HANDLED = 'X'. "在Method的最后，记得加上
  ENDMETHOD.

  METHOD HANDLE_MODIFY.
    "增加更改后需要重新校验的逻辑
    G_EVENT = 'HANDLE_MODIFY'.
    PERFORM FRM_HANDLE_MODIFY IN PROGRAM (P_REPORT) IF FOUND USING E_MODIFIED ET_GOOD_CELLS.
    PERFORM REFRESH_TABLE_ALV USING SPACE.
  ENDMETHOD.

  METHOD HOTSPOT_CLICK .
    G_EVENT = 'HOTSPOT_CLICK'.
    PERFORM FRM_HOTSPOT_CLICK IN PROGRAM (P_REPORT) IF FOUND USING E_ROW_ID E_COLUMN_ID ES_ROW_NO.
  ENDMETHOD.

  "双击事件
  METHOD DOUBLE_CLICK .
    G_EVENT = 'DOUBLE_CLICK'.
  ENDMETHOD.

  METHOD HANDLE_DATA_CHANGED.
    G_EVENT = 'DATA_CHANGED'.
  ENDMETHOD.

  METHOD  HANDLE_NODE_DOUBLE_CLICK.

    "此方法处理树的节点双击事件
    G_EVENT = 'NODE_DOUBLE_CLICK'.
    G_NODE_KEY = NODE_KEY.
  ENDMETHOD.                    "HANDLE_NODE_DOUBLE_CLICK

  METHOD  HANDLE_ITEM_DOUBLE_CLICK.

    "此方法处理树的项双击事件
    G_EVENT = 'ITEM_DOUBLE_CLICK'.
    G_NODE_KEY = NODE_KEY.
    G_ITEM_NAME = ITEM_NAME.

    "每次双击需要重新获取该单位的填报类型来控制按钮的显示问题
    "清除组织单位的描述
    CLEAR: GV_STEXT, GV_ZTBDWLX, GV_OBJID_SUPER, GV_STEXT_SUPER, GV_EDIT, GV_DWCJ.

    "获取当前节点的文本以及上报状态
    PERFORM FRM_GET_SBZT USING G_NODE_KEY GV_SBZT GV_STEXT.

    "获取当前单位的单位层级
    SELECT SINGLE ZDWCJ INTO GV_DWCJ FROM ZHR_SBGXPZ WHERE OBJID  = G_NODE_KEY.

    "若该单位没有提交并且具有编辑权限, 则拥有编辑权限
    READ TABLE GT_ZHR_SBGXPZ INTO DATA(LS_ZHR_SBGXPZ) WITH KEY OBJID = G_NODE_KEY.
    IF SY-SUBRC = 0 AND  GV_SBZT NE '2'.
      GV_EDIT = 'X'.
    ENDIF.

    "每次双击重新获取按钮类型
    PERFORM FRM_GET_ZTBDWLX USING G_NODE_KEY GV_ZTBDWLX.

    "每次双击重置按钮的点击顺序
    IF GV_SUM EQ 'X'.
      GV_FLAG = 0.
    ELSE.
      GV_FLAG = 1.
    ENDIF.

    PERFORM FRM_CREATE_ALV_RIGHT IN PROGRAM (P_REPORT) IF FOUND ."USING gv_stext.

  ENDMETHOD.                    "HANDLE_ITEM_DOUBLE_CLICK

  METHOD  HANDLE_LINK_CLICK.

    "此方法处理树的链接单击事件
    G_EVENT = 'LINK_CLICK'.
    G_NODE_KEY = NODE_KEY.
    G_ITEM_NAME = ITEM_NAME.

  ENDMETHOD.                    "HANDLE_LINK_CLICK

  METHOD  HANDLE_BUTTON_CLICK.

    "此方法处理树的按钮单击事件
    G_EVENT = 'BUTTON_CLICK'.
    G_NODE_KEY = NODE_KEY.
    G_ITEM_NAME = ITEM_NAME.

  ENDMETHOD.                    "HANDLE_BUTTON_CLICK

  METHOD  HANDLE_CHECKBOX_CHANGE.

    "这个方法处理树的checkbox_change事件
    G_EVENT = 'CHECKBOX_CHANGE'.
    G_NODE_KEY = NODE_KEY.
    G_ITEM_NAME = ITEM_NAME.

  ENDMETHOD.                    "HANDLE


  METHOD HANDLE_EXPAND_NO_CHILDREN.

    " 显示扩展节点
    G_EVENT = 'EXPAND_NO_CHILDREN'.
    G_NODE_KEY = NODE_KEY.

    PERFORM FRM_GET_NODS_AND_ITEMS USING G_NODE_KEY.
  ENDMETHOD.                    "HANDLE_EXPAND_NO_CHILDREN


ENDCLASS.

START-OF-SELECTION.
  PERFORM FRM_INIT_DATA.

END-OF-SELECTION.
  CALL SCREEN 9000.


FORM FRM_INIT_DATA.
  DATA:LV_MONTH TYPE N LENGTH 2.

  SELECT *
    INTO CORRESPONDING FIELDS OF TABLE GT_ZHR_SBGXPZ
    FROM ZHR_SBGXPZ
    WHERE USERID = SY-UNAME
    AND BEGDA LE SY-DATUM
    AND ENDDA GE SY-DATUM.

  SORT GT_ZHR_SBGXPZ BY ZDWCJ.
  GT_ZHR_SBGXPZ_TMP[] = GT_ZHR_SBGXPZ[].

  READ TABLE GT_ZHR_SBGXPZ INTO DATA(LS_ZHR_SBGXPZ) INDEX 1.
  IF SY-SUBRC = 0.
    DELETE GT_ZHR_SBGXPZ_TMP WHERE ZDWCJ NE LS_ZHR_SBGXPZ-ZDWCJ.
  ENDIF.

  CREATE OBJECT L_CL_EVENT_HANDLE1.

  CASE P_ZQLX.
    WHEN 1.
      GV_DATE_BEG = P_GJAHR && '0101'.
      GV_DATE_END = P_GJAHR && '1231'.
      GV_NAME = |{ P_GJAHR }年|.

    WHEN 2.

      CASE P_TBJD.
        WHEN 1.
          GV_NAME = |{ P_GJAHR }年 第一季度|.
          GV_DATE_BEG = P_GJAHR && '0301'.
        WHEN 2.
          GV_NAME = |{ P_GJAHR }年 第二季度|.
          GV_DATE_BEG = P_GJAHR && '0601'.
        WHEN 3.
          GV_NAME = |{ P_GJAHR }年 第三季度|.
          GV_DATE_BEG = P_GJAHR && '0901'.
        WHEN 4.
          GV_NAME = |{ P_GJAHR }年 第四季度|.
          GV_DATE_BEG = P_GJAHR && '1201'.
      ENDCASE.

      CALL FUNCTION 'LAST_DAY_OF_MONTHS'
        EXPORTING
          DAY_IN            = GV_DATE_BEG
        IMPORTING
          LAST_DAY_OF_MONTH = GV_DATE_END
        EXCEPTIONS
          DAY_IN_NO_DATE    = 1
          OTHERS            = 2.
    WHEN 3.

      GV_NAME = |{ P_GJAHR }年{ P_TBYD }月|.
      LV_MONTH = P_TBYD.
      LV_MONTH = |{ LV_MONTH ALPHA = IN }|.
      GV_DATE_BEG = P_GJAHR && LV_MONTH && '01'.
      CALL FUNCTION 'LAST_DAY_OF_MONTHS'
        EXPORTING
          DAY_IN            = GV_DATE_BEG
        IMPORTING
          LAST_DAY_OF_MONTH = GV_DATE_END
        EXCEPTIONS
          DAY_IN_NO_DATE    = 1
          OTHERS            = 2.
  ENDCASE.

  SELECT * INTO CORRESPONDING FIELDS OF TABLE GT_ZHR_JYLW FROM ZHR_JYLW WHERE ZHRFLAG EQ 'X' AND ZHRTJBB EQ P_ZHRTJ .

ENDFORM.
*&---------------------------------------------------------------------*
*& Module STATUS_9000 OUTPUT
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
MODULE STATUS_9000 OUTPUT.
  DATA:LV_NAME TYPE STRING.
  SET PF-STATUS 'STANDARD'  .
  LV_NAME = |{ P_BT1 }-{ GV_NAME }|.
  SET TITLEBAR  'STATUS' WITH LV_NAME.
ENDMODULE.

MODULE OUT_SCREEN OUTPUT.
  PERFORM FRM_CREATE_ALV_LEFT.
ENDMODULE.
*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_9000  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE USER_COMMAND_9000 INPUT.
  CASE SY-UCOMM.
    WHEN 'BACK' OR 'UPPER' OR 'EXIT'.
      LEAVE PROGRAM.
  ENDCASE.
ENDMODULE.


FORM FRM_CREATE_ALV_LEFT.

  DEFINE SET_EVENTS.
    CLEAR event.
    event-eventid = &1.
    event-appl_event = 'X'.
    APPEND event TO events.
  END-OF-DEFINITION.

  DATA: NODE_TABLE TYPE TREEV_NTAB,
        NODE       TYPE TREEV_NODE,
        ITEM_TABLE TYPE ITEM_TABLE_TYPE,
        ITEM       TYPE MTREEITM,
        EVENTS     TYPE CNTL_SIMPLE_EVENTS,
        EVENT      TYPE CNTL_SIMPLE_EVENT.
  DATA: LV_SBZT  TYPE C,
        LV_STEXT TYPE HRP1000-STEXT.

  IF ALV_GRID_LEFT IS INITIAL.

    CREATE OBJECT ALV_CONTAINER
      EXPORTING
        REPID     = SY-REPID
        DYNNR     = SY-DYNNR
        EXTENSION = 3000. "alv宽度

    CREATE OBJECT ALV_SPLITTER_CONTAINER
      EXPORTING
        PARENT  = ALV_CONTAINER
        ROWS    = 1
        COLUMNS = 2. "将父容器分为1行两列，两个容器


    CALL METHOD ALV_SPLITTER_CONTAINER->SET_BORDER
      EXPORTING
        BORDER = CL_GUI_CFW=>FALSE.

    CALL METHOD ALV_SPLITTER_CONTAINER->SET_COLUMN_MODE
      EXPORTING
        MODE = ALV_SPLITTER_CONTAINER->MODE_ABSOLUTE.

    CALL METHOD ALV_SPLITTER_CONTAINER->SET_COLUMN_WIDTH
      EXPORTING
        ID    = 1
        WIDTH = 440.


    CALL METHOD ALV_SPLITTER_CONTAINER->GET_CONTAINER
      EXPORTING
        ROW       = 1
        COLUMN    = 1
      RECEIVING
        CONTAINER = REF_CONTAINER_LEFT.

    CREATE OBJECT ALV_GRID_LEFT
      EXPORTING
        PARENT                      = REF_CONTAINER_LEFT
        NODE_SELECTION_MODE         = CL_GUI_SIMPLE_TREE=>NODE_SEL_MODE_SINGLE
        ITEM_SELECTION              = 'X'
        WITH_HEADERS                = ' '
      EXCEPTIONS
        LIFETIME_ERROR              = 1
        CNTL_SYSTEM_ERROR           = 2
        CREATE_ERROR                = 3
        FAILED                      = 4
        ILLEGAL_NODE_SELECTION_MODE = 5
        OTHERS                      = 6.

    IF SY-SUBRC <> 0.
      MESSAGE X208(00) WITH 'ERROR'.
    ENDIF.

    CALL METHOD ALV_SPLITTER_CONTAINER->GET_CONTAINER
      EXPORTING
        ROW       = 1
        COLUMN    = 2
      RECEIVING
        CONTAINER = REF_CONTAINER_RIGHT.

    CREATE OBJECT ALV_GRID_RIGHT
      EXPORTING
        I_PARENT = REF_CONTAINER_RIGHT.

    SET HANDLER: L_CL_EVENT_HANDLE1->HANDLE_TOOLBAR       FOR ALV_GRID_RIGHT,"工具栏
                 L_CL_EVENT_HANDLE1->HANDLE_USER_COMMAND  FOR ALV_GRID_RIGHT,"按钮事件
                 L_CL_EVENT_HANDLE1->HANDLE_MODIFY        FOR ALV_GRID_RIGHT,"
                 L_CL_EVENT_HANDLE1->HANDLE_DATA_CHANGED  FOR ALV_GRID_RIGHT,
                 L_CL_EVENT_HANDLE1->HANDLE_ONF4          FOR ALV_GRID_RIGHT,"F4帮助注册事件
                 L_CL_EVENT_HANDLE1->HOTSPOT_CLICK        FOR ALV_GRID_RIGHT,
                 L_CL_EVENT_HANDLE1->DOUBLE_CLICK         FOR ALV_GRID_RIGHT.

    CALL METHOD ALV_GRID_RIGHT->SET_TOOLBAR_INTERACTIVE. "调用此方法才能激活工具栏上增加的自定义按钮

    CALL METHOD ALV_GRID_RIGHT->REGISTER_EDIT_EVENT "注册编辑事件，否则不会触发更新事件
      EXPORTING
        I_EVENT_ID = CL_GUI_ALV_GRID=>MC_EVT_MODIFIED.

    PERFORM FRM_REGISTER_F4_FOR_FIELDS. "要对需要自定义搜索帮助的字段进行注册。

    SET_EVENTS:CL_GUI_LIST_TREE=>EVENTID_NODE_DOUBLE_CLICK.
    SET_EVENTS:CL_GUI_LIST_TREE=>EVENTID_ITEM_DOUBLE_CLICK.
    SET_EVENTS:CL_GUI_LIST_TREE=>EVENTID_EXPAND_NO_CHILDREN.
    SET_EVENTS:CL_GUI_LIST_TREE=>EVENTID_LINK_CLICK.
    SET_EVENTS:CL_GUI_LIST_TREE=>EVENTID_BUTTON_CLICK.
    SET_EVENTS:CL_GUI_LIST_TREE=>EVENTID_CHECKBOX_CHANGE.

    CALL METHOD ALV_GRID_LEFT->SET_REGISTERED_EVENTS
      EXPORTING
        EVENTS = EVENTS.

    SET HANDLER: L_CL_EVENT_HANDLE1->HANDLE_NODE_DOUBLE_CLICK FOR ALV_GRID_LEFT,
                 L_CL_EVENT_HANDLE1->HANDLE_ITEM_DOUBLE_CLICK FOR ALV_GRID_LEFT ,
                 L_CL_EVENT_HANDLE1->HANDLE_EXPAND_NO_CHILDREN FOR ALV_GRID_LEFT,
                 L_CL_EVENT_HANDLE1->HANDLE_LINK_CLICK FOR ALV_GRID_LEFT,
                 L_CL_EVENT_HANDLE1->HANDLE_BUTTON_CLICK FOR ALV_GRID_LEFT,
                 L_CL_EVENT_HANDLE1->HANDLE_CHECKBOX_CHANGE FOR ALV_GRID_LEFT.


***************************************************构建节点表
    PERFORM FRM_GET_NODS_AND_ITEMS USING ''.

    LOOP AT GT_ZHR_SBGXPZ_TMP INTO DATA(LS_ZHR_SBGXPZ_TMP).
      PERFORM FRM_GET_NODS_AND_ITEMS USING LS_ZHR_SBGXPZ_TMP-OBJID.
    ENDLOOP.

****************************************************构建节点表
  ELSE.
    CALL METHOD CL_GUI_CFW=>FLUSH.
  ENDIF.

ENDFORM.

FORM FRM_GET_ZTBDWLX USING OBJID ZTBDWLX.
  DATA:READY_FOR_INPUT TYPE INT4.

  DATA:LV_SBZT  TYPE C,
       LV_STEXT TYPE HRP1000-STEXT.


  READ TABLE GT_ZHR_SBGXPZ INTO DATA(LS_ZHR_SBGXPZ) WITH KEY OBJID = OBJID.
  IF SY-SUBRC = 0.
    ZTBDWLX = LS_ZHR_SBGXPZ-ZTBDWLX.
  ELSE.
    ZTBDWLX = '05'.
  ENDIF.

  IF ZTBDWLX EQ '02'.
    GV_SUM = 'X'.
  ELSEIF ZTBDWLX EQ '01'.
    GV_SUM = ''.
  ENDIF.

  IF OBJID EQ '10011000'.
    ZTBDWLX = '03'.
  ENDIF.

  CALL METHOD ALV_GRID_RIGHT->IS_READY_FOR_INPUT
    RECEIVING
      READY_FOR_INPUT = READY_FOR_INPUT.

  CASE GV_SBZT.
    WHEN 2.
      ZTBDWLX = '04'.
      IF READY_FOR_INPUT NE 0.
        CALL METHOD ALV_GRID_RIGHT->SET_READY_FOR_INPUT
          EXPORTING
            I_READY_FOR_INPUT = 0.
      ENDIF.
    WHEN OTHERS.
      IF READY_FOR_INPUT NE 1.
        CALL METHOD ALV_GRID_RIGHT->SET_READY_FOR_INPUT
          EXPORTING
            I_READY_FOR_INPUT = 1.
      ENDIF.
  ENDCASE.

ENDFORM.

FORM FRM_SET_TOOLBAR USING E_OBJECT TYPE REF TO CL_ALV_EVENT_TOOLBAR_SET.
  "经整理,填报类型与按钮的对应关系
  "01 有校验 保存 提交 导出
  "02 有校验 保存 提交 导出 汇总 上报单位查询
  "03 有校验 保存 导出 汇总 上报单位查询
  "04 有导出 判断其上级单位是否存在提交数据,如果没提交,则添加驳回按钮
  "05 无按钮
  DATA: LS_TOOLBAR TYPE STB_BUTTON."功能按钮

  DATA:LV_SBZT  TYPE CHAR1,
       LV_STEXT TYPE HRP1000-STEXT.
  DELETE E_OBJECT->MT_TOOLBAR WHERE FUNCTION NE 'ABCDEFG'.

  CLEAR LS_TOOLBAR.
  LS_TOOLBAR-BUTN_TYPE = 3.
  APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.

  IF GV_ZTBDWLX EQ '01' OR GV_ZTBDWLX EQ '02' OR GV_ZTBDWLX EQ '03' .

    CLEAR LS_TOOLBAR.
    LS_TOOLBAR-FUNCTION = 'SAVE'.
    LS_TOOLBAR-ICON = ICON_SYSTEM_SAVE.
    LS_TOOLBAR-QUICKINFO = '保存'.
    LS_TOOLBAR-TEXT = '保存'.
    LS_TOOLBAR-DISABLED = ''.
    APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.

    CLEAR LS_TOOLBAR.
    LS_TOOLBAR-FUNCTION = 'CHECK'.
    LS_TOOLBAR-ICON = ICON_CHECK.
    LS_TOOLBAR-QUICKINFO = '校验'.
    LS_TOOLBAR-TEXT = '校验'.
    LS_TOOLBAR-DISABLED = ''.
    APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.

  ENDIF.

  IF GV_ZTBDWLX EQ '01' OR GV_ZTBDWLX EQ '02'.
    CLEAR LS_TOOLBAR.
    LS_TOOLBAR-FUNCTION = 'TJ'.
    LS_TOOLBAR-ICON = ICON_SYSTEM_OKAY.
    LS_TOOLBAR-QUICKINFO = '提交'.
    LS_TOOLBAR-TEXT = '提交'.
    LS_TOOLBAR-DISABLED = ''.
    APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.
  ENDIF.

  IF GV_ZTBDWLX EQ '01' OR GV_ZTBDWLX EQ '02' OR GV_ZTBDWLX EQ '03' OR GV_ZTBDWLX EQ '04'.
    CLEAR LS_TOOLBAR.
    LS_TOOLBAR-FUNCTION = 'EXCEL'.
    LS_TOOLBAR-ICON = ICON_EXPORT.
    LS_TOOLBAR-QUICKINFO = '导出'.
    LS_TOOLBAR-TEXT = '导出'.
    LS_TOOLBAR-DISABLED = ''.
    APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.
  ENDIF.

  IF GV_ZTBDWLX EQ '02' OR GV_ZTBDWLX EQ '03'.
    CLEAR LS_TOOLBAR.
    LS_TOOLBAR-FUNCTION = 'SUM'.
    LS_TOOLBAR-ICON = ICON_PRINT.
    LS_TOOLBAR-QUICKINFO = '汇总'.
    LS_TOOLBAR-TEXT = '汇总'.
    LS_TOOLBAR-DISABLED = ''.
    APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.

    CLEAR LS_TOOLBAR.
    LS_TOOLBAR-FUNCTION = 'CX'.
    LS_TOOLBAR-ICON = ICON_ICON_LIST.
    LS_TOOLBAR-QUICKINFO = '上报单位进度查询'.
    LS_TOOLBAR-TEXT = '上报单位进度查询'.
    LS_TOOLBAR-DISABLED = ''.
    APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.
  ENDIF.

  IF GV_ZTBDWLX EQ '04'.
    IF GV_OBJID_SUPER IS NOT INITIAL.
      SELECT SINGLE OBJID INTO @DATA(LV_OBJID) FROM ZHR_SBGXPZ WHERE USERID = @SY-UNAME AND OBJID EQ @GV_OBJID_SUPER.
      IF LV_OBJID IS NOT INITIAL.
        PERFORM FRM_GET_SBZT USING LV_OBJID LV_SBZT LV_STEXT.
        IF LV_SBZT NE 2.
          CLEAR LS_TOOLBAR.
          LS_TOOLBAR-FUNCTION = 'BH'.
          LS_TOOLBAR-ICON = ICON_SYSTEM_UNDO.
          LS_TOOLBAR-QUICKINFO = '驳回'.
          LS_TOOLBAR-TEXT = '驳回'.
          LS_TOOLBAR-DISABLED = ''.
          APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDIF.

  CLEAR LS_TOOLBAR.
  LS_TOOLBAR-FUNCTION = 'YJDC'.
  LS_TOOLBAR-ICON = ICON_XXL.
  LS_TOOLBAR-QUICKINFO = '一键导出'.
  LS_TOOLBAR-TEXT = '一键导出'.
  LS_TOOLBAR-DISABLED = ''.
  APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.


*  "新增一键校验,一键提交的按钮
*  IF line_exists( gt_zhr_sbgxpz[ objid = g_node_key ] )  AND ( gv_sbzt EQ 1 OR gv_sbzt EQ '3' ).
*    CLEAR ls_toolbar.
*    ls_toolbar-function = 'YJJY'.
*    ls_toolbar-icon = ''.
*    ls_toolbar-quickinfo = '一键校验'.
*    ls_toolbar-text = '一键校验'.
*    ls_toolbar-disabled = ''.
*    APPEND ls_toolbar TO e_object->mt_toolbar.
*
*    CLEAR ls_toolbar.
*    ls_toolbar-function = 'YJTJ'.
*    ls_toolbar-icon = ''.
*    ls_toolbar-quickinfo = '一键提交'.
*    ls_toolbar-text = '一键提交'.
*    ls_toolbar-disabled = ''.
*    APPEND ls_toolbar TO e_object->mt_toolbar.
*
*  ENDIF.



ENDFORM.

FORM FRM_SET_LEFT_STYLE USING STYLE TYPE I.


  "设置节点对应的文本样式
  CALL METHOD ALV_GRID_LEFT->ITEM_SET_STYLE
    EXPORTING
      NODE_KEY          = G_NODE_KEY
      ITEM_NAME         = G_ITEM_NAME
      STYLE             = STYLE
    EXCEPTIONS
      FAILED            = 1
      NODE_NOT_FOUND    = 2
      ITEM_NOT_FOUND    = 3
      CNTL_SYSTEM_ERROR = 4
      OTHERS            = 5.
  IF SY-SUBRC <> 0.
  ENDIF.


ENDFORM.


FORM FRM_GET_NODS_AND_ITEMS USING NODE_KEY.

  DATA: NODE_TABLE TYPE TREEV_NTAB,
        NODE       TYPE TREEV_NODE,
        ITEM_TABLE TYPE ITEM_TABLE_TYPE,
        ITEM       TYPE MTREEITM.

  DATA: LV_SBZT  TYPE C,
        LV_STEXT TYPE HRP1000-STEXT.

  DATA: LT_ZHR_SBGXPZ LIKE TABLE OF ZHR_SBGXPZ.

  IF NODE_KEY IS INITIAL.
    LT_ZHR_SBGXPZ[] = GT_ZHR_SBGXPZ_TMP[].
  ELSE.
    SELECT * INTO CORRESPONDING FIELDS OF TABLE LT_ZHR_SBGXPZ FROM ZHR_SBGXPZ WHERE SJOBJID = NODE_KEY.
  ENDIF.

  SORT LT_ZHR_SBGXPZ BY OBJID.
  DELETE ADJACENT DUPLICATES FROM LT_ZHR_SBGXPZ COMPARING OBJID.

  LOOP AT LT_ZHR_SBGXPZ INTO DATA(LS_ZHR_SBGXPZ).

    "添加节点
    CLEAR NODE.
    NODE-NODE_KEY = LS_ZHR_SBGXPZ-OBJID.
    NODE-RELATKEY = NODE_KEY.
    NODE-RELATSHIP = CL_GUI_LIST_TREE=>RELAT_LAST_CHILD.
    NODE-HIDDEN = ' '.
    NODE-DISABLED = ' '.

    SELECT SINGLE * INTO @DATA(LS_ZHR_SBGXPZ_TMP) FROM ZHR_SBGXPZ WHERE SJOBJID = @LS_ZHR_SBGXPZ-OBJID.
    IF LS_ZHR_SBGXPZ_TMP IS NOT INITIAL.
      NODE-ISFOLDER = 'X'.
      NODE-EXPANDER = 'X'.
    ELSE.
      NODE-ISFOLDER = ''.
      NODE-EXPANDER = ''.
    ENDIF.
    CLEAR LS_ZHR_SBGXPZ_TMP.
    APPEND NODE TO NODE_TABLE.

    "添加节点描述
    CLEAR: ITEM,LV_SBZT,LV_STEXT.

    PERFORM FRM_GET_SBZT USING LS_ZHR_SBGXPZ-OBJID LV_SBZT LV_STEXT.
    ITEM-NODE_KEY = LS_ZHR_SBGXPZ-OBJID.
    ITEM-ITEM_NAME = '1'.
    ITEM-CLASS = CL_GUI_LIST_TREE=>ITEM_CLASS_TEXT.
    ITEM-ALIGNMENT = CL_GUI_LIST_TREE=>ALIGN_AUTO.
    ITEM-FONT = CL_GUI_LIST_TREE=>ITEM_FONT_PROP.
    CASE LV_SBZT.
      WHEN 2.
        ITEM-STYLE = '6'.
      WHEN 1 OR 3.
        ITEM-STYLE = '7'.
      WHEN OTHERS.
        ITEM-STYLE = '5'.
    ENDCASE.
    ITEM-TEXT = |{ LV_STEXT }-{ LS_ZHR_SBGXPZ-OBJID }|.
    APPEND ITEM TO ITEM_TABLE.

  ENDLOOP.

  PERFORM FRM_ADD_NODS_AND_ITEMS USING NODE_TABLE ITEM_TABLE.

ENDFORM.

FORM FRM_ADD_NODS_AND_ITEMS USING NODE_TABLE ITEM_TABLE.

  "添加节点
  CALL METHOD ALV_GRID_LEFT->ADD_NODES_AND_ITEMS
    EXPORTING
      NODE_TABLE                     = NODE_TABLE
      ITEM_TABLE                     = ITEM_TABLE
      ITEM_TABLE_STRUCTURE_NAME      = 'MTREEITM'
    EXCEPTIONS
      FAILED                         = 1
      CNTL_SYSTEM_ERROR              = 3
      ERROR_IN_TABLES                = 4
      DP_ERROR                       = 5
      TABLE_STRUCTURE_NAME_NOT_FOUND = 6.
  IF SY-SUBRC <> 0.

  ENDIF.

  "展开节点
  CALL METHOD ALV_GRID_LEFT->EXPAND_ROOT_NODES
    EXPORTING
      LEVEL_COUNT         = 1
      EXPAND_SUBTREE      = 'X'
    EXCEPTIONS
      FAILED              = 1
      ILLEGAL_LEVEL_COUNT = 2
      CNTL_SYSTEM_ERROR   = 3
      OTHERS              = 4.
  IF SY-SUBRC <> 0.
  ENDIF.
ENDFORM.


"获取上报状态
FORM FRM_GET_SBZT USING LV_OBJID LV_SBZT LV_STEXT.

  DATA: DYN_TABLE TYPE REF TO DATA,
        COND1(72) TYPE C.

  FIELD-SYMBOLS:<FS_TABLE> TYPE TABLE .

  SELECT SINGLE ZHRTJBB, ZHRTBZQLX INTO @DATA(LS_ZHR_BBTBZQ) FROM ZHR_BBTBZQ  WHERE ZHRTJBBMC = @P_BT1.
  CASE LS_ZHR_BBTBZQ-ZHRTBZQLX.
    WHEN 1.
      COND1 = 'ZHRT_YNDB = P_GJAHR'.
    WHEN 2 .
      COND1 = 'ZHRT_YNDB = P_GJAHR AND ZHRT_YYDB = P_TBJD '.
    WHEN 3.
      COND1 = 'ZHRT_YNDB = P_GJAHR AND ZHRT_YYDB = P_TBYD'.
  ENDCASE.

  CREATE DATA DYN_TABLE TYPE TABLE OF (P_TBNAME).
  ASSIGN DYN_TABLE->* TO <FS_TABLE>.

  SELECT * INTO TABLE <FS_TABLE> FROM (P_TBNAME) WHERE (COND1) AND ZHRT_DWBM EQ LV_OBJID.
  READ TABLE <FS_TABLE> ASSIGNING FIELD-SYMBOL(<FS_STYLE>) INDEX 1.
  IF SY-SUBRC = 0.
    ASSIGN ('<FS_STYLE>-ZHRT_SBZT') TO FIELD-SYMBOL(<FS_ZHRT_SBZT>).
    LV_SBZT = <FS_ZHRT_SBZT>.
  ELSE.
    LV_SBZT = 0.
  ENDIF.

  SELECT SINGLE STEXT INTO LV_STEXT FROM HRP1000 WHERE OTYPE = 'O' AND OBJID = LV_OBJID AND BEGDA LE GV_DATE_END AND ENDDA GE GV_DATE_BEG.


  SELECT SINGLE SJOBJID INTO GV_OBJID_SUPER  FROM ZHR_SBGXPZ WHERE OBJID EQ G_NODE_KEY AND USERID = SY-UNAME.
  IF GV_OBJID_SUPER IS NOT INITIAL.
    SELECT SINGLE STEXT INTO GV_STEXT_SUPER FROM HRP1000 WHERE OTYPE = 'O' AND OBJID = GV_OBJID_SUPER AND BEGDA LE GV_DATE_END AND ENDDA GE GV_DATE_BEG.
  ENDIF.


ENDFORM.

FORM FRM_GET_RESULT.
  SUBMIT ZHRB_021_1 "VIA SELECTION-SCREEN
    WITH P_ZQLX = P_ZQLX
    WITH P_GJAHR = P_GJAHR
    WITH P_TBJD = P_TBJD
    WITH P_TBYD = P_TBYD
    WITH P_OBJID = G_NODE_KEY
    WITH P_BT1 = P_BT1
    WITH P_REPORT  = P_REPORT
    AND RETURN .
ENDFORM.

FORM REFRESH_TABLE_ALV USING CWIDTH_OUT TYPE CHAR1.

  DATA: LS_LAYOUT TYPE LVC_S_LAYO.
  DATA: STBL TYPE LVC_S_STBL.

  IF CWIDTH_OUT EQ 'X'.
    LS_LAYOUT-CWIDTH_OPT = CWIDTH_OUT.

    CALL METHOD ALV_GRID_RIGHT->SET_FRONTEND_LAYOUT
      EXPORTING
        IS_LAYOUT = LS_LAYOUT.

    CALL METHOD ALV_GRID_RIGHT->REFRESH_TABLE_DISPLAY " 功能A: 基本显示
      EXCEPTIONS
        FINISHED = 1
        OTHERS   = 2.
  ENDIF.

  STBL-ROW = 'X'.  " 基于行的稳定刷新 "
  STBL-COL = 'X'.  " 基于列稳定刷新 "
  CALL METHOD ALV_GRID_RIGHT->REFRESH_TABLE_DISPLAY
    EXPORTING
      IS_STABLE = STBL.
*      i_soft_refresh = 'X'.

  CALL METHOD CL_GUI_CFW=>FLUSH.

ENDFORM.

FORM FRM_REGISTER_F4_FOR_FIELDS.

*  DATA: lt_f4 TYPE lvc_t_f4 WITH HEADER LINE.
*
*  DEFINE set_f4.
*    CLEAR lt_f4.
*    lt_f4-fieldname  = &1.
*    lt_f4-register   = 'X'.
*    lt_f4-chngeafter = 'X'.
*    APPEND lt_f4.
*  END-OF-DEFINITION.
*
*  CASE p_report.
*    WHEN 'ZHRB_029_1'.
*      REFRESH lt_f4.
*      set_f4:'VALUE1'.
*      set_f4:'VALUE2'.
*      set_f4:'VALUE3'.
*      set_f4:'VALUE4'.
*  ENDCASE.
*
*  CALL METHOD alv_grid_right->register_f4_for_fields
*    EXPORTING
*      it_f4 = lt_f4[].

ENDFORM.


FORM FRM_USER_COMMAND USING E_UCOMM.

  DATA: SD_USER_ACTION TYPE I,
        SD_LOC_FN      TYPE STRING,
        SD_LOC_DIR     TYPE STRING,
        SD_LOC_FUL     TYPE STRING,
        L_FTYPE_FILTER TYPE STRING,
        L_FILENAME     TYPE STRING.

  CASE E_UCOMM.
    WHEN 'EXCEL'."导出
      L_FTYPE_FILTER = 'EXCEL2010(*.XLSX)|*.XLSX|' & 'ALL(*.*)|*.*'.
      CONCATENATE P_BT1 '_' SY-DATUM SY-UZEIT INTO  L_FILENAME.
      CONDENSE L_FILENAME NO-GAPS.
      PERFORM SUB_DOWNLOAD_EXCEL_N IN PROGRAM (P_REPORT) IF FOUND USING SD_LOC_FN SD_LOC_DIR SD_LOC_FUL SD_USER_ACTION L_FILENAME.

    WHEN 'SUM'.  "汇总:校验通过，则将按钮流程至为1
      PERFORM FRM_SUM_DATA.

    WHEN 'CHECK'. "检查，
      PERFORM FRM_CHECK_DATA.

    WHEN 'SAVE'."保存
      PERFORM FRM_SAVE_DATA.

    WHEN 'TJ'. "提交
      PERFORM FRM_SUBMIT_DATA.

    WHEN 'CX'."上报单位进度查询
      PERFORM FRM_GET_RESULT.

    WHEN 'BH'.
      PERFORM FRM_BH_DATA.

    WHEN 'YJDC'.
      PERFORM FRM_EXCEL_DATA.

  ENDCASE.
*    WAIT UP TO 1 SECONDS.
  PERFORM REFRESH_TABLE_ALV USING SPACE.
ENDFORM.


"按钮点击顺序  1 汇总   2 校验  3 提交

FORM FRM_SUM_DATA.

  TYPES:BEGIN OF TYP_RESULT,
          BOX       TYPE CHAR1,
          XH        TYPE CHAR10, "序号
          ICON      TYPE ICON_D,  "状态灯
          OBJID     TYPE ZHR_SBGXPZ-OBJID, "单位编码
          STEXT     TYPE ZHR_SBGXPZ-STEXT, "单位名称
          ZDWCJ     TYPE ZHR_SBGXPZ-ZDWCJ, "单位层级
          SJOBJID   TYPE ZHR_SBGXPZ-SJOBJID, "上级单位编码
          SJSTEXT   TYPE ZHR_SBGXPZ-SJSTEXT, "上级单位名称
          ZHRT_SBZT TYPE ZHRT_YDBBC-ZHRT_SBZT, "上报状态
          SBZT      TYPE CHAR10, "上报状态
          MESSAGE   TYPE TDLINE,  "消息
        END OF TYP_RESULT.
  DATA:LT_RESULT TYPE TABLE OF TYP_RESULT.

  DATA:LS_DATA TYPE REF TO DATA.
  FIELD-SYMBOLS: <FS_TABLE> TYPE TABLE.
  DATA:LV_ERROR   TYPE CHAR1,
       LV_MESSAGE TYPE STRING.

  CL_SALV_BS_RUNTIME_INFO=>SET( DISPLAY  = '' METADATA = 'X' DATA     = 'X'  ).
  PERFORM FRM_GET_RESULT.
  TRY.
      CL_SALV_BS_RUNTIME_INFO=>GET_DATA_REF( IMPORTING R_DATA = LS_DATA ).
      ASSIGN LS_DATA->* TO <FS_TABLE>.
    CATCH CX_SALV_BS_SC_RUNTIME_INFO.
      MESSAGE 'UNABLE TO RETRIEVE ALV DATA' TYPE 'E'.
  ENDTRY.
  CL_SALV_BS_RUNTIME_INFO=>CLEAR_ALL( ).
  LT_RESULT = <FS_TABLE>.

  LOOP AT LT_RESULT INTO DATA(LS_RESULT) WHERE ICON = '@5C@' OR ICON = '@5D@'.
    SELECT SINGLE * INTO @DATA(LS_ZHRT_JYLW) FROM ZHR_JYLW WHERE ZHRTJBB EQ @P_ZHRTJ AND OBJID EQ @LS_RESULT-OBJID AND ZHRFLAG EQ 'X'.
    IF LS_ZHRT_JYLW IS INITIAL.
      LV_ERROR = 'X'.
      LV_MESSAGE = '存在未完成填报单位，请在上报单位进度查询中检查'.
      EXIT.
    ENDIF.
  ENDLOOP.

  PERFORM SUB_SUM_DATA IN PROGRAM (P_REPORT) IF FOUND USING LV_ERROR LV_MESSAGE.

  IF LV_ERROR IS INITIAL.
    MESSAGE '汇总完成' TYPE 'S'.
    GV_FLAG = '1'.
  ELSE.
    MESSAGE LV_MESSAGE TYPE 'S' DISPLAY LIKE 'E'.
    GV_FLAG = '0'.
  ENDIF.
ENDFORM.


FORM FRM_SAVE_DATA.

  DATA:LV_ERROR   TYPE CHAR1,
       LV_MESSAGE TYPE STRING.

  PERFORM FRM_CHECK_LINES USING LV_ERROR LV_MESSAGE.
  CHECK LV_ERROR IS INITIAL.

  "去除先检查再保存的限制,并且添加保存后需要重新检查的校验
  PERFORM SUB_SAVE_DATA IN PROGRAM (P_REPORT) IF FOUND USING LV_ERROR LV_MESSAGE.

  IF LV_ERROR EQ 'X'.
    MESSAGE LV_MESSAGE TYPE 'S' DISPLAY LIKE 'E'.
  ELSE.
    GV_SBZT  = '1'.  "更新本单位的上报状态为1
    GV_CHANGE = 'X'. "每次保存成功视为更改数据, 需要重新校验
    PERFORM FRM_GET_ZTBDWLX USING G_NODE_KEY GV_ZTBDWLX.

    IF GV_SUM EQ 'X'.
      GV_FLAG = 0.
    ELSE.
      GV_FLAG = 1.
    ENDIF.

    PERFORM FRM_SET_LEFT_STYLE USING 7." 设置本单位的颜色为黄色色
    MESSAGE '保存成功' TYPE 'S'.
  ENDIF.

ENDFORM.

FORM FRM_CHECK_DATA .

  DATA:LV_ERROR   TYPE CHAR1,
       LV_MESSAGE TYPE STRING.

  PERFORM FRM_CHECK_LINES USING LV_ERROR LV_MESSAGE.
  CHECK LV_ERROR IS INITIAL.

  SELECT SINGLE * INTO @DATA(LS_ZHR_JYLW) FROM ZHR_JYLW WHERE ZHRTJBB EQ @P_ZHRTJ AND OBJID EQ @G_NODE_KEY AND ZHRFLAG EQ 'X'.


  IF GV_FLAG NE '1' AND GV_FLAG NE '2' .
    LV_ERROR = 'X'.
    LV_MESSAGE = '请先汇总数据'.
  ELSE.
    IF LS_ZHR_JYLW IS INITIAL.
      PERFORM SUB_CHECK_DATA IN PROGRAM (P_REPORT) IF FOUND USING LV_ERROR LV_MESSAGE.
    ENDIF.

  ENDIF.


  IF LV_ERROR EQ 'X'.
    MESSAGE LV_MESSAGE TYPE 'S' DISPLAY LIKE 'E'.
  ELSE.
    PERFORM FRM_SAVE_DATA.
    GV_FLAG = '2'.  "检查后流程状态为2
    GV_CHANGE = ''. "更改状态为空
    MESSAGE '检查成功' TYPE 'S'.
  ENDIF.

ENDFORM.


FORM FRM_SUBMIT_DATA.
  DATA:LV_ERROR   TYPE CHAR1,
       LV_MESSAGE TYPE STRING.
  IF GV_FLAG EQ '3'.
    LV_ERROR = 'X'.
    LV_MESSAGE = '数据已提交，请勿重复提交'.
  ELSEIF GV_FLAG NE '2' OR GV_CHANGE EQ 'X'.
    LV_ERROR = 'X'.
    LV_MESSAGE = '请先检查数据'.
  ELSE.
    PERFORM FRM_SUBMIT_DATA IN PROGRAM (P_REPORT) IF FOUND USING LV_ERROR LV_MESSAGE.
  ENDIF.

  IF LV_ERROR EQ 'X'.
    MESSAGE LV_MESSAGE TYPE 'S' DISPLAY LIKE 'E'.
  ELSE.
    GV_FLAG = 3.    "更新流程按钮为4
    GV_SBZT  = 2.   "更新本单位的上报状态为2
*    gv_ztbdwlx = '04'."提交后只保留导出按钮
    PERFORM FRM_SET_LEFT_STYLE USING 6. "设置本单位的颜色为绿色
    CALL METHOD ALV_GRID_RIGHT->SET_READY_FOR_INPUT "设置本单位不可输入
      EXPORTING
        I_READY_FOR_INPUT = 0.
    MESSAGE '提交成功' TYPE 'S'.
  ENDIF.

ENDFORM.



FORM FRM_BH_DATA.
  DATA:LV_ERROR   TYPE CHAR1,
       LV_MESSAGE TYPE STRING.

  PERFORM FRM_BH_DATA IN PROGRAM (P_REPORT) IF FOUND USING LV_ERROR LV_MESSAGE.

  IF LV_ERROR EQ 'X'.
    MESSAGE LV_MESSAGE TYPE 'S' DISPLAY LIKE 'E'.
  ELSE.
    PERFORM FRM_GET_ZTBDWLX USING G_NODE_KEY GV_ZTBDWLX.
    IF GV_SUM EQ 'X'.
      GV_FLAG = 0.
    ELSE.
      GV_FLAG = 1.
    ENDIF.
    GV_SBZT  = '3'.   "更新本单位的上报状态为3
    PERFORM FRM_SET_LEFT_STYLE USING 7. " 设置本单位的颜色为黄色
    CALL METHOD ALV_GRID_RIGHT->SET_READY_FOR_INPUT "设置本单位可以输入
      EXPORTING
        I_READY_FOR_INPUT = 1.
    MESSAGE '驳回成功' TYPE 'S'.
  ENDIF.

ENDFORM.

FORM FRM_EXCEL_DATA.

  DATA: LT_OBJID    LIKE TABLE OF ZHRS_051 WITH HEADER LINE,
        ES_SELFIELD TYPE  SLIS_SELFIELD,
        E_EXIT      TYPE CHAR1,
        LV_ERROR    TYPE CHAR1,
        LV_MESSAGE  TYPE CHAR255.

  LOOP AT GT_ZHR_SBGXPZ INTO DATA(LS_ZHR_SBGXPZ).
    MOVE-CORRESPONDING LS_ZHR_SBGXPZ TO LT_OBJID.
    LT_OBJID-CHECKBOX = 'X'.
    COLLECT LT_OBJID. CLEAR LT_OBJID.
  ENDLOOP.

  CALL FUNCTION 'REUSE_ALV_POPUP_TO_SELECT'
    EXPORTING
      I_TITLE              = '选择导出单位'
      I_ZEBRA              = 'X'
      I_CHECKBOX_FIELDNAME = 'CHECKBOX'
      I_TABNAME            = SPACE    "这里默认是空就OK了
      I_STRUCTURE_NAME     = 'ZHRS_051'
    IMPORTING
      ES_SELFIELD          = ES_SELFIELD
      E_EXIT               = E_EXIT
    TABLES
      T_OUTTAB             = LT_OBJID[]  "输出内表.
    EXCEPTIONS
      PROGRAM_ERROR        = 1
      OTHERS               = 2.

  IF E_EXIT EQ 'X'.
    LV_ERROR = 'X'.
    LV_MESSAGE = '用户取消选择'.
  ENDIF.

  IF LV_ERROR NE 'X'.
    PERFORM FRM_EXCEL_DATA IN PROGRAM (P_REPORT) IF FOUND TABLES LT_OBJID USING LV_ERROR LV_MESSAGE.
  ENDIF.

  IF LV_ERROR NE 'X'.
    MESSAGE '导出成功' TYPE 'S'.
  ELSE.
    MESSAGE LV_MESSAGE TYPE 'S' DISPLAY LIKE 'E'.
  ENDIF.

ENDFORM.

FORM FRM_CHECK_LINES USING ERROR MESSAGE.
  DATA:LV_FIELDNAME TYPE STRING,
       LV_FIELDALV  TYPE STRING.

  FIELD-SYMBOLS: <FS_LINES_NEW> TYPE STANDARD TABLE.

  DATA:LV_LINES TYPE I.
  LV_FIELDNAME = |({ P_REPORT })GV_LINES|.
  LV_FIELDALV = |({ P_REPORT })GT_ALV|.
  ASSIGN (LV_FIELDNAME) TO FIELD-SYMBOL(<FS_LINES>).
  ASSIGN (LV_FIELDALV) TO <FS_LINES_NEW> .
  IF <FS_LINES_NEW> IS ASSIGNED.
    LV_LINES = LINES( <FS_LINES_NEW> ).
    IF <FS_LINES> IS ASSIGNED AND <FS_LINES> NE LV_LINES.
      ERROR = 'X'.
      MESSAGE = '条目数发生变化,请退出重新填写'.
      MESSAGE MESSAGE TYPE 'S' DISPLAY LIKE 'E'.
    ENDIF.
  ENDIF.
ENDFORM.
```

## 子程序 ##

### 定义 ###

```abap
*&---------------------------------------------------------------------*
*& 包含               ZHRB_029_TOP
*&---------------------------------------------------------------------*

DATA: ALV_GRID_RIGHT TYPE REF TO CL_GUI_ALV_GRID, "ALV容器
      GT_FCAT        TYPE LVC_T_FCAT,             "字段目录
      GS_FCAT        TYPE LVC_S_FCAT,
      GS_LAYOUT      TYPE LVC_S_LAYO,             "样式设置
      G_NODE_KEY     TYPE TV_NODEKEY,             "当前组织单位的单位编码
      GV_STEXT       TYPE LVC_TITLE,              "当前组织单位的文本描述
      GV_SBZT        TYPE CHAR1,                  "当前组织单位的上报状态
      GV_ZTBDWLX     TYPE ZTBDWLX,                "当前组织单位的填报类型  2 和 3 是汇总单位
      GV_EDIT        TYPE CHAR1,                  "当前组织单位是否具有编辑权限
      GV_DWCJ        TYPE CHAR1,                  "当前组织单位的单位层级
      GV_SUM         TYPE CHAR1,                  "当前组织单位是否是汇总单位
      GV_OBJID_SUPER TYPE HROBJID,                "当前组织单位上级组织单位
      GV_STEXT_SUPER TYPE STEXT.                  "当前组织单位上级组织单位描述


DATA: GV_ZQLX    TYPE CHAR1,    "周期类型
      GV_GJAHR   TYPE GJAHR,    "年度
      GV_QUARTER TYPE CHAR1,    "季度
      GV_MONTH   TYPE CHAR2,    "月度
      GV_TABNAME TYPE TABNAME,  "存储表
      GV_REPNAME TYPE CHAR50,   "报表名称
      GV_REPNO   TYPE ZHRTJBB.  "报表编号

DATA: GT_ZHR_MSPZB TYPE TABLE OF ZHR_MSPZB, "描述配置表
      GV_COND1(72) TYPE C,                  "where条件
      GV_LINES     TYPE I.                  "ALV条数

DATA: GV_USER_NAME TYPE AD_NAMTEXT.


CONSTANTS: GC_I_NUMBER   TYPE STRING VALUE '^[0-9]*$',             "正整数表达式
           GC_I_6_NUMBER TYPE STRING VALUE '^[0-9]+(.[0]{6})?$',
           GC_P_NUMBER   TYPE STRING VALUE '^[0-9]+(.[0-9]{2})?$', "两位小数表达式
           GC_P_6_NUMBER TYPE STRING VALUE '^[0-9]+(.[0-9]{1,6})?$'. "六位小数表达式


"excel 导出变量
DATA: LO_EXCEL         TYPE REF TO ZCL_EXCEL,
      GO_EXCEL         TYPE REF TO ZCL_TOOL_ABAP2XLS,
      LO_EXCEL_WRITER  TYPE REF TO ZIF_EXCEL_WRITER,
      LO_EXCEL_COMMON  TYPE REF TO ZCL_EXCEL_COMMON,
      LO_WORKSHEET     TYPE REF TO ZCL_EXCEL_WORKSHEET,           "
      COLUMN_DIMENSION TYPE REF TO ZCL_EXCEL_WORKSHEET_COLUMNDIME, "
      ROW_DIMENSION    TYPE REF TO ZCL_EXCEL_WORKSHEET_ROWDIMENSI, "
      LO_STYLE_BODY    TYPE REF TO ZCL_EXCEL_STYLE,               "
      LT_COND_STYLE    TYPE REF TO CL_OBJECT_COLLECTION_ITERATOR,
      LO_BORDER_DARK   TYPE REF TO ZCL_EXCEL_STYLE_BORDER,
      GV_OBJECT        TYPE WWWDATATAB-OBJID.



DATA: BEGIN OF GS_ERROR,
        TABIX   TYPE I,
        MESSAGE TYPE CHAR255,
      END OF GS_ERROR.
DATA: GT_ERROR LIKE TABLE OF GS_ERROR.


DEFINE  SET_FIELDCAT.
  CLEAR gs_fcat.
  gs_fcat-fieldname  = &1.
  gs_fcat-coltext    = &2.
  gs_fcat-scrtext_l  = &2.
  gs_fcat-scrtext_m  = &2.
  gs_fcat-scrtext_s  = &2.
  gs_fcat-fix_column = &3.
  gs_fcat-edit       = &4.
  gs_fcat-drdn_field = &5.
  gs_fcat-hotspot    = &6.
  gs_fcat-no_zero    = &7.
  gs_fcat-ref_table  = &8.
  gs_fcat-ref_field  = &9.
  gs_fcat-just       = 'L'.
  APPEND gs_fcat TO gt_fcat.
END-OF-DEFINITION.
```

### 方法 ###

```abap
*&---------------------------------------------------------------------*
*& 包含               ZHRB_029_FORM
*&---------------------------------------------------------------------*

FORM FRM_GET_VALUES_FROM_MAIN.

  ASSIGN ('(ZHRB_029)ALV_GRID_RIGHT') TO FIELD-SYMBOL(<FS_ALV_GRID_RIGHT>).
  ASSIGN ('(ZHRB_029)G_NODE_KEY') TO FIELD-SYMBOL(<FS_NODE_KEY>).
  ASSIGN ('(ZHRB_029)GV_STEXT') TO FIELD-SYMBOL(<FS_STEXT>).
  ASSIGN ('(ZHRB_029)GV_SBZT') TO FIELD-SYMBOL(<FS_SBZT>).
  ASSIGN ('(ZHRB_029)GV_ZTBDWLX') TO FIELD-SYMBOL(<FS_ZTBDWLX>).
  ASSIGN ('(ZHRB_029)GV_EDIT') TO FIELD-SYMBOL(<FS_EDIT>).
  ASSIGN ('(ZHRB_029)GV_SUM') TO FIELD-SYMBOL(<FS_SUM>).
  ASSIGN ('(ZHRB_029)GV_DWCJ') TO FIELD-SYMBOL(<FS_DWCJ>).
  ASSIGN ('(ZHRB_029)P_ZQLX') TO FIELD-SYMBOL(<FS_ZQLX>).
  ASSIGN ('(ZHRB_029)P_GJAHR') TO FIELD-SYMBOL(<FS_GJAHR>).
  ASSIGN ('(ZHRB_029)P_TBJD') TO FIELD-SYMBOL(<FS_TBJD>).
  ASSIGN ('(ZHRB_029)P_TBYD') TO FIELD-SYMBOL(<FS_TBYD>).
  ASSIGN ('(ZHRB_029)P_TBNAME') TO FIELD-SYMBOL(<FS_TBNAME>).
  ASSIGN ('(ZHRB_029)P_BT1') TO FIELD-SYMBOL(<FS_PBT1>).
  ASSIGN ('(ZHRB_029)P_ZHRTJ') TO FIELD-SYMBOL(<FS_ZHRTJ>).
  ASSIGN ('(ZHRB_029)GV_OBJID_SUPER') TO FIELD-SYMBOL(<FS_OBJID_SUPER>).
  ASSIGN ('(ZHRB_029)GV_STEXT_SUPER') TO FIELD-SYMBOL(<FS_STEXT_SUPER>).



  ALV_GRID_RIGHT        = <FS_ALV_GRID_RIGHT>.
  G_NODE_KEY            = <FS_NODE_KEY>.
  GV_STEXT              = <FS_STEXT>.
  GV_SBZT               = <FS_SBZT>.
  GV_ZTBDWLX            = <FS_ZTBDWLX>.
  GV_EDIT               = <FS_EDIT>.
  GV_SUM                = <FS_SUM>.
  GV_ZQLX               = <FS_ZQLX>.
  GV_GJAHR              = <FS_GJAHR>.
  GV_QUARTER            = <FS_TBJD>.
  GV_MONTH              = <FS_TBYD>.
  GV_TABNAME            = <FS_TBNAME>.
  GV_REPNAME            = <FS_PBT1>.
  GV_REPNO              = <FS_ZHRTJ>.
  GV_DWCJ               = <FS_DWCJ>.
  GV_OBJID_SUPER        = <FS_OBJID_SUPER>.
  GV_STEXT_SUPER        = <FS_STEXT_SUPER>.

  PERFORM FRM_SET_WHERE.


  DATA: LV_NAME_FIRST TYPE AD_NAMEFIR,
        LV_NAME_LAST  TYPE AD_NAMELAS.

  CALL FUNCTION 'FDM_CUST_USER_NAME_READ_SINGLE'
    EXPORTING
      I_USER_ID   = SY-UNAME
    IMPORTING
      E_FIRSTNAME = LV_NAME_FIRST
      E_LASTNAME  = LV_NAME_LAST.
  IF SY-SUBRC = 0.
    CONCATENATE LV_NAME_LAST  LV_NAME_FIRST INTO GV_USER_NAME .
    CONDENSE GV_USER_NAME  NO-GAPS.  "去掉空格字符
  ENDIF.


ENDFORM.

FORM FRM_SET_LAYOUT .
  CLEAR: GS_LAYOUT,GT_FCAT.
  GS_LAYOUT-CWIDTH_OPT  = 'A'.
  GS_LAYOUT-SEL_MODE    = 'A'.
  GS_LAYOUT-ZEBRA       = ABAP_FALSE.
  GS_LAYOUT-STYLEFNAME  = 'STYLE'.
  GS_LAYOUT-CTAB_FNAME  = 'CELLCOLOR'.
  GS_LAYOUT-EDIT        = ''.
  GS_LAYOUT-GRID_TITLE  = GV_STEXT.
ENDFORM.

FORM FRM_SET_WHERE.
  CASE GV_ZQLX.
    WHEN 1.
      GV_COND1 = 'ZHRT_YNDB = GV_GJAHR '.
    WHEN 2.
      GV_COND1 = 'ZHRT_YNDB = GV_GJAHR AND ZHRT_YYDB = GV_QUARTER '.
    WHEN 3.
      GV_COND1 = 'ZHRT_YNDB = GV_GJAHR AND ZHRT_YYDB = GV_MONTH '.
  ENDCASE.
ENDFORM.


FORM FRM_GET_DATA_FROM_TABLE TABLES LT_TABLE USING NODE_KEY.
  SELECT *
    INTO CORRESPONDING FIELDS OF TABLE LT_TABLE
    FROM (GV_TABNAME)
    WHERE (GV_COND1)
    AND ZHRT_DWBM EQ NODE_KEY.
ENDFORM.

FORM FRM_SET_SBZT USING P_TYPE.

  FIELD-SYMBOLS: <FS_TABLE> TYPE ANY TABLE,
                 <FS_VALUE> TYPE ANY.

  ASSIGN ('GT_ALV') TO <FS_TABLE>.

  IF P_TYPE EQ '3'.
    LOOP AT <FS_TABLE> ASSIGNING FIELD-SYMBOL(<FS_ALV>).
      DATA(LV_FIELDNAME) = |<FS_ALV>-STYLE|.
      ASSIGN (LV_FIELDNAME) TO <FS_VALUE>.
      CLEAR <FS_VALUE>.
    ENDLOOP.
  ENDIF.

  UPDATE (GV_TABNAME) SET ZHRT_SBZT = P_TYPE WHERE (GV_COND1) AND ZHRT_DWBM EQ G_NODE_KEY.
  COMMIT WORK AND WAIT.
ENDFORM.


FORM FRM_ALV_DISPLAY TABLES LT_ALV.

  GV_LINES = LINES( LT_ALV[] ).

  CALL METHOD ALV_GRID_RIGHT->SET_TABLE_FOR_FIRST_DISPLAY
    EXPORTING
      IS_LAYOUT       = GS_LAYOUT
    CHANGING
      IT_OUTTAB       = LT_ALV[]
      IT_FIELDCATALOG = GT_FCAT[].

  CALL METHOD ALV_GRID_RIGHT->REFRESH_TABLE_DISPLAY
    EXPORTING
      I_SOFT_REFRESH = 'X'
    EXCEPTIONS
      FINISHED       = 1
      OTHERS         = 2.


ENDFORM.


FORM FRM_GET_DESCRIPTION_FROM_TABLE.
  SELECT * INTO CORRESPONDING FIELDS OF TABLE GT_ZHR_MSPZB FROM ZHR_MSPZB WHERE ZHRTJBB EQ GV_REPNO.
  SORT GT_ZHR_MSPZB BY ZHRDLBH ZHRHXMBH.
ENDFORM .


FORM  FRM_DISPLAY_ERROR .
  DATA: LT_MSG                TYPE RS_T_MSG,
        LS_MSG                TYPE BAL_S_MSG,
        LF_ONE_MSG_AS_SYS_MSG TYPE FLAG.

  DEFINE  SET_MSG.
    CLEAR ls_msg.
    ls_msg-msgty = &1.
    ls_msg-msgid = '00'.
    ls_msg-msgno = '001'.
    ls_msg-msgv1 = &2.
    ls_msg-msgv2 = &3.
    ls_msg-msgv3 = &4.
    ls_msg-msgv4 = &5.
    APPEND ls_msg TO lt_msg.
  END-OF-DEFINITION.

  SORT GT_ERROR BY TABIX.
  LOOP AT GT_ERROR INTO GS_ERROR.
    CLEAR LS_MSG.
    LS_MSG-MSGTY = 'E'.
    LS_MSG-MSGID = '00'.
    LS_MSG-MSGNO = '001'.
    LS_MSG-MSGV1 = GS_ERROR-MESSAGE+0(50).
    LS_MSG-MSGV2 = GS_ERROR-MESSAGE+50(50).
    LS_MSG-MSGV3 = GS_ERROR-MESSAGE+150(50).
    LS_MSG-MSGV4 = GS_ERROR-MESSAGE+200(50).
    APPEND LS_MSG TO LT_MSG.
  ENDLOOP.

  IF LT_MSG IS NOT INITIAL.
    CL_EPIC_UI_SERVICES=>SHOW_MESSAGES_WITH_ALOG(
    IT_MESSAGES       = LT_MSG
    IV_ONE_MSG_DIRECT = LF_ONE_MSG_AS_SYS_MSG ).
  ENDIF.

ENDFORM.

FORM FRM_SET_FIELD_COLOR TABLES ITAB USING TABIXS VALUES CLEAR.
  RANGES: R_TABIX FOR SY-INDEX.

  FIELD-SYMBOLS: <FS_ITAB>  TYPE ANY,
                 <FS_VALUE> TYPE ANY.

  DATA: LV_FILDNAME  TYPE STRING,
        GT_CELLCOLOR TYPE LVC_T_SCOL,
        GS_CELLCOLOR TYPE LVC_S_SCOL.

  SPLIT TABIXS AT ',' INTO TABLE DATA(LT_TABIX).
  SPLIT VALUES AT ',' INTO TABLE DATA(LT_VALUES).

  SORT LT_VALUES.
  DELETE ADJACENT DUPLICATES FROM LT_VALUES.

  LOOP AT LT_TABIX INTO DATA(LS_TABIX).
    R_TABIX[] = VALUE #( BASE R_TABIX[] ( SIGN = 'I' OPTION = 'EQ' LOW = LS_TABIX ) ).
  ENDLOOP.


  LV_FILDNAME =  '<FS_ITAB>-CELLCOLOR'.

  IF CLEAR IS NOT INITIAL.
    LOOP AT ITAB ASSIGNING <FS_ITAB>.
      ASSIGN (LV_FILDNAME) TO <FS_VALUE>.
      IF <FS_VALUE> IS ASSIGNED.
        CLEAR <FS_VALUE>.
      ENDIF.
    ENDLOOP.

    REFRESH GT_ERROR.
  ENDIF.

  UNASSIGN:<FS_ITAB>, <FS_VALUE>.
  IF R_TABIX[] IS NOT INITIAL.
    LOOP AT ITAB ASSIGNING <FS_ITAB>.


      IF SY-TABIX IN R_TABIX[].
        ASSIGN (LV_FILDNAME) TO <FS_VALUE>.
        IF <FS_VALUE> IS ASSIGNED.
*          CLEAR <fs_value>.
          REFRESH GT_CELLCOLOR.
          GT_CELLCOLOR = <FS_VALUE>.
        ENDIF.

        LOOP AT LT_VALUES INTO DATA(LS_VALUES).

          IF LS_VALUES IS INITIAL OR LINE_EXISTS( GT_CELLCOLOR[ FNAME = LS_VALUES ] ) .
            CONTINUE.
          ENDIF.

          CLEAR GS_CELLCOLOR.
          GS_CELLCOLOR-FNAME = LS_VALUES.
          GS_CELLCOLOR-COLOR-COL = 6."ALV 控制：颜色
          GS_CELLCOLOR-COLOR-INT = 1."ALV 控制：强化
          GS_CELLCOLOR-COLOR-INV = 0."ALV 控制：相反
          APPEND GS_CELLCOLOR TO GT_CELLCOLOR.
        ENDLOOP.

        <FS_VALUE> = GT_CELLCOLOR.

      ENDIF.
    ENDLOOP.
  ENDIF.

ENDFORM.


FORM FRM_SPLICE_STRINNG USING LV_VALUE LV_VALUE_TMP.

  IF LV_VALUE IS INITIAL.
    LV_VALUE = LV_VALUE_TMP.
  ELSE.
    LV_VALUE = |{ LV_VALUE },{ LV_VALUE_TMP }|.
  ENDIF.

  APPEND GS_ERROR TO GT_ERROR.
  SORT GT_ERROR BY TABIX MESSAGE.
  DELETE ADJACENT DUPLICATES FROM GT_ERROR COMPARING TABIX MESSAGE.
  CLEAR GS_ERROR.
ENDFORM.

FORM FRM_FILL_CELL_FOR_EXCEL USING ROW COL VALUES.

  CALL METHOD GO_EXCEL->CHANGE_CELL
    EXPORTING
      IV_ROW   = ROW
      IV_COL   = COL
      IV_VALUE = VALUES.
ENDFORM.
```



### 程序主体 ###

```abap
REPORT ZHRB_029_1.

INCLUDE ZHRB_029_TOP.
INCLUDE ZHRB_029_FORM.


TYPES: BEGIN OF TY_ALV,
         STEXT1    TYPE CHAR100,
         VALUE1    TYPE CHAR20,
         HANDLE1   TYPE INT4,

         CELLCOLOR TYPE LVC_T_SCOL, "单元格颜色
         STYLE     TYPE LVC_T_STYL, "是否可编辑
       END OF TY_ALV.


DATA: GT_ALV          TYPE TABLE OF TY_ALV,
      GT_ZHRT_GZWDWJB TYPE TABLE OF ZHRT_GZWDWJB.

FORM FRM_CREATE_ALV_RIGHT.

  REFRESH GT_ALV.
  PERFORM FRM_GET_VALUES_FROM_MAIN.                "从主程序获取变量
  PERFORM FRM_GET_DESCRIPTION_FROM_TABLE.          "从表中取描述配置
  PERFORM FRM_SET_LAYOUT.                          "设置layout
  PERFORM FRM_SET_FIELDCAT.                        "设置字段目录
  PERFORM FRM_GET_DATA.                            "从标准表取数
  PERFORM FRM_SET_STYLE.                           "设置样式
  PERFORM FRM_ALV_DISPLAY TABLES GT_ALV.           "展示ALV

ENDFORM.


FORM FRM_SET_FIELDCAT.
  "设置字段目录:  字段  描述 固定列 编辑 下拉框 单击 去除前导零 输出长度
  SET_FIELDCAT 'STEXT1' '描述' '' '' '' '' '' '' '' .
  SET_FIELDCAT 'VALUE1' '值'   '' '' 'HANDLE1' '' '' '' '' .
ENDFORM.

FORM FRM_GET_DATA.

  DATA: LS_ALV   TYPE TY_ALV,
        LV_INDEX TYPE I.


  "取日志表中的数据
  PERFORM FRM_GET_DATA_FROM_TABLE TABLES GT_ZHRT_GZWDWJB USING G_NODE_KEY.

  "根据描述配置表设置ALV数据
  LOOP AT GT_ZHR_MSPZB INTO DATA(LS_ZHR_MSPZB).
    LS_ALV-STEXT1 = LS_ZHR_MSPZB-ZHRHXMS1.
    APPEND LS_ALV TO GT_ALV.
    CLEAR:LS_ALV.
  ENDLOOP.

  LOOP AT GT_ALV ASSIGNING FIELD-SYMBOL(<FS_ALV>).
    ADD 1 TO LV_INDEX.
    READ TABLE GT_ZHRT_GZWDWJB INTO DATA(LS_ZHRT_GZWDWJB) WITH KEY ZHRT_GZWLH = LV_INDEX.
    IF SY-SUBRC = 0.
      <FS_ALV>-VALUE1 = LS_ZHRT_GZWDWJB-ZHRT_GZWMSZ.
    ENDIF.
    CASE LV_INDEX.
      WHEN 1.
        <FS_ALV>-HANDLE1 = '1'.
      WHEN 2.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 3.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 4.
        <FS_ALV>-HANDLE1 = '3'.
      WHEN 5.
        <FS_ALV>-HANDLE1 = '4'.
      WHEN 6.
        <FS_ALV>-HANDLE1 = '5'.
      WHEN 7.
        <FS_ALV>-HANDLE1 = '6'.
      WHEN 8.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 9.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 10.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 11.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 12.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 13.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 14.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 15.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 16.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 17.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 18.
        <FS_ALV>-HANDLE1 = '7'.
      WHEN 19.
        <FS_ALV>-HANDLE1 = '8'.
      WHEN 20.
        <FS_ALV>-HANDLE1 = '9'.
      WHEN 21.
        <FS_ALV>-HANDLE1 = '10'.
      WHEN 22.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 23.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 24.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 25.
        <FS_ALV>-HANDLE1 = '11'.
      WHEN 26.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 27.
        <FS_ALV>-HANDLE1 = '2'.
      WHEN 28.
        <FS_ALV>-HANDLE1 = '2'.
    ENDCASE.
  ENDLOOP.

  PERFORM FRM_SET_HANDLE.

ENDFORM.

FORM FRM_SET_STYLE.
  DATA: LT_CELLTAB  TYPE LVC_T_STYL,
        LS_CELLTAB  TYPE LVC_S_STYL,
        LV_TABIX    TYPE SY-TABIX,
        LV_VALUE_1  TYPE CHAR10,
        LV_VALUE_10 TYPE CHAR10,
        LV_VALUE_12 TYPE CHAR10.

  DEFINE SET_CELLTAB.
    CLEAR ls_celltab.
    ls_celltab-fieldname = &1.
    ls_celltab-style = &2.
    INSERT ls_celltab  INTO TABLE lt_celltab.
  END-OF-DEFINITION.

  IF GV_EDIT EQ 'X'.
    LOOP AT GT_ALV ASSIGNING FIELD-SYMBOL(<FS_ALV>) FROM 1 TO 8.
      CLEAR <FS_ALV>-STYLE.
      REFRESH LT_CELLTAB.
      LV_TABIX = SY-TABIX.

      IF LV_TABIX = '1'.
        LV_VALUE_1 = <FS_ALV>-VALUE1.
      ENDIF.

      IF LV_TABIX = '8'.
        LV_VALUE_10 = <FS_ALV>-VALUE1.
      ENDIF.

      SET_CELLTAB:'VALUE1' CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
      INSERT LINES OF LT_CELLTAB INTO TABLE <FS_ALV>-STYLE.
    ENDLOOP.


    LOOP AT GT_ALV ASSIGNING <FS_ALV> FROM 20 TO 26.
      CLEAR <FS_ALV>-STYLE.
      IF LV_VALUE_1 EQ '2-事业'.
        SET_CELLTAB:'VALUE1' CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
        INSERT LINES OF LT_CELLTAB INTO TABLE <FS_ALV>-STYLE.
      ELSE.
        CLEAR <FS_ALV>-VALUE1.
      ENDIF.
    ENDLOOP.


    LOOP AT GT_ALV ASSIGNING <FS_ALV> FROM 9 TO 10.
      CLEAR <FS_ALV>-STYLE.
      LV_TABIX = SY-TABIX.

      IF LV_VALUE_10 EQ '1-是'..
        SET_CELLTAB:'VALUE1' CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
        INSERT LINES OF LT_CELLTAB INTO TABLE <FS_ALV>-STYLE.
      ELSE.
        CLEAR <FS_ALV>-VALUE1.
      ENDIF.

      IF LV_TABIX = 10.
        LV_VALUE_12 = <FS_ALV>-VALUE1.
      ENDIF.

    ENDLOOP.

    LOOP AT GT_ALV ASSIGNING <FS_ALV> FROM 11 TO 16.
      CLEAR <FS_ALV>-STYLE.
      IF LV_VALUE_12 EQ '1-是'.
        SET_CELLTAB:'VALUE1' CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
        INSERT LINES OF LT_CELLTAB INTO TABLE <FS_ALV>-STYLE.
      ELSE.
        CLEAR <FS_ALV>-VALUE1.
      ENDIF.
    ENDLOOP.

    LOOP AT GT_ALV ASSIGNING <FS_ALV> FROM 17 TO 19.
      CLEAR <FS_ALV>-STYLE.
      SET_CELLTAB:'VALUE1' CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
      INSERT LINES OF LT_CELLTAB INTO TABLE <FS_ALV>-STYLE.
    ENDLOOP.

    LOOP AT GT_ALV ASSIGNING <FS_ALV> FROM 27 TO 28.
      CLEAR <FS_ALV>-STYLE.
      SET_CELLTAB:'VALUE1' CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
      INSERT LINES OF LT_CELLTAB INTO TABLE <FS_ALV>-STYLE.
    ENDLOOP.


  ENDIF.
ENDFORM.

FORM FRM_SET_HANDLE.
  DATA:LT_DDVAL TYPE LVC_T_DROP,
       LS_DDVAL TYPE LVC_S_DROP.

  DEFINE SET_DDVAL.
    CLEAR:ls_ddval.
    ls_ddval-handle = &1.
    ls_ddval-value  = &2.
    APPEND ls_ddval TO lt_ddval.
  END-OF-DEFINITION.

  SET_DDVAL: '1' '1-企业'.
  SET_DDVAL: '1' '2-事业'.

  SET_DDVAL: '2' '1-是'.
  SET_DDVAL: '2' '2-否'.

  SET_DDVAL: '3' '1-集团总部'.
  SET_DDVAL: '3' '2-二级单位'.
  SET_DDVAL: '3' '3-三级单位'.
  SET_DDVAL: '3' '4-四级单位及以下'.

  SET_DDVAL: '4' '1-大型'.
  SET_DDVAL: '4' '2-中型'.
  SET_DDVAL: '4' '3-小型'.

  SET_DDVAL: '5' '1-独资'.
  SET_DDVAL: '5' '2-绝对控制(控股)'.
  SET_DDVAL: '5' '3-相对控制(控股)'.

  SET_DDVAL: '6' '1-境内上市'.
  SET_DDVAL: '6' '2-境外上市'.
  SET_DDVAL: '6' '3-境内境外同时上市'.
  SET_DDVAL: '6' '4-未上市'.

  SET_DDVAL: '7' '1-中国境内'.
  SET_DDVAL: '7' '2-香港/澳门/台湾'.
  SET_DDVAL: '7' '3-国外'.

  SET_DDVAL: '8' 'A-农林牧渔业'.
  SET_DDVAL: '8' 'B-采矿业'.
  SET_DDVAL: '8' 'C-制造业'.
  SET_DDVAL: '8' 'D-电力/热力/燃气及水生产和供应业'.
  SET_DDVAL: '8' 'E-建筑业'.
  SET_DDVAL: '8' 'F-批发和零售业'.
  SET_DDVAL: '8' 'G-交通运输/仓储和邮政业'.
  SET_DDVAL: '8' 'H-住宿和餐饮业'.
  SET_DDVAL: '8' 'I-信息传输/软件和信息技术/服务业'.
  SET_DDVAL: '8' 'G-金融业'.
  SET_DDVAL: '8' 'K-房地产业'.
  SET_DDVAL: '8' 'L-租房和商务服务业'.
  SET_DDVAL: '8' 'M-科学研究和技术服务业'.
  SET_DDVAL: '8' 'N-水利/环境和公共设施管理业'.
  SET_DDVAL: '8' 'O-居民服务/修理和其他服务业'.
  SET_DDVAL: '8' 'P-教育'.
  SET_DDVAL: '8' 'Q-卫生和社会工作'.
  SET_DDVAL: '8' 'R-文化/体育和娱乐业'.
  SET_DDVAL: '8' 'S-公共管理/社会保障和社会组织'.
  SET_DDVAL: '8' 'T-国际组织'.

  SET_DDVAL: '9' '1-教育'.
  SET_DDVAL: '9' '2-科研'.
  SET_DDVAL: '9' '3-文化'.
  SET_DDVAL: '9' '4-卫生'.
  SET_DDVAL: '9' '5-体育'.
  SET_DDVAL: '9' '6-新闻出版'.
  SET_DDVAL: '9' '7-广播电视'.
  SET_DDVAL: '9' '8-社会福利'.
  SET_DDVAL: '9' '9-救助减灾'.
  SET_DDVAL: '9' '10-统计调查'.
  SET_DDVAL: '9' '11-技术推广与实验'.
  SET_DDVAL: '9' '12-公共设施与管理'.
  SET_DDVAL: '9' '13-物资仓储'.
  SET_DDVAL: '9' '14-监测'.
  SET_DDVAL: '9' '15-勘探与勘察'.
  SET_DDVAL: '9' '16-测绘'.
  SET_DDVAL: '9' '17-检验检测与鉴定'.
  SET_DDVAL: '9' '18-法律服务'.
  SET_DDVAL: '9' '19-资源管理事务'.
  SET_DDVAL: '9' '20-质量技术监督事务'.
  SET_DDVAL: '9' '21-经济监管事务'.
  SET_DDVAL: '9' '22-知识产权事务'.
  SET_DDVAL: '9' '23-公证与认证'.
  SET_DDVAL: '9' '24-信息与咨询'.
  SET_DDVAL: '9' '25-人才交流'.
  SET_DDVAL: '9' '26-机关后勤服务'.
  SET_DDVAL: '9' '27-其他服务'.


  SET_DDVAL: '10' '1-公益一类'.
  SET_DDVAL: '10' '2-公益二类'.
  SET_DDVAL: '10' '3-其他'.

  SET_DDVAL: '11' '1-实行人员聘用制度单位'.
  SET_DDVAL: '11' '2-非人员聘用制度单位'.

  SET_DDVAL: '12' '1-实行公开招聘制度单位'.
  SET_DDVAL: '12' '2-非公开招聘制度单位'.


  CALL METHOD ALV_GRID_RIGHT->SET_DROP_DOWN_TABLE
    EXPORTING
      IT_DROP_DOWN = LT_DDVAL.

ENDFORM.


FORM SUB_SAVE_DATA  USING P_LV_ERROR
                          P_LV_MESSAGE.

  DATA:LS_ZHRT_GZWDWJB TYPE ZHRT_GZWDWJB,
       LT_ZHRT_GZWDWJB TYPE TABLE OF ZHRT_GZWDWJB,
       LV_TABIX        TYPE SY-TABIX.



  LOOP AT GT_ALV INTO DATA(LS_ALV).
    LV_TABIX = SY-TABIX.
    CLEAR LS_ZHRT_GZWDWJB.
    LS_ZHRT_GZWDWJB-ZHRT_YNDB   = GV_GJAHR.
    LS_ZHRT_GZWDWJB-ZHRT_DWBM   = G_NODE_KEY.
    LS_ZHRT_GZWDWJB-ZHRT_GZWXH  = 2.
    LS_ZHRT_GZWDWJB-ZHRT_GZWLH  = LV_TABIX.
    LS_ZHRT_GZWDWJB-ZHRT_DWMC   = GV_STEXT.
    LS_ZHRT_GZWDWJB-ZHRT_DWCJ   = GV_DWCJ.
    LS_ZHRT_GZWDWJB-ZHRT_TJBBBH = GV_REPNO.
    LS_ZHRT_GZWDWJB-ZHRT_TJBBMC = GV_REPNAME.
    LS_ZHRT_GZWDWJB-ZHRT_SBZT   = 1.
    LS_ZHRT_GZWDWJB-ZHRT_GZWMS  = LS_ALV-STEXT1.
    LS_ZHRT_GZWDWJB-ZHRT_GZWMSZ = LS_ALV-VALUE1.
    APPEND LS_ZHRT_GZWDWJB TO LT_ZHRT_GZWDWJB.
  ENDLOOP.

  MODIFY ZHRT_GZWDWJB FROM TABLE LT_ZHRT_GZWDWJB.
  COMMIT WORK AND WAIT.

ENDFORM.

FORM FRM_SUBMIT_DATA  USING    P_LV_ERROR
                               P_LV_MESSAGE.

  GV_EDIT = ''.
  PERFORM FRM_SET_SBZT USING 2.

ENDFORM.

FORM FRM_BH_DATA USING P_LV_ERROR
                       P_LV_MESSAGE.
  GV_EDIT = 'X'.
  PERFORM FRM_SET_SBZT USING 3.
  PERFORM FRM_SET_STYLE.

ENDFORM.



FORM FRM_HANDLE_MODIFY  USING MODIFIED TYPE CHAR01
                              ET_GOOD_CELLS TYPE LVC_T_MODI.

  PERFORM FRM_SET_STYLE.

ENDFORM.


FORM SUB_DOWNLOAD_EXCEL_N USING SD_LOC_FN SD_LOC_DIR SD_LOC_FUL SD_USER_ACTION P_FILENAME.
  DATA: LV_STEXT      TYPE STRING,
        LV_TABIX_FROM TYPE I,
        LV_TABIX_TO   TYPE I,
        LV_ROW        TYPE I,
        LV_COL        TYPE I,
        LV_INDEX      TYPE I.

  GV_OBJECT = 'ZHRB_029_1'.

  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      TEXT = '正在导出数据......'.


  CREATE OBJECT GO_EXCEL.

  CALL METHOD GO_EXCEL->LOAD_FROM_SMW0
    EXPORTING
      IV_OBJID = GV_OBJECT.

  CONCATENATE  '数据截至时间: ' SY-DATUM+0(4) '年' SY-DATUM+4(2) '月' SY-DATUM+6(2) '日'  INTO  LV_STEXT.
  CALL METHOD GO_EXCEL->CHANGE_CELL
    EXPORTING
      IV_ROW   = 2
      IV_COL   = 3
      IV_VALUE = LV_STEXT.


  CONCATENATE  '01 单位名称: ' GV_STEXT INTO  LV_STEXT.
  CALL METHOD GO_EXCEL->CHANGE_CELL
    EXPORTING
      IV_ROW   = 3
      IV_COL   = 1
      IV_VALUE = LV_STEXT.



  "写第3到7行的数据
  LV_ROW = 3.
  DO 4 TIMES.
    ADD 1 TO LV_ROW.
    LV_COL = 0.
    DO 4 TIMES.
      ADD 1 TO LV_COL.
      ADD 1 TO LV_INDEX.
      PERFORM FRM_WRITE_VALUE_TO_EXCEL USING LV_INDEX LV_ROW LV_COL.
    ENDDO.
  ENDDO.

  "写第八行的数据
  LV_ROW = 8.
  LV_COL = 0.
  DO 2 TIMES.
    ADD 1 TO LV_INDEX.
    ADD 1 TO LV_COL.
    PERFORM FRM_WRITE_VALUE_TO_EXCEL USING LV_INDEX LV_ROW LV_COL.
  ENDDO.

  "写12行1列的数据
  ADD 1 TO LV_INDEX.
  PERFORM FRM_WRITE_VALUE_TO_EXCEL USING LV_INDEX 12 1.

  "写12行2列的数据
  ADD 1 TO LV_INDEX.
  PERFORM FRM_WRITE_VALUE_TO_EXCEL USING LV_INDEX 12 3.

  "写13到14行的数据
  LV_ROW = 12.
  DO 2 TIMES.
    ADD 1 TO LV_ROW.
    LV_COL = 0.
    DO 4 TIMES.
      ADD 1 TO LV_COL.
      ADD 1 TO LV_INDEX.
      PERFORM FRM_WRITE_VALUE_TO_EXCEL USING LV_INDEX LV_ROW LV_COL.
    ENDDO.
  ENDDO.



*浏览并保存下载
  CALL METHOD GO_EXCEL->BROWSE_AND_SAVE
    EXPORTING
      IV_NAME  = P_FILENAME
      IV_TITLE = '保存文件路径'.
ENDFORM.


FORM FRM_WRITE_VALUE_TO_EXCEL USING INDEX ROW COL.

  DATA: LV_CELL_VALUE TYPE ZEXCEL_CELL_VALUE,
        LV_STEXT1     TYPE ZEXCEL_CELL_VALUE,
        LV_STEXT2     TYPE ZEXCEL_CELL_VALUE,
        LV_KEY        TYPE STRING,
        LV_VALUE      TYPE STRING.

  CALL METHOD GO_EXCEL->SET_STYLE
    EXPORTING
      IV_ROW            = ROW
      IV_COL            = COL
      IV_SAVE_OLD_STYLE = 'X'
    IMPORTING
      IV_CELL_VALUE     = LV_CELL_VALUE.

  IF LV_CELL_VALUE IS NOT INITIAL.
    SPLIT LV_CELL_VALUE AT '：'  INTO LV_STEXT1 LV_STEXT2.
  ENDIF.

  READ TABLE GT_ALV INTO DATA(LS_ALV) INDEX INDEX.
  IF SY-SUBRC = 0 AND LS_ALV-VALUE1 IS NOT INITIAL.
    SPLIT LS_ALV-VALUE1  AT '-' INTO LV_KEY LV_VALUE.
  ENDIF.

  CONCATENATE LV_STEXT1 '：  ' LV_KEY  LV_STEXT2 INTO LV_CELL_VALUE.

  CALL METHOD GO_EXCEL->CHANGE_CELL
    EXPORTING
      IV_ROW   = ROW
      IV_COL   = COL
      IV_VALUE = LV_CELL_VALUE.

ENDFORM.

FORM FRM_EXCEL_DATA TABLES T_ITAB STRUCTURE ZHRS_051 USING ERROR MESSAGE.

  DATA: LV_TABIX_FROM TYPE I,
        LV_TABIX_TO   TYPE I,
        LV_ROW        TYPE I,
        LV_COL        TYPE I,
        LV_INDEX      TYPE I.

  DATA: LT_ZHRT_GZWDWJB TYPE TABLE OF ZHRT_GZWDWJB.

  GV_OBJECT = 'ZHRB_029_1'.

  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      TEXT = '正在导出数据......'.

  LOOP AT T_ITAB WHERE CHECKBOX EQ 'X'.

    PERFORM FRM_GET_DATA_FROM_TABLE TABLES LT_ZHRT_GZWDWJB USING T_ITAB-OBJID.
    SORT LT_ZHRT_GZWDWJB BY ZHRT_GZWLH.

    IF LT_ZHRT_GZWDWJB[] IS INITIAL.
      CONTINUE.
    ENDIF.

    CREATE OBJECT GO_EXCEL.

    CALL METHOD GO_EXCEL->LOAD_FROM_SMW0
      EXPORTING
        IV_OBJID = GV_OBJECT.

    CONCATENATE T_ITAB-OBJID '_' T_ITAB-STEXT '_' GV_REPNAME '_' SY-DATUM '_' SY-UZEIT INTO DATA(LV_FILENAME).
    CONDENSE LV_FILENAME NO-GAPS.

    CONCATENATE  '数据截至时间: ' SY-DATUM+0(4) '年' SY-DATUM+4(2) '月' SY-DATUM+6(2) '日'  INTO  DATA(LV_STEXT).
    CALL METHOD GO_EXCEL->CHANGE_CELL
      EXPORTING
        IV_ROW   = 2
        IV_COL   = 3
        IV_VALUE = LV_STEXT.


    CONCATENATE  '01 单位名称: ' T_ITAB-STEXT INTO  LV_STEXT.
    CALL METHOD GO_EXCEL->CHANGE_CELL
      EXPORTING
        IV_ROW   = 3
        IV_COL   = 1
        IV_VALUE = LV_STEXT.

    "写第3到7行的数据
    LV_ROW = 3.
    DO 4 TIMES.
      ADD 1 TO LV_ROW.
      LV_COL = 0.
      DO 4 TIMES.
        ADD 1 TO LV_COL.
        ADD 1 TO LV_INDEX.
        PERFORM FRM_WRITE_VALUE_TO_EXCEL_2 TABLES LT_ZHRT_GZWDWJB USING LV_INDEX LV_ROW LV_COL.
      ENDDO.
    ENDDO.

    "写第八行的数据
    LV_ROW = 8.
    LV_COL = 0.
    DO 2 TIMES.
      ADD 1 TO LV_INDEX.
      ADD 1 TO LV_COL.
      PERFORM FRM_WRITE_VALUE_TO_EXCEL_2 TABLES LT_ZHRT_GZWDWJB USING LV_INDEX LV_ROW LV_COL.
    ENDDO.

    "写12行1列的数据
    ADD 1 TO LV_INDEX.
    LV_ROW = 12.
    LV_COL = 1.
    PERFORM FRM_WRITE_VALUE_TO_EXCEL_2 TABLES LT_ZHRT_GZWDWJB USING LV_INDEX LV_ROW LV_COL.

    "写12行2列的数据
    ADD 1 TO LV_INDEX.
    LV_ROW = 12.
    LV_COL = 3.
    PERFORM FRM_WRITE_VALUE_TO_EXCEL_2 TABLES LT_ZHRT_GZWDWJB USING LV_INDEX LV_ROW LV_COL.

    "写13到14行的数据
    LV_ROW = 12.
    DO 2 TIMES.
      ADD 1 TO LV_ROW.
      LV_COL = 0.
      DO 4 TIMES.
        ADD 1 TO LV_COL.
        ADD 1 TO LV_INDEX.
        PERFORM FRM_WRITE_VALUE_TO_EXCEL_2 TABLES LT_ZHRT_GZWDWJB USING LV_INDEX LV_ROW LV_COL.
      ENDDO.
    ENDDO.

    CALL METHOD GO_EXCEL->BROWSE_AND_SAVE
      EXPORTING
        IV_NAME  = LV_FILENAME
        IV_TITLE = '保存文件路径'.

    CLEAR: LV_STEXT, LV_FILENAME ,LV_INDEX. REFRESH LT_ZHRT_GZWDWJB.
  ENDLOOP.
ENDFORM.

FORM FRM_WRITE_VALUE_TO_EXCEL_2 TABLES T_ITAB STRUCTURE ZHRT_GZWDWJB USING INDEX ROW COL.

  DATA: LV_CELL_VALUE TYPE ZEXCEL_CELL_VALUE,
        LV_STEXT1     TYPE ZEXCEL_CELL_VALUE,
        LV_STEXT2     TYPE ZEXCEL_CELL_VALUE,
        LV_KEY        TYPE STRING,
        LV_VALUE      TYPE STRING.

  CALL METHOD GO_EXCEL->SET_STYLE
    EXPORTING
      IV_ROW            = ROW
      IV_COL            = COL
      IV_SAVE_OLD_STYLE = 'X'
    IMPORTING
      IV_CELL_VALUE     = LV_CELL_VALUE.

  IF LV_CELL_VALUE IS NOT INITIAL.
    SPLIT LV_CELL_VALUE AT '：'  INTO LV_STEXT1 LV_STEXT2.
  ENDIF.

  READ TABLE T_ITAB INTO DATA(LS_ALV) INDEX INDEX.
  IF SY-SUBRC = 0 AND LS_ALV-ZHRT_GZWMSZ IS NOT INITIAL.
    SPLIT LS_ALV-ZHRT_GZWMSZ  AT '-' INTO LV_KEY LV_VALUE.
  ENDIF.

  CONCATENATE LV_STEXT1 '：  ' LV_KEY  LV_STEXT2 INTO LV_CELL_VALUE.

  CALL METHOD GO_EXCEL->CHANGE_CELL
    EXPORTING
      IV_ROW   = ROW
      IV_COL   = COL
      IV_VALUE = LV_CELL_VALUE.

ENDFORM.
```

