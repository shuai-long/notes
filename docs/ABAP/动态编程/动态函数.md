[TOC]

# 动态函数调用 #

1. 调用语法:

   ```abap
   DATA: LT_PARAM TYPE ABAP_FUNC_PARMBIND_TAB,
         LS_PARAM TYPE ABAP_FUNC_PARMBIND,
         LT_EXCEP TYPE ABAP_FUNC_EXCPBIND_TAB,
         LS_EXCEP TYPE ABAP_FUNC_EXCPBIND.
         
   CALL FUNCTION LV_FUNC_NAME
     PARAMETER-TABLE
     	LT_PARAM
     EXCEPTION-TABLE
    	 LT_EXCEP.
   ```

2. 参数解析

   PARAMETER-TABLE*: 用于为被调用函数模块的所有形参分配实参。参照类型池 ABAP 的表类型 `abap_func_parmbind_tab` 或行类型 `abap_func_parmbind` 创建，类型为排序表。其结构为:

   1. NAME:  函数模块的参数名称，类型 CHAR30

   2. KIND:  类型为 I，形式参数的类型, 可包含以下值

      `abap_func_exporting`  ：for input parameters value 10

      `abap_func_importing`  ：for output parameters value 20

      `abap_func_tables`    ：for table parameters value 30

      `abap_func_changing`  ：for input/output parameters value 4

   3. VALUE:  类型为 REF TO DATA , 作为指向实际参数的指针。 value 中的引用变量所指向的数据对象被分配给 name 中指定的形参

   

   *EXCEPTION-TABLE*: 用于将返回值分配给未标记为异常类的被调用功能模块的异常。参照类型池 ABAP 的表类型 `abap_func_excpbind_tab` 或行类型 `abap_func_excpbind` 创建散列表。

   1. NAME:     对于相应异常的名称，或 error_message，或以大写字母指定 OTHERS。类型 CHAR30
   2. VALUE:    类型为 i ，处理 name 中指定的异常后在 sy-subrc 中可用的数值
   3. MESSAGE:  定义类型为 REF TO DATA

3. 简单实例

   ```abap
   TYPE-POOLS:ABAP,SLIS.
   
   DATA: LV_FUNC_NAME TYPE RS38L_FNAM.
   DATA: LV_MSG    TYPE STRING,
         LT_FLIGHT TYPE TABLE OF SFLIGHT.
   DATA: LT_PARAM TYPE ABAP_FUNC_PARMBIND_TAB,
         LS_PARAM TYPE ABAP_FUNC_PARMBIND,
         LT_EXCEP TYPE ABAP_FUNC_EXCPBIND_TAB,
         LS_EXCEP TYPE ABAP_FUNC_EXCPBIND.
   FIELD-SYMBOLS: <FS_MSG>     TYPE STRING,
                  <FS_DETAILS> TYPE ANY TABLE.
   
   PARAMETERS: P_CARR TYPE SFLIGHT-CARRID,
               P_CONN TYPE SFLIGHT-CONNID,
               P_DATE TYPE SFLIGHT-FLDATE.
   * Build parameters
   LS_PARAM-NAME = 'CARRID'.
   LS_PARAM-KIND = ABAP_FUNC_EXPORTING.
   GET REFERENCE OF P_CARR INTO LS_PARAM-VALUE.
   APPEND LS_PARAM TO LT_PARAM.
   CLEAR LS_PARAM.
   LS_PARAM-NAME = 'CONNID'.
   LS_PARAM-KIND = ABAP_FUNC_EXPORTING.
   GET REFERENCE OF P_CONN INTO LS_PARAM-VALUE.
   APPEND LS_PARAM TO LT_PARAM.
   CLEAR LS_PARAM.
   LS_PARAM-NAME = 'FLDATE'.
   LS_PARAM-KIND = ABAP_FUNC_EXPORTING.
   GET REFERENCE OF P_DATE INTO LS_PARAM-VALUE.
   APPEND LS_PARAM TO LT_PARAM.
   CLEAR LS_PARAM.
   LS_PARAM-NAME = 'MSG'.
   LS_PARAM-KIND = ABAP_FUNC_IMPORTING.
   GET REFERENCE OF LV_MSG INTO LS_PARAM-VALUE.
   APPEND LS_PARAM TO LT_PARAM.
   CLEAR LS_PARAM.
   LS_PARAM-NAME = 'DETAILS'.
   LS_PARAM-KIND = ABAP_FUNC_TABLES.
   GET REFERENCE OF LT_FLIGHT INTO LS_PARAM-VALUE.
   APPEND LS_PARAM TO LT_PARAM.
   CLEAR LS_PARAM.
   "Exception Parameter"
   LS_EXCEP-NAME = 'PROGRAM_ERROR'.
   LS_EXCEP-VALUE = 10.
   INSERT LS_EXCEP INTO TABLE LT_EXCEP.
   * Dynamic FM call with all import,export,table parameters in the Parameter-table
   CALL FUNCTION LV_FUNC_NAME
     PARAMETER-TABLE
     LT_PARAM
     EXCEPTION-TABLE
     LT_EXCEP.
   * FM Result
   READ TABLE LT_PARAM INTO LS_PARAM WITH KEY NAME = 'MSG'.
   IF SY-SUBRC IS INITIAL.
     ASSIGN LS_PARAM-VALUE->* TO <FS_MSG>.
     IF <FS_MSG> IS ASSIGNED AND <FS_MSG> IS NOT INITIAL.
       WRITE:/ <FS_MSG>.
     ELSE.
       READ TABLE LT_PARAM INTO LS_PARAM WITH KEY NAME = 'DETAILS'.
       IF SY-SUBRC IS INITIAL.
         ASSIGN LS_PARAM-VALUE->* TO <FS_DETAILS>.
         IF <FS_DETAILS> IS ASSIGNED AND <FS_DETAILS> IS NOT INITIAL .
           LT_FLIGHT =   <FS_DETAILS>.
           CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
             EXPORTING
               I_CALLBACK_PROGRAM = SY-REPID
               I_STRUCTURE_NAME   = 'SFLIGHT'
             TABLES
               T_OUTTAB           = LT_FLIGHT.
         ENDIF.
       ENDIF.
     ENDIF.
   ENDIF.
   ```

# 函数信息获取 #

## RPY_FUNCTIONMODULE_READ_NEW ##

1. 函数示例:

   ![函数示例](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/1678433437714.jpg)

2. 参数解释:

* IMPORT_PARAMETER

  ![IMPORT_PARAMETER](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/1678433637124.jpg)

* EXPORT_PARAMETER

  ![EXPORT_PARAMETER](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/1678433680347.jpg)

* TABLES_PARAMETER

  ![TABLES_PARAMETER](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/1678433700290.jpg)

  3. 代码示例

     ```abap
     DATA: lv_func_name TYPE RS38L_FNAM.
     DATA: lt_imp_para TYPE TABLE OF rsimp WITH HEADER LINE,
           lt_chg_para TYPE TABLE OF rscha WITH HEADER LINE,
           lt_exp_para TYPE TABLE OF rsexp WITH HEADER LINE,
           lt_tab_para TYPE TABLE OF rstbl WITH HEADER LINE,
           lt_exc_list TYPE TABLE OF rsexc WITH HEADER LINE,
           lt_document TYPE TABLE OF rsfdo WITH HEADER LINE,
           lt_source TYPE TABLE OF rssource WITH HEADER LINE.
     CLEAR:lt_rsimp,lt_rscha,lt_rsexp,lt_rstbl,lt_rsexc,lt_rsfdo,lt_source.
     REFRESH:lt_rsimp,lt_rscha,lt_rsexp,lt_rstbl,lt_rsexc,lt_rsfdo,lt_source.
     lv_func_name = 'BAPI_PO_CREATE'.
     CALL FUNCTION 'RPY_FUNCTIONMODULE_READ_NEW'
       EXPORTING
         functionname       = lv_func_name
       TABLES
         import_parameter   = lt_imp_para
         changing_parameter = lt_chg_para
         export_parameter   = lt_exp_para
         tables_parameter   = lt_tab_para
         exception_list     = lt_exc_list
         documentation      = lt_document
         source             = lt_source.
     ```

  4. 结果解析

     通过反射方法判断类型

     ```abap
     DATA: lo_tabledescr  TYPE REF TO cl_abap_tabledescr,
           lo_structdescr TYPE REF TO cl_abap_structdescr,
           lo_elemdescr   TYPE REF TO cl_abap_elemdescr,
           lv_linetypename TYPE string,
           t_tabdescr      TYPE abap_compdescr_tab.
     LOOP AT lt_chg_para.
       TRY. "如果当前try成功则为Table type，失败则执行下一个try语句判断Structure type"
     *Note: To create a table in Changing Parameter, the type should be a dictionary table type.
           lo_tabledescr   ?= cl_abap_typedescr=>describe_by_name( lt_chg_para-dbfield ).
           lo_structdescr  ?= lo_tabledescr->get_table_line_type( ).
           lv_linetypename = lo_structdescr->get_relative_name( ).
           lo_structdescr ?= cl_abap_typedescr=>describe_by_name( lv_linetypename ).
           t_tabdescr[]  =  lo_structdescr->components[].
         CATCH cx_root.
           TRY."如果当前try成功则为structure type."
               lo_structdescr ?= cl_abap_typedescr=>describe_by_data( lt_chg_para-dbfield ).
               t_tabdescr[] = lo_structdescr->components[].
             CATCH cx_root. 
               "如果上面两个都发生异常，则为variable type"
               lo_elemdescr ?= cl_abap_typedescr=>describe_by_name( lt_chg_para-dbfield ).
           ENDTRY.
       ENDTRY.
     ENDLOOP.
     ```


## RFC_GET_FUNCTION_INTERFACE_US ##

1. 函数示例:

   ![函数示例](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/1678434631642.jpg)

2. 参数解析

   ![参数解析](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/1678434670293.jpg)

   PARAMCLASS:  一般有三个值: I/E/T  除了 T 类型外, 我们需要额外判断该参数的参考类型是表不(可通过 DD02L 判断)

   ```abap
   SELECT SINGLE * INTO @DATA(LS_DD02L) FROM DD02L WHERE TABNAME EQ 'BAPIEKKOC' AND AS4LOCAL EQ 'A' AND AS4VERS EQ '0000'.
   IF LS_DD02L-TABCLASS EQ 'INTTAB'.
   "这是个结构
   ENDIF.
   IF LS_DD02L-TABCLASS EQ 'TRANSP'.
   "这是个表
   ENDIF.
   
   "如果是表类型, 可以去 DD04V/DD40L 中判断
   ROWTYPE
   ```

   PARAMETER:   参数名称

   TABNAME  :   参考表

   FIELDNAME:   参考字段

   EXID     :   参数类型

   DEFAULT  :   默认值
   PARAMTEXT:   参数描述
   OPTIONAL :   可选参数

   ______________________________

    

3. 代码示例

   ```abap
   TYPE-POOLS:ABAP,SLIS.
   DATA: LV_FUNC_NAME TYPE RS38L_FNAM.
   DATA: LT_IMP_PARA TYPE TABLE OF RSIMP WITH HEADER LINE,
         LT_CHG_PARA TYPE TABLE OF RSCHA WITH HEADER LINE,
         LT_EXP_PARA TYPE TABLE OF RSEXP WITH HEADER LINE,
         LT_TAB_PARA TYPE TABLE OF RSTBL WITH HEADER LINE,
         LT_EXC_LIST TYPE TABLE OF RSEXC WITH HEADER LINE,
         LT_DOCUMENT TYPE TABLE OF RSFDO WITH HEADER LINE,
         LT_SOURCE   TYPE TABLE OF RSSOURCE WITH HEADER LINE.
   
   DATA: LV_MSG TYPE STRING.
   DATA: LT_PARAM TYPE ABAP_FUNC_PARMBIND_TAB,
         LS_PARAM TYPE ABAP_FUNC_PARMBIND,
         LT_EXCEP TYPE ABAP_FUNC_EXCPBIND_TAB,
         LS_EXCEP TYPE ABAP_FUNC_EXCPBIND.
   FIELD-SYMBOLS: <FS_MSG> TYPE STRING.
   DATA:BEGIN OF GS_DATA,
          CANUM          TYPE HRFPM_NO_DOC_LINES,
          FUNC_PARAMETER TYPE RS38L_PAR_,
          FUNC_PARAMTYPE TYPE RS38L_KIND,
          FUNC_STRUCTURE TYPE RS38L_TYP,
          JSON_DATA      TYPE STRING,
          PARAM_TYPE     TYPE PARAMTEXT,
        END OF GS_DATA.
   DATA: GT_DATA LIKE TABLE OF GS_DATA.
   "Prepare Data"
   " PERFORM get_data.
   "Get FM Parameter"
   
   CALL FUNCTION 'RPY_FUNCTIONMODULE_READ_NEW'
     EXPORTING
       FUNCTIONNAME       = LV_FUNC_NAME
     TABLES
       IMPORT_PARAMETER   = LT_IMP_PARA
       CHANGING_PARAMETER = LT_CHG_PARA
       EXPORT_PARAMETER   = LT_EXP_PARA
       TABLES_PARAMETER   = LT_TAB_PARA
       EXCEPTION_LIST     = LT_EXC_LIST
       DOCUMENTATION      = LT_DOCUMENT
       SOURCE             = LT_SOURCE.
   CLEAR LT_PARAM .
   "Function Input Parameter"
   LOOP AT LT_IMP_PARA.
     PERFORM PREPARE_PARAMETER_TABLE USING LT_IMP_PARA-PARAMETER LT_IMP_PARA-OPTIONAL
                                           LT_IMP_PARA-TYP 'I'
                                  CHANGING LT_PARAM .
   ENDLOOP.
   "Function Table Parameter"
   LOOP AT LT_TAB_PARA.
     PERFORM PREPARE_PARAMETER_TABLE USING LT_TAB_PARA-PARAMETER LT_TAB_PARA-OPTIONAL
                                           LT_TAB_PARA-DBSTRUCT 'T'
                                  CHANGING LT_PARAM .
   ENDLOOP.
   "Call Function with parameter
   CALL FUNCTION LV_FUNC_NAME
     PARAMETER-TABLE
     LT_PARAM
     EXCEPTION-TABLE
     LT_EXCEP.
   *&---------------------------------------------------------------------*
   *&      Form  PREPARE_PARAMETER_TABLE
   *&---------------------------------------------------------------------*
   *       text
   *----------------------------------------------------------------------*
   *      -->P_LT_RSIMP_PARAMETER  text
   *      -->P_LT_RSIMP_OPTIONAL  text
   *      -->P_LT_RSIMP_TYP  text
   *----------------------------------------------------------------------*
   FORM PREPARE_PARAMETER_TABLE  USING PARAMETER OPTIONAL REF TYP
                              CHANGING IT_PAR TYPE ABAP_FUNC_PARMBIND_TAB.
     DATA WA_PAR TYPE ABAP_FUNC_PARMBIND.
     DATA: LV_STRING TYPE STRING.
     DATA: LV_DATA_TYPE TYPE STRING.
     DATA: L_TABNAME TYPE TABNAME,
           LS_DD40VV TYPE DD40VV.
     DATA: DYN_TABLE TYPE REF TO DATA,
           DYN_WA    TYPE REF TO DATA,
           DYN_VALUE TYPE REF TO DATA.
     FIELD-SYMBOLS: <DYN_TABLE> TYPE ANY TABLE,
                    <DYN_WA>    TYPE ANY,
                    <DYN_VALUE> TYPE ANY,
                    <FVAL>      TYPE ANY.
     "Log记录的Function Module请求参数处理"
     READ TABLE GT_DATA INTO GS_DATA WITH KEY FUNC_PARAMETER = PARAMETER.
     IF SY-SUBRC = 0.
       CLEAR WA_PAR.
       WA_PAR-NAME = PARAMETER.              " Prarter Name "
       IF TYP EQ 'T'.                        " Prarter Type "
         WA_PAR-KIND = ABAP_FUNC_TABLES.
       ELSEIF TYP EQ 'I'.
         WA_PAR-KIND = ABAP_FUNC_EXPORTING.
       ELSEIF TYP EQ 'E'.
         WA_PAR-KIND = ABAP_FUNC_IMPORTING.
       ENDIF.
       " Parameter Value "
       " Paremeter:Structure OR Table "
       IF GS_DATA-JSON_DATA+0(1) = '[' OR GS_DATA-JSON_DATA+0(1) = '{'.
         CLEAR LS_DD40VV.
         SELECT SINGLE * FROM DD40VV INTO LS_DD40VV
          WHERE TYPENAME = REF
            AND DDLANGUAGE = SY-LANGU.
         "判断表类型，如果是 Table Type 则使用 row type 为表结构"
         IF LS_DD40VV IS NOT INITIAL.
           L_TABNAME = LS_DD40VV-ROWTYPE.
         ELSE.
           L_TABNAME = REF.
         ENDIF.
         "创建动态表结构"
         CREATE DATA DYN_TABLE TYPE TABLE OF (L_TABNAME).
         "创建动态内表"
         ASSIGN DYN_TABLE->* TO <DYN_TABLE>.
         "创建动态工作区结构"
         CREATE DATA DYN_WA LIKE LINE OF <DYN_TABLE>.
         "创建动态工作区"
         ASSIGN DYN_WA->* TO <DYN_WA>.
   
         <DYN_WA> = GS_DATA-JSON_DATA.
   *      APPEND <DYN_WA> TO <DYN_TABLE>.
   *      <DYN_TABLE> = GS_DATA-JSON_DATA.
         IF TYP EQ 'T'.       "参数类型为表：整表数据传输"
           GET REFERENCE OF <DYN_TABLE> INTO WA_PAR-VALUE.
         ELSEIF TYP EQ 'I'.   "参数类型为结构:传入单条数据"
           LOOP AT <DYN_TABLE> INTO <DYN_WA>.
             GET REFERENCE OF <DYN_WA> INTO WA_PAR-VALUE.
           ENDLOOP.
         ENDIF.
       ELSE. "Paremeter:单值传入"
         CLEAR LV_STRING .
         LV_STRING = GS_DATA-JSON_DATA.
         CONDENSE LV_STRING NO-GAPS.
         CREATE DATA DYN_VALUE TYPE (REF).   "参照传入参数类型创建动态字段"
         ASSIGN DYN_VALUE->* TO <DYN_VALUE>.
         <DYN_VALUE> = LV_STRING.
         GET REFERENCE OF <DYN_VALUE> INTO WA_PAR-VALUE.
       ENDIF.
       INSERT WA_PAR INTO TABLE IT_PAR.
       CLEAR WA_PAR.
     ELSE. "必输字段一定要处理，否则会报错"
       IF OPTIONAL IS INITIAL.
         CLEAR WA_PAR.
         WA_PAR-NAME = PARAMETER.
         IF TYP EQ 'I'.
           WA_PAR-KIND = ABAP_FUNC_EXPORTING.
           CREATE DATA DYN_VALUE TYPE (REF).
           ASSIGN DYN_VALUE->* TO <DYN_VALUE>.
           CLEAR <DYN_VALUE>.
           GET REFERENCE OF <DYN_VALUE> INTO WA_PAR-VALUE.
           INSERT WA_PAR INTO TABLE IT_PAR.
           CLEAR WA_PAR.
         ELSEIF TYP EQ 'T'.
           WA_PAR-KIND = ABAP_FUNC_TABLES.
           CLEAR LS_DD40VV.
           SELECT SINGLE * FROM DD40VV INTO LS_DD40VV
            WHERE TYPENAME = REF
              AND DDLANGUAGE = SY-LANGU.
           "判断表类型，如果是 Table Type 则使用 row type 为表结构"
           IF LS_DD40VV IS NOT INITIAL.
             L_TABNAME = LS_DD40VV-ROWTYPE.
           ELSE.
             L_TABNAME = REF.
           ENDIF.
           "创建动态表结构
           CREATE DATA DYN_TABLE TYPE TABLE OF (L_TABNAME).
           "创建动态内表
           ASSIGN DYN_TABLE->* TO <DYN_TABLE>.
           REFRESH <DYN_TABLE>.
           GET REFERENCE OF <DYN_TABLE> INTO WA_PAR-VALUE.
           INSERT WA_PAR INTO TABLE IT_PAR.
           CLEAR WA_PAR.
         ENDIF.
       ENDIF.
     ENDIF.
   ENDFORM.                    " PREPARE_PARAMETER_TABLE "
   ```

   

## BDL_FUNCTION_INTERFACE_GET ##

用法与 [**RPY_FUNCTIONMODULE_READ_NEW**](#RPY_FUNCTIONMODULE_READ_NEW) 类似

​         
