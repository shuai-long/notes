[TOC]

# 长文本 #

长文本对应的两张表：**STXL** 和 **STXH**

以及：**TTXID、TTXIT、TTXOB、TTXOT**



## 创建文本对象 ##

1. **使用 T-Code:** ==SE75==

   选择 **文本对象和标识** => 点击更改 =>点击创建

   ![创建长文本-步骤1](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/1681204763483.jpg)

   ![创建长文本-步骤2](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/v2-6cc84f5dc491637eb76dd3e2ff856a69_r.jpg)

2. 填写对象信息:

   **文本对象:** 以 Z 开头, 随便写

   **描述:** 随便写

   **保存模式:** 更新

   **编辑器应用程序:** 默认 TN, 可更改

   **行宽:** 默认 72 可更改

   ![创建长文本-步骤3](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/1681204931337.jpg)

3. 创建文本标识 ID, 双击文本对象, 点击创建

   ![创建长文本-步骤4](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/v2-9176d6ce2514f60cbb03cef713df8246_r.jpg)

   ![创建长文本-步骤5](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/v2-a5b185f6f84a341e1e3b75874c81918d_r.jpg)

   ![创建长文本-步骤6](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/v2-79a1e7b95ed9cb5ce49623019323ca25_r.jpg)

4. 创建标识信息

   ![创建长文本-步骤7](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/v2-847e1697f0c399011257f3970b8e65d1_r.jpg)

5. 保存

   ![创建长文本-步骤8](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/v2-c5fe012ece7aa370dc2eccb9b3327919_r.jpg)

## 常用方法 ##

### 读取长文本 ###

```abap
"读取长文本
FORM FRM_READ_TEXT TABLES LINES USING OBJECT ID NAME.

  CALL FUNCTION 'READ_TEXT'
    EXPORTING
      ID                      = ID          "文本对象ID
      LANGUAGE                = SY-LANGU
      NAME                    = NAME
      OBJECT                  = OBJECT
    TABLES
      LINES                   = LINES[]
    EXCEPTIONS
      ID                      = 1
      LANGUAGE                = 2
      NAME                    = 3
      NOT_FOUND               = 4
      OBJECT                  = 5
      REFERENCE_CHECK         = 6
      WRONG_ACCESS_TO_ARCHIVE = 7
      OTHERS                  = 8.

ENDFORM.
```

### 将文本更新到屏幕 ###

第一种方法: 传入屏幕组件名称

```abap
FORM FRM_EDITOR_SET TABLES LINES USING CONTROLNAME DISPLAY_MODE.

  CALL FUNCTION 'RH_EDITOR_SET'
    EXPORTING
      REPID          = SY-REPID
      DYNNR          = SY-DYNNR
      CONTROLNAME    = CONTROLNAME
      DISPLAY_MODE   = DISPLAY_MODE
    TABLES
      LINES          = LINES[]
    EXCEPTIONS
      CREATE_ERROR   = 1
      INTERNAL_ERROR = 2
      OTHERS         = 3.

ENDFORM.

```

第二种方法: 创建容器

```abap
  DATA: LR_CONTAINER TYPE REF TO CL_GUI_CUSTOM_CONTAINER,
        LR_EDITOR    TYPE REF TO CL_GUI_TEXTEDIT.

FORM FRM_SET_TEXT_TO_SCREEN TABLES LINES USING CONTROLNAME DISPLAY_MODE.

  CREATE OBJECT LR_CONTAINER
    EXPORTING
      CONTAINER_NAME = CONTROLNAME.

  CREATE OBJECT LR_EDITOR
    EXPORTING
      PARENT                     = LR_CONTAINER
      WORDWRAP_MODE              = 1
      WORDWRAP_POSITION          = 72
      WORDWRAP_TO_LINEBREAK_MODE = DISPLAY_MODE.

  CALL METHOD LR_EDITOR->SET_TEXT_AS_R3TABLE
    EXPORTING
      TABLE           = LINES
    EXCEPTIONS
      ERROR_DP        = 1
      ERROR_DP_CREATE = 2.

ENDFORM.

```



### 获取屏幕文本 ###

方法一: 使用函数读取

```abap
FORM FRM_GET_TEXT_FROM_SCREEN TABLES LINES_READ LINES_WRITE USING CONTROLNAME.

  DATA: LT_LINES LIKE TABLE OF TLINE,
        LS_LINES LIKE LINE OF LT_LINES.

  CALL FUNCTION 'RH_EDITOR_GET'
    EXPORTING
      CONTROLNAME    = CONTROLNAME
    TABLES
      LINES          = LINES_READ[]
    EXCEPTIONS
      INTERNAL_ERROR = 1
      OTHERS         = 2.

  LOOP AT LINES_READ.
    CLEAR LS_LINES.
    LS_LINES-TDLINE = LINES_READ.
    APPEND LS_LINES TO LT_LINES.
  ENDLOOP.

  LINES_WRITE[] = LT_LINES.
ENDFORM.
```

方法二: 使用容器读取

```abap
DATA: LT_LINES_READ  TYPE TABLE OF CHAR256,
 	  LR_EDITOR      TYPE REF TO CL_GUI_TEXTEDIT.
 	  
  "获屏幕长文本
  CALL METHOD LR_EDITOR->GET_TEXT_AS_R3TABLE
    IMPORTING
      TABLE                  = LT_LINES_READ
    EXCEPTIONS
      ERROR_DP               = 1
      ERROR_CNTL_CALL_METHOD = 2
      ERROR_DP_CREATE        = 3
      POTENTIAL_DATA_LOSS    = 4
      OTHERS                 = 5.

```

### 更新长文本 ###

```abap
DATA: LT_LINES_WRITE LIKE TABLE OF TLINE.
"创建长文本
FORM FRM_CREATE_TEXT TABLES LINES USING HEADER STRUCTURE HEADER .

  DATA: LS_THEAD LIKE THEAD.

  LS_THEAD = HEADER.

  CALL FUNCTION 'SAVE_TEXT'
    EXPORTING
      HEADER = LS_THEAD
    TABLES
      LINES  = LINES.

ENDFORM.
```

### 删除长文本 ###

```abap
"删除长文本
FORM FRM_DELETE_TEXT USING OBJECT ID NAME.
  DATA: LS_THEAD       LIKE THEAD.

  LS_THEAD-TDOBJECT    = OBJECT.
  LS_THEAD-TDID        = ID.
  LS_THEAD-TDSPRAS     = SY-LANGU.
  LS_THEAD-TDNAME      = NAME.

  CALL FUNCTION 'DELETE_TEXT'
    EXPORTING
      ID       = LS_THEAD-TDID
      LANGUAGE = LS_THEAD-TDSPRAS
      NAME     = LS_THEAD-TDNAME
      OBJECT   = LS_THEAD-TDOBJECT.

ENDFORM.
```

## 示例程序 ##

可直接使用

```abap
SELECTION-SCREEN BEGIN OF BLOCK A WITH FRAME TITLE TEXT-001.
  PARAMETERS: P_TNAME TYPE THEAD-TDNAME DEFAULT 'LTEXT_TEXT001' OBLIGATORY,
              P_2000  RADIOBUTTON GROUP G1 USER-COMMAND UC1 DEFAULT 'X',
              P_3000  RADIOBUTTON GROUP G1.
SELECTION-SCREEN END OF BLOCK A.

START-OF-SELECTION.
  CASE 'X'.
    WHEN P_2000.
      CALL SCREEN 2000.
    WHEN P_3000.
      CALL SCREEN 3000.
    WHEN OTHERS.
  ENDCASE.


MODULE STATUS_2000 OUTPUT.

  DATA: LT_LINES_READ  LIKE TABLE OF TLINE,
        LT_LINES_WRITE TYPE TABLE OF CHAR256,
        LS_THEAD       LIKE THEAD,
        LV_CONTROLNAME TYPE SCRFNAME.

  SET PF-STATUS 'STANDARD'.

  "读取长文本
  LS_THEAD-TDOBJECT  = 'ZTEST_001'.
  LS_THEAD-TDID     = 'Z001'.
  LS_THEAD-TDNAME   = P_TNAME.

  PERFORM FRM_READ_TEXT TABLES LT_LINES_READ USING LS_THEAD-TDOBJECT LS_THEAD-TDID LS_THEAD-TDNAME.

  LOOP AT LT_LINES_READ INTO DATA(LS_LINES_READ).
    APPEND LS_LINES_READ-TDLINE TO LT_LINES_WRITE.
  ENDLOOP.

  "将长文本赋值到屏幕
  LV_CONTROLNAME = 'TEXT'.
  PERFORM FRM_EDITOR_SET TABLES LT_LINES_WRITE USING LV_CONTROLNAME ' '.

ENDMODULE.

MODULE STATUS_3000 OUTPUT.
  DATA: LR_CONTAINER TYPE REF TO CL_GUI_CUSTOM_CONTAINER,
        LR_EDITOR    TYPE REF TO CL_GUI_TEXTEDIT.

  SET PF-STATUS 'STANDARD'.
  "读取长文本
  LS_THEAD-TDOBJECT  = 'ZTEST_001'.
  LS_THEAD-TDID     = 'Z001'.
  LS_THEAD-TDNAME   = P_TNAME.

  PERFORM FRM_READ_TEXT TABLES LT_LINES_READ USING LS_THEAD-TDOBJECT LS_THEAD-TDID LS_THEAD-TDNAME.
  LOOP AT LT_LINES_READ INTO LS_LINES_READ.
    APPEND LS_LINES_READ-TDLINE TO LT_LINES_WRITE.
  ENDLOOP.

  "赋值长文本到屏幕
  LV_CONTROLNAME = 'TEXT'.
  PERFORM FRM_SET_TEXT_TO_SCREEN TABLES  LT_LINES_WRITE USING LV_CONTROLNAME 0.

ENDMODULE.

MODULE USER_COMMAND INPUT.

  CASE SY-UCOMM.
    WHEN '&F03' OR '&F15' OR '&F12'.
      LEAVE PROGRAM.
    WHEN 'SAVE'.
      PERFORM FRM_SAVE_TEXT.
    WHEN 'DELETE'.
      PERFORM FRM_DELETE_TEXT USING 'ZTEST_001' 'Z001' P_TNAME.
  ENDCASE.

ENDMODULE.

"读取长文本
FORM FRM_READ_TEXT TABLES LINES USING OBJECT ID NAME.

  CALL FUNCTION 'READ_TEXT'
    EXPORTING
      ID                      = ID          "文本对象ID
      LANGUAGE                = SY-LANGU
      NAME                    = NAME
      OBJECT                  = OBJECT
    TABLES
      LINES                   = LINES[]
    EXCEPTIONS
      ID                      = 1
      LANGUAGE                = 2
      NAME                    = 3
      NOT_FOUND               = 4
      OBJECT                  = 5
      REFERENCE_CHECK         = 6
      WRONG_ACCESS_TO_ARCHIVE = 7
      OTHERS                  = 8.

ENDFORM.

"将长文本赋值到屏幕 ,不创建容器, 将容器名写入即可
FORM FRM_EDITOR_SET TABLES LINES USING CONTROLNAME DISPLAY_MODE.

  CALL FUNCTION 'RH_EDITOR_SET'
    EXPORTING
      REPID          = SY-REPID
      DYNNR          = SY-DYNNR
      CONTROLNAME    = CONTROLNAME
      DISPLAY_MODE   = DISPLAY_MODE
    TABLES
      LINES          = LINES[]
    EXCEPTIONS
      CREATE_ERROR   = 1
      INTERNAL_ERROR = 2
      OTHERS         = 3.

ENDFORM.

"将长文本赋值到屏幕, 创建容器
FORM FRM_SET_TEXT_TO_SCREEN TABLES LINES USING CONTROLNAME DISPLAY_MODE.

  CREATE OBJECT LR_CONTAINER
    EXPORTING
      CONTAINER_NAME = CONTROLNAME.

  CREATE OBJECT LR_EDITOR
    EXPORTING
      PARENT                     = LR_CONTAINER
      WORDWRAP_MODE              = 1
      WORDWRAP_POSITION          = 72
      WORDWRAP_TO_LINEBREAK_MODE = DISPLAY_MODE.

  CALL METHOD LR_EDITOR->SET_TEXT_AS_R3TABLE
    EXPORTING
      TABLE           = LINES[]
    EXCEPTIONS
      ERROR_DP        = 1
      ERROR_DP_CREATE = 2.

ENDFORM.

"保存长文本
FORM FRM_SAVE_TEXT.
  CASE 'X'.
    WHEN P_2000.
      PERFORM FRM_SAVE_TEXT_1.
    WHEN P_3000.
      PERFORM FRM_SAVE_TEXT_2.
  ENDCASE.
ENDFORM.


"保存长文本
FORM FRM_SAVE_TEXT_1.

  DATA: LT_LINES_WRITE LIKE TABLE OF TLINE,
        LT_LINES_READ  TYPE TABLE OF CHAR256,
        LS_THEAD       LIKE THEAD,
        LV_CONTROLNAME TYPE SCRFNAME.

  "获屏幕长文本
  LV_CONTROLNAME = 'TEXT'.
  PERFORM FRM_GET_TEXT_FROM_SCREEN TABLES LT_LINES_READ LT_LINES_WRITE USING LV_CONTROLNAME.

  "写入长文本
  LS_THEAD-TDOBJECT    = 'ZTEST_001'.
  LS_THEAD-TDID        = 'Z001'.
  LS_THEAD-TDSPRAS     = SY-LANGU.
  LS_THEAD-TDNAME      = P_TNAME.
  PERFORM FRM_CREATE_TEXT TABLES LT_LINES_WRITE USING LS_THEAD.

ENDFORM.

"保存长文本
FORM FRM_SAVE_TEXT_2.

  DATA: LT_LINES_WRITE LIKE TABLE OF TLINE,
        LT_LINES_READ  TYPE TABLE OF CHAR256,
        LS_THEAD       LIKE THEAD,
        LS_LINES_WRITE LIKE TLINE,
        LV_CONTROLNAME TYPE SCRFNAME.

  "获屏幕长文本
  CALL METHOD LR_EDITOR->GET_TEXT_AS_R3TABLE
    IMPORTING
      TABLE                  = LT_LINES_READ
    EXCEPTIONS
      ERROR_DP               = 1
      ERROR_CNTL_CALL_METHOD = 2
      ERROR_DP_CREATE        = 3
      POTENTIAL_DATA_LOSS    = 4
      OTHERS                 = 5.

  LOOP AT LT_LINES_READ INTO DATA(LS_LINES_READ).
    CLEAR LS_LINES_WRITE.
    LS_LINES_WRITE-TDLINE = LS_LINES_READ.
    APPEND LS_LINES_WRITE TO LT_LINES_WRITE.
  ENDLOOP.

  "写入长文本
  LS_THEAD-TDOBJECT    = 'ZTEST_001'.
  LS_THEAD-TDID        = 'Z001'.
  LS_THEAD-TDSPRAS     = SY-LANGU.
  LS_THEAD-TDNAME      = P_TNAME.
  PERFORM FRM_CREATE_TEXT TABLES LT_LINES_WRITE USING LS_THEAD.
ENDFORM.

"获取长文本 , 不创建容器, 将容器名写入即可
FORM FRM_GET_TEXT_FROM_SCREEN TABLES LINES_READ LINES_WRITE USING CONTROLNAME.

  DATA: LT_LINES LIKE TABLE OF TLINE,
        LS_LINES LIKE LINE OF LT_LINES.

  CALL FUNCTION 'RH_EDITOR_GET'
    EXPORTING
      CONTROLNAME    = CONTROLNAME
    TABLES
      LINES          = LINES_READ[]
    EXCEPTIONS
      INTERNAL_ERROR = 1
      OTHERS         = 2.

  LOOP AT LINES_READ.
    CLEAR LS_LINES.
    LS_LINES-TDLINE = LINES_READ.
    APPEND LS_LINES TO LT_LINES.
  ENDLOOP.

  LINES_WRITE[] = LT_LINES.
ENDFORM.

"创建长文本
FORM FRM_CREATE_TEXT TABLES LINES USING HEADER STRUCTURE THEAD .

  DATA: LS_THEAD LIKE THEAD.

  LS_THEAD = HEADER.

  CALL FUNCTION 'SAVE_TEXT'
    EXPORTING
      HEADER = LS_THEAD
    TABLES
      LINES  = LINES.

ENDFORM.

"删除长文本
FORM FRM_DELETE_TEXT USING OBJECT ID NAME.
  DATA: LS_THEAD       LIKE THEAD.

  LS_THEAD-TDOBJECT    = OBJECT.
  LS_THEAD-TDID        = ID.
  LS_THEAD-TDSPRAS     = SY-LANGU.
  LS_THEAD-TDNAME      = NAME.

  CALL FUNCTION 'DELETE_TEXT'
    EXPORTING
      ID       = LS_THEAD-TDID
      LANGUAGE = LS_THEAD-TDSPRAS
      NAME     = LS_THEAD-TDNAME
      OBJECT   = LS_THEAD-TDOBJECT.

ENDFORM.
```





