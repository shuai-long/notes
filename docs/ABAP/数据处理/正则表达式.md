[TOC]

# 正则表达式 #

ABAP 支持正则表达式的语句:

- 关键字: `FIND` , `REPLACE`
- FUNCTION:  `COUNT` ,`COUNT_XXX` ,`CONTAINS`,`FIND`  ,`FIND_XXX`,`MATCH`,`MATCHES`,`REPLACE`,`SUBSTRING`,`SUBSTRING_XXX`
- 类:  `CL_ABAP_REGEX`,`CL_ABAP_MATCHER`

## 正则表达式语句规则 ##

### 单个字符 ###

1. 单个普通字符：A-B,0-9等单个字符，以及一些特殊字符通过反斜杠(\)转义变成普通字符；
2. 特殊字符：. , [,],-,\^这些字符作为特殊操作符，-,\^只有在[]中有特殊意义；

```abap
"1.Single Character Patterns 
  "示例：
  "regex:A  string:a  结果：不匹配
  "regex:AB string:A  结果：不匹配
  IF cl_abap_matcher=>matches( pattern = 'A' text = 'A' ) = abap_true.
    WRITE:/ '1.true'.
  ENDIF.

  ".,[,],-,^特殊操作字符
  ".可以替换任意单个字符；
  "\使用反斜杠将特殊字符变成普通字符；
  "\和一些字符一起表示一组字符(不能再[]中使用)：
  "1.\C:表示字母字符集；
  "2.\d:表示数字字符集；
  "3.\D:表示非数字字符集;
  "4.\l:表示小写字符集；
  "5.\L:表示非小写字符集；
  "6.\s:表示空白字符;
  "7.\S:表示非空白字符;
  "8.\u:表示大写字符集；
  "9.\U:表示非大写字符集;
  "10.\w:表示字母数字下划线字符集;
  "11.\W:表示非字母数字下划线字符集;

  "[]表示一个字符集,只需要匹配字符集中一个字符，表示匹配；
  "[^x]表示对该字符集取反，只需要不匹配字符集中任意字符，表示匹配；
  "[x-x]表示字符集范围，A-Z,a-z,0-1等；
  "ABAP定义的字符集
  "1.[:alnum:]字母数字集；
  "2.[:alpha:]字母集；
  "3.[:digit:]数字集;
  "4.[:blank:]空白字符，水平制表符;
  "5.[:cntrl:]所有控制字符集;
  "6.[:graph:]可显示字符集，除空白和水平制表符;
  "7.[:lower:]小写字符集;
  "8.[:print:]所有可显示字符的集合（[：graph:]和[：blank:]的并集）;
  "9.[:punct:]所有标点字符集;
  "10.[:space:]所有空白字符、制表符和回车符的集合;
  "11.[:unicode:]字符表示大于255的所有字符集（仅在Unicode系统中）;
  "12.[:upper:]所有大写字符集;
  "13.[:word:]包括下划线在内的所有字母数字字符集_;
  "14.[:xdigit:]所有十六进制数字的集合（“0”-“9”，“A”-“F”，和“A”-“F”）;

  "示例：
  "regex:\. string:.  结果：匹配
  "regex:\C string:A  结果:匹配
  "regex:.. string:AB 结果：匹配
  "regex:[ABC]  string:A  结果：匹配
  "regex:[AB][CD] string:AD 结果：匹配
  "regex:[^A-Z] string:1 结果：匹配
  "regex:[A-Z-] string:- 结果：匹配
  IF cl_abap_matcher=>matches( pattern = '[A-Z-]' text = 'A' ) = abap_true.
    WRITE:/ '2.true'.
  ENDIF.
```

### 多正则表达式连接匹配。 ###

​    特殊字符{,},*,+,?,(,),|,\

```abap
"2.Character string patterns 
  "示例：
  "regex:h[ae]llo  string:hello 结果：匹配；
  "regex:h[ae]llo  string:hallo 结果：匹配;
  IF cl_abap_matcher=>matches( pattern = '[A-Z-]' text = 'A' ) = abap_true.
    WRITE:/ '3.true'.
  ENDIF.

  "｛,｝,*,+,?,(,),|,\特殊字符
  "x{n}:表示修饰的字符出现n次；
  "x{n,m}:表示修饰字符出现n~m次；
  "x*:表示修饰字符出现{0,}次；
  "x+:表示修饰字符出现{1,}次；
  "x?:表示修饰字符出现{0,1}次;
  "a|b:表示匹配a或b字符;
  "()：表示分组匹配
  "(?:xxx):表示xxx出现一次
  "使用\1,\2代表分组从左到右
  "\Qxxx\E之间的特殊字符变成普通字符
  "示例:
  "regex:hi{2}o string:hiio 结果:匹配
  "regex:hi{1,3}o string:hiiio 结果:匹配
  "regex:hi?o  string:ho 结果：匹配
  "regex:hi*o    string:ho  结果：匹配
  "regex:hi+o    string:hio  结果：匹配
  "regex:.{0,4}  string:匹配0~4个字符
  "regex:a|bb|c   string:bb    结果：匹配
  "regex:h(a|b)o string:hao   结果：匹配
  "regex:(a|b)(?:ac)  string:bac  结果：匹配
  "regex:(").*\1  string："hi"  结果：匹配
  IF cl_abap_matcher=>matches( pattern = '(a|b)(?:ac)' text = 'bac' ) = abap_true.
    WRITE:/ '4.true'.
  ENDIF.
  IF cl_abap_matcher=>matches( pattern = '(").*\1' text = '"hi"' ) = abap_true.
    WRITE:/ '5.true'.
  ENDIF.

  DATA:TEXT type STRING.
  DATA:result_tab TYPE match_result_tab.
  DATA:wa_result_tab TYPE match_result.
  text = 'aaaaaabaaaaaaacaaaa'.
  FIND ALL OCCURRENCES OF REGEX '(a+)(a)' IN text RESULTS result_tab.
  WRITE:/ text.
  LOOP AT result_tab INTO wa_result_tab.
    WRITE:/ wa_result_tab-line,wa_result_tab-offset,wa_result_tab-length.
  ENDLOOP.
```

###  开始结尾字符匹配 ###

```abap
"3.Search Pattern
  "特殊字符：^,$,\,(,),=,!
  "示例1：Start and end of a line
  "^,$表示前置符号，结尾符号，每一行
  text = |Line1\nLine2\nLine3|.
  FIND ALL OCCURRENCES OF REGEX '^'
     IN text RESULTS result_tab.
  WRITE:/ text.
  LOOP AT result_tab INTO wa_result_tab.
    WRITE:/ wa_result_tab-line,wa_result_tab-offset,wa_result_tab-length.
  ENDLOOP.
  FIND ALL OCCURRENCES OF REGEX '$'
     IN text RESULTS result_tab.
  WRITE:/ text.
  LOOP AT result_tab INTO wa_result_tab.
    WRITE:/ wa_result_tab-line,wa_result_tab-offset,wa_result_tab-length.
  ENDLOOP.

  "示例2：Start and end of a character string
  "\A,\z作为前置符号，结尾符号，字符串开始结尾
  DATA:t_text(10) TYPE c.
  DATA:t_text_tab LIKE TABLE OF text.
  APPEND '     Smile' TO t_text_tab.
  APPEND '     Smile' TO t_text_tab.
  APPEND '     Smile' TO t_text_tab.
  APPEND '     Smile' TO t_text_tab.
  APPEND '     Smile' TO t_text_tab.
  APPEND '     Smile' TO t_text_tab.
  FIND ALL OCCURRENCES OF regex '\A(?:Smile)|(?:Smile)\z'
       IN TABLE t_text_tab RESULTS result_tab.
  WRITE:/ 'Smile匹配'.
  LOOP AT result_tab INTO wa_result_tab.
    WRITE:/ wa_result_tab-line,wa_result_tab-offset,wa_result_tab-length.
  ENDLOOP.

  "示例3
  "\z匹配最后行，\Z忽略换行匹配最后字符
  text = |... this is the end\n\n\n|.
  FIND REGEX 'end\z' IN text.
  IF sy-subrc <> 0.
    WRITE  / `There's no end.`.
  ENDIF.
  FIND  REGEX 'end\Z' IN text.
  IF sy-subrc = 0.
    WRITE / `The end is near the end.`.
  ENDIF.

  "示例4:Start and End of Word
  "\<,\>也表示匹配开头，结尾单词
  "\b表示开头结尾匹配
  "查找s开头
  text = `Sometimes snow seems so soft.`.
  FIND ALL OCCURRENCES OF regex '\<s'
       IN text IGNORING CASE
       RESULTS result_tab.
  WRITE:/ 's开头',text.
  LOOP AT result_tab INTO wa_result_tab.
    WRITE:/ wa_result_tab-line,wa_result_tab-offset,wa_result_tab-length.
  ENDLOOP.
  FIND ALL OCCURRENCES OF regex 's\b'
       IN text IGNORING CASE
       RESULTS result_tab.
  WRITE:/ 's开头或结尾',text.
  LOOP AT result_tab INTO wa_result_tab.
    WRITE:/ wa_result_tab-line,wa_result_tab-offset,wa_result_tab-length.
  ENDLOOP.

  "示例5：Preview Condition
  "预定义匹配内容不作为匹配结果内容
  "(?=x),相当于匹配x
  "(?!x),相当于不匹配x
  text = `Shalalala!`.
  FIND ALL OCCURRENCES OF REGEX '(?:la)(?=!)'
       IN text RESULTS result_tab.
  WRITE:/ text.
  "这里匹配到最后'la','!'不作为匹配到内容
  LOOP AT result_tab INTO wa_result_tab.
    WRITE:/ wa_result_tab-line,wa_result_tab-offset,wa_result_tab-length.
  ENDLOOP.

  "示例6：Cut operator
  DATA:s_text TYPE string.
  DATA:moff TYPE i.
  DATA:mlen TYPE i.
  s_text = `xxaabbaaaaxx`.

  FIND REGEX 'a+b+|[ab]+' IN text
    MATCH OFFSET moff
    MATCH LENGTH mlen.
  WRITE:/ s_text.
  IF sy-subrc = 0.
    WRITE:/ moff.
    WRITE:/ mlen.
    WRITE:/ text+moff(mlen).
  ENDIF.

  FIND REGEX '(?>a+b+|[ab]+)' IN text
    MATCH OFFSET moff
    MATCH LENGTH mlen.
  WRITE:/ s_text.
  IF sy-subrc = 0.
    WRITE:/ moff.
    WRITE:/ mlen.
    WRITE:/ text+moff(mlen).
  ENDIF.

  FIND REGEX '(?>a+|a)a' IN text
    MATCH OFFSET moff
    MATCH LENGTH mlen.
  WRITE:/ s_text.
  IF sy-subrc <> 0.
    WRITE:/ moff.
    WRITE:/ mlen.
    WRITE:/ 'Nothing found'.
  ENDIF.
```

### 替换字符REPLACE ###

```abap
"4.Replace Patterns
  "REPLACE关键词替换字符
  "特殊字符：$,&,`,`
  "示例1：Addressing the Full Occurrence
  text = `Yeah!`.
  REPLACE REGEX `\w+` IN text WITH `$0,$&`.
  WRITE:/ text.

  "示例2：Addressing the Registers of Subgroups
  "自身分组替换，返回`CBA'n'ABC`
  text = `ABC'n'CBA`.
  REPLACE REGEX `(\w+)(\W\w\W)(\w+)` IN text WITH `$3$2$1`.
  WRITE:/ text.

  "示例3：Addressing the Text Before the Occurrence
  text = `ABC and BCD`.
  REPLACE REGEX 'and' IN text WITH '$0 $`'.
  "ABC and ABC  BCD
  WRITE:/ text.
```

### 简化正则表达式 ###

```abap
"5.Simplified Regular Expressions
  "这个类CL_ABAP_REGEX，仅支持简化正则表达式
  "不支持+,|,(?=),(?!),(?:);
  "{} => \{\}
  "() => \(\)
  "示例1
  DATA:lo_regex TYPE REF TO cl_abap_regex.
  DATA:t_res   TYPE match_result_tab.
  DATA:wa_res  TYPE match_result.
  "不使用simplified Regular,+表示前面字符出现｛1，｝
  CREATE OBJECT lo_regex
    EXPORTING
      pattern      = 'a+'
      ignore_case  = abap_true "忽略大小写
      simple_regex = abap_false.
  FIND ALL OCCURRENCES OF REGEX lo_regex IN 'aaa+bbb' RESULTS t_res.
  LOOP AT t_res INTO wa_res.
     WRITE:/ wa_res-line,wa_res-offset,wa_res-length.
  ENDLOOP.
  "使用simplified Regular,+表示普通+
  CREATE OBJECT lo_regex
    EXPORTING
      pattern      = 'a+'
      simple_regex = abap_true.
  FIND ALL OCCURRENCES OF REGEX lo_regex IN 'aaa+bbb' RESULTS t_res.
  LOOP AT t_res INTO wa_res.
     WRITE:/ wa_res-line,wa_res-offset,wa_res-length.
  ENDLOOP.
```

### 正则表达式的特殊应用 ###

```abap
"6.Special Characters in Regular Expressions
  "正则表达式中特殊表达式
  "\ Escape character for special characters
  "反斜杠转义字符
  "$0, $& Placeholder for the whole found location
  "$1, $2, $3... Placeholder for the registration of subgroups
  "$` Placeholder for the text before the found location
  "$' Placeholder for the text after the found location
```

## 正则表达式使用 ##

### FIND,REPLACE关键词 ###

```abap
"使用FIND,REPLACE关键词
  "FIND
  "语法：FIND [{FIRST OCCURRENCE}|{ALL OCCURRENCES} OF] pattern
  "  IN [section_of] dobj
  "  [IN {CHARACTER|BYTE} MODE]
  "  [find_options].
  "pattern = {[SUBSTRING] substring} | {REGEX regex}
  "可以查找substring或匹配regex
  "section_of = SECTION [OFFSET off] [LENGTH len] OF
  "可以指定查找dobj字符串匹配范围，off匹配开始位置，len偏移长度
  "find_options = [{RESPECTING|IGNORING} CASE]
  "   [MATCH COUNT  mcnt]
  "   { {[MATCH OFFSET moff]
  "   [MATCH LENGTH mlen]}
  "  | [RESULTS result_tab|result_wa] }
  "  [SUBMATCHES s1 s2 ...]
  "mcnt:匹配次数，如果first occurrence,mcnt一直为1
  "moff:最后一次匹配偏移值，如果是first occurrence,则是第一次匹配值
  "mlen:最后一次匹配字符串长度,如果是first occurence,则是第一次匹配值
  "submatches:分组匹配字符串
  "示例1
  DATA:s1   TYPE string.
  DATA:s2   TYPE string.
  text = `Hey hey, my my, Rock and roll can never die`.
  FIND REGEX `(\w+)\W+\1\W+(\w+)\W+\2` IN text
       IGNORING CASE
       MATCH OFFSET moff
       MATCH LENGTH mlen
       SUBMATCHES s1 s2.
  WRITE:/ moff,mlen,s1,s2.

  "REPLACE
  "语法：
  "1. REPLACE [{FIRST OCCURRENCE}|{ALL OCCURRENCES} OF] pattern
  "  IN [section_of] dobj WITH new
  "  [IN {CHARACTER|BYTE} MODE]
  "  [replace_options].
  "replace_options = [{RESPECTING|IGNORING} CASE]
  "  [REPLACEMENT COUNT  rcnt]
  "  {{[REPLACEMENT OFFSET roff][REPLACEMENT LENGTH rlen]}
  " |[RESULTS result_tab|result_wa]}

  "2. REPLACE SECTION [OFFSET off] [LENGTH len] OF dobj WITH new
  " [IN {CHARACTER|BYTE} MODE].
  text = 'hello1 world!22'.
  REPLACE
    ALL OCCURRENCES OF
    REGEX '[0-9]'
    IN SECTION OFFSET 0 LENGTH 10 OF text
    WITH '!'.
  WRITE:/ text.
  "指定位置范围替换
  REPLACE SECTION OFFSET 10 LENGTH 5 OF text WITH '!'.
  WRITE:/ text.
```

### 使用function ###

```abap
"使用function
  "find
  "返回匹配字符位置
  "语法：
  "1.find( val = text  {sub = substring}|{regex = regex}[case = case][off = off] [len = len] [occ = occ] )
  "2.find_end( val = text regex = regex [case = case][off = off] [len = len] [occ = occ] )
  "3.find_any_of( val = text  sub = substring [off = off] [len = len] [occ = occ] )
  "4.find_any_not_of( val = text  sub = substring [off = off] [len = len] [occ = occ] )
  "occ表是返回第几次匹配值，如果为正从左到右匹配，如果为负从右到左匹配
  "示例
  DATA:mocc TYPE I VALUE 1.
  DATA:result TYPE I.
  text = 'hello world world'.
  result = find( val = text sub = 'wo' case = abap_true off = moff len = mlen occ = mocc ).
  WRITE:/ text,result,moff,mlen,mocc.

  "count
  "返回匹配次数
  "语法：
  "1.count( val = text  {sub = substring}|{regex = regex} [case = case][off = off] [len = len] )
  "2.count_any_of( val = text  sub = substring [off = off] [len = len] )
  "3.count_any_not_of( val = text  sub = substring [off = off] [len = len] )
  result = count( val = text sub = 'wo' case = abap_true off = moff len = mlen ).
  WRITE:/ text,result,moff,mlen.

  "match
  "返回匹配结果子串
  "语法：
  "match( val = text regex = regex [case = case] [occ = occ] )
  DATA:s_result TYPE string.
  s_result = match( val = text regex = 'wor' case = abap_true occ = 1 ).
  WRITE:/ s_result.

  "contains
  "返回字符串是否包含子串，boolean
  "1.contains( val = text  sub|start|end = substring [case = case][off = off] [len = len] [occ = occ] )
  "2.contains( val = text regex = regex [case = case][off = off] [len = len] [occ = occ] )
  "3.contains_any_of( val = text sub|start|end = substring [off = off] [len = len] [occ = occ] )
  "4.contains_any_not_of( val = text sub|start|end = substring [off = off] [len = len] [occ = occ] )
  "off:匹配开始位置
  "len:从开始偏移量
  "occ:指定匹配次数，如果匹配字符串没有出现大于等于指定次数，返回false
  "case:大小写敏感
  text = 'abcdef egg help'.
  IF contains( val = text sub = 'e' case = abap_true off = 0 len = 15 occ = 2 ).
    WRITE:/ 'contains:匹配成功'.
  ENDIF.

  "matches
  "返回字符串匹配结果，boolean
  "语法：matches( val = text regex = regex [case = case] [off = off] [len = len] ) ...
  "示例：
  text = '33340@334.com'.
  "匹配邮箱
  IF matches( val   = text
              regex = `\w+(\.\w+)*@(\w+\.)+((\l|\u){2,4})` ).
    MESSAGE 'Format OK' TYPE 'S'.
  ELSEIF matches(
           val   = text
           regex = `[[:alnum:],!#\$%&'\*\+/=\?\^_``\{\|}~-]+`     &
                  `(\.[[:alnum:],!#\$%&'\*\+/=\?\^_``\{\|}~-]+)*` &
                  `@[[:alnum:]-]+(\.[[:alnum:]-]+)*`              &
                  `\.([[:alpha:]]{2,})` ).
    MESSAGE 'Syntax OK but unusual' TYPE 'S' DISPLAY LIKE 'W'.
  ELSE.
    MESSAGE 'Wrong Format' TYPE 'S' DISPLAY LIKE 'E'.
  ENDIF.

  "replace
  "替换指定范围字符串，off,len指定
  "1.replace( val = text [off = off] [len = len] with = new )
  "替换匹配字符子串
  "如果off有值,len = 0,表示插入到off处；
  "如果len有值,off = 0,替换头部len长度字符串;
  "如果off等于字符串长度,len=0，表示将子串拼接到字符串后；
  "2.replace( val = text {sub = substring}|{regex = regex} with = new [case = case] [occ = occ] )
  "occ指定替换次数
  "示例：
  text = 'hello world! welcome china!'.
  text = replace( val = text off = 0 len = 5 with = 'hi' ).
  WRITE:/ 'replace:',text.
  "这里只替换第一次匹配的'!'
  text = replace( val = text sub = '!' with = '.' case = abap_true occ = 1 ).
  WRITE:/ 'replace:',text.

  "substring
  "返回子字符串
  "1.substring( val = text [off = off] [len = len] )
  "2.substring_from( val = text {sub = substring}|{regex = regex}[case = case] [occ = occ] [len = len]  )
  "3.substring_after( val = text {sub = substring}|{regex = regex}[case = case] [occ = occ] [len = len] )
  "4.substring_before( val = text {sub = substring}|{regex = regex}[case = case] [occ = occ] [len = len]  )
  "5.substring_to( val = text {sub = substring}|{regex = regex}[case = case] [occ = occ] [len = len]  )
  text = 'ABCDEFGHJKLMN'.
  text = substring( val = text off = 0 len = 10 ).
  WRITE:/ 'substring:',text.
  "返回ABCDE,返回匹配子字符串，len指定返回长度
  text = 'ABCDEFGHJKLMN'.
  text = substring_from( val = text sub = 'ABCDEF' case = abap_true occ = 1 len = 5 ).
  WRITE:/ 'substring:',text.
  "返回DEFGH,返回查找到字符串后面len长度部分
  text = 'ABCDEFGHJKLMN'.
  text = substring_after( val = text sub = 'ABC' case = abap_true occ = 1 len = 5 ).
  WRITE:/ 'substring:',text.
  "返回DEFGH,返回查找到字符串前面len长度部分
  text = 'ABCDEFGHJKLMN'.
  text = substring_before( val = text sub = 'JKL' case = abap_true occ = 1 len = 5 ).
  WRITE:/ 'substring:',text.
  "返回GHJKL,返回查找到字符串前面len长度部分(包含匹配字符串)
  text = 'ABCDEFGHJKLMN'.
  text = substring_to( val = text sub = 'JKL' case = abap_true occ = 1 len = 5 ).
  WRITE:/ 'substring:',text.
```

### 使用cl_abap_regex,cl_abap_matcher ###

```abap
"使用类
  "CL_ABAP_REGEX
  "CL_ABAP_MATCHER
  DATA:lo_matcher TYPE REF TO cl_abap_matcher.
  DATA:ls_match TYPE match_result.
  DATA:lv_match TYPE C LENGTH 1.
  "直接使用cl_abap_matcher类方法matches
  IF cl_abap_matcher=>matches( pattern = 'ABC.*' text = 'ABCDABCE' ) = abap_true.
    "返回静态实例
    lo_matcher = cl_abap_matcher=>get_object( ).
    "获取匹配结果
    ls_match = lo_matcher->get_match( ).
    "cl_abap_matcher的attribute
    "text:匹配的字符串
    "table:匹配的table
    "regex:匹配的正则表达式
    WRITE:/ 'cl_abap_matcher:',lo_matcher->text,ls_match-offset,ls_match-length.
  ENDIF.

  "创建matcher对象，然后匹配
  lo_matcher = cl_abap_matcher=>create( pattern = 'A.*'
    ignore_case = abap_true
    text        = 'ABC' ).
  "匹配结果,匹配‘X’,不匹配为空
  lv_match = lo_matcher->match( ).
  WRITE:/ 'cl_abap_matcher:',lv_match.

  "创建cl_abap_regex,正则表达式对象
  "通过regex对象创建matcher
  "DATA: lo_regex TYPE REF TO cl_abap_regex.
  CREATE OBJECT lo_regex EXPORTING pattern = '^add.*' ignore_case = abap_true.
  lo_matcher = lo_regex->create_matcher( text = 'addition' ).
  lv_match = lo_matcher->match( ).
  WRITE:/'cl_abap_matcher:',lv_match.


  "创建matcher对象，使用构造方法
  DATA:t_result_tab TYPE MATCH_RESULT_TAB.
  DATA:s_result_tab TYPE MATCH_RESULT.
  CREATE OBJECT lo_regex EXPORTING pattern = 'A'.
  CREATE OBJECT lo_matcher EXPORTING REGEX = lo_regex TEXT = 'ABCDABCD'.
  t_result_tab = lo_matcher->find_all( ).
  LOOP AT t_result_tab INTO s_result_tab.
    WRITE:/ 'find_all:',s_result_tab-offset,s_result_tab-length.
  ENDLOOP.
```

## 常用正则表达式 ##

```abap
*特殊表达式
"货币格式
'123123211312.333333'.replace(/(?=(?!^)(?:\d{3})+(?:.|))(\d3(\.\d+))(\d{3}(\.\d+)?)/g, ',$1') "输出 123,123,211,312.333333 ps：TCL 大牛推荐
"匹配中文字符的正则表达式：匹配中文还真是个头疼的事，有了这个表达式就好办了
[u4e00-u9fa5]  
"匹配双字节字符(包括汉字在内)：可以用来计算字符串的长度（一个双字节字符长度计2，ASCII字符计1） 
[X00-XFF]
"匹配空白行的正则表达式：可以用来删除空白行 
ns*r 
"匹配HTML标记的正则表达式：网上流传的版本太糟糕，上面这个也仅仅能匹配部分，对于复杂的嵌套标记依旧无能为力 
<(S*?)[^>]*>.*?|<.*? /> 
"匹配首尾空白字符的正则表达式：可以用来删除行首行尾的空白字符(包括空格、制表符、换页符等等)，非常有用的表达式 
^s*|s*$ 
"匹配Email地址的正则表达式：表单验证时很实用 
w+([-+.]w+)*@w+([-.]w+)*.w+([-.]w+)* 
"匹配网址URL的正则表达式：网上流传的版本功能很有限，上面这个基本可以满足需求 
[a-zA-z]+://[^s]*
"　匹配帐号是否合法(字母开头，允许5-16字节，允许字母数字下划线)：表单验证时很实用 
^[a-zA-Z][a-zA-Z0-9_]{4,15}$ 
"匹配国内电话号码：匹配形式如 0511-4405222 或 021-87888822 
d{3}-d{8}|d{4}-d{7} 
"匹配腾讯QQ号：腾讯QQ号从10000开始 
[1-9][0-9]{4,} 
"匹配中国邮政编码:中国邮政编码为6位数字 
[1-9]d{5}(?!d) 
"匹配身份证：中国的身份证为15位或18位 
d{15}|d{18} 
"匹配ip地址：提取ip地址时有用 
d+.d+.d+.d+ 




*匹配特定数字：处理大量数据时有用，具体应用时注意修正 
"匹配正整数 
^[1-9]d*$　 
"匹配负整数 
^-[1-9]d*$
"匹配整数 
^-?[1-9]d*$　
"匹配非负整数（正整数 + 0）
^[1-9]d*|0$　
"匹配非正整数（负整数 + 0）
^-[1-9]d*|0$　　  
 "匹配正浮点数 
^[1-9]d*.d*|0.d*[1-9]d*$　　
"匹配负浮点数 
^-([1-9]d*.d*|0.d*[1-9]d*)$　 
"匹配浮点数 
^-?([1-9]d*.d*|0.d*[1-9]d*|0?.0+|0)$　 
"匹配非负浮点数（正浮点数 + 0） 
^[1-9]d*.d*|0.d*[1-9]d*|0?.0+|0$　　 
"匹配非正浮点数（负浮点数 + 0） 
^(-([1-9]d*.d*|0.d*[1-9]d*))|0?.0+|0$　　


*匹配特定字符串：
"匹配由26个英文字母组成的字符串 
^[A-Za-z]+$　
"匹配由26个英文字母的大写组成的字符串 
^[A-Z]+$　　
"匹配由26个英文字母的小写组成的字符串 
^[a-z]+$　
"匹配由数字和26个英文字母组成的字符串 
^[A-Za-z0-9]+$　　
"匹配由数字、26个英文字母或者下划线组成的字符串 
^w+$　　


*在使用RegularExpressionValidator验证控件时的验证功能及其验证表达式介绍如下: 
"只能输入数字
^[0-9]*$ 
"只能输入n位的数字
^d{n}$
"只能输入至少n位数字
^d{n,}$” 
"只能输入m-n位的数字：
^d{m,n}$” 
"只能输入零和非零开头的数字：
^(0|[1-9][0-9]*)$” 
"只能输入有两位小数的正实数：
^[0-9]+(.[0-9]{2})?$
"只能输入有1-3位小数的正实数
^[0-9]+(.[0-9]{1,3})?$
"只能输入非零的正整数
^+?[1-9][0-9]*$
"只能输入非零的负整数
^-[1-9][0-9]*$
"只能输入长度为3的字符
^.{3}$
"只能输入由26个英文字母组成的字符串
^[A-Za-z]+$
"只能输入由26个大写英文字母组成的字符串
^[A-Z]+$
"只能输入由26个小写英文字母组成的字符串
^[a-z]+$
"只能输入由数字和26个英文字母组成的字符串
^[A-Za-z0-9]+$
"只能输入由数字、26个英文字母或者下划线组成的字符串
^w+$
"验证用户密码:正确格式为：以字母开头，长度在6-18之间， 
^[a-zA-Z]w{5,17}$
"只能包含字符、数字和下划线。 
"等字符：“[^%&'',;=?$x22]+”
^%&'',;=?$
"只能输入汉字：
^[u4e00-u9fa5],{0,}$
"验证Email地址：
^w+[-+.]w+)*@w+([-.]w+)*.w+([-.]w+)*$
"验证InternetURL：“^http://([w-]+.)+[w-]+(/[w-./?%&=]*)?$” 
"验证电话号码：正确格式为：“XXXX-XXXXXXX”，“XXXX-XXXXXXXX”，“XXX-XXXXXXX”， “XXX-XXXXXXXX”，“XXXXXXX”，“XXXXXXXX”。 
^((d{3,4})|d{3,4}-)?d{7,8}$
"验证身份证号（15位或18位数字）：
^d{15}|d{}18$ 
"验证一年的12个月：
^(0?[1-9]|1[0-2])$”正确格式为：“01”-“09”和“1”“12
"验证一个月的31天：
^((0?[1-9])|((1|2)[0-9])|30|31)$ 
"正确格式为：
01”“09”和“1”“31”。 
"匹配中文字符的正则表达式： 
[u4e00-u9fa5] 
"匹配双字节字符(包括汉字在内)：
[^x00-xff] 
"匹配空行的正则表达式：
n[s| ]*r 
"匹配HTML标记的正则表达式：
/<(.*)>.*|<(.*) />/ 
"匹配首尾空格的正则表达式：
(^s*)|(s*$) 
"匹配Email地址的正则表达式：
w+([-+.]w+)*@w+([-.]w+)*.w+([-.]w+)* 
"匹配网址URL的正则表达式：
http://([w-]+.)+[w-]+(/[w- ./?%&=]*)? 
```

### REPLACE也支持REGEX关键字。 ###

```ABAP
"最后：只能是ECC6或者更高版本才可以(ABAP supports POSIX regular expressions as of Release 7.00)
REPORT z_test.

DATA: str TYPE string ,
result_tab TYPE match_result_tab ,
wa LIKE LINE OF result_tab.

*找出STRING里面的双字节字符
str = 'abc我啊adfsf们'.
FIND ALL OCCURRENCES OF REGEX '[^x00-xff]*' IN str RESULTS result_tab.
LOOP AT result_tab INTO wa.
WRITE / str+wa-offset(wa-length).
ENDLOOP.

*找出STRING里面的单字节字符
str = 'abc我啊adfsf们'.
FIND ALL OCCURRENCES OF REGEX '[x00-xff]*' IN str RESULTS result_tab.
WRITE / '-----'.
LOOP AT result_tab INTO wa.
WRITE / str+wa-offset(wa-length).
ENDLOOP.

*找出STRING里面的IP地址
str = 'IP1:172.16.32.12 IP2:192.168.1.1 '.
FIND ALL OCCURRENCES OF REGEX 'd+.d+.d+.d+' IN str RESULTS result_tab.
WRITE / '-----'.
LOOP AT result_tab INTO wa.
WRITE / str+wa-offset(wa-length).
ENDLOOP.

*找出STRING里面的***-********格式的电话号码
str = 'IP1:172.16.32.12 021-12345678 '.
FIND ALL OCCURRENCES OF REGEX 'd{3}-d{8}|d{4}-d{7}' IN str RESULTS result_tab.
WRITE / '-----'.
LOOP AT result_tab INTO wa.
WRITE / str+wa-offset(wa-length).
ENDLOOP.

*找出STRING里面的15/18位身份证号码
str = 'IP1:172.16.32.12 3722198003041234 '.
FIND ALL OCCURRENCES OF REGEX 'd{15}|d{18}' IN str RESULTS result_tab.
WRITE / '-----'.
LOOP AT result_tab INTO wa.
WRITE / str+wa-offset(wa-length).
ENDLOOP.

***使用CLASS的例子：

REPORT z_barry_test.

PARAMETERS: p_input TYPE string DEFAULT 'IP1:172.16.32.12 IP2:192.168.1.1 ' OBLIGATORY.

DATA: regex TYPE REF TO cl_abap_regex,
matcher TYPE REF TO cl_abap_matcher,
match TYPE c .

DATA: result_tab TYPE match_result_tab ,
wa LIKE LINE OF result_tab.

CREATE OBJECT regex
EXPORTING
pattern = 'd+.d+.d+.d+'
ignore_case = 'X'.

TRY.
CALL METHOD regex->create_matcher
EXPORTING
text = p_input
\* table =
RECEIVING
matcher = matcher .
CATCH cx_sy_matcher .
ENDTRY.

TRY.
CALL METHOD matcher->match "是否完全匹配
RECEIVING
success = match.
CATCH cx_sy_matcher .
ENDTRY.

CALL METHOD matcher->find_all
RECEIVING
matches = result_tab.
LOOP AT result_tab INTO wa.
WRITE / p_input+wa-offset(wa-length).
ENDLOOP.

***SAP给的判断Email地址的例子***

PARAMETERS email TYPE c LENGTH 30 LOWER CASE DEFAULT 
```




　



