[TOC]

# 内表类型 #

ABAP里提供了三种内表，分别是： `STANDARD TABLE`(与TYPE TABLE OF 效果一样，都是标准表)、`SORTED TABLE`、  `HASHED TABLE` ，在不同的场合下，READ TABLE查找数据时三种内表会有不同的效率。具体到使用什么类型的内表：

- 对于一个小于100行的内表，且很少使用关键字操作，则使用标准表没有效率问题；
- 数据量比较巨大，切不存在重复行，只需使用关键字访问的内表应定义为哈希表；
- 排序表适用于运行期内必须以某种排序形式出现的内表。

内表可以通过 `WITH ［UNIQUE｜NON-UNIQUE］ KEY COMP1...COMPn`，`WITH ［UNIQUE｜NON-UNIQUE］ KEY TABLE_LINE`，`WITH ［UNIQUE｜NON-UNIQUE］ DEFAULT KEY` 定义表关键字。

- 在声明内表关键字时 `TABLE LINE` 已经废弃不用，而是使用 `TABLE_LINE` 代替。`TABLE_LINE` 表示使用内表行作为表关键字。因为 `TABLE_LINE` 极象一个行字段，所以内表基于的行结构体不应该包含 `TABLE_LINE` 字段。如果内表行由结构体类型组成，则最好不要使用 `TABLE_LINE` 作为表关键字。
- 在内表中如果不指定任何表关键字则使用 `DEFAULT KEY` 。扁平结构内表的默认表关键字是非数字和非内表的的组件字段。如果内表行是单个基本类型组成，由默认关键字为整个行。如果内表行中包含内表类型字段，则没有默认关键字。
- 标准表不能使用 `UNIQUE` 表关键字，且无需特别指定 `NON-UNIQUE` 表关键字;排序表可以使用 `NON-UNIQUE` 或者 `UNIQUE` 表关键字，哈希表不能使用 `NON-UNIQUE` 表关键字，且必须指定 `UNIQUE` 表关键字。
- 在定义内表时使用 `WITH HEADER LINE` 选项表示定义内表时也定义同名的工作内。如果在使用的过程中不能明确的分辩出是使用内表或是工作区，那么在名称之后加`[]`表示明确的使用内表。

## STANDARD TABLE ##

是标准表，也是最常用的内表，标准表的每一行对应一个逻辑索引 `SY-TABIX`，填充内表的时候，可以将数据追加到末尾，也可以插入到指定的位置，程序对内表行的寻址操作可通过关键字或索引进行。在对表进行插入删除等操作时，各数据行在内存中的位置不变，系统仅重新排列各数据行的索引值。特点如下：

1. 可以不指定`KEY`，若指定，则只能指定 `NON-UNIQUE KEY`
2. 可以使用`INDEX`和`KEY`来查询

> 注：NON-UNIQUE KEY代表内表里该字段可以重复，如果是UNIQUE KEY则代表该字段不能重复，不然程序会发生运行时错误。
>
> 例：`DATA IT_YM TYPE STANDARD TABLE OF VBAP WITH NON-UNIQUE KEY VBELN.`

## SORTED TABLE ##

是排序表，排序表也有逻辑索引，不同的是排序表总是按其表关键字升序排列后再进行存储，也就是在内存中的位置发生改变。特点如下：

1. 必须指定`KEY`（作为排序参考字段），可以指定`UNIQUE KEY`或者是`NON-UNIQUE KEY`
2. 可以使用`INDEX`和`KEY`来查询
3. 已经按照 `KEY` 排序，不可以再排序

在记录条数多时，`SORTED TABLE` 查询效率明显比 `STANDARD TABLE` 高，因为对已经排序的表可以进行二分法查找。可以对 `STANDARD TABLE` 用 `SORT` 语句先进行排序，然后再进行 `BINARY SEARCH`（即二分法查找）的查询，效率接近 `SORTED TABLE`。

> 注：排序本身也是消耗时间，如果只对 `STANDARD TABLE` 进行一次查询，则没有必要使用二分查找，另外在使用`BINARY SEARCH`时，数据必须完全依照 `BINARY SEARCH` 的 `KEY` 字段进行排序。
>
> 例：`DATA IT_YM TYPE SORTED TABLE OF VBAP WITH NON-UNIQUE KEY VBELN.` 

## HASHED TABLE ##

是哈希表，哈希表没有索引，只有关键字。特点如下：

1. 必须指定 `KEY`，并且是 `UNIQUE KEY`
2. 不可以使用 `INDEX` 查询
3. 查询时间和记录数无关

> 例：`DATA IT_YM TYPE HASHED TABLE OF VBAP WITH UNIQUE KEY VBELN.`

|   行访问方式   |    标准表    |      排序表      |     哈希表     |
| :------------: | :----------: | :--------------: | :------------: |
|    索引访问    |     允许     |       允许       |     不允许     |
|   关键字访问   |     允许     |       允许       |      允许      |
| 相同值关键字行 |    可重复    | 可重复或不可重复 |    不可重复    |
|  推荐访问方式  | 主要通过索引 |  主要通过关键字  | 只能通过关键字 |

## INDEX/ANY TABLE ##

除上面三种标准类型外，还有一般性类型，即索引表（**`INDEX TABLE`**）和任意表（**`ANY TABLE`**），一般性类型可以用于类型定义中，但不能用于声明一个内表对象，因为它并没有指明任何明确的表类型，因而系统无法确定对其操作方式。一般性类型还可以用于指明字段符号和接口参数的类型，其实际类型可能在运行期内才能够确定。

# 内表处理 #

## 判断标准是否存在某个数据 ##

```abap
DATA:lt_return TYPE TABLE OF bapiret2.
IF line_exists( lt_return[ type = 'E' ] ) OR
   line_exists( lt_return[ type = 'X' ] ) OR
   line_exists( lt_return[ type = 'A' ] ).
ENDIF.
```

## 构建 FIELDCAT ##

### 根据内表构建 FIELDCAT ###

```abap
*&---------------------------------------------------------------------*
*& Form frm_show_table
*&---------------------------------------------------------------------*
*& text 展示内表
*&---------------------------------------------------------------------*
*&      --> <FS_ALV>

FORM frm_show_table TABLES lt_table.
  DATA: lx_msg       TYPE REF TO cx_salv_msg,
        lo_columns   TYPE REF TO cl_salv_columns_table,
        lt_fcat      TYPE lvc_t_fcat,
        lo_functions TYPE REF TO cl_salv_functions_list,
        lo_display   TYPE REF TO cl_salv_display_settings.

  TRY.
      cl_salv_table=>factory(
      IMPORTING
      r_salv_table   = DATA(salv_table)
      CHANGING
        t_table        = lt_table  ).
    CATCH cx_salv_msg INTO lx_msg.

  ENDTRY.

  "获取展示设置
  lo_display = salv_table->get_display_settings( ).

  "设置自动优化列宽
  lo_columns = salv_table->get_columns( ).
  lo_columns->set_optimize( 'X' ).

  "根据内表获取字段目录
  lt_fcat = cl_salv_controller_metadata=>get_lvc_fieldcatalog(
      r_columns      = salv_table->get_columns( )
      r_aggregations = salv_table->get_aggregations( ) )." ALV Aggregations

  "设置按钮
  lo_functions = salv_table->get_functions( ).
  lo_functions->set_all( abap_true  ).

  "展示ALV
  salv_table->display( ).

ENDFORM.
```



### 根据结构或物理表构建 FIELDCAT ###

```abap
"当ALV类显示时，可以根据结构名而获得对应的fieldcatalog。
DATA: lt_lvc_t_fcat     TYPE lvc_t_fcat.
CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
  EXPORTING
    i_structure_name = '' "这里也可以是表名
  CHANGING
    ct_fieldcat      = lt_lvc_t_fcat.
    
"当做CALL FUNCTION显示时，可以根据结构名而获得对应的fieldcatalog。
DATA: lt_slis_t_fact      TYPE slis_t_fieldcat_alv.
CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
  EXPORTING
    i_structure_name   = ''
  CHANGING
    ct_fieldcat        = lt_slis_t_fact.
    
 "内表获取对应的fieldcat
 CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
  EXPORTING
    i_program_name         = sy-repid
    i_internal_tabname     = 'ITEMTAB'
    i_inclname             = sy-repid
    i_bypassing_buffer     = 'X'
    i_buffer_active        = ''
  CHANGING
    ct_fieldcat            = lt_slis_t_fact
  EXCEPTIONS
    inconsistent_interface = 1
    program_error          = 2
    OTHERS                 = 3.
```

> 扩展：此处也可以使用 `CL_DMC_UI_ALV_FCAT_GENERATOR` 类的方法来实现

## 创建内表 ##

### 根据 FIELDCAT 构建内表 ###

```abap
 DATA: lt_fdcat TYPE lvc_t_fcat,
 	   lt_data  TYPE REF TO data,
 	   ls_data  TYPE REF TO data.
 	   
 FIELD-SYMBOLS: <fs_data>       TYPE table,
                <fs_data_line>  TYPE any.	   
 	   
 cl_alv_table_create=>create_dynamic_table(
    EXPORTING
      it_fieldcatalog           = lt_fdcat    " Field Catalog
    IMPORTING
      ep_table                  = lt_data
    EXCEPTIONS
      generate_subpool_dir_full = 1
      OTHERS                    = 2
     ).

 ASSIGN lt_data->* TO <fs_data>.  
 CREATE DATA ls_data LIKE LINE OF <fs_data>.
 ASSIGN ls_data->* TO  <fs_data_line>.
```

## 获取内表字段目录 ##

```ABAP
"包含 name / type_kind / length / decimals
DATA: idetails         TYPE abap_compdescr_tab, 
      l_tabledescr_ref TYPE REF TO cl_abap_tabledescr,
      l_descr_ref      TYPE REF TO cl_abap_structdescr.

l_tabledescr_ref ?= cl_abap_typedescr=>describe_by_data( <lt_data> )."此处是内表
l_descr_ref ?= l_tabledescr_ref->get_table_line_type( ).
idetails[] = l_descr_ref->components[].   " 从结构描述信息中获取字段列表
```

# 新语法 #

* **循环插入:**

  ```abap
  "老语法:
  " LOOP AT input INTO DATA(row).
  "  INSERT row-text INTO TABLE result.
  " ENDLOOP.
  
  "新语法
  result = VALUE #( FOR row IN input ( row-text ) ).
  ```

* **Read TABLE**

  ```ABAP
  "老语法
  " READ TABLE value_pairs INTO DATA(line) WITH KEY name = 'A'.
  
  "新语法
  DATA(line) = value_pairs[ name = 'A' ].  
  ```

* **结构间赋值:**

  ```abap
  * 不同结构间互相赋值, CORRESPONDING #( )
  DATA: BEGIN OF WA_STRUCTURE1,
          COL1 TYPE CHAR20 VALUE'COL1',
          COL2 TYPE CHAR20 VALUE'COL2',
        END OF WA_STRUCTURE1.
  
  DATA: BEGIN OF WA_STRUCTURE2,
          COL2 TYPE CHAR20,
          COL3 TYPE CHAR20,
        END OF WA_STRUCTURE2.
        
   "从工作区1移动字段名相同的值到工作区2
  WA_STRUCTURE2 = CORRESPONDING #( WA_STRUCTURE1 ).  
  
  "展示结构
  CL_DEMO_OUTPUT=>DISPLAY( WA_STRUCTURE2 ).
  ```

* **不同结构的内表互相赋值**

  ```abap
  DATA: GT_TABLE1 LIKE TABLE OF WA_STRUCTURE1,
        GT_TABLE2 LIKE TABLE OF WA_STRUCTURE2.
        
  "GT_TABLE1插入两条数据
  GT_TABLE1 = VALUE #( ( COL1 = 'S1R1C1' COL2 = 'S1R1C2')
                       ( COL1 = 'S1R2C1' COL2 = 'S1R2C2') ).
  
  "GT_TABLE2插入的第二条数据会覆盖第一条
  GT_TABLE2 = VALUE #( ( COL2 = 'S2R1C1' COL3 = 'S2R1C2') ).  "
  GT_TABLE2 = VALUE #( ( COL2 = 'S2R2C1' COL3 = 'S2R2C2') ).
  
  "从GT_TABLE1移动字段名相同的列到GT_TABLE2，会覆盖GT_TABLE2的旧数据
  GT_TABLE2 = CORRESPONDING #( GT_TABLE1 ).    
  
  "展示内表
  CL_DEMO_OUTPUT=>DISPLAY( GT_TABLE2 ).
  ```

  



