[TOC]

# 字符串处理 #

* **大小写转换:**

  ```abap
  "转大写 ,等同于: TRANSLATE lowercase TO UPPER CASE.
  DATA(uppercase) = to_upper( lowercase ).  
  
  "转小写, 等同于: TRANSLATE uppercase TO LOWER CASE.
  DATA(lowercase) = to_lower( uppercase ).  " 
  ```

* **拆分字符串**

  ```abap
   SPLIT  STR AT ',' INTO TABLE IT_FIELDVALUE.
  ```

* **去除空格**

  ```abap
  "去除所有空格
  CONDENSE l_c NO-GAPS.
  "去除左端空格
  SHIFT lv_var LEFT DELETING LEADING ` `.
  "去除右端空格
  SHIFT lv_var RIGHT DELETING TRAILING ` `.
  ```

* **字符串替换**

  ```abap
  "正则表达式替换
  REPLACE ALL OCCURRENCES  OF  REGEX  '\D' IN <ffff5> WITH space.
  
  "指定字符替换
  REPLACE ALL OCCURRENCES OF 'ABC' IN lv_string WITH space.
  ```

* **字符串拼接**

  ```abap
  CONCATENATE <c1> ... <cn> INTO <c> [SEPARATED BY <s>].
  ```

  

# 小数处理 #

* **向上取整**

  Ceil ( ) 是 ABAP 的向上取整函数，使用时需要注意, 如果参与的都是整形,会发生隐式的 '四舍五入',必须保证中间结果是小数类型,代码如下:

  ```abap
  "写法一
  DATA: lv_upper_result TYPE i,
        lv_float_result TYPE p LENGTH 8 DECIMALS 3,
        lv_count        TYPE i VALUE '3001',
        lv_size         TYPE i VALUE '1000'.
  lv_float_result = lv_count / lv_size.
  lv_upper_result = ceil( lv_float_result ).
  
  "写法二
  DATA: lv_count TYPE i VALUE '3001',
        lv_size  TYPE i VALUE '1000'.
  DATA(lv_float_result) = CONV decfloat16( lv_count / lv_size ).
  DATA(lv_upper_result) = ceil( lv_float_result ).
  
  "写法三
  DATA: lv_count TYPE i VALUE '3001',
        lv_size  TYPE i VALUE '1000'.
  DATA(lv_upper_result) = ceil( CONV decfloat16( lv_count / lv_size ) ).
  ```

* **向下取整**

  floor( ) 是 ABAP 的向下取整函数, 使用时同样要考虑隐式的四舍五入的问题,代码如下:

  ```abap
  "写法一
  DATA: lv_lower_result TYPE i,
        lv_float_result TYPE p LENGTH 8 DECIMALS 3,
        lv_count        TYPE i VALUE '3001',
        lv_size         TYPE i VALUE '1000'.
  lv_float_result = lv_count / lv_size.
  lv_lower_result = floor( lv_float_result ).
  
  "写法二
  DATA: lv_count TYPE i VALUE '3001',
        lv_size  TYPE i VALUE '1000'.
  DATA(lv_float_result) = CONV decfloat16( lv_count / lv_size ).
  DATA(lv_lower_result) = floor( lv_float_result ).
  
  "写法三
  DATA: lv_count TYPE i VALUE '3001',
        lv_size  TYPE i VALUE '1000'.
  DATA(lv_lower_result) = floor( CONV decfloat16( lv_count / lv_size ) ).
  ```

* **保留小数**

  ABAP 提供了两个保留函数用来: 一个是 `ROUND`, 另一个是 `HR_NZ_ROUNDING_DECIMALS`, 前者可以控制向上取舍还是向下取舍,相对灵活一些.例如:

  > 输入 88.54350 ，如果 SIGN = ‘+’ ，保留 2 位小数，会得到: 88.55000；如果 SIGN = ‘-’，会得到：88.54000
  >
  > 输入 -88.54350，如果 SIGN = ‘+’ ，保留 2 位小数，会得到: -88.54000；如果 SIGN = ‘-’，会得到：-88.55000

  ```abap
  DATA : value_in  TYPE p DECIMALS 9 VALUE '12.5445' , 
         value_out TYPE p DECIMALS 9 . 
    
  DATA   dat2 TYPE p DECIMALS 9 VALUE '12.540'.
    
  * 方法一 
  CALL FUNCTION 'HR_NZ_ROUNDING_DECIMALS'
    EXPORTING
      value_in                 = value_in 
      conv_dec                 = 2      " 设置保留几位小数 
    IMPORTING
      value_out                = value_out
    EXCEPTIONS
      no_rounding_required     = 1 
      decimals_greater_than_10 = 2 
      rounding_error           = 3 
      OTHERS                   = 4. 
      
  * 方法二 
  CALL FUNCTION 'ROUND'
    EXPORTING
      decimals      = 2       " 保留多少位小数 
      input         = value_in 
      sign          = '+'     " + 向上取舍 - 向下取舍 （负数也一样） 
    IMPORTING
      output        = value_out    " 输出返回结果 
    EXCEPTIONS
      input_invalid = 1 
      overflow      = 2 
      type_invalid  = 3 
      OTHERS        = 4. 
  ```


# JSON 处理 #

* 内表转 JSON

  ```ABAP
  DATA(lv_json) = /ui2/cl_json=>serialize( data = lt_table compress = abap_false pretty_name = /ui2/cl_json=>pretty_mode-camel_case ).
  ```

* JSON 转内表

  ```abap
  /ui2/cl_json=>deserialize( EXPORTING json = req_json pretty_name = /ui2/cl_json=>pretty_mode-camel_case CHANGING data = ls_input ).
  ```

* 弹窗展示 JSON

  ```abap
  TRY .
  	 CALL TRANSFORMATION sjson2html SOURCE XML ls_alv-field RESULT XML DATA(lv_html).
  CATCH  cx_xslt_runtime_error INTO DATA(lo_err).
  ENDTRY.
  DATA(lv_convert) = cl_abap_codepage=>convert_from( lv_html ).
  IF lv_convert IS INITIAL or lv_convert eq '<html><p style="color:red">No JSON</p></html>'..
    lv_convert = ls_alv-field.
  ENDIF.
  cl_abap_browser=>show_html( html_string = lv_convert ).
  ```


# 解析算数表达式 #

* 解析算数表达式

  ```abap
  DATA: dataf TYPE f,
        datap TYPE lbkum.
  
  PARAMETERS: formula(40) DEFAULT 'A*B*C*D',
              a_val       TYPE lbkum DEFAULT 3,
              b_val       TYPE lbkum DEFAULT 1,
              c_val       TYPE lbkum DEFAULT 2,
              d_val       TYPE lbkum DEFAULT 4.
  
  START-OF-SELECTION.
  
    CALL FUNCTION 'CHECK_FORMULA'
      EXPORTING
        formula           = formula
        program           = sy-repid
        routine           = 'SUB_CHECK_FORMULA'
      EXCEPTIONS
        error_in_formula  = 1
        missing_parameter = 2
        OTHERS            = 3.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
      WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.
  
    CALL FUNCTION 'EVAL_FORMULA'
      EXPORTING
        formula                 = formula
        program                 = sy-repid
        routine                 = 'SUB_GET_VALUE'
      IMPORTING
        value                   = dataf
      EXCEPTIONS
        division_by_zero        = 1
        exp_error               = 2
        formula_table_not_valid = 3
        invalid_expression      = 4
        invalid_value           = 5
        log_error               = 6
        parameter_error         = 7
        sqrt_error              = 8
        units_not_valid         = 9
        missing_parameter       = 10
        OTHERS                  = 11.
  
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
      WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ELSE.
      MOVE dataf TO datap.
      WRITE:/ formula, datap.
    ENDIF.
  
  FORM sub_check_formula USING VALUE(name)
  CHANGING VALUE(subrc).
    subrc = 0.
  
  ENDFORM.
  
  
  FORM sub_get_value USING VALUE(name)
  CHANGING VALUE(value)
    VALUE(subrc).
    subrc = 0.
    value = 0.
    CASE name.
      WHEN 'A'.
        value = a_val.
      WHEN 'B'.
        value = b_val.
      WHEN 'C'.
        value = c_val.
      WHEN 'D'.
        value = d_val.
    ENDCASE.
  ENDFORM.       
  ```


# 拆分文件全路径 #

* **获取路径名 + 文件名**

  ```abap
  DATA: LV_FILE_AND_PATH TYPE CHAR200, "全路径名(注意不要用 string 类型, 会dump)
        LV_FILE          TYPE CHAR200,
        LV_PATH_NAME     TYPE CHAR200.
  
  CALL FUNCTION 'STPU1_EXTRACT_FILENAME'
    EXPORTING
      FILE_AND_PATH = LV_FILE_AND_PATH
    IMPORTING
      FILE          = LV_FILE
      PATHNAME      = LV_PATH_NAME.
  ```

  > **其他函数:**
  >
  > `**SO_SPLIT_FILE_AND_PATH**`



