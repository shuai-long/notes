[toc]

# 选择屏幕定义 #

## **定义框架** ##

```abap
"外边框
SELECTION-SCREEN BEGIN OF BLOCK s1 WITH FRAME TITLE TEXT-001. 
SELECTION-SCREEN END OF BLOCK s1.

"同行显示
SELECTION-SCREEN BEGIN OF LINE.  
  SELECTION-SCREEN COMMENT 1(10) TEXT-101. "此处是个描述
  PARAMETERS: p_text TYPE string.
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN SKIP 1. "中间相隔几行

SELECTION-SCREEN BEGIN OF LINE. "下拉框带描述
  SELECTION-SCREEN COMMENT 1(31) TEXT-l02 FOR FIELD p_list.
  PARAMETERS: p_list TYPE t001-bukrs AS LISTBOX VISIBLE LENGTH 10 USER-COMMAND uc1.
  PARAMETERS: p_butxt TYPE t001-butxt.
SELECTION-SCREEN END OF LINE.


```

## **复选框与单选框** ##

```abap
"单选框
PARAMETERS:	r_1 TYPE c RADIOBUTTON GROUP g1 USER-COMMAND uc1 DEFAULT 'X',
            r_2 TYPE c RADIOBUTTON GROUP g1.
"复选框
PARAMETERS: p_ck1 AS CHECKBOX DEFAULT 'X'.
```

## **下拉框** ##

```abap
PARAMETERS: p_mat LIKE mara-matkl AS LISTBOX VISIBLE LENGTH 10.

INITIALIZATION.
  PERFORM frm_set_values .

FORM frm_set_values .
  DATA lt_values TYPE vrm_values.

  SELECT DISTINCT matkl AS key FROM mara INTO TABLE lt_values.
  CALL FUNCTION 'VRM_SET_VALUES' "调用函数为下拉框赋值
    EXPORTING
      id     = 'P_MAT' "选择屏幕组件名称
      values = lt_values.
ENDFORM
```

## **按钮** ##

### **工具栏按钮** ###

```abap
*自定义按钮必须使用的表
TABLES sscrfields.

*自定义按钮,最多五个
SELECTION-SCREEN: FUNCTION KEY 1.

SELECTION-SCREEN BEGIN OF BLOCK s1 WITH FRAME TITLE TEXT-001.
  PARAMETERS: p_pernr LIKE p0001-pernr, "人员编号
              p_path  LIKE rlgrap-filename . "文件路径
SELECTION-SCREEN END OF BLOCK s1.

INITIALIZATION.
  PERFORM frm_init_sel_screen.

AT SELECTION-SCREEN.
  PERFORM frm_excue_button.

FORM frm_init_sel_screen .

*  第一种方式
  sscrfields-functxt_01 = VALUE smp_dyntxt(
  icon_id = icon_operation
  icon_text = '维护配置表'
  quickinfo = '维护配置表'
).


  "删除按钮 ONLI为执行按钮
  lt_exclude = VALUE #( ( 'ONLI' ) ).
  CALL FUNCTION 'RS_SET_SELSCREEN_STATUS'
    EXPORTING
      p_status  = sy-pfkey
    TABLES
      p_exclude = lt_exclude.

ENDFORM."给按钮赋值

FORM frm_excue_button .

  CASE sscrfields-ucomm.
    WHEN 'FC01'.

      sscrfields-ucomm = 'ONLI' .
      IF p_pernr IS INITIAL.
        SET CURSOR FIELD 'P_PERNR'.   "设置鼠标焦点到字段
        MESSAGE e055(00).
      ENDIF.

    WHEN 'FC02'.
      sscrfields-ucomm = 'ONLI' .
      IF p_path IS INITIAL.
        SET CURSOR FIELD 'P_PATH'.
        MESSAGE e055(00).
      ENDIF.

  ENDCASE.
ENDFORM.  "响应选择屏幕的按钮功能

```

### **PUSHBUTTON** ###

```ABAP
DATA ev_ukurs TYPE tcurr-ukurs.

SELECTION-SCREEN BEGIN OF BLOCK b1.
  "定义屏幕输入
  PARAMETERS p_kurst TYPE tcurr-kurst OBLIGATORY DEFAULT 'M' .
  PARAMETERS p_fcurr TYPE tcurr-fcurr OBLIGATORY DEFAULT 'JPY'.
  PARAMETERS p_tcurr TYPE tcurr-tcurr OBLIGATORY DEFAULT 'EUR'.
  PARAMETERS p_gdatu TYPE sy-datum OBLIGATORY DEFAULT sy-datum.

  "PUSHBUTTON   表示这是一个按钮
  "30(10)       为PUSHBUTTON按钮在屏幕生成的位置与长度。
  "TEXT-001     为文本元素设置文本
  "PB2          按钮上显示的内容,可以是图标,也可以是汉子
  "USER-COMMAND PB1表示给按钮设置上用户命令，命令名称为PB1
  SELECTION-SCREEN SKIP 1. "屏幕换行
  SELECTION-SCREEN PUSHBUTTON 30(10) TEXT-001 USER-COMMAND pb1. "查询按钮
  SELECTION-SCREEN PUSHBUTTON 40(10) pb2 USER-COMMAND pb2.      "发送按钮

  "COMMENT  表示这是一个备注文本
  "30(65)   为COMMENT在屏幕生成的位置与长度。
  "TXT1     为文本内容，这里不用上面那种TEXT-001是因为后面要修改他，而TEXT-001没法修改
  SELECTION-SCREEN SKIP 1. "屏幕换行
  SELECTION-SCREEN COMMENT 30(65) txt1 ."屏幕上输出文本

SELECTION-SCREEN END OF BLOCK b1.

INITIALIZATION.
  txt1 = 'UKURS='.
  WRITE icon_ws_plane AS ICON TO pb2."给按钮添加图标

AT SELECTION-SCREEN OUTPUT.


AT SELECTION-SCREEN.
  CASE sy-ucomm.
    WHEN 'PB1'.
      PERFORM frm_get_data.
      PERFORM frm_display_data.
    WHEN 'PB2'.
      PERFORM frm_get_data.
      PERFORM frm_send_data.
  ENDCASE.


FORM frm_get_data .
  p_fcurr = to_upper( val = p_fcurr ).
  p_tcurr = to_upper( val = p_tcurr ).

  "设置有效日期
  DATA lv_valid_date LIKE tcurr-gdatu.
*  CONVERT DATE SY-DATUM INTO INVERTED-DATE LV_VALID_DATE.
  lv_valid_date = p_gdatu.


  SELECT SINGLE
  ukurs
  FROM tcurr
  INTO ev_ukurs
  BYPASSING BUFFER
  WHERE
  kurst = p_kurst
  AND fcurr = p_fcurr
  AND tcurr = p_tcurr
  AND gdatu = ( SELECT
  MIN( a~gdatu )
  FROM tcurr AS a
  WHERE
  a~kurst = tcurr~kurst
  AND a~fcurr = tcurr~fcurr
  AND a~tcurr = tcurr~tcurr
  AND a~gdatu GE lv_valid_date )
  .
ENDFORM.

FORM frm_display_data .
  txt1 = CONV char20( ev_ukurs ).
  txt1 = 'UKURS=' && txt1.
ENDFORM.

FORM frm_send_data .
  txt1 = '发送成功'.
ENDFORM.		
```

## **附加属性** ##

```ABAP
NO-DISPLAY             	"不显示在画面上
NO-EXTENSION          	"不显示多行输入                         
NO INTERVALS          	"不显示 HIGH 值

LENGTH           		"定义长度，只适用于C,N,X,P    
DECIMALS         		"定义小数位数,只适用于数量型对象
VISIBLE LENGTH        	"设置显示长度  
MATCHCODE OBJECT 	    "设定使用的Search Help (SE11 中定义搜索帮助)   

LOWER CASE            	"区分大小写
OBLIGATORY       		"必输
MEMROY ID        		"SAP内存ID，可以将对象参数临时保存在内存中，通过<pid>存储及读取，长度不能超过3个字符
MODIF ID         		"通过screen-group1为屏幕元素分组 

DEFAULT               	"默认值，区间的单值                               
DEFAULT OPTION SIGN   	"设置初始的 OPTION 和 SIGN                     
DEFAULT TO            	"设置初始的 LOW 值和 HIGH 值                     
DEFAULT TO OPTION SIGN	"设置初始 LOW HIGH OPTION SIGN         
```

## **多TAB选择屏幕** ##

```abap
"定义子屏幕100
SELECTION-SCREEN BEGIN OF SCREEN 100 AS SUBSCREEN.
  PARAMETERS: mat1 LIKE mara-matnr.
  SELECTION-SCREEN SKIP 1.
  PARAMETERS chk1 AS CHECKBOX DEFAULT 'X'.
SELECTION-SCREEN END OF SCREEN 100.

*定义子屏幕200
SELECTION-SCREEN BEGIN OF SCREEN 200 AS SUBSCREEN.
  PARAMETERS : mat2 LIKE mara-matnr.
SELECTION-SCREEN END OF SCREEN 200.

*定义一个TAB空间，取名为MYTAB，空间高度为5，共分为两个页签，BUTTON1,BUTTON2.
*两个页签的功能代码分别为PUSH1，PUSH2
SELECTION-SCREEN: BEGIN OF TABBED BLOCK mytab FOR 5 LINES,
TAB (20) button1 USER-COMMAND push1,
TAB (20) button2 USER-COMMAND push2,
END OF BLOCK mytab.

*为TAB控件分配初始化值，引用TEXT element定义为本
INITIALIZATION.
  button1 = TEXT-010.
  button2 = TEXT-020.
  mytab-prog = sy-repid.
  mytab-dynnr = 100.

*为TAB控件页签单击事件，选择不同的页签显示不同的子屏幕
AT SELECTION-SCREEN.
  CASE sy-ucomm.
    WHEN 'PUSH1'.
      mytab-dynnr = 100.
      mytab-activetab = 'BUTTON1'.
    WHEN 'PUSH2'.
      mytab-dynnr = 200.
      mytab-activetab = 'BUTTON2'.
  ENDCASE.
```

## **动态屏幕** ##

- `NAME`：如果参数是select-options类型参数，则参数名以 LOW 与 HIGH 后缀来区分。
- `GROUP1`：选择屏幕元素通过MODIF ID选项设置GROUP1（对话屏幕通过属性设置），将屏幕元素分为一组，方便屏幕的元素的批量修改
- `REQUIRED`：控制文本框、下拉列表屏幕元素的必输性，使用此属性后会忽略OBLIGATORY选项。取值如下：
  - 0：不必输，框中前面也没有钩
  - 1：必输，框中前面有钩，系统会自动检验是否已输入，相当于OBLIGATORY选项
  - 2：不必输，但框中前面有钩，系统不会检查是否已输入，此时需要手动检验
- `INPUT`：控制屏幕元素（包括复选框、单选框、文本框）的可输性
  - 0: 不可输入
  - 1: 允许输入
- `ACTIVE`：控制屏幕元素的可见性
  - 0: 隐藏
  - 1: 可见

### **常规动态选择屏幕** ###

```abap
PARAMETERS:p_1 TYPE c RADIOBUTTON GROUP g1 USER-COMMAND uc  DEFAULT 'X',
           p_2 TYPE c RADIOBUTTON GROUP g1.

PARAMETERS p_cs TYPE c MODIF ID s1.

AT SELECTION-SCREEN OUTPUT.
  PERFORM frm_set_initial_value.

AT SELECTION-SCREEN.
  PERFORM frm_check_input.

FORM frm_set_initial_value.
  
  LOOP AT SCREEN."动态屏幕，对group1的单选按钮组进行逻辑处理动态显示
    CASE screen-group1.
      WHEN 'S1'.
        IF p_1 IS NOT INITIAL.
          screen-active = '0'.                 "隐藏
        ENDIF.
        IF p_2 IS NOT INITIAL.
          screen-active = '1'.                 "显示
        ENDIF.
    ENDCASE.
    MODIFY SCREEN.

    IF matches( val = screen-name regex = 'P_FILE' )."假必输
      screen-required = 2.
    ENDIF.
  ENDLOOP.
ENDFORM."动态屏幕

FORM frm_check_input.
  IF p_2 IS NOT INITIAL.
    CASE sscrfields-ucomm.
      WHEN 'ONLI'.
        IF p_cs IS INITIAL.
          SET CURSOR FIELD 'P_CS'."在选择屏幕上定位到该字段
          MESSAGE e055(00).
        ENDIF.
    ENDCASE.
  ENDIF.
ENDFORM."检查用户输入
```

### **用户可选动态屏幕** ###

```abap
DATA: where_clauses           TYPE rsds_twhere,
      expressions             TYPE rsds_texpr,
      field_ranges            TYPE rsds_trange,
      number_of_active_fields TYPE sy-tfill,
      tables_tab              TYPE STANDARD TABLE OF rsdstabs,
      tabfields_not_display   TYPE STANDARD TABLE OF rsdsfields,
      fields_tab              TYPE STANDARD TABLE OF rsdsfields,
      field_desc              TYPE STANDARD TABLE OF fldconvert,
      field_texts             TYPE STANDARD TABLE OF rsdstexts,
      events                  TYPE STANDARD TABLE OF rsdsevents,
      event_fields            TYPE STANDARD TABLE OF rsdsevflds,
      fields_not_selected     TYPE STANDARD TABLE OF rsdsfields,
      no_int_check            TYPE STANDARD TABLE OF rsdstabs,
      alv_qinfo               TYPE STANDARD TABLE OF lvc_s_qinf,
      selection_id            TYPE rsdynsel-selid.


DATA: dbtab TYPE string VALUE 'T001',
      dref  TYPE REF TO data.

FIELD-SYMBOLS: <table> TYPE STANDARD TABLE,
               <where> LIKE LINE OF where_clauses.

TRY.
    DATA(checked_dbtab) = cl_abap_dyn_prg=>check_table_name_str(
                    val = dbtab
                    packages = 'FBZCORE' ).

  CATCH cx_abap_not_a_table.
    MESSAGE 'Database table not found' TYPE 'I' DISPLAY LIKE 'E'.
    LEAVE PROGRAM.
  CATCH cx_abap_not_in_package.
    MESSAGE 'Only tables from the flight data model are allowed'
             TYPE 'I' DISPLAY LIKE 'E'.
    LEAVE PROGRAM.
ENDTRY.

tables_tab = VALUE #( ( prim_tab = checked_dbtab ) ).

fields_tab = VALUE #( ( tablename = 'T001' fieldname = 'BUKRS' ) ).

CALL FUNCTION 'FREE_SELECTIONS_INIT'
  EXPORTING
    kind                     = 'T'
*   FIELD_RANGES_INT         = " 选择屏幕初始值
  IMPORTING
    selection_id             = selection_id
  TABLES
    tables_tab               = tables_tab "如果kind为T，则定义选择屏幕字段来源表
    tabfields_not_display    = tabfields_not_display
    fields_tab               = fields_tab "如果kind为T，则为默认选择字段，为F则为可选选择字段清单
    field_desc               = field_desc
    field_texts              = field_texts
    events                   = events " 选择屏幕事件进行逻辑处理
    event_fields             = event_fields
    fields_not_selected      = fields_not_selected
    no_int_check             = no_int_check
    alv_qinfo                = alv_qinfo
  EXCEPTIONS
    fields_incomplete        = 1
    fields_no_join           = 2
    field_not_found          = 3
    no_tables                = 4
    table_not_found          = 5
    expression_not_supported = 6
    incorrect_expression     = 7
    illegal_kind             = 8
    area_not_found           = 9
    inconsistent_area        = 10
    kind_f_no_fields_left    = 11
    kind_f_no_fields         = 12
    too_many_fields          = 13
    dup_field                = 14
    field_no_type            = 15
    field_ill_type           = 16
    dup_event_field          = 17
    node_not_in_ldb          = 18
    area_no_field            = 19
    OTHERS                   = 20.
IF sy-subrc <> 0.
  MESSAGE 'Error in initialization' TYPE 'I' DISPLAY LIKE 'E'.
  LEAVE PROGRAM.
ENDIF.

CALL FUNCTION 'FREE_SELECTIONS_DIALOG'
  EXPORTING
    selection_id    = selection_id
    title           = 'Free Selection'
    status          = '1' "选择屏幕状态
    as_window       = ' ' " 是否弹窗
    tree_visible    = ' ' "选择树是否可见
  IMPORTING
    where_clauses   = where_clauses "选择屏幕返回的选择条件，自动处理的where条件表，可直接使用
  TABLES
    fields_tab      = fields_tab
  EXCEPTIONS
    internal_error  = 1
    no_action       = 2
    selid_not_found = 3
    illegal_status  = 4
    OTHERS          = 5.
IF sy-subrc <> 0.
  MESSAGE 'No free selection created' TYPE 'I'.
  LEAVE PROGRAM.
ENDIF.

START-OF-SELECTION.

  ASSIGN where_clauses[ tablename = checked_dbtab ] TO <where>.

  CREATE DATA dref TYPE TABLE OF (checked_dbtab).
  ASSIGN dref->* TO <table>.

  TRY.
      IF <where> IS ASSIGNED AND <where>-where_tab IS NOT INITIAL.
        SELECT *
          FROM (checked_dbtab)
          WHERE (<where>-where_tab)
          INTO TABLE @<table>.
      ELSE.
        SELECT *
          FROM (checked_dbtab)
          INTO TABLE @<table>.
      ENDIF.
    CATCH cx_sy_dynamic_osql_error.
      MESSAGE 'Error in dynamic Open SQL' TYPE 'I' DISPLAY LIKE 'E'.
      LEAVE PROGRAM.
  ENDTRY.

  TRY.
      cl_salv_table=>factory(
        IMPORTING r_salv_table = DATA(alv)
        CHANGING  t_table      = <table> ).
      alv->display( ).
    CATCH cx_salv_msg.
      MESSAGE 'Error in ALV display' TYPE 'I' DISPLAY LIKE 'E'.
  ENDTRY.
```

> 屏幕文本宏：
>
> ```abap
> DEFINE set_screen_text.
>   lv_screen_element_name = |%_{ &1 }_%_APP_%-TEXT|.
>   ASSIGN (lv_screen_element_name) TO <fs_screen_element>.
>   IF <fs_screen_element> IS ASSIGNED.
>     <fs_screen_element> = &2.
>   ENDIF.
>   UNASSIGN <fs_screen_element>.
> END-OF-DEFINITION.
> 
> FIELD-SYMBOLS: <fs_screen_element> TYPE any.
> DATA: lv_screen_element_name TYPE string.
> ```
>
> 

# **屏幕事件** #

1. **INITIALIZATION 事件**

   > 该事件在屏幕未显示之前执行，可对程序设置值及屏幕元素进行初始化设置

2. **AT SELECTION-SCREEN OUTPUT 事件**

   > 屏幕元素声明完成后执行，并会在程序执行之前优先检查该事件下的所有代码。通常用于对输入值校验数据，如库存是否充足，操作类型是否允许等。

3. **AT SELECTION-SCREEN ON … 事件**

   > 可在程序执行之前指定输入值的校验，与at SELECTION-SCREEN output事件类似；只是output事件检查屏幕输入值的校验，而on 事件则是检查指定的输入值。执行该事件时，其它输入域的输入状态会被锁定。
   >
   > >- AT SELECTION-SCREEN ON VALUE REQUEST FOR {para|selcrit-low|selcrit-high} (f4)
   > >
   > >   > 指定输入域f4搜索帮助的事件，用于实现屏幕输入域的自定义f4搜索帮助。
   > >
   > >- AT SELECTION-SCREEN ON HELP REQUEST FOR {para|selcrit-low|selcrit-high} (f1)
   > >
   > >   > 指定输入域f1帮助的事件，用于实现屏幕输入域的自定义f1帮助。
   > >
   > >- AT SELECTION-SCREEN ON {para|selcrit}
   > >
   > >   > 指定输入域输入值时触发，可以是parameters对象，也可以是select-options对象。
   > >
   > >- AT SELECTION-SCREEN ON BLOCK (block)
   > >
   > >   > 该事件应用于设定框架的屏幕中。
   > >
   > > - AT SELECTION-SCREEN ON RADIOBUTTON GROUP (radi)
   > > 
   > >   > 该事件应用于单选框按钮组中。
   > >- AT SELECTION-SCREEN ON END OF (selcrit)
   > >
   > >   > 该事件只应用于select-options对象，parameters对象不起作用。在输入域进行多行数据输入时触发。

4. **AT SELECTION-SCREEN 事件**

   > 与前面的 AT SELECTION-SCREEN OUTPUT 和at SELECTION-SCREEN ON 事件类似， 区别在于执行顺序优先级低于前两者。

5. **START-OF-SELECTION 事件**

   > 开始执行，该事件在执行程序时触发：

6. **END-OF-SELECTION 事件**

   > 该事件应用于所有数据处理完成后，即在start-of-selection执行完成后，但输出屏幕还未显示前。











