[TOC]

# 邮件发送 #

## 配置 ##

在有些项目上，某些功能执行后，例如接口触发失败，采购订单审批完成等；客户要求通过发送邮件给特定人员进行提示，因此需要配置邮件自动发送功能。其配置步骤为：

- ==RZ10==配置

  ```abap
  " 参数: is/SMTP/virt_host_0 值设为：*:25
  " 参数: icm/server_port_0   值设为：PROT=SMTP,PORT=25 
  ```

  ![RZ10参数设置](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916105936416.png)

- ==SICF==激活服务

  ![双击SAPconnect](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916110117141.png)

  ![文件参数编号设为0](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916110148485.png)

  ![处理器清单：CL_SMTP_EXT_SAPCONNECT](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916110414609.png)

  ![激活服务](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916110516294.png)

- ==SMICM==配置

  1. 转到 -> 服务

     ![SMICM 设置 步骤1](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916111239787.png)

  2. 查看SMTP协议的服务，如果该服务端口不是25000的话，可以在菜单中先删除再创建25000端口

     ![SMICM 设置 步骤2](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916112048066.png)

     ![SMICM 设置 步骤2-删除](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916112114619.png)

     ![SMICM 设置 步骤2-新建](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916112134643.png)

- ==SCOT==配置

  1. 设置缺省域, 缺省域为发送方地址的后缀,比如 `qq.com`

     ![SMTP 设置：步骤1](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916112214864.png)

     ![SMTP 设置：步骤1-1](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916112305903.png)

  2. 新增SMTP节点

     ![SMTP 设置：步骤2](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916112352972.png)

  填写节点名称与描述，填写主机与端口并设置发送方的邮箱地址与密码(邮件主机与邮件端口默认图中即可)：![SMTP 设置：步骤2-1](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916112425583.png)

  填写发送方邮箱和密码

   ![SMTP 设置：步骤2-2](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916112439292.png)

- 程序调用

  ```ABAP
  
  DATA output_options     TYPE ssfcompop .
  DATA job_output_info    TYPE ssfcrescl.
  DATA control_parameters TYPE ssfctrlop.
  
  DATA:lt_obj_cont TYPE TABLE OF solisti1,
       ls_obj_cont TYPE solisti1.
  DATA l_att_subject      TYPE so_obj_des.
  DATA lo_send_request    TYPE REF TO cl_bcs.
  DATA lo_document        TYPE REF TO cl_document_bcs.
  DATA lo_recipient       TYPE REF TO if_recipient_bcs.
  DATA lo_bcs_exception   TYPE REF TO cx_bcs.
  DATA lo_sender          TYPE REF TO if_sender_bcs.     " Sender address
  DATA lv_email           TYPE ad_smtpadr.
  DATA lv_all             TYPE os_boolean.
  
  DATA :l_file_size_char TYPE so_obj_len,
        lit_mailhex      TYPE solix_tab,
        l_file_size      TYPE i.
  
  DATA:lv_filename TYPE string.
  
  DATA:lv_addr TYPE ztcm_mail01-smtp_addr VALUE '895276803@qq.com'.
  
  DATA:g_text TYPE TABLE OF file_table.
  DATA:rc TYPE i.
  
  SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME TITLE TEXT-001.
    PARAMETERS p_fj AS CHECKBOX DEFAULT 'X'.
  SELECTION-SCREEN END OF BLOCK blk1.
  
  START-OF-SELECTION.
  
    IF p_fj <> ''.
      "选择附件路径
      CALL METHOD cl_gui_frontend_services=>file_open_dialog
        EXPORTING
          window_title            = '选择'
          default_extension       = 'XLS'
          default_filename        = '测试'
          file_filter             = 'Excel文件(*.XLS)|*.XLS|全部文件 (*.*)|*.*|'
  *       INITIAL_DIRECTORY       =
  *       MULTISELECTION          =
  *       WITH_ENCODING           =
        CHANGING
          file_table              = g_text
          rc                      = rc
  *       USER_ACTION             =
  *       FILE_ENCODING           =
        EXCEPTIONS
          file_open_dialog_failed = 1
          cntl_error              = 2
          error_no_gui            = 3
          not_supported_by_gui    = 4
          OTHERS                  = 5.
  
      IF rc = 0.
        MESSAGE '已取消' TYPE 'S' DISPLAY LIKE 'E'.
        LEAVE LIST-PROCESSING.
      ENDIF.
  
    ENDIF.
  
  
    PERFORM frm_send_mail.
  
  *&---------------------------------------------------------------------*
  *& Form FRM_SEND_MAIL
  *&---------------------------------------------------------------------*
  *& text
  *&---------------------------------------------------------------------*
  *& -->  p1        text
  *& <--  p2        text
  *&---------------------------------------------------------------------*
  FORM frm_send_mail .
  
    TRY.
  *------------- create persistent send request 创建持续的发送请求 ----------------------*
        lo_send_request = cl_bcs=>create_persistent( ).
  
  *-------------设置邮件主题与正文--------------*
        CLEAR:lt_obj_cont.
        l_att_subject = '邮件主题'.                      "设置邮件的主题（单行）
  
        ls_obj_cont-line = '邮件正文'.                   "邮件的正文部分（详细内容，多行）
        APPEND ls_obj_cont TO lt_obj_cont.
        ls_obj_cont-line = '测试发送'.                   "邮件的正文部分（详细内容，多行）
        APPEND ls_obj_cont TO lt_obj_cont.
        lo_document = cl_document_bcs=>create_document( i_type    = 'RAW'          "文档类别
                                                        i_text    = lt_obj_cont    "邮件正文
                                                        i_subject = l_att_subject ). "邮件标题
  
  *--------添加附件----------*
        LOOP AT g_text INTO DATA(s_text).
  
          CLEAR lv_filename.
          lv_filename = s_text-filename.
  
          CALL METHOD cl_gui_frontend_services=>gui_upload
            EXPORTING
              filename                = lv_filename
              filetype                = 'BIN'  "
  *           has_field_separator     = SPACE
  *           header_length           = 0
  *           read_by_line            = ''
  *           dat_mode                = SPACE
  *           codepage                = SPACE
  *           ignore_cerr             = ABAP_TRUE
  *           replacement             = '#'
  *           virus_scan_profile      =
            IMPORTING
              filelength              = l_file_size
  *           header                  =
            CHANGING
              data_tab                = lit_mailhex
  *           isscanperformed         = SPACE
            EXCEPTIONS
              file_open_error         = 1
              file_read_error         = 2
              no_batch                = 3
              gui_refuse_filetransfer = 4
              invalid_type            = 5
              no_authority            = 6
              unknown_error           = 7
              bad_data_format         = 8
              header_not_allowed      = 9
              separator_not_allowed   = 10
              header_too_long         = 11
              unknown_dp_error        = 12
              access_denied           = 13
              dp_out_of_memory        = 14
              disk_full               = 15
              dp_timeout              = 16
              not_supported_by_gui    = 17
              error_no_gui            = 18
              OTHERS                  = 19.
          IF sy-subrc <> 0.
  * Implement suitable error handling here
            RETURN.
          ENDIF.
          l_file_size_char = l_file_size.
  
  
  *     hard code 附件为excel
          l_att_subject = lv_filename.
  *     添加附件,可以添加多个附件
          CALL METHOD lo_document->add_attachment
            EXPORTING
              i_attachment_type    = 'BIN'
              i_attachment_subject = l_att_subject
              i_attachment_size    = l_file_size_char
              i_att_content_hex    = lit_mailhex.
  
        ENDLOOP.
  
  *-------add document object to send request 添加文档对象以发送请求--------*
        lo_send_request->set_document( lo_document ).
  
  *--------Sender addess 发送方的邮箱地址-----------*
        CALL METHOD lo_send_request->set_sender
          EXPORTING
            i_sender = cl_cam_address_bcs=>create_internet_address( 'SAP@BRUNP.COM.CN' ).
  
  *--------Set sender outbox设置发件人发件箱---------*
        lo_send_request->send_request->set_link_to_outbox( 'X' ).
  
  *----------接收人邮箱地址-----------*
        lo_recipient = cl_cam_address_bcs=>create_internet_address( lv_addr ).
  
  *----------add recipient object to send request添加收件人对象以发送请求---------*
        lo_send_request->add_recipient( lo_recipient ).
  
  *-----------设置立即发送-----------*
        lo_send_request->set_send_immediately( 'X' ).
  
  *---------- 开始发送邮件 ---------*
        lv_all = lo_send_request->send( i_with_error_screen = 'X' ).
  
      CATCH cx_bcs INTO lo_bcs_exception.
  *    es_err_info = '转换期间发生了错误 - 返回代码：' &&  lo_bcs_exception->error_type.
    ENDTRY.
  
    IF lv_all IS NOT INITIAL.
      COMMIT WORK AND WAIT.
      MESSAGE '邮件发送成功' TYPE 'I'.
    ENDIF.
  
  ENDFORM.
  ```

  

## 常用案例 ##

案例使用表1：邮箱发送表单打印结构

![表1](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916113206153.png)

案例使用表2：截货与投诉抄送邮箱表

![表2](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916113303412.png)



```ABAP
*&---------------------------------------------------------------------*
*& Report YQB_TEST01
*&---------------------------------------------------------------------*
*&    邮件发送程序集成DEMO
*&---------------------------------------------------------------------*
REPORT YQB_TEST01.

TABLES:EKKO, EKPO, MARA, EKET.

TYPES: BEGIN OF TY_DATA,
         WERKS   TYPE EKPO-WERKS,    "工厂
         LIFNR   TYPE EKKO-LIFNR,    "供应商
         EBELN   TYPE EKKO-EBELN,    "采购订单号
         EBELP   TYPE EKPO-EBELP,    "行项目编号
         MATNR   TYPE EKPO-MATNR,    "物料编码
         TXZ01   TYPE EKPO-TXZ01,    "物料描述

         SEL     TYPE C,
         ZGX     TYPE C,
         MESSAGE TYPE C LENGTH 200,
       END OF TY_DATA.

DATA: GT_DATA TYPE TABLE OF TY_DATA,
      GS_DATA   TYPE TY_DATA,
      GV_SELECT TYPE C LENGTH 1 VALUE 'X'.  "选择与反选

"ALV展示用的变量
DATA: GT_FIELDCAT TYPE LVC_T_FCAT, "存放字段目录的内表 FIELDCAT
      GS_LAYOUT   TYPE LVC_S_LAYO.        "布局结构 LAYOUT

*--------------------------------------------------------------------*
*** 邮件相关定义
DATA: GT_HTML TYPE STANDARD TABLE OF W3HTML,
      GS_HTML        TYPE W3HTML,
      GV_MAILTO      TYPE AD_SMTPADR,
      LV_TITLE       TYPE SO_OBJ_DES,          "邮件标题
      SEND_REQUEST   TYPE REF TO CL_BCS,
      DOCUMENT       TYPE REF TO CL_DOCUMENT_BCS,
      RECIPIENT      TYPE REF TO IF_RECIPIENT_BCS,
      SENT_TO_ALL    TYPE OS_BOOLEAN,
      BCS_EXCEPTION  TYPE REF TO CX_BCS,
      MAIN_TEXT      TYPE BCSY_TEXT,    "文本
      BINARY_CONTENT TYPE SOLIX_TAB,   "SAPoffice：二进制数据，长度 255
      SIZE           TYPE SO_OBJ_LEN,  "文档内容大小
      LV_STRING      TYPE STRING.


CONSTANTS:
  GC_TAB  TYPE C VALUE CL_BCS_CONVERT=>GC_TAB,  "CL_ABAP_CHAR_UTILITIES=>HORIZONTAL_TAB   下一格
  GC_CRLF TYPE C VALUE CL_BCS_CONVERT=>GC_CRLF. "CL_ABAP_CHAR_UTILITIES=>CR_LF            下一行

DEFINE SET_HTML. "定义排序宏
  CLEAR GS_HTML.
  GS_HTML-LINE = &1.
  APPEND GS_HTML TO GT_HTML.
END-OF-DEFINITION.

*--------------------------------------------------------------------*


*----------------------------------------------------------------------*
* 选择屏幕
*----------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK B1 WITH FRAME TITLE TEXT-001.

SELECT-OPTIONS: S_WERKS FOR EKPO-WERKS, "工厂
                S_EBELN FOR EKKO-EBELN,      "采购订单
                S_LIFNR FOR EKKO-LIFNR,      "供应商
                S_MATNR FOR EKPO-MATNR.      "物料编码

SELECTION-SCREEN END OF BLOCK B1.

START-OF-SELECTION.

  PERFORM FRM_GET_DATA.                                 "获取数据
  PERFORM FRM_BUILD_FIELD CHANGING GT_FIELDCAT .        "定义列标题信息
  PERFORM FRM_BUILD_LAYOUT CHANGING GS_LAYOUT.          "定义ALV格式属性

END-OF-SELECTION.
  PERFORM FRM_DISPLAY_DATA_ALV TABLES GT_DATA[]         "显示数据
                               CHANGING GT_FIELDCAT
                                        GS_LAYOUT.
*&---------------------------------------------------------------------*
*& Form FRM_GET_DATA
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_GET_DATA .

  SELECT EKKO~EBELN  EKKO~LIFNR
         EKPO~EBELP  EKPO~MATNR  EKPO~WERKS  EKPO~TXZ01
    INTO CORRESPONDING FIELDS OF TABLE GT_DATA FROM EKKO
    INNER JOIN EKPO ON EKKO~EBELN = EKPO~EBELN UP TO 20 ROWS
    WHERE EKPO~WERKS = '3100'
    AND   EKPO~WERKS IN S_WERKS
    AND   EKKO~EBELN IN S_EBELN
    AND   EKKO~LIFNR IN S_LIFNR
    AND   EKPO~MATNR IN S_MATNR.
     .
  SORT GT_DATA BY EBELN.

ENDFORM.

*&---------------------------------------------------------------------*
*& FORM FRM_BUILD_FIELD
*&---------------------------------------------------------------------*
*& 构建ALV FIELDCAT
*&---------------------------------------------------------------------*
*&      <-- GT_FIELDCAT
*&---------------------------------------------------------------------*
FORM FRM_BUILD_FIELD CHANGING VALUE(OT_FIELDCAT) TYPE LVC_T_FCAT.
  DATA: L_POS       TYPE I,
        LS_FIELDCAT TYPE LVC_S_FCAT.
  DEFINE ALV_APPEND_FIELD.
    CLEAR LS_FIELDCAT.
    LS_FIELDCAT-COL_POS  = L_POS.
    LS_FIELDCAT-FIELDNAME  = &1.
    LS_FIELDCAT-REPTEXT = &2.
    LS_FIELDCAT-SCRTEXT_L = &2.
    LS_FIELDCAT-SCRTEXT_M = &2.
    LS_FIELDCAT-SCRTEXT_S = &2.
    LS_FIELDCAT-KEY = &3.
    LS_FIELDCAT-REF_TABLE = &4.
    LS_FIELDCAT-REF_FIELD = &5.
    LS_FIELDCAT-HOTSPOT = &6.
    LS_FIELDCAT-EDIT = &7.
    LS_FIELDCAT-NO_ZERO = &8.

    APPEND LS_FIELDCAT TO OT_FIELDCAT.
    L_POS = L_POS + 1.
  END-OF-DEFINITION.

  REFRESH OT_FIELDCAT.
  L_POS = 1.

  ALV_APPEND_FIELD  'SEL'       '选择'              ''   ''          ''         ''   'X'  ''.        "选择
  ALV_APPEND_FIELD  'MESSAGE'   '信息'              ''   ''          'MESSAGE'  ''   ''  ''.        "

  ALV_APPEND_FIELD  'EBELN'     '采购订单号'        ''   'EKET'      'EBELN'    ''   ''   ''.        "采购订单号
  ALV_APPEND_FIELD  'LIFNR'     '供应商'            ''   'EKKO'      'LIFNR'    ''   ''   ''.        "供应商
  ALV_APPEND_FIELD  'WERKS'     '工厂'              ''   'EKPO'      'WERKS'    ''   ''   ''.        "工厂
  ALV_APPEND_FIELD  'MATNR'     '物料编号'          ''   'EKPO'      'MATNR'    ''   ''   ''.        "物料编号
  ALV_APPEND_FIELD  'TXZ01'     '物料名称'          ''   'EKPO'      'TXZ01'    ''   ''   ''.        "物料名称

  OT_FIELDCAT[ FIELDNAME = 'SEL' ]-CHECKBOX = 'X'.

ENDFORM.

*&---------------------------------------------------------------------*
*& FORM FRM_BUILD_LAYOUT
*&---------------------------------------------------------------------*
*& 构建ALV LAYOUT
*&---------------------------------------------------------------------*
*&      <-- GS_LAYOUT
*&---------------------------------------------------------------------*
FORM FRM_BUILD_LAYOUT CHANGING VALUE(OS_LAYOUT) TYPE LVC_S_LAYO.
  CLEAR OS_LAYOUT.
  OS_LAYOUT-ZEBRA = 'X'.       "斑马线
  OS_LAYOUT-CWIDTH_OPT = 'X'.  "自动调整列宽
  OS_LAYOUT-BOX_FNAME = 'ZGX'.       "选择列
*  OS_LAYOUT-STYLEFNAME = 'STYLE'.
ENDFORM.

*&---------------------------------------------------------------------*
*& FORM FRM_DISPLAY_DATA_ALV
*&---------------------------------------------------------------------*
*& 调用FUCNTION显示
*&---------------------------------------------------------------------*
*&      --> GT_FLIGHT
*&      --> GT_FIELDCAT
*&      --> GS_LAYOUT
*&---------------------------------------------------------------------*
FORM FRM_DISPLAY_DATA_ALV TABLES PT_OTAB TYPE STANDARD TABLE
                          CHANGING PT_FIELDCAT TYPE LVC_T_FCAT
                                   PS_LAYOUT   TYPE LVC_S_LAYO.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY_LVC'
    EXPORTING
      I_CALLBACK_PROGRAM       = SY-REPID
      I_CALLBACK_USER_COMMAND  = 'FRM_USER_COMMAND'
      I_CALLBACK_PF_STATUS_SET = 'FRM_STATUS_STANDARD'
      IS_LAYOUT_LVC            = PS_LAYOUT
      IT_FIELDCAT_LVC          = PT_FIELDCAT  "传入FIELDCAT
      I_SAVE                   = 'A'         "STANDARD AND USER-SPECIFIC SAVING
    TABLES
      T_OUTTAB                 = PT_OTAB[]    "传入输出内表
    EXCEPTIONS
      PROGRAM_ERROR            = 1
      OTHERS                   = 2.
  IF SY-SUBRC NE 0.
    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

ENDFORM.

*&---------------------------------------------------------------------*
*& FORM FRM_STATUS_STANDARD
*&---------------------------------------------------------------------*
*&  设置GUI状态(及用户界面)
*&---------------------------------------------------------------------*
FORM FRM_STATUS_STANDARD USING PT_EXTAB TYPE SLIS_T_EXTAB.

  SET PF-STATUS 'STANDARD_FULLSCREEN' EXCLUDING PT_EXTAB.

ENDFORM.

*&---------------------------------------------------------------------*
*&      FORM  FRM_USER_COMMAND
*&---------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
FORM FRM_USER_COMMAND USING PV_UCOMM LIKE SY-UCOMM
                            PS_SELFIELD TYPE SLIS_SELFIELD.
  "刷新"ALV输出表"数据
  DATA: LO_GRID TYPE REF TO CL_GUI_ALV_GRID.
  CALL FUNCTION 'GET_GLOBALS_FROM_SLVC_FULLSCR' "获取ALV对象的函数
    IMPORTING
      E_GRID = LO_GRID.
  CALL METHOD LO_GRID->CHECK_CHANGED_DATA.

  CASE PV_UCOMM.

    WHEN 'SELECT'.
      PERFORM FRM_SELECT_DATA CHANGING GV_SELECT..

    WHEN 'TEXT'.    "发送纯文本邮件
      PERFORM FRM_SEND_TEXT.

    WHEN 'EXCEL'.   "将内表数据转为EXCEL发送邮件
      PERFORM FRM_SEND_EXCEL.

    WHEN 'PDF'.
      DATA(LT_DATA) = GT_DATA[].
      DELETE LT_DATA WHERE SEL <> 'X'.
      SORT LT_DATA BY EBELN.
      DELETE ADJACENT DUPLICATES FROM LT_DATA COMPARING EBELN.

      LOOP AT LT_DATA ASSIGNING FIELD-SYMBOL(<FS_DATA>).
        PERFORM FRM_SEND_PDF USING <FS_DATA>.
      ENDLOOP.

    WHEN OTHERS.
  ENDCASE.

  "重新回写自适应宽度
  GS_LAYOUT-CWIDTH_OPT = 'X'.
  CALL METHOD LO_GRID->SET_FRONTEND_LAYOUT
    EXPORTING
      IS_LAYOUT = GS_LAYOUT.
  "稳定刷新ALV
  DATA:STBL TYPE LVC_S_STBL.
  STBL-ROW = 'X'." 基于行的稳定刷新
  STBL-COL = 'X'." 基于列稳定刷新
  CALL METHOD LO_GRID->REFRESH_TABLE_DISPLAY
    EXPORTING
      IS_STABLE = STBL.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form FRM_SELECT_DATA
*&---------------------------------------------------------------------*
*&      选择与反选
*&---------------------------------------------------------------------*
*&      <-- GV_SELECT
*&---------------------------------------------------------------------*
FORM FRM_SELECT_DATA CHANGING P_GV_SELECT.
  LOOP AT GT_DATA ASSIGNING FIELD-SYMBOL(<FS_DATA>) WHERE ZGX = 'X'.
    <FS_DATA>-SEL = GV_SELECT.
  ENDLOOP.
  IF GV_SELECT = 'X'.
    CLEAR: GV_SELECT.
  ELSE.
    GV_SELECT = 'X'.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*
*& Form FRM_SEND_TEXT
*&---------------------------------------------------------------------*
*&    发送纯文本邮件
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_SEND_TEXT .

  DATA: LV_MSG    TYPE C LENGTH 200.


***  邮件标题
  LV_TITLE = '发送纯文本邮件测试'.

***  邮件正文
  REFRESH GT_HTML.
  CLEAR: LV_STRING.
  LV_STRING = '<html><head><titel><h1 style="font-size:150%" align="center">发送纯文本邮件测试</h1></titel>'.
  SET_HTML     LV_STRING.

  CLEAR: LV_STRING.
  LV_STRING = '<h2 style="font-size:100%">邮件正文文本</h2>'.
  SET_HTML     LV_STRING.


***  发送邮件
  TRY.
***  创建持续的发送请求
      SEND_REQUEST = CL_BCS=>CREATE_PERSISTENT( ).

      DOCUMENT = CL_DOCUMENT_BCS=>CREATE_DOCUMENT(
          I_TYPE    = 'HTM'            " 文档类别代码
          I_TEXT    = GT_HTML          " 正文
          I_SUBJECT = LV_TITLE ).      " 邮件主题名

***  添加文档对象
      SEND_REQUEST->SET_DOCUMENT( DOCUMENT ).


***  添加收件人
      GV_MAILTO = '994837998@qq.com'.     "GV_MAILTO 接收邮件地址
      RECIPIENT = CL_CAM_ADDRESS_BCS=>CREATE_INTERNET_ADDRESS( GV_MAILTO ).
      CALL METHOD SEND_REQUEST->ADD_RECIPIENT
        EXPORTING
          I_RECIPIENT  = RECIPIENT
          I_EXPRESS    = 'X'
          I_COPY       = ' '
          I_BLIND_COPY = ' '
          I_NO_FORWARD = ' '.


***  弹窗选择抄送人
      DATA:BEGIN OF LS_ZTQM_002F,
             SEL,
             WERKS TYPE ZTQM_002F-WERKS,
             ZYXH  TYPE ZTQM_002F-ZYXH,     "电子邮件地址
             ZYXMS TYPE ZTQM_002F-ZYXMS,    "邮箱描述
           END OF LS_ZTQM_002F,
           LT_ZTQM_002F LIKE TABLE OF LS_ZTQM_002F.

      SELECT * INTO CORRESPONDING FIELDS OF TABLE @LT_ZTQM_002F FROM ZTQM_002F WHERE WERKS = '3100'.

      DATA:LT_FIELDCAT TYPE SLIS_T_FIELDCAT_ALV,
           LS_FIELDCAT TYPE LINE OF SLIS_T_FIELDCAT_ALV.
      IF LT_FIELDCAT[] IS INITIAL.
        CLEAR LS_FIELDCAT.
        LS_FIELDCAT-FIELDNAME  = 'SEL'.
        LS_FIELDCAT-SELTEXT_L = '选择'.
        LS_FIELDCAT-OUTPUTLEN = 2.
        APPEND LS_FIELDCAT TO LT_FIELDCAT[].
        CLEAR LS_FIELDCAT.
        LS_FIELDCAT-FIELDNAME  = 'ZYXH'.
        LS_FIELDCAT-SELTEXT_L = '邮箱'.
        LS_FIELDCAT-OUTPUTLEN = 18.
        APPEND LS_FIELDCAT TO LT_FIELDCAT[].
        CLEAR LS_FIELDCAT.
        LS_FIELDCAT-FIELDNAME  = 'ZYXMS'.
        LS_FIELDCAT-SELTEXT_L = '邮箱描述'.
        LS_FIELDCAT-OUTPUTLEN = 18.
        APPEND LS_FIELDCAT TO LT_FIELDCAT[].
      ENDIF.

      CALL FUNCTION 'REUSE_ALV_POPUP_TO_SELECT'
        EXPORTING
          I_TITLE              = '请选择邮箱'
          I_CHECKBOX_FIELDNAME = 'SEL'
          I_TABNAME            = 'LT_ZTQM_002F'
          IT_FIELDCAT          = LT_FIELDCAT[]
        TABLES
          T_OUTTAB             = LT_ZTQM_002F
        EXCEPTIONS
          PROGRAM_ERROR        = 1
          OTHERS               = 2.
      IF SY-SUBRC <> 0.
        CALL FUNCTION 'MESSAGE_TEXT_BUILD'
          EXPORTING
            MSGID               = SY-MSGID
            MSGNR               = SY-MSGNO
            MSGV1               = SY-MSGV1
            MSGV2               = SY-MSGV2
            MSGV3               = SY-MSGV3
            MSGV4               = SY-MSGV4
          IMPORTING
            MESSAGE_TEXT_OUTPUT = LV_MSG.
        MESSAGE LV_MSG TYPE 'E'.
      ENDIF.

***  设置抄送人
      LOOP AT LT_ZTQM_002F INTO LS_ZTQM_002F WHERE SEL = 'X'.

        TRANSLATE LS_ZTQM_002F-ZYXH TO LOWER CASE.    "转为小写
        RECIPIENT = CL_CAM_ADDRESS_BCS=>CREATE_INTERNET_ADDRESS( LS_ZTQM_002F-ZYXH ).
        CALL METHOD SEND_REQUEST->ADD_RECIPIENT
          EXPORTING
            I_RECIPIENT  = RECIPIENT
            I_EXPRESS    = 'X'
            I_COPY       = 'X'
            I_BLIND_COPY = ' '
            I_NO_FORWARD = ' '.

      ENDLOOP.


***  发送
      SENT_TO_ALL = SEND_REQUEST->SEND( I_WITH_ERROR_SCREEN = 'X' ).
      COMMIT WORK.
      WAIT UP TO 1 SECONDS.

***  调用发送队列程序
      SUBMIT RSCONN01 WITH MODE = 'INT'
                    WITH OUTPUT = ''
                    AND RETURN.
      IF SENT_TO_ALL IS INITIAL.
        MESSAGE I500(SBCOMS) WITH GV_MAILTO.    "没有将文档发送到 收件箱
      ELSE.
        MESSAGE S022(SO).   "文件发送
      ENDIF.


***  捕获异常
    CATCH CX_BCS INTO BCS_EXCEPTION.
      MESSAGE I865(SO) WITH BCS_EXCEPTION->ERROR_TYPE.
  ENDTRY.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_SEND_EXCEL
*&---------------------------------------------------------------------*
*&    发送EXCEL
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_SEND_EXCEL .

  DATA: LV_STRING TYPE STRING.

  LOOP AT GT_DATA INTO GS_DATA WHERE SEL = 'X'.

    AT NEW WERKS.
      CONCATENATE 'EXCEL标题'    GC_CRLF
                                 GC_CRLF
                   INTO LV_STRING.

* header line
      CONCATENATE LV_STRING
                  '工厂'             GC_TAB
                  '供应商'           GC_TAB
                  '采购订单号'       GC_TAB
                  '行项目编号'       GC_TAB
                  '物料编码'         GC_TAB
                  '物料描述'         GC_CRLF
                  INTO LV_STRING.
    ENDAT.

    CONCATENATE LV_STRING
                 GS_DATA-WERKS     GC_TAB
                 GS_DATA-LIFNR     GC_TAB
                 GS_DATA-EBELN     GC_TAB
                 GS_DATA-EBELP     GC_TAB
                 GS_DATA-MATNR     GC_TAB
                 GS_DATA-TXZ01     GC_CRLF
                INTO LV_STRING.

  ENDLOOP.

*--------------------------------------------------------------------*
***  发送邮件
  LV_TITLE =  '带EXCEL附件的邮件测试'.
  APPEND |带有EXCEL发送的邮件| TO MAIN_TEXT.    "正文

  TRY.
      CL_BCS_CONVERT=>STRING_TO_SOLIX(
        EXPORTING
          IV_STRING   = LV_STRING
          IV_CODEPAGE = '4103'  "suitable for MS Excel, leave empty
          IV_ADD_BOM  = 'X'     "for other doc types
        IMPORTING
          ET_SOLIX  = BINARY_CONTENT
          EV_SIZE   = SIZE ).
    CATCH CX_BCS.
      MESSAGE E445(SO).   "传输文档内容时出错
  ENDTRY.


  TRY.
***  创建持续的发送请求
      SEND_REQUEST = CL_BCS=>CREATE_PERSISTENT( ).

***  设置邮件主题与正文
      DOCUMENT = CL_DOCUMENT_BCS=>CREATE_DOCUMENT(
        I_TYPE    = 'RAW'            " 文档类别代码
        I_TEXT    = MAIN_TEXT        " 正文
        I_SUBJECT = LV_TITLE ).      " 邮件主题名

***  将电子文档作为附件
      DATA LV_NAME TYPE CHAR50.
      LV_NAME =  |Email_name { SY-DATUM }|.   "附件名称
      DOCUMENT->ADD_ATTACHMENT(
        I_ATTACHMENT_TYPE    = 'xls'                        " 附件格式(只能是3位)
        I_ATTACHMENT_SUBJECT = LV_NAME                      " 附件名称
        I_ATTACHMENT_SIZE    = SIZE                         " 附件大小
        I_ATT_CONTENT_HEX    = BINARY_CONTENT ).            " 附件内容

***  添加文档对象
      SEND_REQUEST->SET_DOCUMENT( DOCUMENT ).

***  设置收件人
      GV_MAILTO = '994837998@qq.com'.     "收件人邮箱地址
      RECIPIENT = CL_CAM_ADDRESS_BCS=>CREATE_INTERNET_ADDRESS( GV_MAILTO ).
      CALL METHOD SEND_REQUEST->ADD_RECIPIENT
        EXPORTING
          I_RECIPIENT  = RECIPIENT
          I_EXPRESS    = 'X'
          I_COPY       = ' '
          I_BLIND_COPY = ' '
          I_NO_FORWARD = ' '.

***  发送
      SENT_TO_ALL = SEND_REQUEST->SEND( I_WITH_ERROR_SCREEN = 'X' ).
      COMMIT WORK.
      WAIT UP TO 1 SECONDS.

***  调用发送队列程序
      SUBMIT RSCONN01 WITH MODE = 'INT'
                    WITH OUTPUT = ''
                    AND RETURN.

      IF SENT_TO_ALL IS INITIAL.
        MESSAGE I500(SBCOMS) WITH GV_MAILTO.    "没有将文档发送到 收件人
      ELSE.
        MESSAGE S022(SO).   "文件发送
      ENDIF.
    CATCH CX_BCS INTO BCS_EXCEPTION.
      MESSAGE I865(SO) WITH BCS_EXCEPTION->ERROR_TYPE.    "转换期间发生了错误 - 返回代码：<&>
  ENDTRY.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_SEND_PDF
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> LS_DATA
*&---------------------------------------------------------------------*
FORM FRM_SEND_PDF USING PS_DATA TYPE TY_DATA.

  DATA: LV_FNAME TYPE RS38L_FNAM."表格内部名称
  DATA: LT_SPOOLIDS TYPE TSFSPOOLID,
        LS_SPOOL    TYPE RSPOID.
  DATA: PDF_SIZE    TYPE SO_OBJ_LEN,
        PDF_CONTENT TYPE SOLIX_TAB.


  PERFORM FRM_CALL_SMARTFORMS USING 'YSSF_TEST01' CHANGING LV_FNAME."获取表格内部名称
  PERFORM FRM_PRINT_BG_EDIT TABLES LT_SPOOLIDS USING LV_FNAME PS_DATA. "调用打印 获取假脱机请求号

  IF LT_SPOOLIDS[] IS INITIAL.  "判断是否成功获取假脱机请求
    RETURN.
  ENDIF.

  LS_SPOOL = LT_SPOOLIDS[ 1 ].

  PERFORM FRM_CREATE_PDF USING LS_SPOOL CHANGING PDF_SIZE PDF_CONTENT.   "用假脱机请求 获取 PDF大小和PDF二进制数据
  PERFORM FRM_SEND_MAIL_PDF USING PS_DATA PDF_SIZE PDF_CONTENT .

ENDFORM.

*&---------------------------------------------------------------------*
*& Form FRM_CALL_SMARTFORMS
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      <-- LV_FNAME
*&---------------------------------------------------------------------*
FORM FRM_CALL_SMARTFORMS USING P_TNAME "表格名称
                          CHANGING C_FNAME. "表格内部名称
  DATA : LV_MESS(220) TYPE C .
  CALL FUNCTION 'SSF_FUNCTION_MODULE_NAME'
    EXPORTING
      FORMNAME           = P_TNAME
    IMPORTING
      FM_NAME            = C_FNAME
    EXCEPTIONS
      NO_FORM            = 1
      NO_FUNCTION_MODULE = 2
      OTHERS             = 3.
  IF SY-SUBRC <> 0.
    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4
            INTO LV_MESS .
  ENDIF.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_PRINT_BG_EDIT
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> LT_SPOOLIDS
*&      --> LV_FNAME
*&      --> PS_DATA
*&---------------------------------------------------------------------*
FORM FRM_PRINT_BG_EDIT TABLES PT_SPOOLIDS TYPE TSFSPOOLID
                        USING    PV_FNAME
                                 PS_DATA.
  DATA : LS_SSFCTRLOP      TYPE  SSFCTRLOP,                         "控制结构
         LS_OUTPUT_OPTIONS TYPE  SSFCOMPOP,
         LS_SSFCRESCL      TYPE  SSFCRESCL,
         LS_DATA           TYPE YSQB_TEST01.

*-SMARTFORMS设置
  LS_OUTPUT_OPTIONS-TDDEST = 'LP01' .
  LS_OUTPUT_OPTIONS-TDIMMED   = ''.
  LS_OUTPUT_OPTIONS-TDDELETE  = ''.
*  ls_output_options-tdnoprev = 'X'.
  LS_OUTPUT_OPTIONS-TDNEWID   = 'X'.
  LS_OUTPUT_OPTIONS-TDIEXIT = 'X'.
  LS_SSFCTRLOP-NO_DIALOG = 'X'.

  MOVE-CORRESPONDING PS_DATA TO LS_DATA.

*-调用打印
  CALL FUNCTION PV_FNAME
    EXPORTING
      CONTROL_PARAMETERS = LS_SSFCTRLOP
      OUTPUT_OPTIONS     = LS_OUTPUT_OPTIONS
      USER_SETTINGS      = ''
      GS_DATA            = LS_DATA
    IMPORTING
      JOB_OUTPUT_INFO    = LS_SSFCRESCL
    EXCEPTIONS
      FORMATTING_ERROR   = 1
      INTERNAL_ERROR     = 2
      SEND_ERROR         = 3
      USER_CANCELED      = 4
      OTHERS             = 5.
  IF SY-SUBRC <> 0.
    MESSAGE ID SY-MSGID TYPE 'S' NUMBER SY-MSGNO DISPLAY LIKE 'E'
              WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    LEAVE LIST-PROCESSING.
  ENDIF.

  PT_SPOOLIDS[] = LS_SSFCRESCL-SPOOLIDS[].    "假脱机请求号

ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_CREATE_PDF
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_CREATE_PDF USING PS_SPOOL CHANGING PV_SIZE PV_CONTENT.

  DATA: RQ          TYPE TSP01,
        DUMMY       TYPE TABLE OF RSPOATTR,
        BIN_SIZE    TYPE I,
        PDF_XSTRING TYPE XSTRING,
        PDF_CONTENT TYPE SOLIX_TAB.

*&---------------------------------------------------------------------*
*& Create PDF
*&---------------------------------------------------------------------*
* 创建PDF内容
* 1) 获取假脱机请求的属性
* 2) 根据文档类型将假脱机请求转换为 PDF
*----------------------------------------------------------------------*

***  获取假脱机请求的属性
  CALL FUNCTION 'RSPO_GET_ATTRIBUTES_SPOOLJOB'
    EXPORTING
      RQIDENT     = PS_SPOOL
    IMPORTING
      RQ          = RQ
    TABLES
      ATTRIBUTES  = DUMMY
    EXCEPTIONS
      NO_SUCH_JOB = 1
      OTHERS      = 2.
  IF SY-SUBRC <> 0.
    RETURN.
  ENDIF.

***  根据需要的 文档类型，将假脱机请求转换为 PDF .
  IF RQ-RQDOCTYPE = 'OTF' OR RQ-RQDOCTYPE = 'SMART'.
    CALL FUNCTION 'CONVERT_OTFSPOOLJOB_2_PDF'
      EXPORTING
        SRC_SPOOLID              = PS_SPOOL
        NO_DIALOG                = 'X'
        PDF_DESTINATION          = 'X'
        NO_BACKGROUND            = 'X'
      IMPORTING
        PDF_BYTECOUNT            = BIN_SIZE
        BIN_FILE                 = PDF_XSTRING
      EXCEPTIONS
        ERR_NO_OTF_SPOOLJOB      = 1
        ERR_NO_SPOOLJOB          = 2
        ERR_NO_PERMISSION        = 3
        ERR_CONV_NOT_POSSIBLE    = 4
        ERR_BAD_DSTDEVICE        = 5
        USER_CANCELLED           = 6
        ERR_SPOOLERROR           = 7
        ERR_TEMSEERROR           = 8
        ERR_BTCJOB_OPEN_FAILED   = 9
        ERR_BTCJOB_SUBMIT_FAILED = 10
        ERR_BTCJOB_CLOSE_FAILED  = 11
        OTHERS                   = 12.
    IF SY-SUBRC <> 0.
      RETURN.
    ENDIF.
  ELSEIF RQ-RQDOCTYPE = 'LIST'.
    CALL FUNCTION 'CONVERT_ABAPSPOOLJOB_2_PDF'
      EXPORTING
        SRC_SPOOLID              = PS_SPOOL
        NO_DIALOG                = 'X'
        PDF_DESTINATION          = 'X'
        NO_BACKGROUND            = 'X'
      IMPORTING
        PDF_BYTECOUNT            = BIN_SIZE
        BIN_FILE                 = PDF_XSTRING
      EXCEPTIONS
        ERR_NO_ABAP_SPOOLJOB     = 1
        ERR_NO_SPOOLJOB          = 2
        ERR_NO_PERMISSION        = 3
        ERR_CONV_NOT_POSSIBLE    = 4
        ERR_BAD_DESTDEVICE       = 5
        USER_CANCELLED           = 6
        ERR_SPOOLERROR           = 7
        ERR_TEMSEERROR           = 8
        ERR_BTCJOB_OPEN_FAILED   = 9
        ERR_BTCJOB_SUBMIT_FAILED = 10
        ERR_BTCJOB_CLOSE_FAILED  = 11
        OTHERS                   = 12.
    IF SY-SUBRC <> 0.
      RETURN.
    ENDIF.
  ELSE.
    RETURN.
  ENDIF.

  TRY.
      PV_CONTENT = CL_DOCUMENT_BCS=>XSTRING_TO_SOLIX( PDF_XSTRING ).
    CATCH CX_BCS INTO BCS_EXCEPTION.
      DATA(LV_ERR) = BCS_EXCEPTION->GET_TEXT( ).
      MESSAGE LV_ERR TYPE 'S' DISPLAY LIKE 'E'.
      RETURN.
  ENDTRY.
  PV_SIZE = BIN_SIZE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_SEND_MAIL_PDF
*&---------------------------------------------------------------------*
*&   带PDF文件发送邮件
*&---------------------------------------------------------------------*
*&      --> PS_DATA
*&---------------------------------------------------------------------*
FORM FRM_SEND_MAIL_PDF USING PS_DATA TYPE TY_DATA
                               PV_SIZE TYPE SO_OBJ_LEN
                               PV_CONTENT TYPE SOLIX_TAB.

  DATA:LV_TITLE TYPE SO_OBJ_DES.
  CLEAR MAIN_TEXT.

***  发送邮件
  LV_TITLE = '发送邮件带PDF文件'.
  APPEND |采购订单号{ PS_DATA-EBELN }行项目{ PS_DATA-EBELP }打印发送PDF| TO MAIN_TEXT.

  TRY.
***  创建持续的发送请求
      SEND_REQUEST = CL_BCS=>CREATE_PERSISTENT( ).

***  设置邮件主题与正文
      DOCUMENT = CL_DOCUMENT_BCS=>CREATE_DOCUMENT(
        I_TYPE    = 'RAW'
        I_TEXT    = MAIN_TEXT
        I_SUBJECT = LV_TITLE ).      " 邮件主题名

***  将电子文档作为附件
      DOCUMENT->ADD_ATTACHMENT(
        I_ATTACHMENT_TYPE    = 'PDF'              " 附件格式
        I_ATTACHMENT_SUBJECT = '采购订单'         " 附件名称
        I_ATTACHMENT_SIZE    = PV_SIZE            " 附件大小
        I_ATT_CONTENT_HEX    = PV_CONTENT ).      " 附件内容

***  添加文档对象
      SEND_REQUEST->SET_DOCUMENT( DOCUMENT ).

***  设置收件人
      GV_MAILTO = '994837998@qq.com'.
      TRANSLATE GV_MAILTO TO LOWER CASE.
      RECIPIENT = CL_CAM_ADDRESS_BCS=>CREATE_INTERNET_ADDRESS( GV_MAILTO ).
      CALL METHOD SEND_REQUEST->ADD_RECIPIENT
        EXPORTING
          I_RECIPIENT  = RECIPIENT
          I_EXPRESS    = 'X'
          I_COPY       = ' '
          I_BLIND_COPY = ' '
          I_NO_FORWARD = ' '.

***  发送
      SENT_TO_ALL = SEND_REQUEST->SEND( I_WITH_ERROR_SCREEN = 'X' ).
      COMMIT WORK.
      WAIT UP TO 1 SECONDS.

***  调用发送队列程序
      SUBMIT RSCONN01 WITH MODE = 'INT' WITH OUTPUT = '' AND RETURN.
      IF SENT_TO_ALL IS INITIAL.
        PS_DATA-MESSAGE = |没有将文档发送到{ GV_MAILTO }|.
*        MESSAGE I500(SBCOMS) WITH GV_MAILTO.
      ELSE.
        PS_DATA-MESSAGE = |文件发送成功|.
*        MESSAGE S022(SO).
      ENDIF.


***  捕获异常
    CATCH CX_BCS INTO BCS_EXCEPTION.
      PS_DATA-MESSAGE = |转换期间发生了错误 - 返回代码：{ BCS_EXCEPTION->ERROR_TYPE }|.
*      MESSAGE I865(SO) WITH BCS_EXCEPTION->ERROR_TYPE.
  ENDTRY.

  MODIFY GT_DATA FROM PS_DATA TRANSPORTING MESSAGE WHERE EBELN = PS_DATA-EBELN AND EBELP = PS_DATA-EBELP.


ENDFORM.


*Selection texts
*----------------------------------------------------------
* S_EBELN         采购订单号
* S_LIFNR         供应商
* S_MATNR         物料编码
* S_WERKS         工厂


*Messages
*----------------------------------------------------------
*
* Message class: SBCOMS
*500   没有将文档发送到 &1
*
* Message class: SO
*022   文件发送
*445   传输文档内容时出错
*865   转换期间发生了错误 - 返回代码：<&>
```





## SAP 查看发送记录 ##

使用 T-CODE `SCOT` 可以查看发送记录：

![查看发送记录](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220916112907784.png)

## 示例代码

```abap
FORM FRM_SEND_EMAIL USING IS_ALV.

  "Tcode:SOST可以查看邮件发送情况
*  类: CL_BCS 发送邮件主要用到的功能类, 包括创建发送请求, 添加发送内容,添加发送地址, 到最终的发送指令发出.
*  类: CL_DOCUMENT_BCS, 用来放置发送的内容.
*  类: CX_BCS, 异常类, 用于捕捉发送邮件过程中出现的异常.
*  接口: IF_RECIPIENT_BCS, 用来做邮件地址的存储转换.
  DATA: CL_SEND_MAIL TYPE REF TO CL_BCS.
  DATA: CL_CONTENT TYPE REF TO CL_DOCUMENT_BCS.
  DATA: LO_EXCEPTION TYPE REF TO CX_BCS.
  DATA: LV_ATTACHMENT_TYPE TYPE SO_OBJ_TP VALUE 'BIN'.

  "主题
  DATA:LV_SUBJECT TYPE SO_OBJ_DES.

  "正文内容
  DATA:LV_TEXT TYPE STRING.
  DATA:LT_MAIN_TEXT TYPE SOLI_TAB.

  "附件名字
  DATA:LV_ATTACH_SUBJECT TYPE SOOD-OBJDES.

  "附件内容字节长度
  DATA:LV_BYTECOUNT TYPE SOOD-OBJLEN.

  "发件人,收件人,抄送人
  DATA:LO_RECIPIENT TYPE REF TO IF_RECIPIENT_BCS.
  DATA:LO_SENDER TYPE REF TO IF_SENDER_BCS.
  DATA:LV_SENDER TYPE ADR6-SMTP_ADDR.
  DATA:LV_MAILTO TYPE AD_SMTPADR,
       LV_MAILCO TYPE AD_SMTPADR.

  "返回结果
  DATA:LV_RESULT TYPE OS_BOOLEAN.

  "异常
  DATA: LV_ERROR    TYPE C,
        LV_ERR_TEXT TYPE STRING.

  "数据源
  DATA: LS_ALV LIKE GS_ALV.

  DEFINE APPEND_MAIN_BODY.
    LV_TEXT = &1.
    APPEND LV_TEXT TO LT_MAIN_TEXT.
  END-OF-DEFINITION.

  MOVE-CORRESPONDING IS_ALV TO LS_ALV.
*  CLEAR GS_DATA_LOG.
*  CLEAR GS_DATA_LOG_FJ.

  READ TABLE GT_EMAIL INTO DATA(LS_EMAIL) WITH KEY GUID = LS_ALV-GUID.
  IF  SY-SUBRC EQ 0.

    TRY.
        "创建发送请求
        CL_SEND_MAIL = CL_BCS=>CREATE_PERSISTENT( ).

        "主题
        IF P_JJX IS NOT INITIAL.
          LV_SUBJECT =  |Bonus Letter for Y{ LS_ALV-GJAHR }|.
        ELSE.
          LV_SUBJECT =  |{ SY-DATUM+0(4) } Annual Target Income Notice|.
        ENDIF.

        "正文
        IF P_JJX IS NOT INITIAL.
          DATA(LV_STR) = |<p>Pls find enclosed your bonus letter for Y{ LS_ALV-GJAHR }.</p>|.
        ELSE.
          LV_STR = |<p>Pls find your { SY-DATUM+0(4) } Annual Target Income Notice per attachment.</p>|.
        ENDIF.
        APPEND_MAIN_BODY:
        '<!DOCTYPE html>',
        '<html lang="en">',
           '<head>',
              '<meta charset="UTF-8">',
              '<meta name="viewport" content="width=device-width, initial-scale=1.0">',
              '<style>',
                'body { font-family: Arial, sans-serif; margin: 40px;}',
                '.message { border: 1px solid #e0e0e0;padding: 20px;border-radius: 5px;box-shadow: 0 0 10px rgba(0,0,0,0.1);}',
              '</style>',
           '</head>',
           '<body>',
              '<div class="message">',
                '<p>Dear colleague,</p>',
                LV_STR,
                '<br><br>',
                '<p>With kind regards</p>',
                '<br>',
                '<p>C&amp;B team</p>',
                '<p>Human Resources Department</p>' ,
              '</div>',
           '</body>',
        '</html>'.
        "创建正文发送内容
        CL_CONTENT = CL_DOCUMENT_BCS=>CREATE_DOCUMENT(
          I_TYPE    = 'HTM'
          I_TEXT    = LT_MAIN_TEXT
          I_SUBJECT = LV_SUBJECT
         ).

        "添加附件
        "LV_ATTACH_SUBJECT = '附件.docx'.
        "LV_BYTECOUNT      = LS_EMAIL-FILE_SIZE.
        DATA: LV_PERNR_STRING TYPE STRING.
        LV_PERNR_STRING = |{ LS_EMAIL-PERNR ALPHA = OUT }|.
        LV_ATTACH_SUBJECT = |{ SY-DATUM+0(4) } Annual Target Income Notice_{ LS_EMAIL-ENAME }({ LS_EMAIL-PERNR }).pdf|.
        LV_BYTECOUNT      = LS_EMAIL-FILE_SIZE_PDF.
        CL_CONTENT->ADD_ATTACHMENT(
          I_ATTACHMENT_TYPE    = LV_ATTACHMENT_TYPE
          I_ATTACHMENT_SUBJECT = LV_ATTACH_SUBJECT
*          I_ATT_CONTENT_HEX    = LS_EMAIL-EMAIL
          I_ATT_CONTENT_HEX    = LS_EMAIL-EMAIL_PDF
          I_ATTACHMENT_SIZE    = LV_BYTECOUNT
          ).

        "添加邮件内容到发送请求
        CL_SEND_MAIL->SET_DOCUMENT( CL_CONTENT ).

        "设置发件人
        LV_SENDER = P_EMAIL.
        LO_SENDER = CL_CAM_ADDRESS_BCS=>CREATE_INTERNET_ADDRESS( LV_SENDER ).
        CL_SEND_MAIL->SET_SENDER( I_SENDER = LO_SENDER ).

        "设置收件人,设置多个人，设置多次不同邮件地址即可
        LV_MAILTO = LS_ALV-EMAIL.
        LO_RECIPIENT = CL_CAM_ADDRESS_BCS=>CREATE_INTERNET_ADDRESS( LV_MAILTO ).
        CL_SEND_MAIL->ADD_RECIPIENT( EXPORTING I_RECIPIENT = LO_RECIPIENT ).

        "设置抄送人
        LV_MAILCO = LS_ALV-ZGYX.
        LO_RECIPIENT = CL_CAM_ADDRESS_BCS=>CREATE_INTERNET_ADDRESS( LV_MAILCO ).
        CL_SEND_MAIL->ADD_RECIPIENT( EXPORTING I_RECIPIENT = LO_RECIPIENT I_COPY = ABAP_TRUE ).


        "设置状态
        CL_SEND_MAIL->SET_STATUS_ATTRIBUTES(
          I_REQUESTED_STATUS = 'E'
          I_STATUS_MAIL      = 'E' ).

        "设置立即发送
        CL_SEND_MAIL->SET_SEND_IMMEDIATELY( 'X' ).

        "正式发送并提交作业
        LV_RESULT = CL_SEND_MAIL->SEND( I_WITH_ERROR_SCREEN = 'X' ).

        COMMIT WORK AND WAIT.

      CATCH CX_BCS INTO LO_EXCEPTION.
        "邮件发送存在异常，写处理代码
        LV_ERROR = 'X'.
        MESSAGE ID LO_EXCEPTION->MSGID TYPE LO_EXCEPTION->MSGTY NUMBER  LO_EXCEPTION->MSGNO
          WITH LO_EXCEPTION->MSGV1 LO_EXCEPTION->MSGV2 LO_EXCEPTION->MSGV3 LO_EXCEPTION->MSGV4 INTO LV_ERR_TEXT.
        LV_ERR_TEXT = |{ LO_EXCEPTION->ERROR_TYPE }：{ LV_ERR_TEXT }|.
    ENDTRY.

*    "获取发送结果
*    DATA(LV_SEND_REQUEST) = CL_SEND_MAIL->SEND_REQUEST->GETU_OID( ).
*    CL_SEND_MAIL->GET_SR_STATUS( ).
*
*    DATA: LS_FILTER TYPE SOXRF VALUE 'X',
*          LT_REC    TYPE TABLE OF SOOS5.
*
*    CALL FUNCTION 'SO_RECEIVER_LIST_READ_NEW'
*      EXPORTING
*        FILTER       = LS_FILTER
*        SEND_REQUEST = LV_SEND_REQUEST
*      TABLES
*        REC_TAB      = LT_REC
*      EXCEPTIONS
*        OTHERS       = 1.

*    GS_DATA_LOG-MAIL_UUID = LV_SEND_REQUEST. "邮箱的唯一标识

    " 如果发生异常，则未发送成功
    IF LV_ERROR IS NOT INITIAL.
      LS_ALV-FSZT   = 'E'.
      LS_ALV-FSXX   = LV_ERR_TEXT.
*      LS_ALV-STATUS = '@0A@'.

*      GS_DATA_LOG-ZFSJG_YG = 'E'.
*      GS_DATA_LOG-ZFSXX_YG = LV_ERR_TEXT.
*      GS_DATA_LOG-ZFSJG_ZG = 'E'.
*      GS_DATA_LOG-ZFSXX_ZG = LV_ERR_TEXT.

    ELSE. "如果没有发生异常， 则判断是否发送成功

      IF LV_RESULT EQ 'X'. "文档已经发送,是否发送成功需要判断
        DATA: LV_STATUS TYPE C,
              LV_TEXT1  TYPE NATXT.

        CL_SEND_MAIL->GET_SR_STATUS(
          IMPORTING
            EV_STATUS = LV_STATUS
            EV_TEXT   = LV_TEXT1
        ).
        LS_ALV-FSZT = LV_STATUS.
        LS_ALV-FSXX = LV_TEXT1.
*        IF NOT LINE_EXISTS( LT_REC[ MSGTY = 'E' ] ).
*        LS_ALV-FSZT        = 'S'.
*        LS_ALV-FSXX        = '发送成功'.
*        LS_ALV-STATUS      = '@08@'.

*          GS_DATA_LOG-ZFSJG_YG  = 'S'.
*          GS_DATA_LOG-ZFSXX_YG  = '发送成功'.
*          GS_DATA_LOG-ZFSJG_ZG  = 'S'.
*          GS_DATA_LOG-ZFSXX_ZG  = '发送成功'.
*        ELSE.

        "判断员工邮箱报错信息
*          DATA: LV_ERR_STRING TYPE STRING.
*          LOOP AT LT_REC INTO DATA(LS_REC_YG) WHERE SNDCP IS INITIAL.
*
*            CLEAR LV_ERR_STRING.
*
*            MESSAGE ID LS_REC_YG-MSGID TYPE LS_REC_YG-MSGTY NUMBER LS_REC_YG-STATUS
*             WITH LS_REC_YG-MSGV1 LS_REC_YG-MSGV2 LS_REC_YG-MSGV3 LS_REC_YG-MSGV4
*             INTO LV_ERR_STRING.
*
*            GS_DATA_LOG-ZFSXX_YG = |{ LV_ERR_STRING }{ GS_DATA_LOG-ZFSXX_YG }|.
*            GS_DATA_LOG-ZFSJG_YG = 'E'.
*          ENDLOOP.
*
*
*          "获取主管邮箱报错信息
*          LOOP AT LT_REC INTO DATA(LS_REC_ZG) WHERE SNDCP EQ 'X'.
*
*            CLEAR LV_ERR_STRING.
*
*            MESSAGE ID LS_REC_ZG-MSGID TYPE LS_REC_ZG-MSGTY NUMBER LS_REC_ZG-STATUS
*             WITH LS_REC_ZG-MSGV1 LS_REC_ZG-MSGV2 LS_REC_ZG-MSGV3 LS_REC_ZG-MSGV4
*             INTO LV_ERR_STRING.
*
*            GS_DATA_LOG-ZFSXX_ZG = |{ LV_ERR_STRING }{ GS_DATA_LOG-ZFSXX_ZG }|.
*            GS_DATA_LOG-ZFSJG_ZG = 'E'.
*          ENDLOOP.

*        LS_ALV-FSZT   = 'E'.
*          LS_ALV-FSXX   = |{ GS_DATA_LOG-ZFSXX_YG }{ GS_DATA_LOG-ZFSXX_ZG }|.
*        LS_ALV-STATUS = '@0A@'.

*        ENDIF.
      ELSE. "文档未发送
        LS_ALV-FSZT   = 'E'.
        LS_ALV-FSXX   = |邮件发送失败|.
      ENDIF.

    ENDIF.

  ENDIF.

  MOVE-CORRESPONDING LS_ALV TO IS_ALV.
ENDFORM.

```

