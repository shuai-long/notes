

## 面向对象

类分为局部类与全局类。全局类是通过SE24创建，可供所有ABAP程序使用；而在程序内部（如报表程序内）定义的类则一般只应用于本程序，除了使用范围不同外，其它都是一样的；在使用类时系统首先会寻找程序内部定义的局部类，不存在时才去寻找全局类。

### CLASS

语句CLASS用于定义类, 发布类, 或指定属性.

- 类的完整定义应该包含两部分: 声明部分和实现部分, 应该都由CLASS开始, 以ENDCLASS结尾。

  - 声明部分: 指定类的属性，并声明其方法。类的所有成员变量和成员方法都必须在 DEFINITION 部分进行声明，在声明时需指定成员的可见性。

    - 类的属性在类的声明部分定义.

    - 类的方法则需要在类的声明和实现两部分才能完成定义：在声明部分说明方法的接口参数

    ```abap
    "类的声明部分
    CLASS cl_demo DEFINITION.
    	PUBLIC SECTION.
    		DATA: GV_NAME TYPE CHAR4.
    		CLASS-METHODS: GET_NAME.
    ENDCLASS.
    ```

    

  - 实现部分: 实现类的方法, 不是所有类都需要有实现部分的。只有在类定义时，定义了方法时，才需要实现，如果没有方法，或者类本身定义成了抽象类，则不需要进行实现了

    - 类的方法则需要在类的声明和实现两部分才能完成定义：类的实现部分则说明了具体的功能。

    ```ABAP
    "类的实现部分
    CLASS cl_demo IMPLEMENTATION.
    	METHOD GET_NAME.
    		WRITE: GV_NAME.
    	ENDMETHOD.
    ENDCLASS.
    ```

- 临时声明变量时不用 ENDCLASS 关闭

  ```abap
  "该语句的作用是使类在当前位置已知, 而不管类的实际定义在程序中的位置
  "不引入声明部分;不能使用 ENDCLASS 关闭
  CLASS cl_demo DEFINITION DEFERRED. 
  ```

### 可选附加项

- PUBLIC:

  该选项是由 Class Builder 在创建时生成，不能用在程序中的局部类中

- INHERITING FROM: 

  在定义类时, 可以使用 `INHERITING FROM` 选项指定从哪个类继承, 即父类(父类不能被FINAL修饰 并且可见), 每个类只能有一个父类, 若未指定父类, 则默认继承Object类(该类是顶级父类)

  父类的所有public与protected修饰的组件都会被继承, 子类可以重写父类的方法, 但是不能重写父类的属性 , 若重写子类的方法需要使用 `REDEFINITION` 关键字指定,否则无法通过编译.

  ABAP 是单继承, 多实现

  ```ABAP
  CLASS person DEFINITION.
    PUBLIC SECTION.
      METHODS:
        "父类构造函数， 用来初始化 ID 和 NAME
        constructor
          IMPORTING id   TYPE string
                    name TYPE string,
  
        get_id
          RETURNING VALUE(id) TYPE string,
  
        get_name
          RETURNING VALUE(name) TYPE string,
  
        learn.
  
    PROTECTED SECTION. "父类中的两个属性
      DATA: id   TYPE string,
            name TYPE string.
  ENDCLASS.
  
  "teacher 类继承了 person 类
  CLASS teacher DEFINITION INHERITING FROM person.
    PUBLIC SECTION.
      METHODS:
  
        "新增了方法 teach
        teach,
  
        "重写了父类中的 learn 方法
        learn REDEFINITION.
  
    PROTECTED SECTION. "新增了属性 course
      DATA: course TYPE string VALUE 'ABAP'.
  
  ENDCLASS.
  
  
  "student 类继承了 person 类
  CLASS student DEFINITION INHERITING FROM person.
    PUBLIC SECTION.
      METHODS:
  
        "新增了 play 方法
        play,
  
        "重写了父类中的 learn 方法
        learn REDEFINITION.
  
    PROTECTED SECTION.
      DATA: course TYPE string VALUE 'ABAP'.
  ENDCLASS.
  
  
  "person 类的实现
  CLASS person IMPLEMENTATION.
    METHOD constructor.
      me->id = id.
      me->name = name.
    ENDMETHOD.
  
    METHOD get_id.
      id = me->id.
    ENDMETHOD.
  
    METHOD get_name.
      name = me->name.
    ENDMETHOD.
  
    METHOD learn.
      WRITE: 'A person is learning.'.
    ENDMETHOD.
  ENDCLASS.
  
  "teacher 类的实现
  CLASS teacher IMPLEMENTATION.
    METHOD teach.
      WRITE: /  'I am teacher' , name , 'I can teach' , course.
    ENDMETHOD.
  
    METHOD learn.
      WRITE: /  'I am teacher', name , 'I learn all by myself'.
    ENDMETHOD.
  ENDCLASS.
  
  "student 类的实现
  CLASS student IMPLEMENTATION.
    METHOD play.
      WRITE: /  'I am student' , name,'I often paly games'.
    ENDMETHOD.
  
    METHOD learn.
      WRITE: / 'I am student', name,'I learn',course,'from the teacher'.
    ENDMETHOD.
  ENDCLASS.
  
  
  DATA: go_teacher TYPE REF TO teacher,
        go_student TYPE REF TO student.
  
  START-OF-SELECTION.
  
    CREATE OBJECT go_teacher
      EXPORTING
        id   = 'T0001'
        name = '张三'.
    CALL METHOD: go_teacher->teach.
    CALL METHOD: go_teacher->learn.
  
    CREATE OBJECT go_student
      EXPORTING
        id   = 'S0001'
        name = '李四'.
    CALL METHOD: go_student->play.
    CALL METHOD: go_student->learn.
  ```

- ABSTRACT:

  被该关键字修饰的 类 为抽象类, 此类不能被实例化, 只能用来被继承.

  - 含有抽象方法的类必须是抽象类,  但是抽象类不只可以包含抽象方法
  - 若抽象类中只包含抽象方法, 则只用写该类的声明部分即可
  - 子类继承父类必须重写父类的抽象方法

  ```abap
  "定义抽象类
  CLASS cl_father DEFINITION ABSTRACT.
    PUBLIC SECTION.
      METHODS:
        play ABSTRACT. "抽象方法play
  
  ENDCLASS.
  
  "子类继承父类
  CLASS cl_son DEFINITION INHERITING FROM cl_father.
    PUBLIC SECTION.
      METHODS:
        play REDEFINITION."重写父类的抽象方法
  
  ENDCLASS.
  
  CLASS cl_son IMPLEMENTATION.
    METHOD play.
      WRITE: / '继承父类的抽象方法并重写'.
    ENDMETHOD.
  ENDCLASS.
  
  DATA: go_son TYPE REF TO cl_son.
  
  START-OF-SELECTION.
  
    CREATE OBJECT go_son.
    CALL METHOD go_son->play.
  ```

- FINAL:

  FINAL类不能被继承, final 类中的所有方法默认都是 final 方法, 且 final类中的方法不能再使用 final 关键字修饰.

  ```abap
  " 定义 final 类
  CLASS cl_demo DEFINITION FINAL.
    PUBLIC SECTION.
      METHODS:
  *      method_final FINAL,"报错, 不能再使用final修饰
        method_test . " 该方法为 final 方法 不能被重写
  ENDCLASS.
  
  CLASS cl_demo IMPLEMENTATION.
    METHOD method_test.
      WRITE: / '我是 final 方法, 不能被重写'.
    ENDMETHOD.
  ENDCLASS.
  
  "报错: final 类不能被继承
  *CLASS cl_demo_son DEFINITION INHERITING FROM cl_demo.
  *ENDCLASS.
  ```

  > [!WARNING]
  >
  > 我们再声明类时, 不要将 abstract 和 final 同时修饰一个类, 虽然不会报错, 但是没有意义 , abstract 类就是为了被继承, final 就是为了保证该类不能被继承. 若定义为 abstract final 类型的,  则最好只定义**静态成员**, 因为 abstract 使该类不能被实例化, final 决定了该类不能被继承 ,  所以非静态成员不能被访问, 意义不大.  同理 ,方法不能同时被 abstract 和 final 修饰
  >
  > ```abap
  > class cl_demo definition abstract.
  >     public section.
  >        methods:
  > *      method_final_abstract ABSTRACT FINAL . "方法不能同时被 final 和 abstrct 修饰
  >       method_final final,
  >       method_abstract abstract.
  > endclass.
  > 
  > class cl_demo implementation.
  >   method method_final.
  >     write: / '我是 final 方法, 不能被重写'.
  >   endmethod.
  > 
  >   "报错, 此处不能有方法实现
  > *  METHOD method_abstract.
  > *    WRITE: / '我是 ABSTRACT 方法, 只能子类重写'.
  > *  ENDMETHOD.
  > 
  > endclass.
  > 
  > class cl_demo_son definition inheriting from cl_demo.
  >   public section.
  >     methods:
  > *    method_final REDEFINITION,  "报错, final 方法不能被重写
  >       method_abstract redefinition. "必须重写抽象方法, 不然报错
  > endclass.
  > 
  > class cl_demo_son implementation.
  >   method method_abstract.
  >     write: / '重写父类的抽象方法'.
  >   endmethod.
  > endclass.
  > ```

## 可用组件

ABAP 类中可以定义三种不同的类型的成员: 属性 / 方法 / 事件, 各种成员的可见性 及生存周期只能在类定义时指定, 而不是在类实现时指定. 

- 静态属性: 是在非静态成员前边加上 `CLASS-` 来实现的, 除此之外,在类中使用  `CONSTANTS`  和  `TYPES` 定义的属性也是静态的属性, 例如:

  ```abap
  class cl_demo definition.
    public section.
  
      class-data: gv_value type string value '我是类变量'.
  
      constants: gc_value type string value '我是常量, 是静态属性, 可通过类直接访问'.
  
      types: begin of ty_alv,
               name type string,
             end of ty_alv.
  
  
  endclass.
  
  data: gs_alv type cl_demo=>ty_alv .
  gs_alv-name = '我是通过类变量声明的结构'.
  
  start-of-selection.
    write: / cl_demo=>gv_value.
    write: / cl_demo=>gc_value.
    write: / gs_alv-name.
  ```

- 只读属性: 在类或接口中定义的变量是 public 的, 可以加上 `READ-ONLY` 修饰,  被修饰的变量只能被外部读取, 而不能修改,  但是可以被内部修改.

  ```abap
  class cl_demo definition.
    public section.
  
      data: gv_value type string value '我是被 read-only 修饰的变量, 只能内部修改, 不能外部修改' read-only.
  
      constants: gc_value type string value '我是常量,所有地方都不能修改'.
  
      methods: set_value
        importing
          lv_value type string  .
  
  endclass.
  
  class cl_demo implementation.
    method set_value.
      me->gv_value = lv_value.  " 内部可以修改 read-only 修改的变量,
  *    me->gc_value = lv_value. " 内部不可以修改变量
    endmethod.
  endclass.
  
  
  data: go_demo type ref to cl_demo.
  
  start-of-selection.
    create object go_demo.
  *  go_demo->gv_value = '外部想要直接修改 read-only 修改的变量'.
  *  go_demo->gc_value = '外部想要直接修改 常量'.
    call method go_demo->set_value exporting lv_value = '通过类的方法修改 read-only 修饰的变量'.
    write go_demo->gv_value.
  ```

- 事件: 在类的定义过程中，事件也可以被声明为一种成员组件，声明之后，所有类内部访问均可以触发该事件，还可以在类或者其他类中定义特定的捕捉方法对事件进行处理。


- 接口组件**: **所有接口组件可以使用 `ALIASES` 指定其别名（Alias Name），这样在访问接口时，可以简化 Interface~ 前缀。`ALIASES`  只能在接口内或者类的定义部分使用，不能在类的实现部分使用.

  ```abap
  interface interface_name.
    methods interface_method importing p1 type i.
  endinterface.
  
  class cls1 definition.
    public section.
      interfaces interface_name.
      aliases itmd for interface_name~interface_method. "为 account~calc  起别名为 calc
  endclass.
  
  class cls1 implementation.
    "METHOD interface_name~interface_method. "若不起别名, 则需要通过该语句实现方法
    method itmd.
      write: / 'Deposit money is:',p1.
    endmethod.
  endclass.
  
  data: go_demo type ref to cls1.
  
  start-of-selection.
  
    create object:go_demo.
  
    "同理, 没有别名则用此访问
    "CALL METHOD:go_demo->interface_name~interface_method EXPORTING p1 = 200.
    call method:go_demo->itmd exporting p1 = 200.
    "可以去掉 interface_name~ 前缀，使用别名直接进行访问
  ```

- 属性

  `TYPE-POOLS` , ` TYPES`, `DATA`, `CLASS-DATA`, `CONSTANTS`

- 方法 和 事件

  `METHODS`, `CLASS-METHODS`, `EVENTS`, `CLASS-EVENTS`

- 继承和实现

  `INTERFACES`

## SECTION

用于控制类组件的可访问性, 通常有三种, 包括:

- PUBLIC SECTION:

  ​    该语句只在类的声明部分使用, 并定义类的**公共可见性**部分: 在该语句之后区域中声明的类组件可以被所有的外部对象使用

  ```ABAP
  "声明 公共常量 const, 并在外部访问
  CLASS cls DEFINITION. 
    PUBLIC SECTION. 
      CONSTANTS const TYPE string VALUE `I'm public`. 
  ENDCLASS. 
  
  START-OF-SELECTION. 
    cl_demo_output=>display( cls=>const ). 
  ```

- PROTECTED SECTION:

  ​    该语句只在类的声明部分使用, 并定义类的**受保护的可见性**部分: 在该语句之后区域中声明的类组件只能被本类及派生类中使用，对其他类或程序不可见

  ```abap
  "在 父类 中声明受保护的常量 const ,并将其作为子类 sttr 变量的初始值
  CLASS cls1 DEFINITION. 
    PROTECTED SECTION. 
      CONSTANTS const TYPE string VALUE `I'm protected`. 
  ENDCLASS. 
  
  CLASS cls2 DEFINITION INHERITING FROM cls1. 
    PUBLIC SECTION. 
      CLASS-DATA attr TYPE string VALUE cls1=>const. 
  ENDCLASS. 
  
  START-OF-SELECTION. 
    cl_demo_output=>display( cls2=>attr ). 
  ```

- PRIVATE SECTION:

  ​	该语句只在类的声明部分使用, 并定义类的**私有的可见性**部分: 在该语句之后区域中声明的类组件只能被类自身其他成员及**友类**( *友类是将两个类缝合在一起?* )访问

  ```abap
  "在 cls1 中声明的 私有变量 const , 并将其作为友类 cls2 变量的初始值
  CLASS cls2 DEFINITION DEFERRED. 
  
  CLASS cls1 DEFINITION FRIENDS cls2. 
    PRIVATE SECTION. 
      CONSTANTS const TYPE string VALUE `I'm private`. 
  ENDCLASS. 
  
  CLASS cls2 DEFINITION. 
    PUBLIC SECTION. 
      CLASS-DATA attr TYPE string VALUE cls1=>const. 
  ENDCLASS. 
  
  START-OF-SELECTION. 
    cl_demo_output=>display( cls2=>attr ). 
  ```
  
  > 扩展：一般不在类的公有部分定义类的属性， 因为公有的可以直接被外界修改， 违背了面向对象的封装原则。定义在公共区的一般是常量，不需要修改的。

## 方法 

- 构造方法:  `constructor`  构造方法的名称是固定的, 默认为 final 修饰

  - 构造方法只有输入参数, 并且不能包含任何形式的输出参数和返回值

  - 若在创建对象并调用构造方法时出现异常, 则该对象将被删除

  - 每一个类都有一个默认无参构造器,但是当重新定义了构造器后, 默认构造器将被覆盖

  - 如果 son 继承了 father . 此时若 father 重新定义了构造器 , son 一定要在构造器中明确使用 `super->constructor` 方法调用一下父类的构造器, 否则编译不通过. ( 因为 son 构造器只能正确的构造出子类的那部分属性, 对于从 father 中继承的属性应由 father 构造器来完成. 在调用 father 构造器前只能访问 类组件, 只有调用了 father 构造器后再能访问实例组件)

    ```abap
    CLASS father DEFINITION.
      PUBLIC SECTION.
        METHODS: constructor IMPORTING id   TYPE string
                                       name TYPE string.
    
      PROTECTED SECTION.
        DATA: id   TYPE string,
              name TYPE string.
    ENDCLASS.
    
    CLASS son DEFINITION INHERITING FROM father.
      PUBLIC SECTION.
        METHODS:
          constructor IMPORTING id     TYPE string
                                name   TYPE string
                                course TYPE string,
    
          get_son.
    
      PRIVATE SECTION.
        DATA: course TYPE string VALUE 'I like abap.'.
    ENDCLASS.
    
    CLASS father IMPLEMENTATION.
      METHOD constructor.
        me->id = id.
        me->name = name.
      ENDMETHOD.
    
    ENDCLASS.
    
    CLASS son IMPLEMENTATION.
      METHOD constructor.
    
        "必须显示的调用 father 类的构造器
        super->constructor( id = id name = name ).
        me->course = course.
      ENDMETHOD.
    
      METHOD get_son.
        WRITE:/ id , name , course.
      ENDMETHOD.
    ENDCLASS.
    
    DATA: go_son TYPE REF TO son.
    
    START-OF-SELECTION.
      CREATE OBJECT go_son
        EXPORTING
          id     = '0001'
          name   = '张三'
          course = '测试'.
    
      CALL METHOD go_son->get_son.
    ```

  > [!Tip]
  >
  > 每个类都有一个静态的构造方法, 我们也可以重新定义来完成某些初始化工作, 静态的构造器只在程序中被调用一次, 且不能被程序显示的调用.  用 `CLASS-METHOD` 定义, 名称必须是 `class_constract`, 因为是静态方法, 只能访问类的静态属性.
  >
  > ```abap
  > CLASS father DEFINITION.
  >   PUBLIC SECTION.
  >     CLASS-METHODS class_constructor.
  >     METHODS constructor.
  > ENDCLASS.
  > 
  > CLASS son  DEFINITION INHERITING FROM father.
  >   PUBLIC SECTION.
  >     CLASS-METHODS class_constructor.
  >     METHODS constructor.
  > ENDCLASS.
  > 
  > CLASS father IMPLEMENTATION.
  >   METHOD class_constructor.
  >     WRITE: / '父类的静态构造方法被调用'.
  >   ENDMETHOD.
  > 
  >   METHOD constructor.
  >     WRITE: / '父类的普通构造方法被调用'.
  >   ENDMETHOD.
  > ENDCLASS.
  > 
  > CLASS son IMPLEMENTATION.
  >   METHOD class_constructor.
  >     "静态构造方法不用显示的调用 , 也不能显示的调用
  >     WRITE: / '子类的静态构造方法被调用'.
  >   ENDMETHOD.
  > 
  >   METHOD constructor.
  >     "父类重写了构造方法, 必须显示的调用
  >     super->constructor( ).
  >     WRITE: / '子类的普通构造方法被调用'.
  >   ENDMETHOD.
  > ENDCLASS.
  > 
  > DATA: go_demo_father TYPE REF TO father,
  >       go_demo_son    TYPE REF TO son.
  > 
  > START-OF-SELECTION.
  > 
  >   CREATE OBJECT go_demo_father.
  >   SKIP.
  > 
  >   "因为父类的静态构造方法被调用过, 此处不再调用
  >   CREATE OBJECT go_demo_father TYPE son.
  >   SKIP.
  > 
  >   "子类的静态构造方法被调用过, 此处也不再调用
  >   CREATE OBJECT go_demo_son.
  > ```

- 普通方法:  能访问对象实例中的所有属性和事件以及类的静态成员

  ```abap
  METHODS meth [ABSTRACT|FINAL] 
      [IMPORTING parameters [PREFERRED PARAMETER p]] 
      [EXPORTING parameters] 
      [CHANGING parameters] 
      [{RAISING|EXCEPTIONS} exc1 exc2 ...].
  ```

- 静态方法:  只能访问类的静态属性和静态事件

  ```abap
  CLASS-METHODS meth [ABSTRACT|FINAL] 
      [IMPORTING parameters [PREFERRED PARAMETER p]] 
      [EXPORTING parameters] 
      [CHANGING parameters] 
      [{RAISING|EXCEPTIONS} exc1 exc2 ...].
  ```

  - parameters

    参数的传递形式:  **按值传递( `value(p1)` )**    /   **按引用传递( `reference(p1)` )** , 若参数名前没有修饰, 则默认按引用传递参数,  引用传递时不能在方法中修改按引用传递的输入参数.

    **可选参数( `OPTIONAL`)**  和  **默认值( `DEFAULT` )**  两个选项不能同时使用, 且 `EXPORTING` 中的参数不可使用

  - PREFERRED PARAMETER

    首选参数: 当所有的 `IMPORTING` 类型都为 可选参数时, 可以通过 `PREFERRED PARAMETER`指定某一个可选参数为首选参数, 可以实现简单的调用 `CALL METHOD meth_name( a )`. 实参 a 就会传递给首选参数.( 若 `IMPORTING`  中有必输项则没什么意义)

  - RAISING exc1 exc2

    这里的 xec1 和 xec2 为`class-based exceptions` 的异常, 若方法出现了异常,则会抛出 `CX_STATIC_CHECK` 或 `CX_DYNAMIC_CHECK` 的子类( 若需要抛出多个异常, 且有父子关系, 应将子异常优先抛出)

    > 
    >
    > 基于类的异常作为异常类的实例实现。异常类可以在系统中全局预定义，也可以由用户( 全局或本地 )定义。基于类的异常由 ABAP 运行时环境或程序引发。
    >
    > 当引发异常时，可以创建一个异常对象，其属性包含有关错误情况的信息。基于类的异常可以在 `TRY` 控制结构中处理。`TRY` 块定义了一个受保护区域，其异常可以在后续的 `CATCH` 块中处理。因为所有异常类都是公共超类的子类，所以可以通过处理各自的超类同时处理相关的异常。系统传播异常对象或基于类的异常，直到异常被处理或违反接口为止。

    ```abap
    CLASS math DEFINITION.
      PUBLIC SECTION.
    
        METHODS divide_1_by
          IMPORTING operand TYPE i
          EXPORTING result  TYPE f
          RAISING   cx_sy_arithmetic_error.
    
    ENDCLASS.
    
    CLASS math IMPLEMENTATION.
      METHOD divide_1_by.
        result = 1 / operand.
      ENDMETHOD.
    ENDCLASS.
    
    START-OF-SELECTION.
    
      DATA: oref TYPE REF TO math,
            exc  TYPE REF TO cx_sy_arithmetic_error,
            res  TYPE f,
            text TYPE string.
    
      CREATE OBJECT oref.
    
    
      TRY.
          "此时会 捕捉 CX_SY_ZERODIVIDE 异常
          oref->divide_1_by( EXPORTING operand = 0 IMPORTING result = res ).
    
          text = res.
        CATCH cx_sy_arithmetic_error INTO exc.
          text = exc->get_text( ).
    
      ENDTRY.
    
      MESSAGE text TYPE 'I'.
    ```

  - EXCEPTIONS exc1 exc2

    这里的 exc1 和 exc2为 `non-class-based exceptions` 类型的异常，可以由方法中的 `RAISE` 或者 `MESSAGE RAISING` 语句来触发

    > [!Tip]
    >
    > 通过在方法调用和函数模块调用中添加 `EXCEPTIONS`，可以处理非基于类的异常。编号值被分配给异常，用于在引发异常时填充系统字段 `sy-subrc` 。当系统计算 `sy-subrc ne 0`  实际的错误处理才发生 .
    
    ```abap
    CLASS math DEFINITION.
      PUBLIC SECTION.
        METHODS divide_1_by
          IMPORTING  operand TYPE i
          EXPORTING  result  TYPE f
          EXCEPTIONS arith_error.
    ENDCLASS.
    
    CLASS math IMPLEMENTATION.
      METHOD divide_1_by.
        TRY.
            result = 1 / operand.
          CATCH cx_sy_arithmetic_error.
            RAISE arith_error.
        ENDTRY.
      ENDMETHOD.
    ENDCLASS.
    
    START-OF-SELECTION.
      DATA: res  TYPE f,
            oref TYPE REF TO math.
    
      CREATE OBJECT oref.
    
      oref->divide_1_by( EXPORTING  operand = 4 IMPORTING result  = res EXCEPTIONS arith_error = 4 ).
    
      IF sy-subrc = 0.
        WRITE res.
      ELSE.
        WRITE'Arithmetic error!'.
      ENDIF.
    ```

- 函数方法:  return 唯一返回值

  ```abap
  METHODS meth [ABSTRACT|FINAL]
      [IMPORTING parameters [PREFERRED PARAMETER p]]
  RETURNING VALUE(r) typing
      [{RAISING|EXCEPTIONS} exc1 exc2 ...].
  ```

  - RETURNING 

    用来替换 `EXPORTING` 、`CHANGING`，不能同时使用。定义了一个形式参数 r 来接收返回值，并且只能是 值传递, **此处的 r 只能是完全限制类型，不能是通用类型**

    ```abap
    CLASS math DEFINITION.
      PUBLIC SECTION.
        METHODS factorial
          IMPORTING n           TYPE i
          RETURNING VALUE(fact) TYPE i.
    ENDCLASS.
    
    CLASS math IMPLEMENTATION.
      METHOD factorial.
        fact = 1.
        IF n = 0.
          RETURN.
        ELSE.
          DO n TIMES.
            fact = fact * sy-index.
          ENDDO.
        ENDIF.
      ENDMETHOD.
    ENDCLASS.
    
    START-OF-SELECTION.
      DATA: oref   TYPE REF TO math,
            result TYPE i.
      CREATE OBJECT oref.
      result = oref->factorial( 4 ).
      WRITE: result. 
    ```

    > [!Tip]
    >
    > 具有唯一返回值的函数方法可以直接用在以下语句中：
    >
    > - 逻辑表达式：IF、ELSEIF、WHILE、CHECK、WAIT
    >
    > - CASE
    >
    > - LOOP
    >
    > - 算术表达式
    >
    > - 赋值语句
    > - MOVE
    >
    > ​      定义函数方法时必须保证:
    >
    > - 只有一个返回参数
    > - 只能定义`IMPORTING` 输入参数, 不能定义 `EXPORTING` 和 `CHANGING` 参数
    >
    > ​      此类方法可以采用简单调用方式, 如内表行数 `LINES( it_itab )`.  



