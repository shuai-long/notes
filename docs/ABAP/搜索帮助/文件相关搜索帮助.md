## 文件搜索帮助

- 方法一

  ```abap
  at selection-screen on value-request for p_path.
    call function 'F4_FILENAME'
      importing
        file_name = p_path.
  ```

- 方法二

  ```abap
  class-methods set_f4_for_file
    returning value(rv_result) type string.
  ```

  ```abap
  method set_f4_for_file.
  
    data: lt_filetab     type filetable,
          lv_rc          type i,
          lv_user_action type i.
  
    call method cl_gui_frontend_services=>file_open_dialog
      changing
        file_table              = lt_filetab
        rc                      = lv_rc
        user_action             = lv_user_action
      exceptions
        file_open_dialog_failed = 1
        cntl_error              = 2
        error_no_gui            = 3
        not_supported_by_gui    = 4
        others                  = 5.
  
    if sy-subrc <> 0.
      message id sy-msgid type sy-msgty number sy-msgno
        with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      return.
    endif.
  
    read table lt_filetab into data(ls_file) index 1.
    if sy-subrc eq 0.
      rv_result = ls_file-filename.
    endif.
  
  endmethod.
  ```

## 文件夹搜索帮助

- 方法一

  ```abap
  at selection-screen on value-request for p_path.
    call function 'TMP_GUI_BROWSE_FOR_FOLDER'
      importing
        selected_folder = p_path.
  ```

- 方法二

  ```abap
  class-methods set_f4_for_folder
    returning value(rv_result) type string.
  ```

  ```abap
  method set_f4_for_folder.
  
    data: lv_selected_folder type string.
  
    call method cl_gui_frontend_services=>directory_browse
      changing
        selected_folder      = lv_selected_folder
      exceptions
        cntl_error           = 1
        error_no_gui         = 2
        not_supported_by_gui = 3
        others               = 4.
        
   if sy-subrc <> 0.
      message id sy-msgid type sy-msgty number sy-msgno
        with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      return.
    endif.
    
    rv_result = lv_selected_folder.
  
  endmethod.
  ```
  
  





