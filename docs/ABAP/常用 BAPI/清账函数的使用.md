## 清账函数的使用

SAP没有清帐的BAPI，但是提供了清帐的一组函数：

```ABAP
POSTING_INTERFACE_START；
POSTING_INTERFACE_CLEARING；
POSTING_INTERFACE_END ;
```

发票校验产生的应收凭证需要转成预收，然后清掉应付:

- A凭证   借：费用  　     贷：应付暂估
- B凭证   借：应付暂估 	借：应缴增值税　　 贷：应付  
- C凭证   借： 预收　　	贷：费用　

A和B 可以清掉应付暂估 , C凭证的产生可以直接同时把应付清掉 , 这边主要说明C凭证的产生。

这边着重说明一下函数POSTING_INTERFACE_CLEARING的传值:

![POSTING_INTERFACE_CLEARING](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/%E6%B8%85%E8%B4%A6%E5%87%BD%E6%95%B0-POSTING_INTERFACE_CLEARING.png)

T_FTPOST的值是产生清帐 并生成预收凭证C的 抬头字段和项目字段值：

> 注意：给T_FTPOST传值的时候金额需要去前面的空格，千万不要对每一个字段都去空格，不然会带不到FB05的行项目上

![T_FTPOST的传值](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/%E6%B8%85%E8%B4%A6%E5%87%BD%E6%95%B0-T_FTPOST%E4%BC%A0%E5%80%BC.png)

T_FTPOST 的值与 FB05 的对应效果:

![T_FTPOST 的值与 FB05 的对应效果](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/%E6%B8%85%E8%B4%A6%E5%87%BD%E6%95%B0-T_FTPOST%E5%AF%B9%E5%BA%94%E7%9A%84FB05%E5%80%BC.png)

![T_FTPOST 的值与 FB05 的对应效果2](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/%E6%B8%85%E8%B4%A6%E5%87%BD%E6%95%B0-T_FTPOST%E5%AF%B9%E5%BA%94%E7%9A%84FB05%E7%9A%84%E5%80%BC2.png)

T_FTCLEAR的值是需要清的凭证，展开如下，是要清掉会计凭证9511007857,如果需要清多个凭证需要填多个:

![T_FTCLEAR传值](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/%E6%B8%85%E8%B4%A6%E5%87%BD%E6%95%B0-T_FTCLEAR%E4%BC%A0%E5%80%BC.png)

T_FTCLEAR 的值相当于 FB05 的:

![T_FTCLEAR 的值与 FB05 对应关系](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/%E6%B8%85%E8%B4%A6%E5%87%BD%E6%95%B0-T_FTCLEAR%E5%AF%B9%E5%BA%94%E7%9A%84FB05%E5%80%BC.png)

![T_FTCLEAR 的值与 FB05 对应关系](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/%E6%B8%85%E8%B4%A6%E5%87%BD%E6%95%B0-T_FTCLEAR%20%E7%9A%84%E5%80%BC%E4%B8%8EFB05%E5%AF%B9%E5%BA%94%E7%9A%84%E5%80%BC2.png)

 点击 FB05 模拟:

![FB05模拟界面](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/%E6%B8%85%E8%B4%A6%E5%87%BD%E6%95%B0-FB05%E6%A8%A1%E6%8B%9F%E7%95%8C%E9%9D%A2.png)

SE37里面执行 POSTING_INTERFACE_CLEARING 可以debug看到FB05的界面: 

如果生成的清帐凭证是一个没有行项目的凭证：

参数：T_FTCLEAR： 传入的是所要清帐的内容,比如清供应商的应付行项目:

![T_FTCLEAR参数描述](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/%E6%B8%85%E8%B4%A6%E5%87%BD%E6%95%B0-T_FTCLEAR%20%E5%8F%82%E6%95%B0%E6%8F%8F%E8%BF%B0.png)

 参数：T_FTPOST 是要生成的SH清帐凭证的抬头:

![T_FTPOST 参数描述](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/%E6%B8%85%E8%B4%A6%E5%87%BD%E6%95%B0-T_FTPOST%E5%8F%82%E6%95%B0%E6%8F%8F%E8%BF%B0.png)

```abap
*不生产行项目的凭证示例代码
FORM frm_create_sh  USING    p_haeder  TYPE zfis011
                              pt_list   TYPE zfit009
                     CHANGING pt_return TYPE zfit010.
 
   DATA: lt_list   TYPE  zfit009,
         ls_list   TYPE  zfis009,
         wa_list   TYPE  zfis009,
         lt_return TYPE  zfit010,
         ls_return TYPE  zfis010,
         lv_itemno TYPE posnr_acc.
 
   DATA  lv_message TYPE bapiret2-message.
   DATA: lv_msgid  LIKE sy-msgid,
         lv_msgno  LIKE sy-msgno,
         lv_msgty  LIKE sy-msgty,
         lv_msgv1  LIKE sy-msgv1,
         lv_msgv2  LIKE sy-msgv2,
         lv_msgv3  LIKE sy-msgv3,
         lv_msgv4  LIKE sy-msgv4,
         lv_subrc  LIKE sy-subrc.
 
 DATA: lt_blntab  LIKE TABLE OF blntab  WITH HEADER LINE,
       lt_ftclear LIKE TABLE OF ftclear WITH HEADER LINE,
       lt_ftpost  LIKE TABLE OF ftpost  WITH HEADER LINE,
       lt_fttax   LIKE TABLE OF fttax   WITH HEADER LINE.
 
 
   lt_list   = pt_list.
   lt_return = pt_return.
   SORT lt_return BY group blart.
   DELETE lt_list WHERE flag = ' '.
   REFRESH:lt_ftclear,lt_ftpost.
   LOOP AT lt_list INTO ls_list.
 
 *--需要清帐的凭证和供应商
     READ TABLE lt_return INTO ls_return WITH KEY group = ls_list-group
                                                  blart = 'KR' BINARY SEARCH.
     IF sy-subrc = 0.
       CLEAR lt_ftclear.
       lt_ftclear-agkoa = 'K'.
       lt_ftclear-agkon = ls_list-kunnr.
       lt_ftclear-agbuk = p_haeder-bukrs.
       lt_ftclear-xnops = 'X'.
       lt_ftclear-selfd = 'BELNR'.
       lt_ftclear-selvon = ls_return-belnr.
       APPEND lt_ftclear.
     ENDIF.
 
     READ TABLE lt_return INTO ls_return WITH KEY group = ls_list-group
                                                  blart = 'DY' BINARY SEARCH.
     IF sy-subrc = 0.
       CLEAR lt_ftclear.
       lt_ftclear-agkoa = 'K'.
       lt_ftclear-agkon = ls_list-kunnr.
       lt_ftclear-agbuk = p_haeder-bukrs.
       lt_ftclear-xnops = 'X'.
       lt_ftclear-selfd = 'BELNR'.
       lt_ftclear-selvon = ls_return-belnr.
       APPEND lt_ftclear.
     ENDIF.
 
 *--清帐凭证抬头
     CLEAR lt_ftpost.
     lt_ftpost-stype = 'K'.
     lt_ftpost-fnam  = 'BKPF-BLDAT'.
     lt_ftpost-fval  = sy-datum.
     APPEND  lt_ftpost.
 
     CLEAR lt_ftpost.
     lt_ftpost-stype = 'K'.
     lt_ftpost-fnam  = 'BKPF-BLART'.
     lt_ftpost-fval  = 'SH'.
     APPEND  lt_ftpost.
 
     CLEAR lt_ftpost.
     lt_ftpost-stype = 'K'.
     lt_ftpost-fnam  = 'BKPF-BUKRS'.
     lt_ftpost-fval  = p_haeder-bukrs.
     APPEND  lt_ftpost.
 
     CLEAR lt_ftpost.
     lt_ftpost-stype = 'K'.
     lt_ftpost-fnam  = 'BKPF-BUDAT'.
     lt_ftpost-fval  = sy-datum.
     APPEND  lt_ftpost.
 
     CLEAR lt_ftpost.
     lt_ftpost-stype = 'K'.
     lt_ftpost-fnam  = 'BKPF-WAERS'.
     lt_ftpost-fval  = 'RMB'.
     APPEND  lt_ftpost.
   ENDLOOP.
 
 *--开始清帐
     CALL FUNCTION 'POSTING_INTERFACE_START'
       EXPORTING
        i_client                 = sy-mandt
        i_function               = 'C'
        i_mode                   = 'N'           "不显示调用屏幕
        i_update                 = 'S'           "同步
        i_user                   = sy-uname.
 
 
      CLEAR:lv_msgid,lv_msgno,lv_msgty,lv_msgv1,
             lv_msgv2,lv_msgv3,lv_msgv4,lv_subrc,lv_message.
      CALL FUNCTION 'POSTING_INTERFACE_CLEARING'
        EXPORTING
          i_auglv                          = 'UMBUCHNG'
          i_tcode                          = 'FB05'
          i_sgfunct                        = 'C'
       IMPORTING
          e_msgid                          = lv_msgid
          e_msgno                          = lv_msgno
          e_msgty                          = lv_msgty
          e_msgv1                          = lv_msgv1
          e_msgv2                          = lv_msgv2
          e_msgv3                          = lv_msgv3
          e_msgv4                          = lv_msgv4
          e_subrc                          = lv_subrc
        TABLES
          t_blntab                         = lt_blntab
          t_ftclear                        = lt_ftclear
          t_ftpost                         = lt_ftpost
          t_fttax                          = lt_fttax.
       CLEAR lv_message.
       MESSAGE ID  lv_msgid
            TYPE   lv_msgty
            NUMBER lv_msgno
            WITH   lv_msgv1 lv_msgv2 lv_msgv3 lv_msgv4
            INTO   lv_message.
       CLEAR ls_return.
 
 *---记录清帐产生的凭证号
       READ TABLE lt_blntab INDEX 1.
       ls_return-blart   = 'SH'.
       ls_return-bukrs   = lt_blntab-bukrs.
       ls_return-belnr   = lt_blntab-belnr.
       ls_return-gjahr   = lt_blntab-gjahr.
       IF lv_subrc = 0.
         ls_return-flag    = 'S'.
       ELSE.
         ls_return-flag    = 'E'.
       ENDIF.
       ls_return-message = lv_message.
       APPEND ls_return TO pt_return.
       CALL FUNCTION 'POSTING_INTERFACE_END'
          EXCEPTIONS
            session_not_processable = 1
            OTHERS                  = 2.
 
 
 ENDFORM.                    " FRM_CREATE_SH
```



































