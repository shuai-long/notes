# 1. 创建检验计划

常用表：PLMK

事物码：QP01

创建BAPI：BAPI_INSPECTIONPLAN_CREATE

[参考链接](https://blog.csdn.net/z_x_xing_/article/details/90172812)

```abap
  method call_bapi_inspectionplan_creat.

    data: ls_alv type ty_alv.

    data: lv_testrun                type bapiflag,
          lv_profile                type profid_std,
          lv_group                  type plnnr,
          lv_groupcounter           type plnal,
          lt_task                   type standard table of bapi1191_tsk_c,
          lt_materialtaskallocation type standard table of bapi1191_mtk_c,
          lt_operation              type standard table of bapi1191_opr_c,
          lt_referenceoperation     type standard table of bapi1191_ref_opr_c,
          lt_productionresource     type standard table of bapi1191_prt_c,
          lt_inspcharacteristic     type standard table of bapi1191_cha_c,
          lt_textallocation         type standard table of bapi1191_txt_hdr_c,
          lt_text                   type standard table of bapi1012_txt_c,
          lt_return                 type standard table of bapiret2,
          lt_insp_char_values       type standard table of bapi1191_chv_c.

    move-corresponding cs_data to ls_alv.

*    lv_profile = '0000001'.

    "抬头数据
    lt_task = value #(
    ( valid_from           = ls_alv-sttag  " 有效开始日期
      task_list_usage      = ls_alv-verwe  " 用途
      task_list_status     = ls_alv-statu  " 状态
      lot_size_from        = '0'           " 从批量
      lot_size_to          = '9999999999'  " 到批量
      planning_work_center = ''            " 计划员组
      plant                = ls_alv-werks  " 工厂
      task_measure_unit    = ls_alv-plnme  " 单位
*      dyn_modif_level      = ''            " 动态修改参数的级别
    ) ).

    "工序
    lt_operation = value #(
    ( valid_from             = ls_alv-sttag " 有效开始日期
      activity               = ls_alv-vornr " 操作/活动编号
      plant                  = ls_alv-werks " 工厂
      control_key            = ls_alv-steus " 控制码
      work_cntr              = ''           " 工作中心
      standard_text_key      = ''           " 标准文本码
      description            = ''           " 工序短文本
      operation_measure_unit = ls_alv-plnme " 作业/工序的计量单位
      denominator            = '1'          " 用于转换工艺路线和工序单位的分母
      nominator              = '1'          " 用于转换任务清单和工序计量单位的计数器
      base_quantity          = '1.000'      " 基本数量
    ) ).

    "检验特性
    lt_inspcharacteristic = value #(
    ( activity                = ls_alv-vornr     " 操作/活动编号
      inspchar                = '0010'           " 检验特性编号
      valid_from              = ls_alv-sttag     " 有效起始日期
      mstr_char               = ls_alv-verwmerkm " 主文件检验特性
      pmstr_char              = ls_alv-werks     " 工厂
      smpl_procedure          = ls_alv-qstprver  " 检验特性中的采样程序
      smpl_unit               = ls_alv-plnme     " 采样计量单位
      smpl_quant              = '1.00'           " 采样的采样数量因子（多重采样计量单位）
      cha_master_import_modus = 'C'              " 复制主检验特征数据

    ) ).


    call function 'BAPI_INSPECTIONPLAN_CREATE'
      exporting
        testrun                = lv_testrun
        profile                = lv_profile
      importing
        group                  = lv_group
        groupcounter           = lv_groupcounter
      tables
        task                   = lt_task
        materialtaskallocation = lt_materialtaskallocation
        operation              = lt_operation
        referenceoperation     = lt_referenceoperation
        productionresource     = lt_productionresource
        inspcharacteristic     = lt_inspcharacteristic
        textallocation         = lt_textallocation
        text                   = lt_text
        return                 = lt_return
        insp_char_values       = lt_insp_char_values.

    if not line_exists( lt_return[ type = 'E' ] ) and
       not line_exists( lt_return[ type = 'A' ] ) and
       not line_exists( lt_return[ type = 'X' ] ).

      ls_alv-status  = icon_led_green.
      ls_alv-message = |导入成功：{ lv_group }|.

      call function 'BAPI_TRANSACTION_COMMIT'
        exporting
          wait = 'X'.

    else.

      ls_alv-status = icon_led_red.

      loop at lt_return into data(ls_return) where type ca 'AEX'.
        message id ls_return-id type ls_return-type number ls_return-number
          with ls_return-message_v1 ls_return-message_v2 ls_return-message_v3 ls_return-message_v4
          into data(lv_message).

        ls_alv-message = |{ ls_alv-message }; { lv_message }|.

      endloop.

      call function 'BAPI_TRANSACTION_ROLLBACK'.

    endif.

    move-corresponding ls_alv to cs_data.

  endmethod.
```

## 2. 更改检验计划

更改检验计划的物料分配

```abap
  method call_bapi_inspectionplan_chang.

    data: ls_alv type ty_alv.

    data: lv_change_no                   type aennr,
          lv_key_date                    type datum,
          lv_group                       type plnnr,
          lv_groupcounter                type plnal,
          lv_material                    type matnr18,
          lv_plant                       type werks_d,
          ls_task                        type bapi_es_task_maint_tsk,
          ls_task_x                      type bapi_es_task_maint_tsk_x,
          lv_material_long               type matnr40,
          lt_material_task_allocations   type standard table of bapi_es_task_maint_mtk,
          lt_material_task_allocations_x type standard table of bapi_es_task_maint_mtk_x,
          lt_sequences                   type standard table of bapi_es_task_maint_seq,
          lt_sequences_x                 type standard table of bapi_es_task_maint_seq_x,
          lt_operations                  type standard table of bapi_es_task_maint_opr,
          lt_operations_x                type standard table of bapi_es_task_maint_opr_x,
          lt_suboperations               type standard table of bapi_es_task_maint_sopr,
          lt_suboperations_x             type standard table of bapi_es_task_maint_sopr_x,
          lt_production_resources_tools  type standard table of bapi_es_task_maint_prt,
          lt_production_resources_toolsx type standard table of bapi_es_task_maint_prt_x,
          lt_inspection_characteristics  type standard table of bapi_es_task_maint_cha,
          lt_inspection_characteristicsx type standard table of bapi_es_task_maint_cha_x,
          lt_inspection_values           type standard table of bapi_es_task_maint_chv,
          lt_inspection_values_x         type standard table of bapi_es_task_maint_chv_x,
          lt_referenced_operations       type standard table of bapi_es_task_maint_ropr,
          lt_text                        type standard table of bapi_es_task_maint_txt,
          lt_text_allocations            type standard table of bapi_es_task_maint_txthdr,
          lt_return                      type standard table of bapiret2.

    move-corresponding cs_data to ls_alv.

    lv_key_date     = sy-datum.
    lv_group        = ls_alv-plnnr.    "组
    lv_groupcounter = '01'.            "组计数器
    lv_plant        = ls_alv-werks.    "工厂

    "分配物料
    lt_material_task_allocations = value #(
    ( maintain_mode = 'C'
      material      = ls_alv-matnr  "物料编号
      plant         = ls_alv-werks  "工厂
    ) ).

    call function 'BAPI_INSPECTIONPLAN_CHANGE'
      exporting
        change_no                   = lv_change_no
        key_date                    = lv_key_date
        group                       = lv_group
        groupcounter                = lv_groupcounter
*       material                    = lv_material
        plant                       = lv_plant
*       task                        = ls_task
*       task_x                      = ls_task_x
*       material_long               = lv_material_long
      tables
        material_task_allocations   = lt_material_task_allocations
        material_task_allocations_x = lt_material_task_allocations_x
*       sequences                   = lt_sequences
*       sequences_x                 = lt_sequences_x
*       operations                  = lt_operations
*       operations_x                = lt_operations_x
*       suboperations               = lt_suboperations
*       suboperations_x             = lt_suboperations_x
*       production_resources_tools  = lt_production_resources_tools
*       production_resources_tools_x = lt_production_resources_toolsx
*       inspection_characteristics  = lt_inspection_characteristics
*       inspection_characteristics_x = lt_inspection_characteristicsx
*       inspection_values           = lt_inspection_values
*       inspection_values_x         = lt_inspection_values_x
*       referenced_operations       = lt_referenced_operations
*       text                        = lt_text
*       text_allocations            = lt_text_allocations
        return                      = lt_return.

    if not line_exists( lt_return[ type = 'E' ] ) and
       not line_exists( lt_return[ type = 'A' ] ) and
       not line_exists( lt_return[ type = 'X' ] ).

      ls_alv-status  = icon_led_green.
      ls_alv-message = |导入成功：{ lv_group }|.

      call function 'BAPI_TRANSACTION_COMMIT'
        exporting
          wait = 'X'.

    else.

      ls_alv-status = icon_led_red.

      loop at lt_return into data(ls_return) where type ca 'AEX'.
        message id ls_return-id type ls_return-type number ls_return-number
          with ls_return-message_v1 ls_return-message_v2 ls_return-message_v3 ls_return-message_v4
          into data(lv_message).

        ls_alv-message = concatenated_string(
          exporting
            iv_string = ls_alv-message
            iv_append = lv_message ).

      endloop.

      call function 'BAPI_TRANSACTION_ROLLBACK'.

    endif.

    move-corresponding ls_alv to cs_data.

  endmethod.
```

