## 生成会计凭证

```ABAP
TYPES:BEGIN OF ty_mnpz_alv,
        pzbh         TYPE int4,                "记录第几张凭证
        comp_code    TYPE bapiache09-comp_code, "公司代码
        doc_type     TYPE bapiache09-doc_type, "凭证类型
        doc_date     TYPE bapiache09-doc_date, "凭证日期
        pstng_date   TYPE bapiache09-pstng_date, "过账日期
        fis_period   TYPE bapiache09-fis_period, "过账期间
        currency     TYPE bapiaccr09-currency, "币种
        header_txt   TYPE bapiache09-header_txt, "抬头文本
        itemno_acc   TYPE bapiacgl09-itemno_acc, "行项目编号
        bschl        TYPE zexten-bschl, "过账代码
        customer     TYPE bapiacgl09-customer, " 供应商/客户
        alloc_nmbr   TYPE bapiacgl09-alloc_nmbr, " 分配号
        gl_account   TYPE bapiacgl09-gl_account, "科目
        po_number    TYPE bapiacgl09-po_number, "采购订单
        po_item      TYPE bapiacgl09-po_item, "采购订单行项目
        gl_accountxt TYPE char50, "科目描述
        amt_doccur   TYPE dmbtr, "bapiaccr09-amt_doccur, "金额
        profit_ctr   TYPE bapiacgl09-profit_ctr, "利润中心
        costcenter   TYPE bapiacgl09-costcenter, "成本中心
        wbs_element  TYPE bapiacgl09-wbs_element, "WBS编号
        item_text    TYPE bapiacgl09-item_text, "行项目文本
        zzfi01       TYPE zexten-zzfi01, "业务板块
        zzfi02       TYPE zexten-zzfi02, "合同
        zzfi05       TYPE zexten-zzfi05, "供应商
        zzfi06       TYPE zexten-zzfi06, "部门
        zzfi07       TYPE zexten-zzfi07, "是否在岗
        rstgr        TYPE zexten-rstgr, "原因代码
        esrre        TYPE zexten-esrre, "ISR/QR 参考编号
        funds_ctr    TYPE bapiacgl09-funds_ctr, "基金中心
        cmmt_item    TYPE bapiacgl09-cmmt_item, "承诺项目
        style        TYPE lvc_t_styl,
      END OF ty_mnpz_alv.

DATA: gt_mnpz_alv TYPE TABLE OF ty_mnpz_alv, "模拟凭证展示ALV

FORM frm_create_belnr.
  DATA: lv_count TYPE int4, "记录一共需要生成多少张凭证
        lv_index TYPE int4. "记录一共生成了几张凭证.
  DATA:lv_message(250).
  DATA:lv_message1(250).

  DATA:lt_return1           TYPE TABLE OF bapiret2,     "返回消息
       ls_return1           TYPE bapiret2,
       lt_return2           TYPE TABLE OF bapiret2,     "返回消息
       ls_return2           TYPE bapiret2,
       lt_documentheader    TYPE TABLE OF bapiache09,   "抬头
       ls_documentheader    TYPE bapiache09,
       lt_currencyamount    TYPE TABLE OF bapiaccr09,   "货币
       ls_currencyamount    TYPE bapiaccr09,
       ls_accountreceivable TYPE bapiacar09,                    "客户项目 工作区
       lt_accountreceivable TYPE STANDARD TABLE OF bapiacar09,  "客户项目 内表
       lt_accountpayable    TYPE TABLE OF bapiacap09,   "供应商
       ls_accountpayable    TYPE bapiacap09,
       lt_accountgl         TYPE TABLE OF bapiacgl09,   "总账科目项
       ls_accountgl         TYPE bapiacgl09,
       ls_zexten            TYPE zexten,
       ls_zexten_ps         TYPE zexten_ps,
       lt_extension2        TYPE TABLE OF bapiparex,    "BAPI 参数 xtensionIn/ExtensionOut 的参考结构
       ls_extension2        TYPE bapiparex.
  DATA:lv_type    TYPE bapiache09-obj_type,
       lv_psp_pnr TYPE ps_psp_pnr,
       lv_key     TYPE bapiache09-obj_key,
       lv_sys     TYPE bapiache09-obj_sys.
  DATA:lv_return_message TYPE char255.

  DELETE gt_mnpz_alv WHERE  gl_account IS INITIAL.

  SORT gt_mnpz_alv BY pzbh DESCENDING.
  READ TABLE gt_mnpz_alv INTO DATA(ls_mnpz_alv) INDEX 1.
  IF sy-subrc = 0.
    lv_count = ls_mnpz_alv-pzbh.
  ENDIF.

  DO lv_count TIMES.
    ADD 1 TO lv_index.

    READ TABLE gt_mnpz_alv INTO ls_mnpz_alv WITH KEY pzbh = lv_index.
    IF sy-subrc = 0.
      CLEAR ls_documentheader.
      ls_documentheader-comp_code  = ls_mnpz_alv-comp_code.    "公司代码
      ls_documentheader-doc_type   = ls_mnpz_alv-doc_type.     "凭证类型
      ls_documentheader-doc_date   = ls_mnpz_alv-doc_date.     "凭证日期
      ls_documentheader-pstng_date = ls_mnpz_alv-pstng_date.   "过账日期
      ls_documentheader-fis_period = ls_mnpz_alv-fis_period.   "记账期间
      ls_documentheader-header_txt = ls_mnpz_alv-header_txt.   "凭证抬头文本
      ls_documentheader-username   = sy-uname.                 "用户名
    ENDIF.

    LOOP AT gt_mnpz_alv INTO ls_mnpz_alv WHERE pzbh = lv_index.

      CASE ls_mnpz_alv-bschl.
        WHEN '01' OR '11'.
          ls_accountreceivable-itemno_acc = ls_mnpz_alv-itemno_acc.            "会计凭证中的行项目数
          ls_accountreceivable-customer   = ls_mnpz_alv-customer.         "客户
          ls_accountreceivable-gl_account = ls_mnpz_alv-gl_account.
          ls_accountreceivable-profit_ctr = ls_mnpz_alv-profit_ctr.
          ls_accountreceivable-item_text  = ls_mnpz_alv-item_text.
          ls_accountreceivable-alloc_nmbr  = ls_mnpz_alv-alloc_nmbr.     "分配号
          APPEND ls_accountreceivable TO lt_accountreceivable.
          CLEAR ls_accountreceivable.

        WHEN '21' OR '31'.
          ls_accountpayable-itemno_acc = ls_mnpz_alv-itemno_acc.       "会计凭证中的行项目数
          ls_accountpayable-vendor_no = ls_mnpz_alv-customer.
          ls_accountpayable-gl_account = ls_mnpz_alv-gl_account.
          ls_accountpayable-profit_ctr = ls_mnpz_alv-profit_ctr.
          ls_accountpayable-item_text  = ls_mnpz_alv-item_text.
          ls_accountpayable-alloc_nmbr  = ls_mnpz_alv-alloc_nmbr.     "分配号
          APPEND ls_accountpayable TO lt_accountpayable.
          CLEAR ls_accountpayable.
        WHEN '40' OR '50'.
          ls_accountgl-itemno_acc = ls_mnpz_alv-itemno_acc.     "会计凭证中的行项目数
          ls_accountgl-gl_account = ls_mnpz_alv-gl_account.    "总账科目
          ls_accountgl-costcenter = ls_mnpz_alv-costcenter.    "成本中心成本中心
          ls_accountgl-profit_ctr = ls_mnpz_alv-profit_ctr.    "利润中心
          ls_accountgl-item_text  = ls_mnpz_alv-item_text.     "项目文本
          ls_accountgl-wbs_element = ls_mnpz_alv-wbs_element.  "WBS
          ls_accountgl-cmmt_item  = ls_mnpz_alv-cmmt_item.     "承诺项目
          ls_accountgl-funds_ctr  = ls_mnpz_alv-funds_ctr.     "基金中心
          ls_accountgl-alloc_nmbr  = ls_mnpz_alv-alloc_nmbr.     "分配号
          ls_accountgl-po_number   = ls_mnpz_alv-po_number.
          ls_accountgl-po_item = ls_mnpz_alv-po_item.
          APPEND ls_accountgl TO lt_accountgl.
          CLEAR ls_accountgl.
      ENDCASE.
      
      

*---------------------增强结构-------------------------------------*
      CLEAR: ls_zexten,ls_extension2.
      ls_zexten-posnr  = ls_mnpz_alv-itemno_acc.
      ls_zexten-bschl  = ls_mnpz_alv-bschl.                 "过账码
      ls_zexten-bupla  = 'A1'.                              "业务地点
      ls_zexten-zzfi01 = ls_mnpz_alv-zzfi01.                "板块业务
      ls_zexten-zzfi02 = ls_mnpz_alv-zzfi02.                "合同
      ls_zexten-zzfi05 = ls_mnpz_alv-zzfi05.                "供应商
      ls_zexten-zzfi06 = ls_mnpz_alv-zzfi06.                "成本中心
      ls_zexten-zzfi07 = ls_mnpz_alv-zzfi07.                "在岗
      ls_zexten-rstgr  = ls_mnpz_alv-rstgr.                  "付款原因代码
      ls_zexten-esrre = ls_mnpz_alv-esrre.                   "ISR/QR 参考编号
      ls_extension2-structure  = 'ZEXTEN'.
      ls_extension2-valuepart1 = ls_zexten.
      ls_extension2-valuepart4 = ls_mnpz_alv-esrre.
      APPEND ls_extension2 TO lt_extension2.
      CLEAR:lv_psp_pnr.

*---------------------金额字段-------------------------------------*
      ls_currencyamount-itemno_acc = ls_mnpz_alv-itemno_acc.
      ls_currencyamount-amt_doccur = ls_mnpz_alv-amt_doccur.   "凭证货币金额
      IF ls_mnpz_alv-bschl EQ '50' OR ls_mnpz_alv-bschl EQ '11' OR ls_mnpz_alv-bschl EQ '31'.
        ls_currencyamount-amt_doccur = 0 - ls_currencyamount-amt_doccur.   "凭证货币金额
      ENDIF.

      ls_currencyamount-currency = ls_mnpz_alv-currency.
      APPEND ls_currencyamount TO lt_currencyamount.
      CLEAR:ls_currencyamount.

    ENDLOOP.

    "调用会计凭证记账前的检查
    CALL FUNCTION 'BAPI_ACC_DOCUMENT_CHECK'
      EXPORTING
        documentheader    = ls_documentheader
      TABLES
        accountgl         = lt_accountgl
        accountreceivable = lt_accountreceivable  "客户项目
        accountpayable    = lt_accountpayable     "供应商项目
        currencyamount    = lt_currencyamount
        return            = lt_return1
        extension2        = lt_extension2.

    "调用过账
    CALL FUNCTION 'BAPI_ACC_DOCUMENT_POST'
      EXPORTING
        documentheader    = ls_documentheader
      IMPORTING
        obj_type          = lv_type
        obj_key           = lv_key
        obj_sys           = lv_sys
      TABLES
        accountgl         = lt_accountgl
        accountreceivable = lt_accountreceivable  "客户项目
        accountpayable    = lt_accountpayable     "供应商项目
        currencyamount    = lt_currencyamount
        return            = lt_return2
        extension2        = lt_extension2.


    IF line_exists( lt_return2[ type = 'E' ] ) OR
    line_exists( lt_return2[ type = 'X' ] ) OR
    line_exists( lt_return2[ type = 'A' ] ).
      CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
      LOOP AT lt_return2 INTO ls_return2 WHERE type EQ 'E' OR type EQ 'A'.
        CALL FUNCTION 'MESSAGE_TEXT_BUILD'
          EXPORTING
            msgid               = ls_return2-id
            msgnr               = ls_return2-number
            msgv1               = ls_return2-message_v1
            msgv2               = ls_return2-message_v2
            msgv3               = ls_return2-message_v3
            msgv4               = ls_return2-message_v4
          IMPORTING
            message_text_output = lv_message.
        lv_message1 = lv_message1 && lv_message.
      ENDLOOP.

      LOOP AT gt_alv ASSIGNING FIELD-SYMBOL(<fs_alv>) WHERE pzbh EQ lv_index AND jzpz IS INITIAL..
        <fs_alv>-message = lv_message1.
        <fs_alv>-status =  icon_led_red.
      ENDLOOP.
    ELSE.

      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
        EXPORTING
          wait = abap_true.
    ENDIF.
    CLEAR lv_message.
  ENDDO.
ENDFORM.
```

## 冲销会计凭证

```abap
FORM frm_pzcx .
  DATA:BEGIN OF ls_belnr,
         belnr TYPE rbkp-belnr,
       END OF ls_belnr.
  DATA:lt_belnr LIKE TABLE OF ls_belnr.
  DATA:lv_message(250) TYPE c.

  DATA: lv_lgo_sys    TYPE tbdls-logsys,
        ls_reversal   TYPE bapiacrev,
        lv_bus_act    TYPE bapiache09-bus_act,
        lv_obj_key    TYPE bapiacrev-obj_key,
        lt_return     TYPE STANDARD TABLE OF bapiret2,
        lv_error_flag TYPE c.

  DATA:lt_data_log TYPE TABLE OF zfit_057,
       ls_data_log TYPE zfit_057.

  "弹窗的内容
  DATA: it_value  TYPE TABLE OF sval,
        wa_value  TYPE sval,
        lv_rtn_cd.

  DATA:lv_stgrd TYPE uf05a-stgrd,
       lv_budat TYPE bkpf-budat,
       lv_monat TYPE bkpf-monat.

  IF NOT line_exists( gt_alv[ checkbox = 'X' ] ).
    MESSAGE '请选择数据' TYPE 'E'.
  ENDIF.

  CLEAR:it_value,wa_value,lv_rtn_cd.
  wa_value-tabname = 'UF05A'.
  wa_value-fieldname = 'STGRD'.
  wa_value-value = '03'.
  APPEND wa_value TO it_value.
  CLEAR wa_value.

  wa_value-tabname = 'BKPF'.
  wa_value-fieldname = 'BUDAT'.
  wa_value-value = sy-datum.
  APPEND wa_value TO it_value.
  CLEAR wa_value.

  wa_value-tabname = 'BKPF'.
  wa_value-fieldname = 'MONAT'.
  wa_value-value = sy-datum+4(2).
  APPEND wa_value TO it_value.
  CLEAR wa_value.

  CALL FUNCTION 'POPUP_GET_VALUES'
    EXPORTING
      popup_title = '选择'
    IMPORTING
      returncode  = lv_rtn_cd
    TABLES
      fields      = it_value.

  IF lv_rtn_cd IS INITIAL.

    READ TABLE it_value INTO wa_value WITH KEY fieldname = 'STGRD'.
    IF sy-subrc = 0 AND wa_value-value IS NOT INITIAL.
      lv_stgrd = wa_value-value.
    ELSE.
      MESSAGE '请填写冲销原因' TYPE 'E'.
    ENDIF.

    READ TABLE it_value INTO wa_value WITH KEY fieldname = 'BUDAT'.
    IF sy-subrc = 0 AND wa_value-value IS NOT INITIAL.
      lv_budat = wa_value-value.
    ELSE.
      MESSAGE '请填写过账日期' TYPE 'E'.
    ENDIF.

    READ TABLE it_value INTO wa_value WITH KEY fieldname = 'MONAT'.
    IF sy-subrc = 0 AND wa_value-value IS NOT INITIAL.
      lv_monat = wa_value-value.
    ELSE.
      MESSAGE '请选择过账期间' TYPE 'E'.
    ENDIF.

  ELSE.
    MESSAGE '用户已取消' TYPE 'E'.
  ENDIF.

  LOOP AT gt_alv INTO DATA(ls_alv) WHERE checkbox = 'X' AND jzpz IS NOT INITIAL.
    READ TABLE lt_belnr INTO ls_belnr WITH KEY belnr = ls_alv-jzpz.
    IF sy-subrc <> 0.
      ls_belnr-belnr = ls_alv-jzpz.
      APPEND ls_belnr TO lt_belnr.
      CLEAR ls_belnr.
    ENDIF.
  ENDLOOP.

  IF lt_belnr[] IS NOT INITIAL.
    SELECT
    belnr,
    awtyp,
    awkey,
    budat,
    monat,
    glvor
    INTO TABLE @DATA(lt_bkpf)
          FROM bkpf
          FOR ALL ENTRIES IN @lt_belnr
          WHERE bukrs = @p_bukrs
          AND   belnr = @lt_belnr-belnr
          AND   gjahr = @p_gjahr.
  ENDIF.

  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = lv_lgo_sys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.


  LOOP AT lt_bkpf INTO DATA(ls_bkpf).
    ls_reversal = VALUE #(
    obj_type   = ls_bkpf-awtyp
    obj_key    = ls_bkpf-awkey
    obj_sys    = lv_lgo_sys
    obj_key_r  = ls_bkpf-awkey
    pstng_date = lv_budat
    fis_period = lv_monat
    comp_code  = p_bukrs
*    ac_doc_no  = p_belnr
    reason_rev = lv_stgrd ).

    lv_bus_act = ls_bkpf-glvor.

    CALL FUNCTION 'BAPI_ACC_DOCUMENT_REV_POST'
      EXPORTING
        reversal = ls_reversal
        bus_act  = lv_bus_act
      IMPORTING
*       OBJ_TYPE =
        obj_key  = lv_obj_key
*       OBJ_SYS  =
      TABLES
        return   = lt_return.

    LOOP AT lt_return INTO DATA(wa_return).
      lv_message = lv_message && wa_return-message .
    ENDLOOP.

    LOOP AT gt_alv ASSIGNING FIELD-SYMBOL(<fs_alv>) WHERE jzpz EQ ls_bkpf-belnr.
      IF line_exists( lt_return[ type = 'E' ] ) OR
      line_exists( lt_return[ type = 'X' ] ) OR
      line_exists( lt_return[ type = 'A' ] ).
        CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
        lv_message = '凭证冲销失败,失败原因:' && lv_message.
        <fs_alv>-message = lv_message.
      ELSE.
        <fs_alv>-jzrq = ''.
        <fs_alv>-jzpz = ''.
        <fs_alv>-cxpz = ls_bkpf-belnr.

        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
          EXPORTING
            wait = abap_true.

      ENDIF.
    ENDLOOP.
    CLEAR:lv_message,ls_reversal,lv_bus_act.
    REFRESH: lt_return ,lt_data_log.
  ENDLOOP.
  MESSAGE:'冲销已完成' TYPE 'S'.
ENDFORM.

```

