# 1. 检验特性

## 1.1 获取主文件检验特性

根据工厂获取下一个特性编号

```abap
call function 'NUMBER_GET_NEXT'.
	EXPORTING
    nr_range_nr = '01'
    object      = 'QMERKMALE'
    subobject   = t_data-werks
    quantity    = '1'
  IMPORTING
*   returncode  = lt_return
    number      = t_data-mkmnr.
```

## 1.2 创建检验特性

[参考链接](https://blog.csdn.net/geyulai/article/details/125327488)

事物码：QS21

常用表：QPMK，QPMT，QPMZ

检验特性创建分为：定性特征和定量特征

使用 BAPI：BAPI_QPMK_SAVEREPLICA

创建 = '009'

修改 = '004'

报表字段：![报表字段](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20241012111831181.png)

代码：

```abap
    data: ls_alv type ty_alv.

    data: lv_save             type c,
          lv_no_commit        type c,
          lt_mstcha           type standard table of bapiqpmk_qpmk,
          lt_mstcha_insp_meth type standard table of bapiqpmk_qpmz,
          lt_mstcha_text      type standard table of bapiqpmk_qpmt,
          lt_mstcha_longtext  type standard table of bapiqpmk_longtext,
          lt_mstcha_smpl_ltxt type standard table of bapiqpmk_smpl_ltxt,
          lt_return           type standard table of bapiret2,
          lt_extension1       type standard table of bapiextc.

    move-corresponding cs_data to ls_alv.

    lv_save = 'X'.

    lt_mstcha = value #(
    ( function     = '009'                                        " 创建
      plant_qpmk   = ls_alv-werks                                 " 工厂
      mstr_char    = ls_alv-mkmnr                                 " 主检验特性
      vmstr_char   = '000001'                                     " 主检验特性版本
      valid_from   = ls_alv-gueltigab                             " 有效起始日期
      sort_field   = ls_alv-sortfeld                              " 搜索字段
      status       = ls_alv-loekz                                 " 主记录的状态
      ind_complete = ls_alv-konsistent                            " 复制模型/参考特性

      ind_control  = value qmkst( quantitat = ' '                 " 数量特性：X=定量； 空=定性
                                  pruefkat  = ls_alv-pruefkat     " 特性属性
                                  stichpr   = ls_alv-stichpr      " 采样过程
                                  estukz    = ' '                 " 汇总记录，+=单个结果
                                  rzwang    = 'X'                 " X=所需特征 空=可选特征
                                  pumfkz    = '='                 " ==固定范围 空=范围未固定
                               )
    ) ).


    lt_mstcha_insp_meth = value #(
    ( plant_qpmk             = ls_alv-werks                       "工厂
      mst_insp_cha           = ls_alv-mkmnr                       "检验特性
      vmstr_char             = '000001'                           "检验特性版本
      ind_selset_or_codegrp1 = 'X'                                "目录
      cat_type1              = '1'                                "目录类型
      sel_set1               = 'B01'                              "选择集
      psel_set1              = '1000'                             "工厂
    ) ).

    lt_mstcha_text = value #(
    ( plant_qpmk = ls_alv-werks                                   " 工厂
      mstr_char  = ls_alv-mkmnr                                   " 主检验特性
      vmstr_char = '000001'                                       " 主检验特性版本
      valid_from = ls_alv-gueltigab                               " 有效起始日期
      langu      = '1'                                            " 语言
      char_descr = ls_alv-kurztext                                " 短文本

    ) ).


    call function 'BAPI_QPMK_SAVEREPLICA'
      exporting
        i_save           = lv_save
        i_no_commit      = lv_no_commit
      tables
        mstcha           = lt_mstcha
        mstcha_insp_meth = lt_mstcha_insp_meth
        mstcha_text      = lt_mstcha_text
        mstcha_longtext  = lt_mstcha_longtext
        mstcha_smpl_ltxt = lt_mstcha_smpl_ltxt
        return           = lt_return
        extension1       = lt_extension1.

    if not line_exists( lt_return[ type = 'E' ] ) and
       not line_exists( lt_return[ type = 'A' ] ) and
       not line_exists( lt_return[ type = 'X' ] ).

      ls_alv-status  = icon_led_green.
      ls_alv-message = '导入成功'.

      call function 'BAPI_TRANSACTION_COMMIT'
        exporting
          wait = 'X'.

    else.

      ls_alv-status = icon_led_red.

      loop at lt_return into data(ls_return) where type ca 'AEX'.
        message id ls_return-id type ls_return-type number ls_return-number
          with ls_return-message_v1 ls_return-message_v2 ls_return-message_v3 ls_return-message_v4
          into data(lv_message).

        ls_alv-message = |{ ls_alv-message }; { lv_message }|.

      endloop.

      call function 'BAPI_TRANSACTION_ROLLBACK'.

    endif.

    move-corresponding ls_alv to cs_data.
```

