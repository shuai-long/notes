## 创建项目

```abap
  CALL FUNCTION 'BAPI_PS_INITIALIZATION'.
  
  DATA: LS_PROJECT_DEFINITION TYPE BAPI_BUS2001_NEW,
        LT_RETURN             TYPE STANDARD TABLE OF BAPIRET2.
        
*  项目名称
  LS_PROJECT_DEFINITION-DESCRIPTION = GS_ALV_0100-POST1.
*  立项类型
  LS_PROJECT_DEFINITION-LXLX = GS_ALV_0100-ZLXLX.
*  项目编号
  LS_PROJECT_DEFINITION-PROJECT_DEFINITION = GS_ALV_0100-PSPID.
*  项目报备编号
  LS_PROJECT_DEFINITION-BBBH = GS_ALV_0100-ZBBBH.
*  项目类型
  LS_PROJECT_DEFINITION-XMLX = GS_ALV_0100-ZXMLX.
*  公司代码
  LS_PROJECT_DEFINITION-COMPANY_CODE = GS_ALV_0100-BUKRS.
*  利润中心
  LS_PROJECT_DEFINITION-PROFIT_CTR = GS_ALV_0100-PRCTR.
*  成本中心

*  经营部门
  LS_PROJECT_DEFINITION-JYBM = GS_ALV_0100-ZJYBM.
  CALL FUNCTION 'Z_PS_BAPI_BUS2001_CREATE'
    EXPORTING
      I_PROJECT_DEFINITION = LS_PROJECT_DEFINITION
    TABLES
      ET_RETURN            = LT_RETURN.

  IF LINE_EXISTS( LT_RETURN[ TYPE = 'E' ] ) OR
     LINE_EXISTS( LT_RETURN[ TYPE = 'A' ] ) OR
     LINE_EXISTS( LT_RETURN[ TYPE = 'X' ] ).
    CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
  ELSE.
    CALL FUNCTION 'BAPI_PS_PRECOMMIT'.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' .
  ENDIF.
```

可重写创建项目的函数，代码如下所示： `Z_PS_BAPI_BUS2001_CREATE`

```abap
FUNCTION z_ps_bapi_bus2001_create.
*"----------------------------------------------------------------------
*"*"本地接口：
*"  IMPORTING
*"     VALUE(I_PROJECT_DEFINITION) LIKE  BAPI_BUS2001_NEW STRUCTURE
*"        BAPI_BUS2001_NEW
*"  TABLES
*"      ET_RETURN STRUCTURE  BAPIRET2 OPTIONAL
*"      EXTENSIONIN STRUCTURE  BAPIPAREX OPTIONAL
*"      EXTENSIONOUT STRUCTURE  BAPIPAREX OPTIONAL
*"----------------------------------------------------------------------
DATA: lt_extensionin TYPE STANDARD TABLE OF bapiparex,
      wa_extensionin TYPE bapiparex.

"输入参数，扩展字段
DATA:  wa_bapi_te_project_definition TYPE bapi_te_project_definition."这是多出来的,要定义扩充的结构

FIELD-SYMBOLS: <wa>   TYPE any.
CLEAR:extensionin,extensionin[],lt_extensionin,wa_extensionin,wa_bapi_te_project_definition.

MOVE-CORRESPONDING i_project_definition TO wa_bapi_te_project_definition.
wa_bapi_te_project_definition-project_definition     = i_project_definition-project_definition. "要赋值项目定义

"这是多出来的，要把存储扩展信息的结构放到wa_EXTENSIONIN的structure
CLEAR: wa_extensionin.
wa_extensionin-structure = 'BAPI_TE_PROJECT_DEFINITION'.

ASSIGN wa_extensionin+30(960) TO <wa> CASTING TYPE bapi_te_project_definition.
<wa> = wa_bapi_te_project_definition.
APPEND wa_extensionin TO extensionin.

"创建项目
CALL FUNCTION 'BAPI_BUS2001_CREATE'
EXPORTING
i_project_definition = i_project_definition
TABLES
et_return            = et_return[]
extensionin          = extensionin[]
extensionout         = extensionout[].
ENDFUNCTION.
```



## 修改项目

```abap
DATA: LS_PROJECT_DEFINITION     TYPE BAPI_BUS2001_CHG,
      LS_PROJECT_DEFINITION_UPD TYPE BAPI_BUS2001_UPD,
      LT_RETURN                 TYPE TABLE OF BAPIRET2.

"项目编号
LS_PROJECT_DEFINITION-PROJECT_DEFINITION = LS_DATA_IN-PROJECTCODE.
"项目描述
LS_PROJECT_DEFINITION-DESCRIPTION        = LS_DATA_IN-PROJECTNAME.
LS_PROJECT_DEFINITION_UPD-DESCRIPTION    = 'X'.
"公司代码
LS_PROJECT_DEFINITION-COMPANY_CODE       = LS_CEPC_BUKRS-BUKRS.
LS_PROJECT_DEFINITION_UPD-COMPANY_CODE   = 'X'.
"利润中心
LS_PROJECT_DEFINITION-PROFIT_CTR         = LS_DATA_IN-UNDERTAKEUNIT.
LS_PROJECT_DEFINITION_UPD-PROFIT_CTR     = 'X'.
"开始时间
LS_PROJECT_DEFINITION-START              = LS_DATA_IN-PROJECTSTARTDATE.
LS_PROJECT_DEFINITION_UPD-START          = 'X'.
"结束时间
LS_PROJECT_DEFINITION-FINISH             = LS_DATA_IN-PROJECTENDDATE.
LS_PROJECT_DEFINITION_UPD-FINISH         = 'X'.

"初始化
CALL FUNCTION 'BAPI_PS_INITIALIZATION'.

"更改
CALL FUNCTION 'ZPS_BAPI_BUS2001_CHANGE'
  EXPORTING
    I_PROJECT_DEFINITION     = LS_PROJECT_DEFINITION
    I_PROJECT_DEFINITION_UPD = LS_PROJECT_DEFINITION_UPD
  TABLES
    ET_RETURN                = LT_RETURN.

"预提交
CALL FUNCTION 'BAPI_PS_PRECOMMIT'
  TABLES
    ET_RETURN = LT_RETURN.

"提交
CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.
```

可重写修改项目的函数，代码如下所示：`ZPS_BAPI_BUS2001_CHANGE`

```abap
FUNCTION ZPS_BAPI_BUS2001_CHANGE.
*"--------------------------------------------------------------------
*"*"局部接口：
*"  IMPORTING
*"     VALUE(I_PROJECT_DEFINITION) LIKE  BAPI_BUS2001_CHG
*"  STRUCTURE  BAPI_BUS2001_CHG
*"     VALUE(I_PROJECT_DEFINITION_UPD) LIKE  BAPI_BUS2001_UPD
*"  STRUCTURE  BAPI_BUS2001_UPD
*"  TABLES
*"      ET_RETURN STRUCTURE  BAPIRET2 OPTIONAL
*"      EXTENSIONIN STRUCTURE  BAPIPAREX OPTIONAL
*"      EXTENSIONOUT STRUCTURE  BAPIPAREX OPTIONAL
*"--------------------------------------------------------------------
DATA: LT_EXTENSIONIN TYPE STANDARD TABLE OF BAPIPAREX,
     WA_EXTENSIONIN TYPE BAPIPAREX.
"输入参数，扩展字段
DATA:  WA_BAPI_TE_PROJECT_DEFINITION TYPE BAPI_TE_PROJECT_DEFINITION."这是多出来的,要定义扩充的结构

FIELD-SYMBOLS: <WA>   TYPE ANY.
CLEAR:EXTENSIONIN,EXTENSIONIN[],LT_EXTENSIONIN,WA_EXTENSIONIN,WA_BAPI_TE_PROJECT_DEFINITION.

MOVE-CORRESPONDING I_PROJECT_DEFINITION TO WA_BAPI_TE_PROJECT_DEFINITION.
WA_BAPI_TE_PROJECT_DEFINITION-PROJECT_DEFINITION     = I_PROJECT_DEFINITION-PROJECT_DEFINITION. "要赋值项目定义

CLEAR: WA_EXTENSIONIN.
WA_EXTENSIONIN-STRUCTURE = 'BAPI_TE_PROJECT_DEFINITION'."这是多出来的，要把存储扩展信息的结构放到wa_EXTENSIONIN的structure

ASSIGN WA_EXTENSIONIN+30(960) TO <WA> CASTING TYPE BAPI_TE_PROJECT_DEFINITION.
<WA> = WA_BAPI_TE_PROJECT_DEFINITION.
APPEND WA_EXTENSIONIN TO EXTENSIONIN.

CALL FUNCTION 'BAPI_BUS2001_CHANGE'
 EXPORTING
   I_PROJECT_DEFINITION     = I_PROJECT_DEFINITION
   I_PROJECT_DEFINITION_UPD = I_PROJECT_DEFINITION_UPD
 TABLES
   ET_RETURN                = ET_RETURN
   EXTENSIONIN              = EXTENSIONIN
   EXTENSIONOUT             = EXTENSIONOUT.

ENDFUNCTION.
```

