```abap
*&---------------------------------------------------------------------*
*& Report ZTEST_0026
*&---------------------------------------------------------------------*
*& 
*&---------------------------------------------------------------------*
REPORT ZTEST_0026.

TYPE-POOLS: SLIS.

TABLES: PERNR, PCL1, PCL2, T549S.
INFOTYPES: 0000, 0001, 0002, 0007, 2001, 2002, 2003, 0182, 2006, 2010.

DEFINE SET_FCAT.
  LV_POS = LINES( GT_FIELDCAT ) + 1.
  CLEAR:  LS_FIELDCAT.
  LS_FIELDCAT-COL_POS       = LV_POS.
  LS_FIELDCAT-KEY           = &1.
  LS_FIELDCAT-FIELDNAME     = &2.
  LS_FIELDCAT-REPTEXT       = &3.
  LS_FIELDCAT-JUST          = &4.
  LS_FIELDCAT-OUTPUTLEN     = &5.
  LS_FIELDCAT-DO_SUM        = &6.
  LS_FIELDCAT-NO_OUT        = &7.
  LS_FIELDCAT-SCRTEXT_L     = &3.
  LS_FIELDCAT-SCRTEXT_M     = &3.
  LS_FIELDCAT-SCRTEXT_S     = &3.
  APPEND LS_FIELDCAT TO  GT_FIELDCAT.
END-OF-DEFINITION.

DEFINE SET_SORT.
  LV_POS = LINES( GT_SORT ) + 1.
  CLEAR:  LS_SORT.
  LS_SORT-SPOS       = LV_POS.
  LS_SORT-FIELDNAME  = &1.
  LS_SORT-UP         = &2.
  LS_SORT-SUBTOT     = &3.
  APPEND LS_SORT TO GT_SORT.
END-OF-DEFINITION.

TYPES: BEGIN OF TS_DATA,
         ANZHL01 TYPE ANZHL,            "Plan working hours
         ANZHL02 TYPE ANZHL,            "Actual working hours
         ANZHL03 TYPE ANZHL,            "Late
         ANZHL04 TYPE ANZHL,            "Early leave
         ANZHL05 TYPE ANZHL,            "Absenteeism
         ANZHL06 TYPE ANZHL,            "Annual Leave
         ANZHL07 TYPE ANZHL,            "Personal Leave
         ANZHL08 TYPE ANZHL,            "Sick Leave
         ANZHL09 TYPE ANZHL,            "Marriage Leave
         ANZHL10 TYPE ANZHL,            "Maternity Leave
         ANZHL11 TYPE ANZHL,            "Prenatal Leave
         ANZHL12 TYPE ANZHL,            "Nursing Leave
         ANZHL13 TYPE ANZHL,            "The Nurses Leave
         ANZHL14 TYPE ANZHL,            "Bereavement Leave
         ANZHL15 TYPE ANZHL,            "Injury Leave
         ANZHL16 TYPE ANZHL,            "False Temperature Leave
         ANZHL17 TYPE ANZHL,            "Supplementary Holidays in Spring Festival
         ANZHL18 TYPE ANZHL,            "Time of in lieu
         ANZHL19 TYPE ANZHL,            "Overtime 150%
         ANZHL20 TYPE ANZHL,            "Overtime 200%
         ANZHL21 TYPE ANZHL,            "Overtime 300%
         ANZHL22 TYPE ANZHL,            "Overtime time off
         ANZHL23 TYPE ANZHL,            "Overtime Total
         ANZHL24 TYPE ANZHL,            "Business trip
         ANZHL25 TYPE ANZHL,            "Training
         ANZHL26 TYPE ANZHL,            "Out of office
         ANZHL27 TYPE ANZHL,            "Shuttle bus delay
         ANZHL28 TYPE ANZHL,            "Company event
         ANZHL29 TYPE ANZHL,            "Public
         ANZHL30 TYPE ANZHL,            "Benefit leave
         ANZHL31 TYPE ANZHL,            "Late meal subsidy
         ANZHL32 TYPE ANZHL,            "Afternoon shift allowance
         ANZHL33 TYPE ANZHL,            "Night shift allowance
         ANZHL34 TYPE ANZHL,            "Childcare Leave
         ANZHL35 TYPE ANZHL,            "Elderly Care Leave

         ANZHL36 TYPE ANZHL,            "Working Hours Bank
         ANZHL37 TYPE ANZHL,            "Actual Shift Hours
         ANZHL38 TYPE ANZHL,            "Standard Calendar Hours
       END OF TS_DATA.

TYPES: BEGIN OF TS_DATA_LINE,
         BEGDA TYPE D,
         ENDDA TYPE D,
         HOURS TYPE ANZHL,
       END OF TS_DATA_LINE.
TYPES: TT_DATA_LINE TYPE STANDARD TABLE OF TS_DATA_LINE WITH DEFAULT KEY.

DATA: GT_DATA_LINE TYPE TT_DATA_LINE.

FIELD-SYMBOLS: <FS_TABLE> TYPE STANDARD TABLE,
               <FS_STRUC> TYPE ANY.

DATA: GT_FIELDCAT TYPE LVC_T_FCAT,
      GT_SORT     TYPE LVC_T_SORT,
      GS_LAYOUT   TYPE LVC_S_LAYO.

DATA: GT_T552A TYPE TABLE OF T552A.

SELECTION-SCREEN BEGIN OF BLOCK B1 WITH FRAME TITLE TEXT-001.

  SELECTION-SCREEN BEGIN OF LINE.
    PARAMETERS: P_COUNT RADIOBUTTON GROUP G1 USER-COMMAND UC1.
    SELECTION-SCREEN COMMENT 2(15) T_COUNT FOR FIELD P_COUNT VISIBLE LENGTH 10.
    PARAMETERS: P_DETAIL RADIOBUTTON GROUP G1 DEFAULT 'X'.
    SELECTION-SCREEN COMMENT 19(15) T_DETAIL FOR FIELD P_DETAIL VISIBLE LENGTH 10.
  SELECTION-SCREEN END OF LINE.

  SELECTION-SCREEN SKIP.

  PARAMETERS: P_DAY TYPE NUM DEFAULT '01' MODIF ID S1.

SELECTION-SCREEN END OF BLOCK B1.

INITIALIZATION.
  PERFORM FRM_INIT_SCREEN.

AT SELECTION-SCREEN OUTPUT.
  PERFORM FRM_SET_SCREEN_OUT.

START-OF-SELECTION.
  PERFORM FRM_CHECK_INPUT.
  PERFORM FRM_GET_DATA_LINES.
  PERFORM FRM_CREATE_ALV.

GET PERNR.
  PERFORM FRM_APPEND_PERNR.

END-OF-SELECTION.
  PERFORM FRM_ALV_DISPLAY.


FORM FRM_INIT_SCREEN.
  %_P_DAY_%_APP_%    = '薪资结算开始日期'.
  T_COUNT   = '汇总表'.
  T_DETAIL  = '明细表'.
ENDFORM.

FORM FRM_SET_SCREEN_OUT.
  LOOP AT SCREEN.

    IF SCREEN-GROUP1 EQ 'S1'.
      IF P_COUNT EQ 'X'.
        SCREEN-ACTIVE = '0'.
      ELSE.
        SCREEN-ACTIVE = '1'.
      ENDIF.
    ENDIF.

    MODIFY SCREEN.
  ENDLOOP.
ENDFORM.

FORM FRM_CHECK_INPUT.

  IF PN-BEGDA = '18000101' OR PN-ENDDA = '99991231'.
    MESSAGE 'Please enter the period.' TYPE 'S' DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
    STOP.
  ENDIF.

  IF P_DETAIL IS NOT INITIAL AND P_DAY IS INITIAL.
    MESSAGE 'Please enter the split date.' TYPE 'S' DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
    STOP.
  ENDIF.

  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      TEXT = 'Ready read data...'.

ENDFORM.

FORM FRM_GET_DATA_LINES.

  IF P_COUNT EQ 'X'.
    P_DAY = '01'.
  ENDIF.

  PERFORM FRM_GET_MONTH_IN_TWO_DAYS USING PN-BEGDA PN-ENDDA P_DAY GT_DATA_LINE.

  REFRESH GT_T552A.

  SELECT * INTO CORRESPONDING FIELDS OF TABLE GT_T552A FROM T552A
    WHERE ZEITY EQ '2' AND MOFID EQ 'CN' AND MOSID EQ '28' AND SCHKZ EQ 'W120' AND KJAHR GE PN-BEGDA+0(4) AND KJAHR LE PN-ENDDA+0(4).

  LOOP AT GT_DATA_LINE ASSIGNING FIELD-SYMBOL(<FS_DATA_LINE>).
    PERFORM FRM_GET_STANDARD_HOURS TABLES GT_T552A USING <FS_DATA_LINE>-BEGDA <FS_DATA_LINE>-ENDDA <FS_DATA_LINE>-HOURS.
  ENDLOOP.

  IF P_COUNT EQ 'X'.
    DATA(LT_DATA_LINE) = GT_DATA_LINE.
    GT_DATA_LINE = VALUE #( ( BEGDA = PN-BEGDA ENDDA = PN-ENDDA ) ).
    READ TABLE GT_DATA_LINE ASSIGNING <FS_DATA_LINE> INDEX 1.
    IF SY-SUBRC EQ 0.
      LOOP AT LT_DATA_LINE INTO DATA(LS_DATA_LINE).
        ADD LS_DATA_LINE-HOURS TO <FS_DATA_LINE>-HOURS.
      ENDLOOP.
    ENDIF.
  ENDIF.

ENDFORM.

FORM FRM_GET_MONTH_IN_TWO_DAYS USING IV_START_DATE IV_END_DATE IV_DAY ET_MONTH TYPE TT_DATA_LINE.

  DATA: LV_DATE_FIRST    TYPE D,
        LV_DATE_LAST     TYPE D,
        LV_DATE_LAST_TMP TYPE D.

  DATA: LV_START_DATE TYPE D,
        LV_END_DATE   TYPE D.

  DATA: LV_DAY     TYPE NUM,
        LV_DAY_TMP TYPE  NUM.

  CHECK IV_START_DATE LE IV_END_DATE.

  CHECK IV_DAY IS NOT INITIAL AND IV_DAY GE '1' AND IV_DAY LE '31'.

  LV_DAY = IV_DAY.
  LV_DAY_TMP = LV_DAY - 1.
  LV_START_DATE = IV_START_DATE.
  LV_END_DATE = IV_END_DATE.


  WHILE LV_START_DATE LE LV_END_DATE.

    APPEND INITIAL LINE TO ET_MONTH ASSIGNING FIELD-SYMBOL(<FS_MONTH>).
    <FS_MONTH>-BEGDA = LV_START_DATE.

    IF LV_DAY EQ 1.

      <FS_MONTH>-ENDDA = CL_HRPAD_DATE_COMPUTATIONS=>GET_LAST_DAY_IN_MONTH( <FS_MONTH>-BEGDA ).
      LV_START_DATE    = CL_HRPAD_DATE_COMPUTATIONS=>ADD_MONTHS_TO_DATE( START_DATE = LV_START_DATE MONTHS = 1 ).
      LV_START_DATE    = |{ LV_START_DATE+0(6) }01|.

    ELSE.

      LV_DATE_FIRST = |{ LV_START_DATE+0(6) }01|.

      IF LV_START_DATE+6(2) LT LV_DAY.

        LV_DATE_LAST  = CL_HRPAD_DATE_COMPUTATIONS=>GET_LAST_DAY_IN_MONTH( LV_DATE_FIRST ).
        LV_DATE_LAST_TMP = |{ LV_START_DATE+0(6) }{ LV_DAY_TMP ALPHA = IN  }|.

      ELSE.

        LV_DATE_FIRST = CL_HRPAD_DATE_COMPUTATIONS=>ADD_MONTHS_TO_DATE( START_DATE = LV_DATE_FIRST MONTHS = 1 ).
        LV_DATE_LAST  = CL_HRPAD_DATE_COMPUTATIONS=>GET_LAST_DAY_IN_MONTH( LV_DATE_FIRST ).
        LV_DATE_LAST_TMP = |{ LV_DATE_FIRST+0(6) }{ LV_DAY_TMP ALPHA = IN }|.

      ENDIF.

      IF LV_DATE_LAST_TMP GE LV_DATE_LAST.
        <FS_MONTH>-ENDDA = LV_DATE_LAST.
        LV_START_DATE = CL_HRPAD_DATE_COMPUTATIONS=>ADD_MONTHS_TO_DATE( START_DATE = LV_DATE_FIRST MONTHS = 1 ).
      ELSE.
        <FS_MONTH>-ENDDA = LV_DATE_LAST_TMP.
        LV_DATE_LAST_TMP = |{ LV_DATE_FIRST+0(6) }{ LV_DAY ALPHA = IN }|.
        IF LV_DATE_LAST_TMP GT LV_DATE_LAST.
          LV_START_DATE = CL_HRPAD_DATE_COMPUTATIONS=>ADD_MONTHS_TO_DATE( START_DATE = LV_DATE_FIRST MONTHS = 1 ).
        ELSE.
          LV_START_DATE = LV_DATE_LAST_TMP.
        ENDIF.
      ENDIF.

    ENDIF.

    IF <FS_MONTH>-ENDDA GE LV_END_DATE.
      <FS_MONTH>-ENDDA = LV_END_DATE.
    ENDIF.

  ENDWHILE.

ENDFORM.

FORM FRM_GET_STANDARD_HOURS TABLES IT_T552A USING IV_BEGDA IV_ENDDA EV_HOURS .
  DATA: LT_T552A      TYPE TABLE OF T552A,
        LV_FIELD_TPR  TYPE STRING,
        LV_FIELD_FTK  TYPE STRING,
        LV_FIELD_TIME TYPE NUM,
        LV_TIMES      TYPE I.


  CLEAR EV_HOURS.
  MOVE-CORRESPONDING IT_T552A[] TO LT_T552A.

  READ TABLE LT_T552A INTO DATA(LS_T552A_BEG) WITH KEY KJAHR = IV_BEGDA+0(4) MONAT = IV_BEGDA+4(2).
  IF SY-SUBRC EQ 0.
    LV_FIELD_TIME = IV_BEGDA+6(2).
    IF IV_BEGDA+0(6) EQ IV_ENDDA+0(6).
      LV_TIMES = IV_ENDDA+6(2) - LV_FIELD_TIME + 1.
    ELSE.
      LV_TIMES = 31 - LV_FIELD_TIME + 1.
    ENDIF.
    DO LV_TIMES TIMES.
      LV_FIELD_TPR = |TPR{ LV_FIELD_TIME ALPHA = IN }|.
      LV_FIELD_FTK = |FTK{ LV_FIELD_TIME ALPHA = IN }|.
      ASSIGN COMPONENT LV_FIELD_TPR OF STRUCTURE LS_T552A_BEG TO FIELD-SYMBOL(<FS_T552A_BEG_TPR>).
      ASSIGN COMPONENT LV_FIELD_FTK OF STRUCTURE LS_T552A_BEG TO FIELD-SYMBOL(<FS_T552A_BEG_FTK>).
      IF <FS_T552A_BEG_TPR> IS ASSIGNED AND <FS_T552A_BEG_FTK> IS ASSIGNED.
        IF <FS_T552A_BEG_FTK> NE '1' AND <FS_T552A_BEG_TPR> NE 'FREI' AND <FS_T552A_BEG_TPR> IS NOT INITIAL.
          ADD 1 TO EV_HOURS.
        ENDIF.
      ELSE.
        EXIT.
      ENDIF.
      ADD 1 TO LV_FIELD_TIME.
    ENDDO.
  ENDIF.

  IF IV_BEGDA+0(6) NE IV_ENDDA+0(6).
    READ TABLE LT_T552A INTO DATA(LS_T552A_END) WITH KEY KJAHR = IV_ENDDA+0(4) MONAT = IV_ENDDA+4(2).
    IF SY-SUBRC EQ 0.
      LV_FIELD_TIME = IV_ENDDA+6(2).
      LV_TIMES = LV_FIELD_TIME.
      DO LV_TIMES TIMES.
        LV_FIELD_TPR = |TPR{ LV_FIELD_TIME ALPHA = IN }|.
        LV_FIELD_FTK = |FTK{ LV_FIELD_TIME ALPHA = IN }|.
        ASSIGN COMPONENT LV_FIELD_TPR OF STRUCTURE LS_T552A_END TO FIELD-SYMBOL(<FS_T552A_END_TPR>).
        ASSIGN COMPONENT LV_FIELD_FTK OF STRUCTURE LS_T552A_END TO FIELD-SYMBOL(<FS_T552A_END_FTK>).
        IF <FS_T552A_END_TPR> IS ASSIGNED AND <FS_T552A_END_FTK> IS ASSIGNED.
          IF <FS_T552A_END_FTK> NE '1' AND <FS_T552A_END_TPR> NE 'FREI' AND <FS_T552A_END_TPR> IS NOT INITIAL.
            ADD 1 TO EV_HOURS.
          ENDIF.
        ELSE.
          EXIT.
        ENDIF.
        SUBTRACT 1 FROM LV_FIELD_TIME.
      ENDDO.
    ENDIF.
  ENDIF.

  EV_HOURS = EV_HOURS * 8.

ENDFORM.

FORM FRM_CREATE_ALV.

  PERFORM FRM_SET_FCAT.
  PERFORM FRM_SET_SORT.
  PERFORM FRM_SET_LAYOUT.

ENDFORM.

FORM FRM_SET_FCAT.

  DATA: LS_FIELDCAT TYPE LVC_S_FCAT,
        LV_POS      TYPE I.

  REFRESH GT_FIELDCAT.

  SET_FCAT: 'X'    'PERNR'    'Employee Number'                             'L'        '10'     ' '  ' '.
  SET_FCAT: 'X'    'ENAME'    'EN Name'                                     'L'        '40'     ' '  ' '.
  SET_FCAT: 'X'    'ALNAM'    'CN Name'                                     'L'        '40'     ' '  ' '.
  SET_FCAT: 'X'    'SCHKZ'    'WS Rule'                                     'L'        '40'     ' '  ' '.
  SET_FCAT: 'X'    'STEXT'    'Department'                                  'L'        '40'     ' '  ' '.
  SET_FCAT: 'X'    'BEGDA'    'Start Time'                                  'L'        '40'     ' '  ' '.
  SET_FCAT: 'X'    'ENDDA'    'End Time'                                    'L'        '40'     ' '  ' '.

  SET_FCAT: ' '    'ANZHL38'  'Standard Calendar Hours'                     'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL37'  'Actual Shift Hours'                          'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL36'  'Working Hours Bank'                          'L'        ' '      ' '  ' '.

  SET_FCAT: ' '    'ANZHL01'  'Plan Working Hours'                          'L'        ' '      ' '  'X'.
  SET_FCAT: ' '    'ANZHL02'  'Actual Working Hours'                        'L'        ' '      ' '  'X'.
  SET_FCAT: ' '    'ANZHL03'  'Late'                                        'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL04'  'Early Leave'                                 'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL05'  'Absenteeism'                                 'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL06'  'Annual Leave'                                'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL07'  'Personal Leave'                              'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL08'  'Sick Leave'                                  'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL09'  'Marriage Leave'                              'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL10'  'Maternity Leave'                             'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL11'  'Prenatal Leave'                              'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL12'  'Nursing Leave'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL13'  'The Nurses Leave'                            'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL14'  'Bereavement Leave'                           'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL15'  'Injury Leave'                                'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL34'  'Childcare Leave'                             'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL35'  'Elderly Care Leave'                          'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL16'  'False Temperature Leave'                     'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL17'  'Supplementary Holidays in Spring Festival'   'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL18'  'Time of in lieu'                             'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL19'  'Overtime 150%'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL20'  'Overtime 200%'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL21'  'Overtime 300%'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL22'  'Overtime Time Off'                           'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL23'  'Overtime Total'                              'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL24'  'Business Trip'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL25'  'Training'                                    'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL26'  'Out of Office'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL27'  'Shuttle Bus Delay'                           'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL28'  'Company Event'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL29'  'Public'                                      'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL30'  'Benefit Leave'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL31'  'Late meal subsidy'                           'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL32'  'Afternoon shift allowance'                   'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL33'  'Night shift allowance'                       'L'        ' '      ' '  ' '.

  DATA: LT_DATA TYPE REF TO DATA,
        LS_DATA TYPE REF TO DATA.

  CALL METHOD CL_ALV_TABLE_CREATE=>CREATE_DYNAMIC_TABLE
    EXPORTING
      IT_FIELDCATALOG = GT_FIELDCAT
    IMPORTING
      EP_TABLE        = LT_DATA.

  ASSIGN LT_DATA->* TO <FS_TABLE>.
  CREATE DATA LS_DATA LIKE LINE OF <FS_TABLE>.
  ASSIGN LS_DATA->* TO <FS_STRUC>.

ENDFORM.

FORM FRM_SET_SORT.

  DATA: LS_SORT TYPE LVC_S_SORT,
        LV_POS  TYPE I.

  REFRESH GT_SORT.

  SET_SORT: 'PERNR' 'X' 'X'.
  SET_SORT: 'ENAME' 'X' 'X'.
  SET_SORT: 'ALNAM' 'X' 'X'.
  SET_SORT: 'SCHKZ' 'X' 'X'.
  SET_SORT: 'STEXT' 'X' 'X'.
  SET_SORT: 'BEGDA' 'X' 'X'.
  SET_SORT: 'ENDDA' 'X' 'X'.

ENDFORM.

FORM FRM_SET_LAYOUT.

  CLEAR: GS_LAYOUT.

  GS_LAYOUT-CWIDTH_OPT   = 'X'.   "宽度自动调整
  GS_LAYOUT-SEL_MODE     = 'C'.

ENDFORM.

FORM FRM_APPEND_PERNR.

  DATA: LT_PSP           TYPE TABLE OF PTPSP,
        LV_RDCLUST       TYPE RDCLST VALUE 'X',
        LV_SWITCH_ACTIVE TYPE C VALUE '0'.
  CALL FUNCTION 'HR_PERSONAL_WORK_SCHEDULE'
    EXPORTING
      PERNR             = PERNR-PERNR
      BEGDA             = PN-BEGDA
      ENDDA             = PN-ENDDA
      SWITCH_ACTIV      = LV_SWITCH_ACTIVE
      I0001_I0007_ERROR = '0'
      READ_CLUSTER      = LV_RDCLUST
    TABLES
      I0000             = P0000
      I0001             = P0001
      I0002             = P0002
      I0007             = P0007
      I2001             = P2001
      I2002             = P2002
      I2003             = P2003
      PERWS             = LT_PSP
    EXCEPTIONS
      ERROR_OCCURED     = 1
      ABORT_OCCURED     = 2
      OTHERS            = 3.
  DELETE LT_PSP WHERE ACTIV NE 'X'.

  DATA: LS_TIME_B2 TYPE HRF_TIM_B2.
  CALL FUNCTION 'HR_FORMS_TIM_GET_B2_RESULTS'
    EXPORTING
      PERNR  = PERNR-PERNR
      BEGDA  = PN-BEGDA
      ENDDA  = PN-ENDDA
    IMPORTING
      TIM_B2 = LS_TIME_B2.

  DATA: LV_DEL TYPE CHAR1.

  LOOP AT GT_DATA_LINE INTO DATA(LS_DATA_LINE).

    CLEAR <FS_STRUC>.

    RP-PROVIDE-FROM-LAST P0001 SPACE LS_DATA_LINE-BEGDA LS_DATA_LINE-ENDDA.
    RP-PROVIDE-FROM-LAST P0002 SPACE LS_DATA_LINE-BEGDA LS_DATA_LINE-ENDDA.
    RP-PROVIDE-FROM-LAST P0007 SPACE LS_DATA_LINE-BEGDA LS_DATA_LINE-ENDDA.
    RP-PROVIDE-FROM-LAST P0182 3     LS_DATA_LINE-BEGDA LS_DATA_LINE-ENDDA.

    PERFORM FRM_GET_PERNR_INFOR TABLES LT_PSP USING LS_DATA_LINE-BEGDA LS_DATA_LINE-ENDDA LS_TIME_B2 LS_DATA_LINE-HOURS LV_DEL.
    IF LV_DEL IS INITIAL.
      APPEND <FS_STRUC> TO <FS_TABLE>.
    ENDIF.

  ENDLOOP.

ENDFORM.

FORM FRM_GET_PERNR_INFOR TABLES IT_PSP USING IV_BEGDA TYPE D IV_ENDDA TYPE D IS_TIME_B2 IV_HOURS EV_DEL.

  DATA: LS_DATA TYPE TS_DATA.

  CLEAR EV_DEL.

**********************************************************************
  IF P0001-BUKRS NE '8300' AND P0001-BUKRS IS NOT INITIAL.
    REJECT.
  ENDIF.

**********************************************************************
  ASSIGN COMPONENT 'PERNR' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_PERNR>).
  <FS_PERNR> = P0001-PERNR.
  IF P0001-PERNR IS INITIAL OR P0001-PERNR EQ '00000000'.
    EV_DEL = 'X'.
    RETURN.
  ENDIF.

  ASSIGN COMPONENT 'ENAME' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_ENAME>).
  <FS_ENAME> = P0001-ENAME.

  ASSIGN COMPONENT 'ALNAM' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_ALNAM>).
  <FS_ALNAM> = P0182-ALNAM.
  IF <FS_ALNAM> IS INITIAL.
    <FS_ALNAM> = <FS_ENAME>.
  ENDIF.

  ASSIGN COMPONENT 'SCHKZ' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_SCHKZ>).
  <FS_SCHKZ> = P0007-SCHKZ.

  ASSIGN COMPONENT 'STEXT' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_STEXT>).
  SELECT SINGLE STEXT INTO <FS_STEXT> FROM HRP1000 WHERE
    PLVAR EQ '99' AND
    OTYPE EQ 'O' AND
    OBJID EQ P0001-ORGEH AND
    ISTAT EQ '1' AND
    BEGDA LE IV_ENDDA AND
    ENDDA GE IV_BEGDA AND
    LANGU EQ SY-LANGU.

  ASSIGN COMPONENT 'BEGDA' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_BEGDA>).
  <FS_BEGDA> = IV_BEGDA.

  ASSIGN COMPONENT 'ENDDA' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_ENDDA>).
  <FS_ENDDA> = IV_ENDDA.

**********************************************************************


  DATA: LT_PSP  TYPE TABLE OF PTPSP.
  LT_PSP = IT_PSP[].
  DELETE LT_PSP WHERE DATUM LT <FS_BEGDA> OR DATUM GT <FS_ENDDA> OR TPROG EQ 'FREI'.

  SORT LT_PSP BY STDAZ DESCENDING.
  READ TABLE LT_PSP INTO DATA(LS_PSP) INDEX 1.
  IF SY-SUBRC NE 0 OR ( SY-SUBRC EQ 0 AND LS_PSP-STDAZ EQ 0 ).
    EV_DEL = 'X'.
    RETURN.
  ENDIF.


  DATA: LT_P2001 TYPE TABLE OF P2001.
  LT_P2001 = P2001[].
  DELETE LT_P2001 WHERE BEGDA GT <FS_ENDDA> OR ENDDA LT <FS_BEGDA>.


  DATA: LT_P2002 TYPE TABLE OF P2002.
  LT_P2002 = P2002[].
  DELETE LT_P2002 WHERE BEGDA GT <FS_ENDDA> OR ENDDA LT <FS_BEGDA>.

  DATA: LS_HRF_TIM_B2 TYPE HRF_TIM_B2.
  LS_HRF_TIM_B2 = IS_TIME_B2.
  DELETE LS_HRF_TIM_B2-FT_ZES WHERE DATUM LT <FS_BEGDA> OR DATUM GT <FS_ENDDA>.
  DELETE LS_HRF_TIM_B2-FT_ZL  WHERE DATUM LT <FS_BEGDA> OR DATUM GT <FS_ENDDA>.

  DATA: LV_FIELD_NAME TYPE STRING.
  LOOP AT LT_PSP INTO LS_PSP.
    ADD LS_PSP-STDAZ TO LS_DATA-ANZHL01.

    IF LS_PSP-TPROG NE 'COFF' AND LS_PSP-FTKLA NE '1'.

      LOOP AT LT_P2001 INTO DATA(LS_P2001) WHERE BEGDA LE LS_PSP-DATUM AND ENDDA GE LS_PSP-DATUM.
        CLEAR LV_FIELD_NAME.

        CASE LS_P2001-SUBTY.
          WHEN 'C100'.
            LV_FIELD_NAME = 'ANZHL06'. "Annual
          WHEN 'C200'.
            LV_FIELD_NAME = 'ANZHL07'. "Personal
          WHEN 'C300' OR 'C310' OR 'C320' OR 'C330'.
            LV_FIELD_NAME = 'ANZHL08'. "Sick
          WHEN 'C400'.
            LV_FIELD_NAME = 'ANZHL09'. "Marriage
          WHEN 'C410'.
            LV_FIELD_NAME = 'ANZHL10'. "Maternity
          WHEN 'C420'.
            LV_FIELD_NAME = 'ANZHL11'. "Prenatal
          WHEN 'C430'.
            LV_FIELD_NAME = 'ANZHL12'. "Nursing
          WHEN 'C440'.
            LV_FIELD_NAME = 'ANZHL13'. "The Nurses
          WHEN 'C450'.
            LV_FIELD_NAME = 'ANZHL14'. "Bereavement
          WHEN 'C460'.
            LV_FIELD_NAME = 'ANZHL15'. "Injury
          WHEN 'C470'.
            LV_FIELD_NAME = 'ANZHL16'. "False Temperature
          WHEN 'C500'.
            LV_FIELD_NAME = 'ANZHL05'. "Absenteeism
          WHEN 'C600'.
            LV_FIELD_NAME = 'ANZHL18'. "Time of in lieu
          WHEN 'C480'.
            LV_FIELD_NAME = 'ANZHL34'. "Childcare
          WHEN 'C490'.
            LV_FIELD_NAME = 'ANZHL35'. "Elderly Care
        ENDCASE.

        IF LV_FIELD_NAME IS NOT INITIAL.
          ASSIGN COMPONENT LV_FIELD_NAME OF STRUCTURE LS_DATA TO FIELD-SYMBOL(<FS_ANZHL_2001>).
          IF <FS_ANZHL_2001> IS ASSIGNED.
            IF LS_P2001-BEGUZ NE '' AND LS_P2001-ENDUZ NE ''.
              ADD LS_P2001-ABRST TO <FS_ANZHL_2001>.
            ELSE.
              ADD 8 TO <FS_ANZHL_2001>.
            ENDIF.
          ENDIF.
        ENDIF.

      ENDLOOP.



      LOOP AT LT_P2002 INTO DATA(LS_P2002) WHERE BEGDA LE LS_PSP-DATUM AND ENDDA GE LS_PSP-DATUM.
        CLEAR LV_FIELD_NAME.

        CASE LS_P2002-SUBTY.
          WHEN 'C800'.
            LV_FIELD_NAME = 'ANZHL24'. "Trip
          WHEN 'C810'.
            LV_FIELD_NAME = 'ANZHL25'. "Tranining
          WHEN 'C820'.
            LV_FIELD_NAME = 'ANZHL26'. "Out
          WHEN 'C830'.
            LV_FIELD_NAME = 'ANZHL27'. "Bus delay
          WHEN 'C840'.
            LV_FIELD_NAME = 'ANZHL28'. "Event
          WHEN 'C850'.
            LV_FIELD_NAME = 'ANZHL29'. "Public
          WHEN 'C860'.
            LV_FIELD_NAME = 'ANZHL30'. "Benefit
        ENDCASE.

        IF LV_FIELD_NAME IS NOT INITIAL.
          ASSIGN COMPONENT LV_FIELD_NAME OF STRUCTURE LS_DATA TO FIELD-SYMBOL(<FS_ANZHL_2002>).
          IF <FS_ANZHL_2002> IS ASSIGNED.
            IF LS_P2002-BEGUZ NE '' AND LS_P2002-ENDUZ NE ''.
              ADD LS_P2002-STDAZ TO <FS_ANZHL_2002>.
            ELSE.
              ADD 8 TO <FS_ANZHL_2002>.
            ENDIF.
          ENDIF.
        ENDIF.

      ENDLOOP.

    ENDIF.

  ENDLOOP.

  LOOP AT LS_HRF_TIM_B2-FT_ZL INTO DATA(LS_ZL).
    CLEAR LV_FIELD_NAME.

    CASE LS_ZL-LGART.
      WHEN '6010'.
        ADD LS_ZL-ANZHL TO LS_DATA-ANZHL19. "OT-150
      WHEN '6020'.
        ADD LS_ZL-ANZHL TO LS_DATA-ANZHL20. "OT-200
      WHEN '6030'.
        ADD LS_ZL-ANZHL TO LS_DATA-ANZHL21. "OT-300
      WHEN '3065'.
        ADD LS_ZL-ANZHL TO LS_DATA-ANZHL31. "Late meal subsidy
      WHEN '3110'.
        ADD LS_ZL-ANZHL TO LS_DATA-ANZHL32. "Afternoon shift allowance
      WHEN '3115'.
        ADD LS_ZL-ANZHL TO LS_DATA-ANZHL33. "Night shift allowance
    ENDCASE.

  ENDLOOP.

  LOOP AT LS_HRF_TIM_B2-FT_ZES INTO DATA(LS_ZES).

    CASE LS_ZES-ZTART.
      WHEN '2802'.
        ADD LS_ZES-ANZHL TO LS_DATA-ANZHL03. "Late
      WHEN '2804'.
        ADD LS_ZES-ANZHL TO LS_DATA-ANZHL04. "Early
      WHEN '2806'.
        ADD LS_ZES-ANZHL TO LS_DATA-ANZHL05. "Absenteeism
      WHEN '0410'.
        ADD LS_ZES-ANZHL TO LS_DATA-ANZHL22. "OT-Time off
    ENDCASE.

  ENDLOOP.

**********************************************************************
  DATA: LT_P2010 TYPE TABLE OF P2010.
  LT_P2010 = P2010[].
  DELETE LT_P2010 WHERE BEGDA GT <FS_ENDDA> OR ENDDA LT <FS_BEGDA> OR ( SUBTY NE '6010' AND SUBTY NE '6020' AND SUBTY NE '6030' ).

  DATA: LV_ANZHL_SUM TYPE P2010-ANZHL.
  LOOP AT LT_P2010 INTO DATA(LS_P2010).
    ADD LS_P2010-ANZHL TO LV_ANZHL_SUM.
  ENDLOOP.

  LS_DATA-ANZHL23 = LS_DATA-ANZHL19 + LS_DATA-ANZHL20 + LS_DATA-ANZHL21 + LS_DATA-ANZHL22 + LV_ANZHL_SUM.   "Overtime Total

  LS_DATA-ANZHL02 =  LS_DATA-ANZHL01 + LS_DATA-ANZHL23 + LV_ANZHL_SUM - (
                     LS_DATA-ANZHL03 + LS_DATA-ANZHL04 + LS_DATA-ANZHL05 + LS_DATA-ANZHL06 + LS_DATA-ANZHL07 +
                     LS_DATA-ANZHL08 + LS_DATA-ANZHL09 + LS_DATA-ANZHL10 + LS_DATA-ANZHL11 + LS_DATA-ANZHL12 +
                     LS_DATA-ANZHL13 + LS_DATA-ANZHL14 + LS_DATA-ANZHL15 + LS_DATA-ANZHL16 + LS_DATA-ANZHL17 +
                     LS_DATA-ANZHL18 + LS_DATA-ANZHL34 + LS_DATA-ANZHL35 ).

  LS_DATA-ANZHL38 = IV_HOURS.

  LS_DATA-ANZHL37 = LS_DATA-ANZHL01.

  LS_DATA-ANZHL36 = LS_DATA-ANZHL37 - LS_DATA-ANZHL38.


  DATA: LV_NUM TYPE NUM.
  DO 38 TIMES.
    LV_NUM = SY-INDEX.
    CLEAR LV_FIELD_NAME.
    CONCATENATE 'ANZHL' LV_NUM INTO LV_FIELD_NAME.
    ASSIGN COMPONENT LV_FIELD_NAME OF STRUCTURE LS_DATA TO FIELD-SYMBOL(<FS_DATA_ANZHL>).
    ASSIGN COMPONENT LV_FIELD_NAME OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_STRUC_ANZHL>).
    IF <FS_DATA_ANZHL> IS ASSIGNED AND <FS_STRUC_ANZHL> IS ASSIGNED.
      <FS_STRUC_ANZHL> = <FS_DATA_ANZHL>.
      CONDENSE <FS_STRUC_ANZHL> NO-GAPS.
    ENDIF.
    UNASSIGN: <FS_DATA_ANZHL>, <FS_STRUC_ANZHL>.
  ENDDO.


ENDFORM.

FORM FRM_ALV_DISPLAY.

  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      TEXT = 'Ready for output...'.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY_LVC'
    EXPORTING
      I_CALLBACK_PROGRAM       = SY-REPID
      I_CALLBACK_PF_STATUS_SET = 'FRM_CB_PF_STATUS_SET'
      I_CALLBACK_USER_COMMAND  = 'FRM_CB_USER_COMMAND'
      IS_LAYOUT_LVC            = GS_LAYOUT
      IT_FIELDCAT_LVC          = GT_FIELDCAT
      IT_SORT_LVC              = GT_SORT
      I_SAVE                   = 'U'
    TABLES
      T_OUTTAB                 = <FS_TABLE>
    EXCEPTIONS
      PROGRAM_ERROR            = 1
      OTHERS                   = 2.

  IF SY-SUBRC NE 0.
    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

ENDFORM.

FORM FRM_CB_PF_STATUS_SET USING RT_EXTAB TYPE SLIS_T_EXTAB.

  SET PF-STATUS  'STATUS' EXCLUDING RT_EXTAB.

ENDFORM.

FORM FRM_CB_USER_COMMAND USING UCOMM SELLINE TYPE SLIS_SELFIELD.

ENDFORM.
```





报表进阶版，以 PA0000 的时间数据进行拆分

```abap
*&---------------------------------------------------------------------*
*& Report ZTEST_0027
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT ZTEST_0027.

TYPE-POOLS: SLIS.

TABLES: PERNR, PCL1, PCL2, T549S.
INFOTYPES: 0000, 0001, 0002, 0007, 2001, 2002, 2003, 0182, 2006, 2010.

DEFINE SET_FCAT.
  LV_POS = LINES( GT_FIELDCAT ) + 1.
  CLEAR:  LS_FIELDCAT.
  LS_FIELDCAT-COL_POS       = LV_POS.
  LS_FIELDCAT-KEY           = &1.
  LS_FIELDCAT-FIELDNAME     = &2.
  LS_FIELDCAT-REPTEXT       = &3.
  LS_FIELDCAT-JUST          = &4.
  LS_FIELDCAT-OUTPUTLEN     = &5.
  LS_FIELDCAT-DO_SUM        = &6.
  LS_FIELDCAT-NO_OUT        = &7.
  LS_FIELDCAT-SCRTEXT_L     = &3.
  LS_FIELDCAT-SCRTEXT_M     = &3.
  LS_FIELDCAT-SCRTEXT_S     = &3.
  APPEND LS_FIELDCAT TO  GT_FIELDCAT.
END-OF-DEFINITION.

DEFINE SET_SORT.
  LV_POS = LINES( GT_SORT ) + 1.
  CLEAR:  LS_SORT.
  LS_SORT-SPOS       = LV_POS.
  LS_SORT-FIELDNAME  = &1.
  LS_SORT-UP         = &2.
  LS_SORT-SUBTOT     = &3.
  APPEND LS_SORT TO GT_SORT.
END-OF-DEFINITION.

TYPES: BEGIN OF TS_DATA,
         ANZHL01 TYPE ANZHL,            "Plan working hours
         ANZHL02 TYPE ANZHL,            "Actual working hours
         ANZHL03 TYPE ANZHL,            "Late
         ANZHL04 TYPE ANZHL,            "Early leave
         ANZHL05 TYPE ANZHL,            "Absenteeism
         ANZHL06 TYPE ANZHL,            "Annual Leave
         ANZHL07 TYPE ANZHL,            "Personal Leave
         ANZHL08 TYPE ANZHL,            "Sick Leave
         ANZHL09 TYPE ANZHL,            "Marriage Leave
         ANZHL10 TYPE ANZHL,            "Maternity Leave
         ANZHL11 TYPE ANZHL,            "Prenatal Leave
         ANZHL12 TYPE ANZHL,            "Nursing Leave
         ANZHL13 TYPE ANZHL,            "The Nurses Leave
         ANZHL14 TYPE ANZHL,            "Bereavement Leave
         ANZHL15 TYPE ANZHL,            "Injury Leave
         ANZHL16 TYPE ANZHL,            "False Temperature Leave
         ANZHL17 TYPE ANZHL,            "Supplementary Holidays in Spring Festival
         ANZHL18 TYPE ANZHL,            "Time of in lieu
         ANZHL19 TYPE ANZHL,            "Overtime 150%
         ANZHL20 TYPE ANZHL,            "Overtime 200%
         ANZHL21 TYPE ANZHL,            "Overtime 300%
         ANZHL22 TYPE ANZHL,            "Overtime time off
         ANZHL23 TYPE ANZHL,            "Overtime Total
         ANZHL24 TYPE ANZHL,            "Business trip
         ANZHL25 TYPE ANZHL,            "Training
         ANZHL26 TYPE ANZHL,            "Out of office
         ANZHL27 TYPE ANZHL,            "Shuttle bus delay
         ANZHL28 TYPE ANZHL,            "Company event
         ANZHL29 TYPE ANZHL,            "Public
         ANZHL30 TYPE ANZHL,            "Benefit leave
         ANZHL31 TYPE ANZHL,            "Late meal subsidy
         ANZHL32 TYPE ANZHL,            "Afternoon shift allowance
         ANZHL33 TYPE ANZHL,            "Night shift allowance
         ANZHL34 TYPE ANZHL,            "Childcare Leave
         ANZHL35 TYPE ANZHL,            "Elderly Care Leave

         ANZHL36 TYPE ANZHL,            "Working Hours Bank
         ANZHL37 TYPE ANZHL,            "Actual Shift Hours
         ANZHL38 TYPE ANZHL,            "Standard Calendar Hours
       END OF TS_DATA.

TYPES: BEGIN OF TS_DATA_LINE,
         BEGDA TYPE D,
         ENDDA TYPE D,
         HOURS TYPE ANZHL,
       END OF TS_DATA_LINE.
TYPES: TT_DATA_LINE TYPE STANDARD TABLE OF TS_DATA_LINE WITH DEFAULT KEY.

DATA: GT_DATA_LINE TYPE TT_DATA_LINE.

FIELD-SYMBOLS: <FS_TABLE> TYPE STANDARD TABLE,
               <FS_STRUC> TYPE ANY.

DATA: GT_FIELDCAT TYPE LVC_T_FCAT,
      GT_SORT     TYPE LVC_T_SORT,
      GS_LAYOUT   TYPE LVC_S_LAYO.

DATA: GT_T552A TYPE TABLE OF T552A.

SELECTION-SCREEN BEGIN OF BLOCK B1 WITH FRAME TITLE TEXT-001.

  SELECTION-SCREEN BEGIN OF LINE.
    PARAMETERS: P_COUNT RADIOBUTTON GROUP G1 USER-COMMAND UC1.
    SELECTION-SCREEN COMMENT 2(15) T_COUNT FOR FIELD P_COUNT VISIBLE LENGTH 10.
    PARAMETERS: P_DETAIL RADIOBUTTON GROUP G1 DEFAULT 'X'.
    SELECTION-SCREEN COMMENT 19(15) T_DETAIL FOR FIELD P_DETAIL VISIBLE LENGTH 10.
  SELECTION-SCREEN END OF LINE.

  SELECTION-SCREEN SKIP.

  PARAMETERS: P_DAY TYPE NUM DEFAULT '01' MODIF ID S1.

SELECTION-SCREEN END OF BLOCK B1.

INITIALIZATION.
  PERFORM FRM_INIT_SCREEN.

AT SELECTION-SCREEN OUTPUT.
  PERFORM FRM_SET_SCREEN_OUT.

START-OF-SELECTION.
  PERFORM FRM_CHECK_INPUT.
  PERFORM FRM_GET_OTHER_DATA.
  PERFORM FRM_CREATE_ALV.

GET PERNR.
  PERFORM FRM_GET_DATA_LINES.
  PERFORM FRM_APPEND_PERNR.

END-OF-SELECTION.
  PERFORM FRM_ALV_DISPLAY.


FORM FRM_INIT_SCREEN.
  %_P_DAY_%_APP_%    = '薪资结算开始日期'.
  T_COUNT   = '汇总表'.
  T_DETAIL  = '明细表'.
ENDFORM.

FORM FRM_SET_SCREEN_OUT.
  LOOP AT SCREEN.

    IF SCREEN-GROUP1 EQ 'S1'.
      IF P_COUNT EQ 'X'.
        SCREEN-ACTIVE = '0'.
      ELSE.
        SCREEN-ACTIVE = '1'.
      ENDIF.
    ENDIF.

    MODIFY SCREEN.
  ENDLOOP.
ENDFORM.

FORM FRM_CHECK_INPUT.

  IF PN-BEGDA = '18000101' OR PN-ENDDA = '99991231'.
    MESSAGE 'Please enter the period.' TYPE 'S' DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
    STOP.
  ENDIF.

  IF P_DETAIL IS NOT INITIAL AND P_DAY IS INITIAL.
    MESSAGE 'Please enter the split date.' TYPE 'S' DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
    STOP.
  ENDIF.

  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      TEXT = 'Ready read data...'.

ENDFORM.

FORM FRM_GET_OTHER_DATA.

  IF P_COUNT EQ 'X'.
    P_DAY = '01'.
  ENDIF.

  REFRESH GT_T552A.

  SELECT * INTO CORRESPONDING FIELDS OF TABLE GT_T552A FROM T552A
    WHERE ZEITY EQ '2' AND MOFID EQ 'CN' AND MOSID EQ '28' AND SCHKZ EQ 'W120' AND KJAHR GE PN-BEGDA+0(4) AND KJAHR LE PN-ENDDA+0(4).

ENDFORM.

FORM FRM_GET_DATA_LINES.

  DATA: LT_PERIODS LIKE GT_DATA_LINE.

  REFRESH GT_DATA_LINE.

  DATA(LV_INDEX) = 1.
  DATA(LT_P0000) = P0000[].
  SORT LT_P0000 BY BEGDA.

  WHILE LV_INDEX <= LINES( LT_P0000 ).

    DATA(LS_P0000) = LT_P0000[ LV_INDEX ].

    IF LV_INDEX EQ 1 AND PN-BEGDA GE LS_P0000-BEGDA.
      LS_P0000-BEGDA = PN-BEGDA.
    ENDIF.

    IF LV_INDEX EQ LINES( LT_P0000 ) AND LS_P0000-ENDDA GE PN-ENDDA.
      LS_P0000-ENDDA = PN-ENDDA.
    ENDIF.

    IF LS_P0000-STAT2 EQ '3'.
      APPEND INITIAL LINE TO LT_PERIODS ASSIGNING FIELD-SYMBOL(<FS_PERIODS>).
      <FS_PERIODS>-BEGDA = LS_P0000-BEGDA.
      <FS_PERIODS>-ENDDA = LS_P0000-ENDDA.

      DO.
        ADD 1 TO LV_INDEX.

        IF LV_INDEX > LINES( LT_P0000 ).
          EXIT.
        ENDIF.

        DATA(LS_NEXT) = LT_P0000[ LV_INDEX ].
        IF LV_INDEX EQ LINES( LT_P0000 ) AND LS_NEXT-ENDDA GE PN-ENDDA.
          LS_NEXT-ENDDA = PN-ENDDA.
        ENDIF.

        IF LS_NEXT-STAT2 NE '3'.
          EXIT.
        ELSE.
          <FS_PERIODS>-ENDDA = LS_NEXT-ENDDA.
        ENDIF.

      ENDDO.
    ENDIF.

    ADD 1 TO LV_INDEX.
  ENDWHILE.

  DATA: LT_DATA_LINE LIKE GT_DATA_LINE.
  LOOP AT LT_PERIODS INTO DATA(LS_PERIODS).
    REFRESH LT_DATA_LINE.
    PERFORM FRM_GET_MONTH_IN_TWO_DAYS USING LS_PERIODS-BEGDA LS_PERIODS-ENDDA P_DAY LT_DATA_LINE.

    LOOP AT LT_DATA_LINE ASSIGNING FIELD-SYMBOL(<FS_DATA_LINE>).
      PERFORM FRM_GET_STANDARD_HOURS TABLES GT_T552A USING <FS_DATA_LINE>-BEGDA <FS_DATA_LINE>-ENDDA <FS_DATA_LINE>-HOURS.
    ENDLOOP.

    IF P_COUNT EQ 'X'.
      APPEND INITIAL LINE TO GT_DATA_LINE ASSIGNING <FS_DATA_LINE>.
      <FS_DATA_LINE>-BEGDA = LS_PERIODS-BEGDA.
      <FS_DATA_LINE>-ENDDA = LS_PERIODS-ENDDA.
      LOOP AT LT_DATA_LINE INTO DATA(LS_DATA_LINE).
        ADD LS_DATA_LINE-HOURS TO <FS_DATA_LINE>-HOURS.
      ENDLOOP.
    ELSE.
      APPEND LINES OF LT_DATA_LINE TO GT_DATA_LINE.
    ENDIF.
  ENDLOOP.

ENDFORM.

FORM FRM_GET_MONTH_IN_TWO_DAYS USING IV_START_DATE IV_END_DATE IV_DAY ET_MONTH TYPE TT_DATA_LINE.

  DATA: LV_DATE_FIRST    TYPE D,
        LV_DATE_LAST     TYPE D,
        LV_DATE_LAST_TMP TYPE D.

  DATA: LV_START_DATE TYPE D,
        LV_END_DATE   TYPE D.

  DATA: LV_DAY     TYPE NUM,
        LV_DAY_TMP TYPE  NUM.

  CHECK IV_START_DATE LE IV_END_DATE.

  CHECK IV_DAY IS NOT INITIAL AND IV_DAY GE '1' AND IV_DAY LE '31'.

  LV_DAY = IV_DAY.
  LV_DAY_TMP = LV_DAY - 1.
  LV_START_DATE = IV_START_DATE.
  LV_END_DATE = IV_END_DATE.


  WHILE LV_START_DATE LE LV_END_DATE.

    APPEND INITIAL LINE TO ET_MONTH ASSIGNING FIELD-SYMBOL(<FS_MONTH>).
    <FS_MONTH>-BEGDA = LV_START_DATE.

    IF LV_DAY EQ 1.

      <FS_MONTH>-ENDDA = CL_HRPAD_DATE_COMPUTATIONS=>GET_LAST_DAY_IN_MONTH( <FS_MONTH>-BEGDA ).
      LV_START_DATE    = CL_HRPAD_DATE_COMPUTATIONS=>ADD_MONTHS_TO_DATE( START_DATE = LV_START_DATE MONTHS = 1 ).
      LV_START_DATE    = |{ LV_START_DATE+0(6) }01|.

    ELSE.

      LV_DATE_FIRST = |{ LV_START_DATE+0(6) }01|.

      IF LV_START_DATE+6(2) LT LV_DAY.

        LV_DATE_LAST  = CL_HRPAD_DATE_COMPUTATIONS=>GET_LAST_DAY_IN_MONTH( LV_DATE_FIRST ).
        LV_DATE_LAST_TMP = |{ LV_START_DATE+0(6) }{ LV_DAY_TMP ALPHA = IN  }|.

      ELSE.

        LV_DATE_FIRST = CL_HRPAD_DATE_COMPUTATIONS=>ADD_MONTHS_TO_DATE( START_DATE = LV_DATE_FIRST MONTHS = 1 ).
        LV_DATE_LAST  = CL_HRPAD_DATE_COMPUTATIONS=>GET_LAST_DAY_IN_MONTH( LV_DATE_FIRST ).
        LV_DATE_LAST_TMP = |{ LV_DATE_FIRST+0(6) }{ LV_DAY_TMP ALPHA = IN }|.

      ENDIF.

      IF LV_DATE_LAST_TMP GE LV_DATE_LAST.
        <FS_MONTH>-ENDDA = LV_DATE_LAST.
        LV_START_DATE = CL_HRPAD_DATE_COMPUTATIONS=>ADD_MONTHS_TO_DATE( START_DATE = LV_DATE_FIRST MONTHS = 1 ).
      ELSE.
        <FS_MONTH>-ENDDA = LV_DATE_LAST_TMP.
        LV_DATE_LAST_TMP = |{ LV_DATE_FIRST+0(6) }{ LV_DAY ALPHA = IN }|.
        IF LV_DATE_LAST_TMP GT LV_DATE_LAST.
          LV_START_DATE = CL_HRPAD_DATE_COMPUTATIONS=>ADD_MONTHS_TO_DATE( START_DATE = LV_DATE_FIRST MONTHS = 1 ).
        ELSE.
          LV_START_DATE = LV_DATE_LAST_TMP.
        ENDIF.
      ENDIF.

    ENDIF.

    IF <FS_MONTH>-ENDDA GE LV_END_DATE.
      <FS_MONTH>-ENDDA = LV_END_DATE.
    ENDIF.

  ENDWHILE.

ENDFORM.

FORM FRM_GET_STANDARD_HOURS TABLES IT_T552A USING IV_BEGDA IV_ENDDA EV_HOURS .
  DATA: LT_T552A      TYPE TABLE OF T552A,
        LV_FIELD_TPR  TYPE STRING,
        LV_FIELD_FTK  TYPE STRING,
        LV_FIELD_TIME TYPE NUM,
        LV_TIMES      TYPE I.


  CLEAR EV_HOURS.
  MOVE-CORRESPONDING IT_T552A[] TO LT_T552A.

  READ TABLE LT_T552A INTO DATA(LS_T552A_BEG) WITH KEY KJAHR = IV_BEGDA+0(4) MONAT = IV_BEGDA+4(2).
  IF SY-SUBRC EQ 0.
    LV_FIELD_TIME = IV_BEGDA+6(2).
    IF IV_BEGDA+0(6) EQ IV_ENDDA+0(6).
      LV_TIMES = IV_ENDDA+6(2) - LV_FIELD_TIME + 1.
    ELSE.
      LV_TIMES = 31 - LV_FIELD_TIME + 1.
    ENDIF.
    DO LV_TIMES TIMES.
      LV_FIELD_TPR = |TPR{ LV_FIELD_TIME ALPHA = IN }|.
      LV_FIELD_FTK = |FTK{ LV_FIELD_TIME ALPHA = IN }|.
      ASSIGN COMPONENT LV_FIELD_TPR OF STRUCTURE LS_T552A_BEG TO FIELD-SYMBOL(<FS_T552A_BEG_TPR>).
      ASSIGN COMPONENT LV_FIELD_FTK OF STRUCTURE LS_T552A_BEG TO FIELD-SYMBOL(<FS_T552A_BEG_FTK>).
      IF <FS_T552A_BEG_TPR> IS ASSIGNED AND <FS_T552A_BEG_FTK> IS ASSIGNED.
        IF <FS_T552A_BEG_FTK> NE '1' AND <FS_T552A_BEG_TPR> NE 'FREI' AND <FS_T552A_BEG_TPR> IS NOT INITIAL.
          ADD 1 TO EV_HOURS.
        ENDIF.
      ELSE.
        EXIT.
      ENDIF.
      ADD 1 TO LV_FIELD_TIME.
    ENDDO.
  ENDIF.

  IF IV_BEGDA+0(6) NE IV_ENDDA+0(6).
    READ TABLE LT_T552A INTO DATA(LS_T552A_END) WITH KEY KJAHR = IV_ENDDA+0(4) MONAT = IV_ENDDA+4(2).
    IF SY-SUBRC EQ 0.
      LV_FIELD_TIME = IV_ENDDA+6(2).
      LV_TIMES = LV_FIELD_TIME.
      DO LV_TIMES TIMES.
        LV_FIELD_TPR = |TPR{ LV_FIELD_TIME ALPHA = IN }|.
        LV_FIELD_FTK = |FTK{ LV_FIELD_TIME ALPHA = IN }|.
        ASSIGN COMPONENT LV_FIELD_TPR OF STRUCTURE LS_T552A_END TO FIELD-SYMBOL(<FS_T552A_END_TPR>).
        ASSIGN COMPONENT LV_FIELD_FTK OF STRUCTURE LS_T552A_END TO FIELD-SYMBOL(<FS_T552A_END_FTK>).
        IF <FS_T552A_END_TPR> IS ASSIGNED AND <FS_T552A_END_FTK> IS ASSIGNED.
          IF <FS_T552A_END_FTK> NE '1' AND <FS_T552A_END_TPR> NE 'FREI' AND <FS_T552A_END_TPR> IS NOT INITIAL.
            ADD 1 TO EV_HOURS.
          ENDIF.
        ELSE.
          EXIT.
        ENDIF.
        SUBTRACT 1 FROM LV_FIELD_TIME.
      ENDDO.
    ENDIF.
  ENDIF.

  EV_HOURS = EV_HOURS * 8.

ENDFORM.

FORM FRM_CREATE_ALV.

  PERFORM FRM_SET_FCAT.
  PERFORM FRM_SET_SORT.
  PERFORM FRM_SET_LAYOUT.

ENDFORM.

FORM FRM_SET_FCAT.

  DATA: LS_FIELDCAT TYPE LVC_S_FCAT,
        LV_POS      TYPE I.

  REFRESH GT_FIELDCAT.

  SET_FCAT: 'X'    'PERNR'    'Employee Number'                             'L'        '10'     ' '  ' '.
  SET_FCAT: 'X'    'ENAME'    'EN Name'                                     'L'        '40'     ' '  ' '.
  SET_FCAT: 'X'    'ALNAM'    'CN Name'                                     'L'        '40'     ' '  ' '.
  SET_FCAT: 'X'    'SCHKZ'    'WS Rule'                                     'L'        '40'     ' '  ' '.
  SET_FCAT: 'X'    'STEXT'    'Department'                                  'L'        '40'     ' '  ' '.
  SET_FCAT: 'X'    'BEGDA'    'Start Time'                                  'L'        '40'     ' '  ' '.
  SET_FCAT: 'X'    'ENDDA'    'End Time'                                    'L'        '40'     ' '  ' '.

  SET_FCAT: ' '    'ANZHL38'  'Standard Calendar Hours'                     'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL37'  'Actual Shift Hours'                          'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL36'  'Working Hours Bank'                          'L'        ' '      ' '  ' '.

  SET_FCAT: ' '    'ANZHL01'  'Plan Working Hours'                          'L'        ' '      ' '  'X'.
  SET_FCAT: ' '    'ANZHL02'  'Actual Working Hours'                        'L'        ' '      ' '  'X'.
  SET_FCAT: ' '    'ANZHL03'  'Late'                                        'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL04'  'Early Leave'                                 'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL05'  'Absenteeism'                                 'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL06'  'Annual Leave'                                'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL07'  'Personal Leave'                              'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL08'  'Sick Leave'                                  'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL09'  'Marriage Leave'                              'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL10'  'Maternity Leave'                             'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL11'  'Prenatal Leave'                              'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL12'  'Nursing Leave'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL13'  'The Nurses Leave'                            'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL14'  'Bereavement Leave'                           'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL15'  'Injury Leave'                                'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL34'  'Childcare Leave'                             'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL35'  'Elderly Care Leave'                          'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL16'  'False Temperature Leave'                     'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL17'  'Supplementary Holidays in Spring Festival'   'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL18'  'Time of in lieu'                             'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL19'  'Overtime 150%'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL20'  'Overtime 200%'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL21'  'Overtime 300%'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL22'  'Overtime Time Off'                           'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL23'  'Overtime Total'                              'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL24'  'Business Trip'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL25'  'Training'                                    'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL26'  'Out of Office'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL27'  'Shuttle Bus Delay'                           'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL28'  'Company Event'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL29'  'Public'                                      'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL30'  'Benefit Leave'                               'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL31'  'Late meal subsidy'                           'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL32'  'Afternoon shift allowance'                   'L'        ' '      ' '  ' '.
  SET_FCAT: ' '    'ANZHL33'  'Night shift allowance'                       'L'        ' '      ' '  ' '.

  DATA: LT_DATA TYPE REF TO DATA,
        LS_DATA TYPE REF TO DATA.

  CALL METHOD CL_ALV_TABLE_CREATE=>CREATE_DYNAMIC_TABLE
    EXPORTING
      IT_FIELDCATALOG = GT_FIELDCAT
    IMPORTING
      EP_TABLE        = LT_DATA.

  ASSIGN LT_DATA->* TO <FS_TABLE>.
  CREATE DATA LS_DATA LIKE LINE OF <FS_TABLE>.
  ASSIGN LS_DATA->* TO <FS_STRUC>.

ENDFORM.

FORM FRM_SET_SORT.

  DATA: LS_SORT TYPE LVC_S_SORT,
        LV_POS  TYPE I.

  REFRESH GT_SORT.

  SET_SORT: 'PERNR' 'X' 'X'.
  SET_SORT: 'ENAME' 'X' 'X'.
  SET_SORT: 'ALNAM' 'X' 'X'.
  SET_SORT: 'SCHKZ' 'X' 'X'.
  SET_SORT: 'STEXT' 'X' 'X'.
  SET_SORT: 'BEGDA' 'X' 'X'.
  SET_SORT: 'ENDDA' 'X' 'X'.

ENDFORM.

FORM FRM_SET_LAYOUT.

  CLEAR: GS_LAYOUT.

  GS_LAYOUT-CWIDTH_OPT   = 'X'.   "宽度自动调整
  GS_LAYOUT-SEL_MODE     = 'C'.

ENDFORM.

FORM FRM_APPEND_PERNR.

  DATA: LT_PSP           TYPE TABLE OF PTPSP,
        LV_RDCLUST       TYPE RDCLST VALUE 'X',
        LV_SWITCH_ACTIVE TYPE C VALUE '0'.
  CALL FUNCTION 'HR_PERSONAL_WORK_SCHEDULE'
    EXPORTING
      PERNR             = PERNR-PERNR
      BEGDA             = PN-BEGDA
      ENDDA             = PN-ENDDA
      SWITCH_ACTIV      = LV_SWITCH_ACTIVE
      I0001_I0007_ERROR = '0'
      READ_CLUSTER      = LV_RDCLUST
    TABLES
      I0000             = P0000
      I0001             = P0001
      I0002             = P0002
      I0007             = P0007
      I2001             = P2001
      I2002             = P2002
      I2003             = P2003
      PERWS             = LT_PSP
    EXCEPTIONS
      ERROR_OCCURED     = 1
      ABORT_OCCURED     = 2
      OTHERS            = 3.
  DELETE LT_PSP WHERE ACTIV NE 'X'.

  DATA: LS_TIME_B2 TYPE HRF_TIM_B2.
  CALL FUNCTION 'HR_FORMS_TIM_GET_B2_RESULTS'
    EXPORTING
      PERNR  = PERNR-PERNR
      BEGDA  = PN-BEGDA
      ENDDA  = PN-ENDDA
    IMPORTING
      TIM_B2 = LS_TIME_B2.

  DATA: LV_DEL TYPE CHAR1.

  LOOP AT GT_DATA_LINE INTO DATA(LS_DATA_LINE).

    CLEAR <FS_STRUC>.

    RP-PROVIDE-FROM-LAST P0001 SPACE LS_DATA_LINE-BEGDA LS_DATA_LINE-ENDDA.
    RP-PROVIDE-FROM-LAST P0002 SPACE LS_DATA_LINE-BEGDA LS_DATA_LINE-ENDDA.
    RP-PROVIDE-FROM-LAST P0007 SPACE LS_DATA_LINE-BEGDA LS_DATA_LINE-ENDDA.
    RP-PROVIDE-FROM-LAST P0182 3     LS_DATA_LINE-BEGDA LS_DATA_LINE-ENDDA.

    PERFORM FRM_GET_PERNR_INFOR TABLES LT_PSP USING LS_DATA_LINE-BEGDA LS_DATA_LINE-ENDDA LS_TIME_B2 LS_DATA_LINE-HOURS LV_DEL.
    IF LV_DEL IS INITIAL.
      APPEND <FS_STRUC> TO <FS_TABLE>.
    ENDIF.

  ENDLOOP.

ENDFORM.

FORM FRM_GET_PERNR_INFOR TABLES IT_PSP USING IV_BEGDA TYPE D IV_ENDDA TYPE D IS_TIME_B2 IV_HOURS EV_DEL.

  DATA: LS_DATA TYPE TS_DATA.

  CLEAR EV_DEL.

**********************************************************************
  IF P0001-BUKRS NE '8300' AND P0001-BUKRS IS NOT INITIAL.
    REJECT.
  ENDIF.

**********************************************************************
  ASSIGN COMPONENT 'PERNR' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_PERNR>).
  <FS_PERNR> = P0001-PERNR.
  IF P0001-PERNR IS INITIAL OR P0001-PERNR EQ '00000000'.
    EV_DEL = 'X'.
    RETURN.
  ENDIF.

  ASSIGN COMPONENT 'ENAME' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_ENAME>).
  <FS_ENAME> = P0001-ENAME.

  ASSIGN COMPONENT 'ALNAM' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_ALNAM>).
  <FS_ALNAM> = P0182-ALNAM.
  IF <FS_ALNAM> IS INITIAL.
    <FS_ALNAM> = <FS_ENAME>.
  ENDIF.

  ASSIGN COMPONENT 'SCHKZ' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_SCHKZ>).
  <FS_SCHKZ> = P0007-SCHKZ.

  ASSIGN COMPONENT 'STEXT' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_STEXT>).
  SELECT SINGLE STEXT INTO <FS_STEXT> FROM HRP1000 WHERE
    PLVAR EQ '99' AND
    OTYPE EQ 'O' AND
    OBJID EQ P0001-ORGEH AND
    ISTAT EQ '1' AND
    BEGDA LE IV_ENDDA AND
    ENDDA GE IV_BEGDA AND
    LANGU EQ SY-LANGU.

  ASSIGN COMPONENT 'BEGDA' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_BEGDA>).
  <FS_BEGDA> = IV_BEGDA.

  ASSIGN COMPONENT 'ENDDA' OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_ENDDA>).
  <FS_ENDDA> = IV_ENDDA.

**********************************************************************


  DATA: LT_PSP  TYPE TABLE OF PTPSP.
  LT_PSP = IT_PSP[].
  DELETE LT_PSP WHERE DATUM LT <FS_BEGDA> OR DATUM GT <FS_ENDDA> OR TPROG EQ 'FREI'.

  SORT LT_PSP BY STDAZ DESCENDING.
  READ TABLE LT_PSP INTO DATA(LS_PSP) INDEX 1.
  IF SY-SUBRC NE 0 OR ( SY-SUBRC EQ 0 AND LS_PSP-STDAZ EQ 0 ).
    EV_DEL = 'X'.
    RETURN.
  ENDIF.


  DATA: LT_P2001 TYPE TABLE OF P2001.
  LT_P2001 = P2001[].
  DELETE LT_P2001 WHERE BEGDA GT <FS_ENDDA> OR ENDDA LT <FS_BEGDA>.


  DATA: LT_P2002 TYPE TABLE OF P2002.
  LT_P2002 = P2002[].
  DELETE LT_P2002 WHERE BEGDA GT <FS_ENDDA> OR ENDDA LT <FS_BEGDA>.

  DATA: LS_HRF_TIM_B2 TYPE HRF_TIM_B2.
  LS_HRF_TIM_B2 = IS_TIME_B2.
  DELETE LS_HRF_TIM_B2-FT_ZES WHERE DATUM LT <FS_BEGDA> OR DATUM GT <FS_ENDDA>.
  DELETE LS_HRF_TIM_B2-FT_ZL  WHERE DATUM LT <FS_BEGDA> OR DATUM GT <FS_ENDDA>.

  DATA: LV_FIELD_NAME TYPE STRING.
  LOOP AT LT_PSP INTO LS_PSP.
    ADD LS_PSP-STDAZ TO LS_DATA-ANZHL01.

    IF LS_PSP-TPROG NE 'COFF' AND LS_PSP-FTKLA NE '1'.

      LOOP AT LT_P2001 INTO DATA(LS_P2001) WHERE BEGDA LE LS_PSP-DATUM AND ENDDA GE LS_PSP-DATUM.
        CLEAR LV_FIELD_NAME.

        CASE LS_P2001-SUBTY.
          WHEN 'C100'.
            LV_FIELD_NAME = 'ANZHL06'. "Annual
          WHEN 'C200'.
            LV_FIELD_NAME = 'ANZHL07'. "Personal
          WHEN 'C300' OR 'C310' OR 'C320' OR 'C330'.
            LV_FIELD_NAME = 'ANZHL08'. "Sick
          WHEN 'C400'.
            LV_FIELD_NAME = 'ANZHL09'. "Marriage
          WHEN 'C410'.
            LV_FIELD_NAME = 'ANZHL10'. "Maternity
          WHEN 'C420'.
            LV_FIELD_NAME = 'ANZHL11'. "Prenatal
          WHEN 'C430'.
            LV_FIELD_NAME = 'ANZHL12'. "Nursing
          WHEN 'C440'.
            LV_FIELD_NAME = 'ANZHL13'. "The Nurses
          WHEN 'C450'.
            LV_FIELD_NAME = 'ANZHL14'. "Bereavement
          WHEN 'C460'.
            LV_FIELD_NAME = 'ANZHL15'. "Injury
          WHEN 'C470'.
            LV_FIELD_NAME = 'ANZHL16'. "False Temperature
          WHEN 'C500'.
            LV_FIELD_NAME = 'ANZHL05'. "Absenteeism
          WHEN 'C600'.
            LV_FIELD_NAME = 'ANZHL18'. "Time of in lieu
          WHEN 'C480'.
            LV_FIELD_NAME = 'ANZHL34'. "Childcare
          WHEN 'C490'.
            LV_FIELD_NAME = 'ANZHL35'. "Elderly Care
        ENDCASE.

        IF LV_FIELD_NAME IS NOT INITIAL.
          ASSIGN COMPONENT LV_FIELD_NAME OF STRUCTURE LS_DATA TO FIELD-SYMBOL(<FS_ANZHL_2001>).
          IF <FS_ANZHL_2001> IS ASSIGNED.
            IF LS_P2001-BEGUZ NE '' AND LS_P2001-ENDUZ NE ''.
              ADD LS_P2001-ABRST TO <FS_ANZHL_2001>.
            ELSE.
              ADD 8 TO <FS_ANZHL_2001>.
            ENDIF.
          ENDIF.
        ENDIF.

      ENDLOOP.



      LOOP AT LT_P2002 INTO DATA(LS_P2002) WHERE BEGDA LE LS_PSP-DATUM AND ENDDA GE LS_PSP-DATUM.
        CLEAR LV_FIELD_NAME.

        CASE LS_P2002-SUBTY.
          WHEN 'C800'.
            LV_FIELD_NAME = 'ANZHL24'. "Trip
          WHEN 'C810'.
            LV_FIELD_NAME = 'ANZHL25'. "Tranining
          WHEN 'C820'.
            LV_FIELD_NAME = 'ANZHL26'. "Out
          WHEN 'C830'.
            LV_FIELD_NAME = 'ANZHL27'. "Bus delay
          WHEN 'C840'.
            LV_FIELD_NAME = 'ANZHL28'. "Event
          WHEN 'C850'.
            LV_FIELD_NAME = 'ANZHL29'. "Public
          WHEN 'C860'.
            LV_FIELD_NAME = 'ANZHL30'. "Benefit
        ENDCASE.

        IF LV_FIELD_NAME IS NOT INITIAL.
          ASSIGN COMPONENT LV_FIELD_NAME OF STRUCTURE LS_DATA TO FIELD-SYMBOL(<FS_ANZHL_2002>).
          IF <FS_ANZHL_2002> IS ASSIGNED.
            IF LS_P2002-BEGUZ NE '' AND LS_P2002-ENDUZ NE ''.
              ADD LS_P2002-STDAZ TO <FS_ANZHL_2002>.
            ELSE.
              ADD 8 TO <FS_ANZHL_2002>.
            ENDIF.
          ENDIF.
        ENDIF.

      ENDLOOP.

    ENDIF.

  ENDLOOP.

  LOOP AT LS_HRF_TIM_B2-FT_ZL INTO DATA(LS_ZL).
    CLEAR LV_FIELD_NAME.

    CASE LS_ZL-LGART.
      WHEN '6010'.
        ADD LS_ZL-ANZHL TO LS_DATA-ANZHL19. "OT-150
      WHEN '6020'.
        ADD LS_ZL-ANZHL TO LS_DATA-ANZHL20. "OT-200
      WHEN '6030'.
        ADD LS_ZL-ANZHL TO LS_DATA-ANZHL21. "OT-300
      WHEN '3065'.
        ADD LS_ZL-ANZHL TO LS_DATA-ANZHL31. "Late meal subsidy
      WHEN '3110'.
        ADD LS_ZL-ANZHL TO LS_DATA-ANZHL32. "Afternoon shift allowance
      WHEN '3115'.
        ADD LS_ZL-ANZHL TO LS_DATA-ANZHL33. "Night shift allowance
    ENDCASE.

  ENDLOOP.

  LOOP AT LS_HRF_TIM_B2-FT_ZES INTO DATA(LS_ZES).

    CASE LS_ZES-ZTART.
      WHEN '2802'.
        ADD LS_ZES-ANZHL TO LS_DATA-ANZHL03. "Late
      WHEN '2804'.
        ADD LS_ZES-ANZHL TO LS_DATA-ANZHL04. "Early
      WHEN '2806'.
        ADD LS_ZES-ANZHL TO LS_DATA-ANZHL05. "Absenteeism
      WHEN '0410'.
        ADD LS_ZES-ANZHL TO LS_DATA-ANZHL22. "OT-Time off
    ENDCASE.

  ENDLOOP.

**********************************************************************
  DATA: LT_P2010 TYPE TABLE OF P2010.
  LT_P2010 = P2010[].
  DELETE LT_P2010 WHERE BEGDA GT <FS_ENDDA> OR ENDDA LT <FS_BEGDA> OR ( SUBTY NE '6010' AND SUBTY NE '6020' AND SUBTY NE '6030' ).

  DATA: LV_ANZHL_SUM TYPE P2010-ANZHL.
  LOOP AT LT_P2010 INTO DATA(LS_P2010).
    ADD LS_P2010-ANZHL TO LV_ANZHL_SUM.
  ENDLOOP.

  LS_DATA-ANZHL23 = LS_DATA-ANZHL19 + LS_DATA-ANZHL20 + LS_DATA-ANZHL21 + LS_DATA-ANZHL22 + LV_ANZHL_SUM.   "Overtime Total

  LS_DATA-ANZHL02 =  LS_DATA-ANZHL01 + LS_DATA-ANZHL23 + LV_ANZHL_SUM - (
                     LS_DATA-ANZHL03 + LS_DATA-ANZHL04 + LS_DATA-ANZHL05 + LS_DATA-ANZHL06 + LS_DATA-ANZHL07 +
                     LS_DATA-ANZHL08 + LS_DATA-ANZHL09 + LS_DATA-ANZHL10 + LS_DATA-ANZHL11 + LS_DATA-ANZHL12 +
                     LS_DATA-ANZHL13 + LS_DATA-ANZHL14 + LS_DATA-ANZHL15 + LS_DATA-ANZHL16 + LS_DATA-ANZHL17 +
                     LS_DATA-ANZHL18 + LS_DATA-ANZHL34 + LS_DATA-ANZHL35 ).

  LS_DATA-ANZHL38 = IV_HOURS.

  LS_DATA-ANZHL37 = LS_DATA-ANZHL01.

  LS_DATA-ANZHL36 = LS_DATA-ANZHL37 - LS_DATA-ANZHL38.


  DATA: LV_NUM TYPE NUM.
  DO 38 TIMES.
    LV_NUM = SY-INDEX.
    CLEAR LV_FIELD_NAME.
    CONCATENATE 'ANZHL' LV_NUM INTO LV_FIELD_NAME.
    ASSIGN COMPONENT LV_FIELD_NAME OF STRUCTURE LS_DATA TO FIELD-SYMBOL(<FS_DATA_ANZHL>).
    ASSIGN COMPONENT LV_FIELD_NAME OF STRUCTURE <FS_STRUC> TO FIELD-SYMBOL(<FS_STRUC_ANZHL>).
    IF <FS_DATA_ANZHL> IS ASSIGNED AND <FS_STRUC_ANZHL> IS ASSIGNED.
      <FS_STRUC_ANZHL> = <FS_DATA_ANZHL>.
      CONDENSE <FS_STRUC_ANZHL> NO-GAPS.
    ENDIF.
    UNASSIGN: <FS_DATA_ANZHL>, <FS_STRUC_ANZHL>.
  ENDDO.


ENDFORM.

FORM FRM_ALV_DISPLAY.

  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      TEXT = 'Ready for output...'.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY_LVC'
    EXPORTING
      I_CALLBACK_PROGRAM       = SY-REPID
      I_CALLBACK_PF_STATUS_SET = 'FRM_CB_PF_STATUS_SET'
      I_CALLBACK_USER_COMMAND  = 'FRM_CB_USER_COMMAND'
      IS_LAYOUT_LVC            = GS_LAYOUT
      IT_FIELDCAT_LVC          = GT_FIELDCAT
      IT_SORT_LVC              = GT_SORT
      I_SAVE                   = 'U'
    TABLES
      T_OUTTAB                 = <FS_TABLE>
    EXCEPTIONS
      PROGRAM_ERROR            = 1
      OTHERS                   = 2.

  IF SY-SUBRC NE 0.
    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

ENDFORM.

FORM FRM_CB_PF_STATUS_SET USING RT_EXTAB TYPE SLIS_T_EXTAB.

  SET PF-STATUS  'STATUS' EXCLUDING RT_EXTAB.

ENDFORM.

FORM FRM_CB_USER_COMMAND USING UCOMM SELLINE TYPE SLIS_SELFIELD.

ENDFORM.
```

