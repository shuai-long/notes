```abap
*&---------------------------------------------------------------------*
*& Report ZTYR_001
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
report ztyr_001.

define set_events.
  clear ls_event.
  ls_event-eventid = &1.
  ls_event-appl_event = 'X'.
  append ls_event to lt_events.
end-of-definition.

data: ok_code type sy-ucomm.

class lcl_drog_object definition.
  public section.
    data: gs_node type treev_node.
endclass.

data: gt_events type table of char100.

class cl_report definition.
  public section.

    constants: gc_root_key type string value 'GLOBAL'.

    methods:
      handle_node_double_click for event node_double_click of cl_gui_list_tree
        importing
          node_key,

      handle_expand_no_children for event expand_no_children of cl_gui_list_tree
        importing
          node_key,

      handle_item_double_click  for event item_double_click  of cl_gui_list_tree
        importing
          node_key
          item_name,

      handle_node_context_menu_req for event node_context_menu_request of cl_gui_list_tree
        importing
          node_key
          menu,

      handle_item_context_menu_req for event item_context_menu_request of cl_gui_list_tree
        importing
          node_key
          item_name
          menu,

      handle_item_context_menu_sel for event item_context_menu_select of cl_gui_list_tree
        importing
          node_key
          item_name
          fcode,

      handle_button_click for event button_click of cl_gui_list_tree
        importing
          node_key
          item_name,

      handle_link_click for event link_click of cl_gui_list_tree
        importing
          node_key
          item_name,

      handle_checkbox_change for event checkbox_change of cl_gui_list_tree
        importing
          node_key
          item_name
          checked,

      handle_on_drop_get_flavor for event on_drop_get_flavor of cl_gui_list_tree
        importing
          node_key
          flavors
          drag_drop_object,

      handle_on_drag_mul for event on_drag_multiple of cl_gui_list_tree
        importing
          node_key_table
          item_name
          drag_drop_object,

      handle_on_drag  for event on_drag of cl_gui_list_tree
        importing
          node_key
          item_name
          drag_drop_object,

      handle_on_drop for event on_drop of cl_gui_list_tree
        importing
          node_key
          drag_drop_object ,

      handle_on_drop_complete_mul for event on_drop_complete_multiple of cl_gui_list_tree
        importing
          node_key_table
          item_name
          drag_drop_object,


      handle_on_drop_complete for event on_drop_complete of cl_gui_list_tree
        importing
          node_key
          item_name
          drag_drop_object.


    methods get_user_answer
      importing
                iv_text          type any
      returning value(ev_answer) type char1.
    methods call_screen.
    methods show_tree.
    methods set_events.
    methods set_handler.
    methods set_nodes.
    methods add_items_for_node
      importing
        iv_node_key    type any
        iv_drop_handle type any
        iv_new         type char1 default ' '.
    methods del_node_from_tree.
    methods add_node_to_tree.
    methods change_node.
    methods call_sm30
      importing
        iv_tabname type tabname.
    methods show_table_data
      importing
        iv_tabname type tabname.
    methods get_new_node_key
      importing
        iv_node_key      type tv_nodekey
      exporting
        ev_num           type numc3
      returning
        value(ev_result) type ztyt_001-tree_id.

  private section.
    data: go_list_tree            type ref to cl_gui_list_tree,
          go_alv_grid             type ref to cl_gui_alv_grid,
          go_split_container      type ref to cl_gui_splitter_container,
          go_dock_container_left  type ref to cl_gui_container,
          go_dock_container_right type ref to cl_gui_container,

          gt_ztyt_001             type table of ztyt_001.

    data: ls_hierarchy_header type treev_hhdr,
          ls_list_header      type treev_lhdr.

    types: begin of ty_tree_list.
             include type ztyt_001.
    types:   new_tree type char1,
           end of ty_tree_list.

    data: begin of gs_tree,
            tree_id   type ztyt_001-tree_id,
            tree_list type hashed table of ty_tree_list with unique key tree_id,
          end of gs_tree.
    data: gt_tree like hashed table of gs_tree with unique key tree_id.

    data: gv_node_key  type tv_nodekey,
          gv_item_name type tv_itmname,
          gv_refresh   type char1.

endclass.

start-of-selection.
  data(go_report) =  new cl_report( ).

  go_report->call_screen( ).


class cl_report implementation.

  method handle_node_double_click.
    "此方法处理树的节点双击事件
    gv_node_key = node_key.
  endmethod.

  method handle_expand_no_children.
    " 显示扩展子节点
    gv_node_key = node_key.

    data: lo_drogdrop    type ref to cl_dragdrop,
          lv_drop_handle type i.

    lo_drogdrop = new cl_dragdrop( ).
    lo_drogdrop->add(
      exporting
        flavor     = 'TreeMove'        "名称
        dragsrc    = 'X'               "拖动源
        droptarget = 'X'               "放置目标
        effect     = cl_dragdrop=>move "移动/复制？
    ).

    lo_drogdrop->add(
      exporting
        flavor     = 'TreeCopy'        "名称
        dragsrc    = 'X'               "拖动源
        droptarget = 'X'               "放置目标
        effect     = cl_dragdrop=>copy "移动/复制？
    ).


    lo_drogdrop->get_handle( importing handle = lv_drop_handle ).

    add_items_for_node( iv_node_key = gv_node_key iv_drop_handle = lv_drop_handle ).

  endmethod.

  method handle_item_double_click .
    "此方法处理树的项双击事件
    gv_node_key  = node_key.
    gv_item_name = item_name.

    if gv_item_name eq '1'.
      read table gt_ztyt_001 into data(ls_ztyt_001) with key tree_id = gv_node_key.
      if sy-subrc eq 0.
        call_sm30( ls_ztyt_001-tabname ).
      endif.
    else.
      read table gt_ztyt_001 into ls_ztyt_001 with key tree_id = gv_node_key.
      if sy-subrc eq 0 and ls_ztyt_001-tabname is not initial.
        select count( * ) from dd02l where tabname eq ls_ztyt_001-tabname.
        if sy-subrc eq 0.
          show_table_data( ls_ztyt_001-tabname ).
        endif.
      endif.
    endif.

  endmethod.

  method handle_node_context_menu_req.

    "此方法给Node节点增加右键菜单
    gv_node_key  = node_key.

  endmethod.

  method handle_item_context_menu_req.

    gv_node_key  = node_key.
    gv_item_name = item_name.

    if gv_node_key ne sy-uname.
      menu->add_function( fcode = 'ADD_NODES' text = '增加节点' ).

      if gv_node_key ne gc_root_key and gv_node_key ne sy-uname.
        menu->add_function( fcode = 'CHA_NODES' text = '更改节点' ).
        read table gt_tree into data(ls_tree) with key tree_id = gv_node_key.
        if sy-subrc ne 0 or ls_tree-tree_list is initial.
          menu->add_function( fcode = 'DEL_NODES' text = '删除节点' ).
        endif.
      endif.
    endif.

  endmethod.

  method handle_item_context_menu_sel.
    gv_node_key  = node_key.
    gv_item_name = item_name.
    case fcode.
      when 'DEL_NODES'.
        del_node_from_tree( ).
      when 'CHA_NODES'.
        change_node( ).
      when 'ADD_NODES'.
        add_node_to_tree( ).
    endcase.
    cl_gui_cfw=>set_new_ok_code( 'REFRESH' ).
  endmethod.

  method handle_button_click.
    "此方法处理树的按钮单击事件
    gv_node_key  = node_key.
    gv_item_name = item_name.
  endmethod.

  method handle_link_click.
    "此方法处理树的链接单击事件
    gv_node_key  = node_key.
    gv_item_name = item_name.
  endmethod.

  method  handle_checkbox_change .
    "这个方法处理树的checkbox_change事件
    gv_node_key  = node_key.
    gv_item_name = item_name.
  endmethod.

  method handle_on_drag_mul.

    data: lo_drag_object type ref to lcl_drog_object.

    gt_events = value #( base gt_events ( 'handle_on_drag_mul' ) ).

*    read table node_key_table with key node_key = node_key into node.
*
*    create object drag_object.
*    drag_object->text = node-text.
*    drag_drop_object->object = drag_object.


  endmethod.

  method  handle_on_drag.
    gt_events = value #( base gt_events ( 'handle_on_drag' ) ).
    gv_node_key = node_key.
    gv_item_name = item_name.
    data(lo_drag_obj) = new lcl_drog_object( ).
    lo_drag_obj->gs_node-node_key = gv_node_key.
    drag_drop_object->object = lo_drag_obj.

  endmethod.

  method handle_on_drop_complete_mul.

    gt_events = value #( base gt_events ( 'handle_on_drag' ) ).

  endmethod.

  method  handle_on_drop_complete.

    gt_events = value #( base gt_events ( 'handle_on_drop_complete' ) ).

  endmethod.

  method handle_on_drop.
    gv_node_key = node_key.

    gt_events = value #( base gt_events ( 'handle_on_drop' ) ).
    data: lo_drag_obj type ref to lcl_drog_object.
    catch system-exceptions move_cast_error = 1.
      lo_drag_obj ?= drag_drop_object->object.

      search gv_node_key for gc_root_key.
      if sy-subrc eq 0.

      else.

      endif.

    endcatch.
    if sy-subrc = 1.
      call method drag_drop_object->abort.
      exit.
    endif.

    break-point.

  endmethod.

  method handle_on_drop_get_flavor .

    gt_events = value #( base gt_events ( 'handle_on_drop_get_flavor ' ) ).

  endmethod.

  method call_screen.
    call screen 9000.
  endmethod.

  method show_tree.

    if gv_refresh is not initial and go_list_tree is not initial.
      go_list_tree->free( ).
      go_dock_container_left->free( ).

      if go_alv_grid is not initial.
        go_alv_grid->free( ).
        go_dock_container_right->free( ).
      endif.
      go_split_container->free( ).
      free: go_list_tree, go_dock_container_left, go_alv_grid, go_dock_container_right, go_split_container.
      clear gv_refresh.
    endif.

    if go_list_tree is initial.

      go_split_container = new cl_gui_splitter_container(  parent = cl_gui_container=>default_screen rows = 1 columns = 2 ).
      go_split_container->set_column_width( id = 1 width = 20 ).
      go_dock_container_left  = go_split_container->get_container( row = 1 column = 1 ).
      go_dock_container_right = go_split_container->get_container( row = 1 column = 2 ).

      ls_hierarchy_header-width = 40.
      ls_hierarchy_header-heading = '节点'.

      ls_list_header-heading = '表名'.

      go_list_tree = new cl_gui_list_tree(
        parent              = go_dock_container_left
        node_selection_mode = cl_gui_simple_tree=>node_sel_mode_single
        item_selection      = 'X'
        with_headers        = ' '
*       hierarchy_header    = ls_hierarchy_header
*       list_header         = ls_list_header
      ).

*      go_list_tree->set_alignment( 40 ).

      set_events( ).

      set_handler( ).

      set_nodes( ).

    else.
      cl_gui_cfw=>flush( ).
    endif.

  endmethod.

  method set_events.

    data: lt_events type cntl_simple_events,
          ls_event  type cntl_simple_event.

    set_events: cl_gui_list_tree=>eventid_node_double_click.
    set_events: cl_gui_list_tree=>eventid_item_double_click.
    set_events: cl_gui_list_tree=>eventid_expand_no_children.
    set_events: cl_gui_list_tree=>eventid_link_click.
    set_events: cl_gui_list_tree=>eventid_button_click.
    set_events: cl_gui_list_tree=>eventid_checkbox_change.
    set_events: cl_gui_list_tree=>eventid_node_context_menu_req.
    set_events: cl_gui_list_tree=>eventid_item_context_menu_req.

    go_list_tree->set_registered_events( lt_events ).

  endmethod.

  method set_handler.

    set handler: handle_node_double_click for go_list_tree,
                 handle_item_double_click for go_list_tree,
                 handle_expand_no_children for go_list_tree,
                 handle_link_click for go_list_tree,
                 handle_button_click for go_list_tree,
                 handle_node_context_menu_req for go_list_tree,
                 handle_item_context_menu_req for go_list_tree,
                 handle_item_context_menu_sel for go_list_tree,
                 handle_checkbox_change for go_list_tree,
                 handle_on_drop_get_flavor for go_list_tree,
                 handle_on_drag for go_list_tree,
                 handle_on_drag_mul for go_list_tree,
                 handle_on_drop for go_list_tree,
                 handle_on_drop_complete for go_list_tree,
                 handle_on_drop_complete_mul for go_list_tree.

  endmethod.

  method set_nodes.

    refresh: gt_ztyt_001, gt_tree.
    data: ls_tree_line type ty_tree_list.

    select  * into corresponding fields of table gt_ztyt_001 from ztyt_001.

    loop at gt_ztyt_001 into data(ls_ztyt_001).
      read table gt_tree assigning field-symbol(<fs_tree>) with key tree_id = ls_ztyt_001-ftree_id.
      if sy-subrc eq 0.
        clear ls_tree_line.
        move-corresponding ls_ztyt_001 to ls_tree_line.
        insert ls_tree_line into table <fs_tree>-tree_list.
      else.
        clear gs_tree.
        gs_tree-tree_id = ls_ztyt_001-ftree_id.
        clear ls_tree_line.
        move-corresponding ls_ztyt_001 to ls_tree_line.
        insert ls_tree_line into table gs_tree-tree_list.
        insert gs_tree into table gt_tree.
      endif.
    endloop.

    if gt_tree is initial.
      gt_tree = value #( ( tree_id = gc_root_key ) ( tree_id = sy-uname ) ).
    endif.

    data: lt_ztyt_001 type table of ztyt_001.
    move-corresponding gt_ztyt_001 to lt_ztyt_001.
    delete lt_ztyt_001 where tree_id ne gc_root_key and tree_id ne sy-uname.
    if lines( lt_ztyt_001 ) ne 2.
      lt_ztyt_001 = value #( ( tree_id = gc_root_key tree_text = 'Local'  tree_id_num = '1' )
                             ( tree_id = sy-uname    tree_text = sy-uname tree_id_num = '1' )
                           ).

      modify ztyt_001 from table lt_ztyt_001.
    endif.

    data: lo_drogdrop    type ref to cl_dragdrop,
          lv_drop_handle type i.
    lo_drogdrop = new cl_dragdrop( ).
    lo_drogdrop->add(
      exporting
        flavor     = 'TreeCopy' "名称
        dragsrc    = ' '         "拖动源
        droptarget = 'X'         "放置目标
        effect     = cl_dragdrop=>copy "移动/复制？
    ).
    lo_drogdrop->get_handle( importing handle = lv_drop_handle ).

    add_items_for_node( iv_node_key = '1' iv_drop_handle = ' ' ).
    add_items_for_node( iv_node_key = '2' iv_drop_handle = lv_drop_handle ).

  endmethod.

  method add_items_for_node.

    data: ls_node  type treev_node,
          lt_nodes type treev_ntab,
          ls_item  type mtreeitm,
          lt_items type table of mtreeitm.

    data: lv_node_key  type treev_node-node_key,
          lv_relatkey  type treev_node-node_key,
          lv_item_text type text40.

    case iv_node_key.
      when '1'.
        lv_node_key  = gc_root_key.
        lv_relatkey  = ' '.
        lv_item_text = 'Local'.
      when '2'.
        lv_node_key  = sy-uname.
        lv_relatkey  = ' '.
        lv_item_text = sy-uname.
      when others.
        lv_relatkey  = iv_node_key.
    endcase.

    if lv_relatkey is initial.
      clear ls_node.
      ls_node-node_key  = lv_node_key.
      ls_node-relatkey  = lv_relatkey.
      ls_node-relatship = cl_gui_list_tree=>relat_last_child.
      ls_node-hidden    = ' '.
      ls_node-disabled  = ' '.
      ls_node-isfolder  = 'X'.
      ls_node-expander  = 'X'.
      ls_node-dragdropid = iv_drop_handle.
      append ls_node to lt_nodes.

      clear ls_item.
      ls_item-node_key  = lv_node_key.
      ls_item-item_name = '2'.
      ls_item-class     = cl_gui_list_tree=>item_class_text.
      ls_item-alignment = cl_gui_list_tree=>align_auto.
      ls_item-font      = cl_gui_list_tree=>item_font_prop.
      ls_item-style     = '0'.
      ls_item-text      = |{ lv_item_text }|.
      append ls_item to lt_items.
    endif.

    data: lv_where type string.
    if iv_new is not initial.
      lv_where = |NEW_TREE eq 'X'|.
    else.
      lv_where = |NEW_TREE eq ' '|.
    endif.

    read table gt_tree into gs_tree with key tree_id = iv_node_key.
    if sy-subrc eq 0.
      loop at gs_tree-tree_list assigning field-symbol(<fs_tree_line>) where (lv_where).

        clear ls_node.
        ls_node-node_key  = <fs_tree_line>-tree_id.
        ls_node-relatkey  = lv_relatkey.
        ls_node-relatship = cl_gui_list_tree=>relat_last_child.
        ls_node-hidden    = ' '.
        ls_node-disabled  = ' '.
        read table gt_tree into data(ls_tree) with key tree_id = <fs_tree_line>-tree_id.
        if sy-subrc eq 0 and ls_tree-tree_list is not initial.
          ls_node-isfolder  = 'X'.
          ls_node-expander  = 'X'.
        else.
          ls_node-isfolder  = ' '.
          ls_node-expander  = ' '.
        endif.

        ls_node-dragdropid = iv_drop_handle.

        append ls_node to lt_nodes.

        if <fs_tree_line>-tabname is not initial.
          clear ls_item.
          ls_item-node_key  = <fs_tree_line>-tree_id.
          ls_item-item_name = '1'.
          ls_item-class     = cl_gui_list_tree=>item_class_text.
          ls_item-alignment = cl_gui_list_tree=>align_auto.
          ls_item-font      = cl_gui_list_tree=>item_font_prop.
          ls_item-style     = '0'.
          ls_item-length    = '2'.
          ls_item-t_image   = icon_execute_object.
          append ls_item to lt_items.
        endif.

        clear ls_item.
        ls_item-node_key  = <fs_tree_line>-tree_id.
        ls_item-item_name = '2'.
        ls_item-class     = cl_gui_list_tree=>item_class_text.
        ls_item-alignment = cl_gui_list_tree=>align_auto.
        ls_item-font      = cl_gui_list_tree=>item_font_prop.
        ls_item-style     = '0'.
        ls_item-text      = |{ <fs_tree_line>-tree_text }|.
        append ls_item to lt_items.

        if lv_relatkey is not initial.

          clear ls_item.
          ls_item-node_key  = <fs_tree_line>-tree_id.
          ls_item-item_name = '3'.
          ls_item-class     = cl_gui_list_tree=>item_class_text.
          ls_item-alignment = cl_gui_list_tree=>align_auto.
          ls_item-font      = cl_gui_list_tree=>item_font_prop.
          ls_item-style     = '0'.
          ls_item-text      = |{ <fs_tree_line>-tabname }|.
          append ls_item to lt_items.

*          clear ls_item.
*          ls_item-node_key  = ls_ztyt_001-tree_id.
*          ls_item-item_name = '4'.
*          ls_item-class     = cl_gui_list_tree=>item_class_text.
*          ls_item-alignment = cl_gui_list_tree=>align_auto.
*          ls_item-font      = cl_gui_list_tree=>item_font_prop.
*          ls_item-style     = '0'.
*          ls_item-text      = |{ ls_ztyt_001-ddtext }|.
*          append ls_item to lt_items.
        endif.
        clear <fs_tree_line>-new_tree.
      endloop.

    endif.

    check lt_nodes is not initial.

    "添加节点

    if iv_new eq 'U'.
      "更新时 item 表必须包含 treev_uite 结构和一个名为 text 的字段
      types: begin of ty_items_u.
               include type treev_uite.
      types:   text type char72,
             end of ty_items_u.
      data: lt_items_u type standard table of ty_items_u.

      move-corresponding lt_items to lt_items_u.

      loop at lt_items_u assigning field-symbol(<fs_item>) where text is not initial.
        <fs_item>-u_text = 'X'.
      endloop.

      go_list_tree->update_nodes_and_items(
        exporting
          item_table                     = lt_items_u
          item_table_structure_name      = 'TREEV_UITE'
        exceptions
          failed                         = 1
          cntl_system_error              = 2
          error_in_tables                = 3
          dp_error                       = 4
          table_structure_name_not_found = 5
          others                         = 6
      ).
    else.
      go_list_tree->add_nodes_and_items(
        exporting
          node_table                     = lt_nodes
          item_table                     = lt_items
          item_table_structure_name      = 'MTREEITM'
        exceptions
          failed                         = 1
          cntl_system_error              = 3
          error_in_tables                = 4
          dp_error                       = 5
          table_structure_name_not_found = 6
      ).
    endif.

    "展开节点
    go_list_tree->expand_root_nodes(
      exporting
        level_count         = 1
        expand_subtree      = 'X'
      exceptions
        failed              = 1
        illegal_level_count = 2
        cntl_system_error   = 3
        others              = 4
    ).

  endmethod.

  method get_user_answer.

    data: lv_text type char100.
    lv_text = iv_text.
    call function 'POPUP_TO_CONFIRM_STEP'
      exporting
        titel          = '用户确认'
        textline1      = lv_text
        cancel_display = 'X'
      importing
        answer         = ev_answer.

  endmethod.

  method add_node_to_tree.

    select single * into @data(ls_ztyt_001) from ztyt_001 where tree_id eq @gv_node_key.

    "弹窗的内容
    data: lv_text      type string,
          lt_value     type table of sval,
          lv_rtn_cd,
          lv_pop_title type char70.

    lt_value = value #( tabname = 'ZTYT_001'
                        ( fieldname = 'TREE_TEXT' value = '' fieldtext = '节点描述' )
                        ( fieldname = 'TABNAME'   value = '' fieldtext = '表/视图' )
                        ( fieldname = 'DDTEXT'    value = '' fieldtext = '表/视图描述' )
    ).
    lv_pop_title = |在节点 { ls_ztyt_001-tree_text } 下添加数据|.

    call function 'POPUP_GET_VALUES'
      exporting
        popup_title = lv_pop_title
      importing
        returncode  = lv_rtn_cd
      tables
        fields      = lt_value.

    data: lv_numc type numc3.
    data(lv_tree_id) = get_new_node_key(
      exporting
        iv_node_key = gv_node_key
      importing
        ev_num      = lv_numc
    ).

    if lv_rtn_cd is initial.
      data: ls_ztyt_001_new type ztyt_001.
      ls_ztyt_001_new-tree_id     = lv_tree_id.
      ls_ztyt_001_new-tree_id_num = lv_numc.
      read table lt_value into data(ls_value) with key fieldname = 'TREE_TEXT'.
      if sy-subrc = 0 .
        ls_ztyt_001_new-tree_text = ls_value-value.
      endif.

      ls_ztyt_001_new-ftree_id = gv_node_key.
      read table lt_value into ls_value with key fieldname = 'TABNAME'.
      if sy-subrc = 0 .
        if ls_value-value is not initial.
          select count(*) from dd02l where tabname eq ls_value-value and tabclass in ( 'TRANSP','VIEW' ) and mainflag eq 'X'.
          if sy-subrc ne 0.
            select count( * ) from vcldir where vclname eq ls_value-value .
            if sy-subrc ne 0.
              message '表不存在，或不存在相关维护视图' type 'S' display like 'E'.
              return.
            endif.
          endif.
        endif.
        ls_ztyt_001_new-tabname = ls_value-value.
      endif.

      read table lt_value into ls_value with key fieldname = 'DDTEXT'.
      if sy-subrc = 0 .
        ls_ztyt_001_new-ddtext = ls_value-value.
      endif.

      if ls_ztyt_001_new-tree_text is initial and ls_ztyt_001_new-tabname is initial and ls_ztyt_001_new-ddtext is initial.
        message '请至少维护一种信息' type 'S' display like 'E'.
        return.
      endif.

      lv_text = |确认在节点 { ls_ztyt_001-tree_text } 下添加数据？|.
      if get_user_answer( lv_text ) eq 'J'.
        if ls_ztyt_001 is not initial.
          modify ztyt_001 from ls_ztyt_001_new.
          insert ls_ztyt_001_new into table gt_ztyt_001.

          read table gt_tree assigning field-symbol(<fs_tree>) with key tree_id = gv_node_key.
          if sy-subrc eq 0.
            data: ls_tree_line type ty_tree_list.
            move-corresponding ls_ztyt_001_new to ls_tree_line.
            ls_tree_line-new_tree = 'X'.
            insert ls_tree_line into table <fs_tree>-tree_list.
          else.
            data: ls_tree like line of gt_tree.
            ls_tree-tree_id = gv_node_key.
            move-corresponding ls_ztyt_001_new to ls_tree_line.
            ls_tree_line-new_tree = 'X'.
            insert ls_tree_line into table ls_tree-tree_list.
            insert ls_tree into table gt_tree.
          endif.

          data: lo_drogdrop    type ref to cl_dragdrop,
                lv_drop_handle type i.

          lo_drogdrop = new cl_dragdrop( ).
          lo_drogdrop->add(
            exporting
              flavor     = 'TreeMove'        "名称
              dragsrc    = 'X'               "拖动源
              droptarget = 'X'               "放置目标
              effect     = cl_dragdrop=>move "移动/复制？
          ).

          lo_drogdrop->add(
            exporting
              flavor     = 'TreeCopy'        "名称
              dragsrc    = 'X'               "拖动源
              droptarget = 'X'               "放置目标
              effect     = cl_dragdrop=>copy "移动/复制？
          ).


          lo_drogdrop->get_handle( importing handle = lv_drop_handle ).
          add_items_for_node(
            iv_node_key    = gv_node_key
            iv_drop_handle = lv_drop_handle
            iv_new         = 'N'
          ).
        endif.
      endif.

    else.
      message '用户已取消' type 'S' display like 'E'.
    endif.

  endmethod.

  method change_node.

    select single * into @data(ls_ztyt_001) from ztyt_001 where tree_id eq @gv_node_key.
    data(lv_node_key) = ls_ztyt_001-ftree_id.

    "弹窗的内容
    data: lv_text      type string,
          lt_value     type table of sval,
          lv_rtn_cd,
          lv_pop_title type char70.

    lt_value = value #( tabname = 'ZTYT_001'
                        ( fieldname = 'TREE_TEXT' value = '' fieldtext = '节点描述' )
                        ( fieldname = 'TABNAME'   value = '' fieldtext = '表/视图' )
                        ( fieldname = 'DDTEXT'    value = '' fieldtext = '表/视图描述' )
    ).
    lv_pop_title = |更改节点 { ls_ztyt_001-tree_text } 信息|.

    call function 'POPUP_GET_VALUES'
      exporting
        popup_title = lv_pop_title
      importing
        returncode  = lv_rtn_cd
      tables
        fields      = lt_value.

    if lv_rtn_cd is initial.

      read table lt_value into data(ls_value) with key fieldname = 'TREE_TEXT'.
      if sy-subrc = 0 .
        ls_ztyt_001-tree_text = ls_value-value.
      endif.

      read table lt_value into ls_value with key fieldname = 'TABNAME'.
      if sy-subrc = 0 .
        if ls_value-value is not initial.
          select count(*) from dd02l where tabname eq ls_value-value and tabclass in ( 'TRANSP','VIEW' ) and mainflag eq 'X'.
          if sy-subrc ne 0.
            message '表不存在，或不存在相关维护视图' type 'S' display like 'E'.
            return.
          endif.
        endif.
        ls_ztyt_001-tabname = ls_value-value.
      endif.

      read table lt_value into ls_value with key fieldname = 'DDTEXT'.
      if sy-subrc = 0 .
        ls_ztyt_001-ddtext = ls_value-value.
      endif.

      if ls_ztyt_001-tree_text is initial and ls_ztyt_001-tabname is initial and ls_ztyt_001-ddtext is initial.
        message '请至少维护一种信息' type 'S' display like 'E'.
        return.
      endif.

      lv_text = |确认更改节点 { lv_pop_title } 的数据？|.
      if get_user_answer( lv_text ) eq 'J'.
        if ls_ztyt_001 is not initial.
          modify ztyt_001 from ls_ztyt_001.
          modify table gt_ztyt_001 from ls_ztyt_001.
          read table gt_tree assigning field-symbol(<fs_tree>) with key tree_id = lv_node_key.
          if sy-subrc eq 0.
            data: ls_tree_line type ty_tree_list.
            move-corresponding ls_ztyt_001 to ls_tree_line.
            ls_tree_line-new_tree = 'X'.
            modify table <fs_tree>-tree_list from ls_tree_line .
          else.
            insert ls_tree_line into table <fs_tree>-tree_list.
          endif.
          add_items_for_node(
            iv_node_key    = lv_node_key
            iv_drop_handle = ' '
            iv_new         = 'U'
          ).
        endif.
      endif.

    else.
      message '用户已取消' type 'S' display like 'E'.
    endif.

  endmethod.

  method del_node_from_tree.

    data: lv_text type string.
    select single * into @data(ls_ztyt_001) from ztyt_001 where tree_id eq @gv_node_key.
    lv_text = |确认删除节点 { ls_ztyt_001-tree_text } 吗？|.
    if get_user_answer( lv_text ) eq 'J'.
      if ls_ztyt_001 is not initial.
        delete ztyt_001 from ls_ztyt_001 .
        delete table gt_ztyt_001 from ls_ztyt_001.
        go_list_tree->delete_node( gv_node_key ).
      endif.
    endif.

  endmethod.

  method call_sm30.

    select count( * ) from vcldir where vclname eq iv_tabname.

    if sy-subrc ne 0.
      call function 'VIEW_MAINTENANCE_CALL'
        exporting
          action                       = 'U'
          view_name                    = iv_tabname
          no_warning_for_clientindep   = 'X'
        exceptions
          client_reference             = 1
          foreign_lock                 = 2
          invalid_action               = 3
          no_clientindependent_auth    = 4
          no_database_function         = 5
          no_editor_function           = 6
          no_show_auth                 = 7
          no_tvdir_entry               = 8
          no_upd_auth                  = 9
          only_show_allowed            = 10
          system_failure               = 11
          unknown_field_in_dba_sellist = 12
          view_not_found               = 13
          others                       = 14.

    else.

      data: lv_viewname type vcldir-vclname.
      lv_viewname = iv_tabname.
      call function 'VIEWCLUSTER_MAINTENANCE_CALL'
        exporting
          viewcluster_name             = lv_viewname
          maintenance_action           = 'U'
        exceptions
          client_reference             = 1
          foreign_lock                 = 2
          viewcluster_not_found        = 3
          viewcluster_is_inconsistent  = 4
          missing_generated_function   = 5
          no_upd_auth                  = 6
          no_show_auth                 = 7
          object_not_found             = 8
          no_tvdir_entry               = 9
          no_clientindep_auth          = 10
          invalid_action               = 11
          saving_correction_failed     = 12
          system_failure               = 13
          unknown_field_in_dba_sellist = 14
          missing_corr_number          = 15
          others                       = 16.
    endif.
  endmethod.


  method show_table_data.

    if go_alv_grid is not initial.
      go_alv_grid->free( ).
      free go_alv_grid.
    endif.

    select count( * ) from vcldir where vclname eq iv_tabname.
    check sy-subrc ne 0.

    data: lt_table type ref to data.
    field-symbols: <fs_table> type standard table.

    create data lt_table type table of (iv_tabname).
    assign lt_table->* to <fs_table>.

    select * into corresponding fields of table <fs_table> from (iv_tabname).

    if go_alv_grid is initial.
      go_alv_grid = new cl_gui_alv_grid( i_parent = go_dock_container_right ).

      go_alv_grid->set_table_for_first_display(
        exporting
          i_structure_name = iv_tabname
        changing
          it_outtab        = <fs_table> ).
    endif.

  endmethod.

  method get_new_node_key.

    data: lv_numc    type numc3 value '001',
          lv_tree_id type ztyt_001-tree_id.

    select max( tree_id_num ) into @data(lv_tree_id_num) from ztyt_001 where ftree_id eq @iv_node_key .
    if lv_tree_id_num is not initial.
      add 1 to lv_tree_id_num.
      lv_numc = lv_tree_id_num.
    endif.
    lv_tree_id = |{ gv_node_key }{ lv_numc alpha = in }|.
    ev_num = lv_numc.
    ev_result = lv_tree_id.

  endmethod.

endclass.

module status_9000 output.
  set titlebar 'STATUS' with '配置表维护'.
  go_report->show_tree( ).
endmodule.

module user_command_9000 input.

  case sy-ucomm.
    when 'BACK' or '%EX' or 'RW'.
      leave program.
  endcase.
  clear ok_code.

endmodule.
```



程序所使用表如下：
![树配置表](https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20240525140440089.png)

