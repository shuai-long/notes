```abap
class ZCL_SEND_EMAIL definition
  public
  final
  create private .

public section.

  types:
    begin of ty_object,
             uuid      type sysuuid_c32,
             send_mail type ref to zcl_send_email,
           end of ty_object .
  types:
    begin of ty_email_address,
             email type ad_smtpadr,
           end of ty_email_address .
  types:
    begin of ty_attr_info,
             attr_type    type so_obj_tp,        "邮件附件类型
             attr_subject type so_obj_des,       "邮件附件描述
             attr_xstring type xstring,          "邮件附件内容
             attr_size    type sood-objlen,      "邮件附件大小
           end of ty_attr_info .
  types:
    tt_attr_list type standard table of ty_attr_info with default key .
  types:
    tt_email_list type standard table of ty_email_address with default key .

  constants GC_ATTACHMENT_TYPE type SO_OBJ_TP value 'BIN' ##NO_TEXT. "附件格式

  class-methods FACTORY
    importing
      !IV_UUID type SYSUUID_C32 optional
      !IV_SUBJECT type SO_OBJ_DES optional
      !IV_MAINTYPE type SO_OBJ_TP default 'HTM'
      !IT_MAINVALUE type SOLI_TAB optional
      !IV_SENDER type AD_SMTPADR optional
      !IV_RECIPIENT type AD_SMTPADR optional
      !IV_RECIPIENT_COPY type AD_SMTPADR optional
      !IV_ATTACHMENT_TYPE type SO_OBJ_TP default GC_ATTACHMENT_TYPE
      !IV_ATTACHMENT_SUBJECT type SO_OBJ_DES optional
      !IV_ATTACHMENT_XTRING type XSTRING optional
      !IV_ATTACHMENT_TABLE type SOLIX_TAB optional
    exporting
      !EV_UUID type SYSUUID_C32
    returning
      value(EO_RESULT) type ref to ZCL_SEND_EMAIL
    raising
      CX_UUID_ERROR
      CX_BCS .
  methods SET_SUBJECT
    importing
      !IV_SUBJECT type SO_OBJ_DES .
  methods SET_MAIN_CONTENT
    importing
      !IV_MAINTYPE type SO_OBJ_TP default 'HTM'
      !IT_MAINVALUE type SOLI_TAB .
  methods SET_SENDER
    importing
      !IV_EMAIL type ANY .
  methods ADD_RECIPIENT
    importing
      !IV_EMAIL type ANY .
  methods ADD_RECIPIENT_COPY
    importing
      !IV_EMAIL type ANY .
  methods ADD_ATTACHMENT
    importing
      !IV_ATTACHMENT_TYPE type SO_OBJ_TP default GC_ATTACHMENT_TYPE
      !IV_ATTACHMENT_SUBJECT type SO_OBJ_DES
      !IV_ATTACHMENT_XTRING type XSTRING optional
      !IV_ATTACHMENT_TABLE type SOLIX_TAB optional
    raising
      CX_DOCUMENT_BCS .
  methods SEND_MAIL
    raising
      CX_BCS .
  methods GET_SEND_MAIL
    returning
      value(R_RESULT) type ref to CL_BCS .
  methods GET_DOCUMENT
    returning
      value(R_RESULT) type ref to CL_DOCUMENT_BCS .
  methods GET_SENDER
    returning
      value(R_RESULT) type AD_SMTPADR .
  methods GET_SUBJECT
    returning
      value(R_RESULT) type SO_OBJ_DES .
  methods GET_MAINTYPE
    returning
      value(R_RESULT) type SO_OBJ_TP .
  methods GET_MAINVALUE
    returning
      value(R_RESULT) type SOLI_TAB .
  methods GET_ATTR_LIST
    returning
      value(R_RESULT) type TT_ATTR_LIST .
  methods GET_RECIPIENT
    returning
      value(R_RESULT) type TT_EMAIL_LIST .
  methods GET_RECIPIENT_COPY
    returning
      value(R_RESULT) type TT_EMAIL_LIST .
  protected section.
private section.

  class-data:
    gt_object type hashed table of ty_object with unique key uuid .
  data GO_SEND_MAIL type ref to CL_BCS .
  data GO_DOCUMENT type ref to CL_DOCUMENT_BCS .    "用来放置发送的内容.
  data GO_EXCEPTION type ref to CX_BCS .            "发送邮件主要用到的功能类, 包括创建发送请求, 添加发送内容,添加发送地址, 到最终的发送指令发出. "异常类, 用于捕捉发送邮件过程中出现的异常.
  data GV_SENDER type AD_SMTPADR .
  data GV_SUBJECT type SO_OBJ_DES .                "邮件主题
  data GV_MAINTYPE type SO_OBJ_TP .                "邮件内容类型
  data GT_MAINVALUE type SOLI_TAB .                "邮件内容正文
  data:
    gt_attr_list      type standard table of ty_attr_info with default key .      "附件列表
  data:
    gt_recipient      type standard table of ty_email_address with default key .      "收件人
  data:                                            "发件人
    gt_recipient_copy type standard table of ty_email_address with default key .      "抄送人

  methods CONSTRUCTOR
    importing
      !IV_SUBJECT type SO_OBJ_DES optional
      !IV_MAINTYPE type SO_OBJ_TP default 'HTM'
      !IT_MAINVALUE type SOLI_TAB optional
      !IV_SENDER type AD_SMTPADR optional
      !IV_RECIPIENT type AD_SMTPADR optional
      !IV_RECIPIENT_COPY type AD_SMTPADR optional
      !IV_ATTACHMENT_TYPE type SO_OBJ_TP default GC_ATTACHMENT_TYPE
      !IV_ATTACHMENT_SUBJECT type SO_OBJ_DES optional
      !IV_ATTACHMENT_XTRING type XSTRING optional
      !IV_ATTACHMENT_TABLE type SOLIX_TAB optional
    raising
      CX_BCS .
ENDCLASS.



CLASS ZCL_SEND_EMAIL IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Static Public Method ZCL_SEND_EMAIL=>FACTORY
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_UUID                        TYPE        SYSUUID_C32(optional)
* | [--->] IV_SUBJECT                     TYPE        SO_OBJ_DES(optional)
* | [--->] IV_MAINTYPE                    TYPE        SO_OBJ_TP (default ='HTM')
* | [--->] IT_MAINVALUE                   TYPE        SOLI_TAB(optional)
* | [--->] IV_SENDER                      TYPE        AD_SMTPADR(optional)
* | [--->] IV_RECIPIENT                   TYPE        AD_SMTPADR(optional)
* | [--->] IV_RECIPIENT_COPY              TYPE        AD_SMTPADR(optional)
* | [--->] IV_ATTACHMENT_TYPE             TYPE        SO_OBJ_TP (default =GC_ATTACHMENT_TYPE)
* | [--->] IV_ATTACHMENT_SUBJECT          TYPE        SO_OBJ_DES(optional)
* | [--->] IV_ATTACHMENT_XTRING           TYPE        XSTRING(optional)
* | [--->] IV_ATTACHMENT_TABLE            TYPE        SOLIX_TAB(optional)
* | [<---] EV_UUID                        TYPE        SYSUUID_C32
* | [<-()] EO_RESULT                      TYPE REF TO ZCL_SEND_EMAIL
* | [!CX!] CX_UUID_ERROR
* | [!CX!] CX_BCS
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method factory.

    data: lv_notfound type c.

    if iv_uuid is not initial.
      read table gt_object into data(ls_object) with key uuid = iv_uuid.
      if sy-subrc ne 0.
        lv_notfound = 'X'.
      endif.
    endif.

    if iv_uuid is initial or lv_notfound is not initial.
      clear ls_object.
      if lv_notfound is not initial.
        ls_object-uuid = iv_uuid.
      else.
        ls_object-uuid = cl_system_uuid=>if_system_uuid_static~create_uuid_c32( ).
      endif.

      ls_object-send_mail = new zcl_send_email(
        iv_subject            = iv_subject
        iv_maintype           = iv_maintype
        it_mainvalue          = it_mainvalue
        iv_sender             = iv_sender
        iv_recipient          = iv_recipient
        iv_recipient_copy     = iv_recipient_copy
        iv_attachment_type    = iv_attachment_type
        iv_attachment_subject = iv_attachment_subject
        iv_attachment_xtring  = iv_attachment_xtring
        iv_attachment_table   = iv_attachment_table
      ).
      insert ls_object into table gt_object.
    endif.

    ev_uuid = ls_object-uuid.
    eo_result = ls_object-send_mail.

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method ZCL_SEND_EMAIL->CONSTRUCTOR
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_SUBJECT                     TYPE        SO_OBJ_DES(optional)
* | [--->] IV_MAINTYPE                    TYPE        SO_OBJ_TP (default ='HTM')
* | [--->] IT_MAINVALUE                   TYPE        SOLI_TAB(optional)
* | [--->] IV_SENDER                      TYPE        AD_SMTPADR(optional)
* | [--->] IV_RECIPIENT                   TYPE        AD_SMTPADR(optional)
* | [--->] IV_RECIPIENT_COPY              TYPE        AD_SMTPADR(optional)
* | [--->] IV_ATTACHMENT_TYPE             TYPE        SO_OBJ_TP (default =GC_ATTACHMENT_TYPE)
* | [--->] IV_ATTACHMENT_SUBJECT          TYPE        SO_OBJ_DES(optional)
* | [--->] IV_ATTACHMENT_XTRING           TYPE        XSTRING(optional)
* | [--->] IV_ATTACHMENT_TABLE            TYPE        SOLIX_TAB(optional)
* | [!CX!] CX_BCS
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method constructor.

    go_send_mail = cl_bcs=>create_persistent( ).

    check go_send_mail is not initial.

    "设置邮件主题
    if iv_subject is supplied.
      set_subject( iv_subject ).
    endif.

    "设置邮件正文
    if iv_maintype is supplied and it_mainvalue is supplied.
      set_main_content(
        iv_maintype  = iv_maintype
        it_mainvalue = it_mainvalue
      ).
    endif.

    "设置发件人
    if iv_sender is not initial.
      set_sender( iv_sender ).
    endif.

    "设置收件人
    if iv_recipient is not initial.
      add_recipient( iv_recipient ).
    endif.

    "设置抄送人
    if iv_recipient_copy is not initial.
      add_recipient_copy( iv_recipient_copy ).
    endif.

    "设置附件信息
    if iv_attachment_type is not initial and iv_attachment_subject is not initial and
        ( iv_attachment_xtring is not initial or iv_attachment_table is not initial ).

      add_attachment(
        iv_attachment_type    = iv_attachment_type
        iv_attachment_subject = iv_attachment_subject
        iv_attachment_xtring  = iv_attachment_xtring
        iv_attachment_table   = iv_attachment_table
      ).

    endif.


  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->SET_SUBJECT
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_SUBJECT                     TYPE        SO_OBJ_DES
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method set_subject.
    gv_subject = iv_subject.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->SET_MAIN_CONTENT
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_MAINTYPE                    TYPE        SO_OBJ_TP (default ='HTM')
* | [--->] IT_MAINVALUE                   TYPE        SOLI_TAB
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method set_main_content.
    gv_maintype  = iv_maintype.
    gt_mainvalue = it_mainvalue.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->SET_SENDER
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_EMAIL                       TYPE        ANY
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method set_sender.
    gv_sender = iv_email.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->ADD_RECIPIENT
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_EMAIL                       TYPE        ANY
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method add_recipient.
    append initial line to gt_recipient assigning field-symbol(<fs_recilient>).
    <fs_recilient>-email = iv_email.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->ADD_RECIPIENT_COPY
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_EMAIL                       TYPE        ANY
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method add_recipient_copy.
    append initial line to gt_recipient assigning field-symbol(<fs_recilient_copy>).
    <fs_recilient_copy>-email = iv_email.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->ADD_ATTACHMENT
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_ATTACHMENT_TYPE             TYPE        SO_OBJ_TP (default =GC_ATTACHMENT_TYPE)
* | [--->] IV_ATTACHMENT_SUBJECT          TYPE        SO_OBJ_DES
* | [--->] IV_ATTACHMENT_XTRING           TYPE        XSTRING(optional)
* | [--->] IV_ATTACHMENT_TABLE            TYPE        SOLIX_TAB(optional)
* | [!CX!] CX_DOCUMENT_BCS
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method add_attachment.

    data: lv_file type xstring.

    if iv_attachment_xtring is not initial.
      lv_file = iv_attachment_xtring.
    else.
      lv_file = cl_bcs_convert=>solix_to_xstring( it_solix = iv_attachment_table ).
    endif.

    check lv_file is not initial.

    append initial line to gt_attr_list assigning field-symbol(<fs_attr_list>).
    <fs_attr_list>-attr_type    = iv_attachment_type.
    <fs_attr_list>-attr_subject = iv_attachment_subject.
    <fs_attr_list>-attr_xstring = lv_file.
    <fs_attr_list>-attr_size    = xstrlen( lv_file ).

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->SEND_MAIL
* +-------------------------------------------------------------------------------------------------+
* | [!CX!] CX_BCS
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method send_mail.

    go_document = cl_document_bcs=>create_document(
      i_type    = gv_maintype
      i_subject = gv_subject
      i_text    = gt_mainvalue
    ).

    loop at gt_attr_list into data(ls_attr).
      data(lt_att_content) = cl_bcs_convert=>xstring_to_solix( ls_attr-attr_xstring ).
      go_document->add_attachment(
        i_attachment_type    = ls_attr-attr_type
        i_attachment_subject = ls_attr-attr_subject
        i_attachment_size    = ls_attr-attr_size
        i_att_content_hex    = lt_att_content
      ).
    endloop.

    go_send_mail->set_document( go_document ).

    data(lo_sender) = cl_cam_address_bcs=>create_internet_address( gv_sender ).
    go_send_mail->set_sender( lo_sender ).

    loop at gt_recipient into data(ls_recipient).
      data(lo_recipient) = cl_cam_address_bcs=>create_internet_address( ls_recipient-email ).
      go_send_mail->add_recipient( lo_recipient ).
    endloop.

    loop at gt_recipient_copy into data(ls_recipient_copy).
      data(lo_recipient_copy) = cl_cam_address_bcs=>create_internet_address( ls_recipient_copy-email ).
      go_send_mail->add_recipient( i_recipient = lo_recipient i_copy = abap_true ).
    endloop.

    go_send_mail->set_status_attributes(
      i_requested_status = 'E'
      i_status_mail      = 'E' ).

    go_send_mail->set_send_immediately( 'X' ).

    data(lv_result) = go_send_mail->send( 'X' ).

    commit work and wait.

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->GET_SEND_MAIL
* +-------------------------------------------------------------------------------------------------+
* | [<-()] R_RESULT                       TYPE REF TO CL_BCS
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method get_send_mail.
    r_result = me->go_send_mail.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->GET_DOCUMENT
* +-------------------------------------------------------------------------------------------------+
* | [<-()] R_RESULT                       TYPE REF TO CL_DOCUMENT_BCS
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method get_document.
    r_result = me->go_document.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->GET_SENDER
* +-------------------------------------------------------------------------------------------------+
* | [<-()] R_RESULT                       TYPE        AD_SMTPADR
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method get_sender.
    r_result = me->gv_sender.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->GET_SUBJECT
* +-------------------------------------------------------------------------------------------------+
* | [<-()] R_RESULT                       TYPE        SO_OBJ_DES
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method get_subject.
    r_result = me->gv_subject.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->GET_MAINTYPE
* +-------------------------------------------------------------------------------------------------+
* | [<-()] R_RESULT                       TYPE        SO_OBJ_TP
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method get_maintype.
    r_result = me->gv_maintype.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->GET_MAINVALUE
* +-------------------------------------------------------------------------------------------------+
* | [<-()] R_RESULT                       TYPE        SOLI_TAB
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method get_mainvalue.
    r_result = me->gt_mainvalue.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->GET_ATTR_LIST
* +-------------------------------------------------------------------------------------------------+
* | [<-()] R_RESULT                       TYPE        TT_ATTR_LIST
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method get_attr_list.
    r_result = me->gt_attr_list.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->GET_RECIPIENT
* +-------------------------------------------------------------------------------------------------+
* | [<-()] R_RESULT                       TYPE        TT_EMAIL_LIST
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method get_recipient.
    r_result = me->gt_recipient.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_SEND_EMAIL->GET_RECIPIENT_COPY
* +-------------------------------------------------------------------------------------------------+
* | [<-()] R_RESULT                       TYPE        TT_EMAIL_LIST
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method get_recipient_copy.
    r_result = me->gt_recipient_copy.
  endmethod.
ENDCLASS.
```

