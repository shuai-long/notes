# 1. 日志查看程序

```abap
*&---------------------------------------------------------------------*
*& Report ztyr_002
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
report ztyr_002.

tables: sscrfields,ztint_info, ztint_para.

class lcl_report definition deferred.

data: go_report    type ref to lcl_report,
      ls_ztint_log type ztint_log.

selection-screen: function key 1.

selection-screen begin of block b1 with frame title text-001.
  select-options: s_systid for ztint_para-system_id,
                  s_zkfqq  for ztint_para-zkfqq,
                  s_zkfxh  for ztint_para-zkfxh,
                  s_crdate for ztint_info-crdate no-extension default sy-datum,
                  s_crtime for ztint_info-crtime no-extension,
                  s_key    for ls_ztint_log-key1 no intervals.
  parameters: p_cont type char200.

  parameters: p_inout as listbox visible length 15 default '3' obligatory.

selection-screen end of block b1.

class lcl_report definition.
  public section.
    types: begin of ty_alv,
             status         type char10,          "状态
             system_id      type ze_system_id,    "系统ID
             system_name    type ze_system_name,  "系统描述
             zkfqq          type ze_kfqq,         "开发请求
             zkfxh          type ze_kfxh,         "开发序号
             zkfqqtext      type ze_kfqqtext,     "开发请求文本
             zinout         type char10,          "调入/调出接口
             zurl           type ze_intname,      "接口地址
             zurl_full      type ze_intname,      "接口地址（全）
             ipport         type ze_ipport,       "域名：端口
             guid           type sysuuid_c32,     "唯一标识
             key1           type ze_key1,         "日志关键字1
             key2           type ze_key2,         "日志关键字2
             key3           type ze_key3,         "日志关键字3
             msgty          type msgty,           "消息类型
             msgtx          type msgtx,           "消息文本
             crname         type ze_crname,       "创建人
             crdate         type ze_crdate,       "创建日期
             crtime         type ze_crtime,       "创建时间
             crtimestamp    type ze_crtimestamp,  "创建时间戳
             chname         type ze_crname,       "更改人
             chdate         type ze_crdate,       "更改日期
             chtime         type ze_crtime,       "更改时间
             chtimestamp    type ze_crtimestamp,  "更改时间戳
             zinput_char    type char100,         "入参报文
             zoutput_char   type char100,         "反参报文
             zinput         type xstring,         "入参报文
             zoutput        type xstring,         "出参报文
             zinput_string  type string,
             zoutput_string type string,
           end of ty_alv.
    class-methods init_sel_screen.
    class-methods deal_ucomm.

    constants: gc_report_title type string value '接口日志表'.

    methods: constructor.

    methods:
      call_screen,
      set_data_to_alv,
      create_alv,
      set_fcat,
      set_layout,
      set_handler,
      set_variant,
      show_json_data
        importing
          iv_data type any.

    methods:
      handle_toolbar for event toolbar of cl_gui_alv_grid
        importing
          e_object
          e_interactive,

      handle_user_command for event user_command of cl_gui_alv_grid
        importing
          e_ucomm,

      handle_double_click for event double_click of cl_gui_alv_grid
        importing
          e_row
          e_column.


  private section.
    data: go_alv_grid    type ref to cl_gui_alv_grid,
          gt_alv_data    type table of ty_alv,
          gt_alv_fcat    type lvc_t_fcat,
          gs_alv_layout  type lvc_s_layo,
          gs_alv_variant type disvariant,
          gs_alv_stbl    type lvc_s_stbl.

endclass.


class lcl_report implementation.
  method constructor.

  endmethod.

  method init_sel_screen.
    sscrfields-functxt_01 = value smp_dyntxt(
      icon_id   = icon_configuration
      icon_text = '接口地址维护'
      quickinfo = '接口地址维护'
    ).

    s_crtime-sign = 'I'.
    s_crtime-option = 'BT'.
    s_crtime-high = sy-uzeit.
    if sy-uzeit ge '120000'.
      s_crtime-low = '120000'.
    else.
      s_crtime-low = '080000'.
    endif.
    append s_crtime to s_crtime.

    data(lt_values) = value vrm_values(
        ( key = '1' text = '调入接口' )
        ( key = '2' text = '调出接口' )
        ( key = '3' text = '调入调出接口' ) ).

    call function 'VRM_SET_VALUES'
      exporting
        id     = 'P_INOUT'
        values = lt_values.

  endmethod.

  method deal_ucomm.
    case sscrfields-ucomm.
      when 'FC01'.
        call transaction 'ZTY002_CONFIG'.
    endcase.

  endmethod.

  method call_screen.
    call screen 9000.
  endmethod.

  method set_data_to_alv.

    data: lv_where        type string,
          ls_alv          type ty_alv,
          lt_ztint_ipport type hashed table of ztint_ipport with unique key mandt system_id,
          lt_ztint_info   type hashed table of ztint_info with unique key mandt system_id zkfqq zkfxh.

    if p_inout ne '3'.
      lv_where = |ZINOUT EQ '{ p_inout }'|.
    endif.

    select * into corresponding fields of table lt_ztint_ipport from ztint_ipport.

    select * into corresponding fields of table lt_ztint_info from ztint_info.

    select * into table @data(lt_ztint_log) from ztint_log where
       system_id in @s_systid and
       zkfqq     in @s_zkfqq  and
       zkfxh     in @s_zkfxh  and
       ( crdate in @s_crdate or chdate in @s_crdate ) and
       ( crtime in @s_crtime or chtime in @s_crtime ) and
       ( key1 in @s_key or key2 in @s_key or key3 in @s_key ) and
       (lv_where).

    loop at lt_ztint_log into data(ls_ztint_log).
      clear ls_alv.
      move-corresponding ls_ztint_log to ls_alv.

      read table lt_ztint_ipport into data(ls_ztint_ipport) with key mandt = sy-mandt system_id = ls_alv-system_id.
      if sy-subrc eq 0.
        ls_alv-system_name = ls_ztint_ipport-system_name.
        ls_alv-ipport      = ls_ztint_ipport-ipport.
      endif.

      read table lt_ztint_info into data(ls_ztint_info) with key mandt = sy-mandt system_id = ls_alv-system_id zkfqq = ls_alv-zkfqq zkfxh = ls_alv-zkfxh.
      if sy-subrc eq 0.
        ls_alv-zkfqqtext = ls_ztint_info-zkfqqtext.
        ls_alv-zurl = |{ ls_ztint_info-zurl }|.
      endif.

      case ls_alv-msgty.
        when 'S'.
          ls_alv-status = icon_led_green.
        when 'E'.
          ls_alv-status = icon_led_red.
        when others.
          ls_alv-status = icon_led_yellow.
      endcase.

      case ls_alv-zinout.
        when '1'.
          ls_alv-zinout = '调入接口'.
        when '2'.
          ls_alv-zinout = '调出接口'.
      endcase.

      ls_alv-zurl_full = |{ ls_alv-ipport }{ ls_alv-zurl }|.
      ls_alv-zinput_string  = cl_abap_codepage=>convert_from( ls_alv-zinput ).
      ls_alv-zoutput_string = cl_abap_codepage=>convert_from( ls_alv-zoutput ).
      ls_alv-zinput_char    = ls_alv-zinput_string.
      ls_alv-zoutput_char   = ls_alv-zoutput_string.

      if ls_alv-zinput_string ns p_cont and ls_alv-zoutput_string ns p_cont.
        continue.
      endif.

      append ls_alv to gt_alv_data.
    endloop.

    sort gt_alv_data by crtime descending.

  endmethod.

  method create_alv.
    if go_alv_grid is initial.

      go_alv_grid = new cl_gui_alv_grid(
        i_parent = cl_gui_container=>default_screen
      ).

      set_fcat( ).
      set_layout( ).
      set_handler( ).
      set_variant( ).

      go_alv_grid->set_table_for_first_display(
        exporting
          is_layout       = gs_alv_layout
          i_save          = 'A'
          is_variant      = gs_alv_variant
        changing
          it_outtab       = gt_alv_data
          it_fieldcatalog = gt_alv_fcat
      ).

    else.
      go_alv_grid->refresh_table_display( is_stable = gs_alv_stbl ).
    endif.
  endmethod.


  method set_fcat.

    define append_fcat.
      append initial line to gt_alv_fcat assigning <fs_fcat>.
      <fs_fcat>-col_pos = lines( gt_alv_fcat ).
      <fs_fcat>-fieldname = &1.
      <fs_fcat>-coltext   = &2.
      <fs_fcat>-reptext   = &2.
      <fs_fcat>-scrtext_l = &2.
      <fs_fcat>-scrtext_m = &2.
      <fs_fcat>-scrtext_s = &2.
      <fs_fcat>-key       = &3.
      <fs_fcat>-inttype   = &4.
      <fs_fcat>-intlen    = &5.
      if &1 eq 'STATUS'.
        <fs_fcat>-icon = 'X'.
      endif.
    end-of-definition.

    field-symbols: <fs_fcat> type lvc_s_fcat.
    " 字段 、 描述 、 key、 内部类型、 内部长度
    append_fcat 'STATUS' '状态' 'X' '' ''.
    append_fcat 'SYSTEM_ID' '系统ID' 'X' '' ''.
    append_fcat 'SYSTEM_NAME' '系统描述' 'X' '' ''.
    append_fcat 'ZKFQQ' '开发请求' 'X' '' ''.
    append_fcat 'ZKFXH' '开发序号' 'X' '' ''.
    append_fcat 'ZKFQQTEXT' '开发请求文本' 'X' '' ''.
    append_fcat 'ZINOUT' '调入/调出接口' 'X' '' ''.
    append_fcat 'GUID' '唯一标识' ' ' '' ''.
    append_fcat 'KEY1' '日志关键字1' ' ' '' ''.
    append_fcat 'KEY2' '日志关键字2' ' ' '' ''.
    append_fcat 'KEY3' '日志关键字3' ' ' '' ''.
    append_fcat 'ZINPUT_CHAR'  '入参报文' ' ' '' ''.
    append_fcat 'ZOUTPUT_CHAR' '反参报文' ' ' '' ''.
    append_fcat 'MSGTY' '消息类型' ' ' '' ''.
    append_fcat 'MSGTX' '消息文本' ' ' '' ''.
    append_fcat 'ZURL_FULL' '接口地址' ' ' '' ''.
    append_fcat 'CRNAME'        '创建人' ' ' '' ''.
    append_fcat 'CRDATE'        '创建日期' ' ' '' ''.
    append_fcat 'CRTIME'        '创建时间' ' ' '' ''.
    append_fcat 'CRTIMESTAMP'   '创建时间戳' ' ' '' ''.
    append_fcat 'CHNAME'        '更改人' ' ' '' ''.
    append_fcat 'CHDATE'        '更改日期' ' ' '' ''.
    append_fcat 'CHTIME'        '更改时间' ' ' '' ''.
    append_fcat 'CHTIMESTAMP'   '更改时间戳' ' ' '' ''.

  endmethod.


  method set_layout.

    gs_alv_layout-cwidth_opt = 'A'.
    gs_alv_layout-zebra      = 'X'.

  endmethod.

  method set_handler.

    define set_handler.
      set handler &1 for go_alv_grid.
    end-of-definition.

    set_handler: handle_toolbar,
                 handle_user_command,
                 handle_double_click.

    go_alv_grid->set_toolbar_interactive( ).

    go_alv_grid->register_edit_event(
      exporting
        i_event_id = cl_gui_alv_grid=>mc_evt_modified ).

  endmethod.

  method set_variant.
    gs_alv_variant-report = sy-repid.
*    gs_alv_variant-handle = ''
  endmethod.

  method handle_toolbar.
    data: ls_toolbar type stb_button.
    define append_toolbar.
      ls_toolbar-function  = &1.
      ls_toolbar-icon      = &2.
      ls_toolbar-quickinfo = &3.
      ls_toolbar-text      = &4.
      ls_toolbar-disabled  = &5.
      append ls_toolbar to e_object->mt_toolbar.
    end-of-definition.
  endmethod.

  method handle_user_command.
    case e_ucomm.
      when ''.
    endcase.
    go_alv_grid->refresh_table_display( is_stable = gs_alv_stbl ).
  endmethod.

  method handle_double_click.

    check e_row-index is not initial.
    case e_column-fieldname.
      when 'ZINPUT_CHAR'.
        read table gt_alv_data into data(ls_alv) index e_row-index.
        if sy-subrc eq 0.
          show_json_data( ls_alv-zinput_string ).
        endif.
      when 'ZOUTPUT_CHAR'.
        read table gt_alv_data into ls_alv index e_row-index.
        if sy-subrc eq 0.
          show_json_data( ls_alv-zoutput_string ).
        endif.
    endcase.
  endmethod.

  method show_json_data.

    data: lv_data type string.

    lv_data = iv_data.

    try .
        call transformation sjson2html source xml lv_data result xml data(lv_html).
        data(lv_convert) = cl_abap_codepage=>convert_from( lv_html ).
      catch cx_root.
    endtry.
    if lv_convert is initial or lv_convert eq '<html><p style="color:red">No JSON</p></html>'..
      lv_convert = lv_data.
    endif.
    cl_abap_browser=>show_html( html_string = lv_convert ).

  endmethod.

endclass.


initialization.
  lcl_report=>init_sel_screen( ).

at selection-screen.
  lcl_report=>deal_ucomm( ).

start-of-selection.
  go_report = new lcl_report( ).
  go_report->set_data_to_alv( ).

end-of-selection.
  go_report->call_screen( ).

*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_9000  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
module status_9000 output.

  set titlebar 'STATUS' with lcl_report=>gc_report_title.
  go_report->create_alv( ).

endmodule.

*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_9000  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
module user_command_9000 input.

  case sy-ucomm.
    when 'BACK'.
      set screen 0.
  endcase.

endmodule.
```

# 2. 日志记录程序

```abap
class zcl_http_extension definition
  public
  create private .

  public section.

    interfaces if_http_extension .

    types:
      begin of ty_list_params,
        name  type char100,
        value type char255,
      end of ty_list_params .

    constants gc_success type string value 'S' ##NO_TEXT.
    constants gc_error type string value 'E' ##NO_TEXT.
*      gc_cloud_dev_u type string value 'sb-910674ea-5e02-49e3-9393-f0000ed1e03a!b3057|it-rt-dev-d4f8kc88!b39',
*      gc_cloud_dev_p type string value '5263af57-43b6-4449-8ce9-0340eaa4a66e$4V-Td8_bJy41Jz_L_gBfCwvped50GMSgMkJSRcGYDHM=',
*      gc_cloud_prd_u type string value 'sb-886fc0cb-b6b4-4672-8f32-bd7d20c8b0eb!b3064|it-rt-prd-vnkayiea!b39',
*      gc_cloud_prd_p type string value 'af013566-bcae-4662-af92-f8225693c33c$bDusvmstFDb7kNbs5DUzjiRNGkpDo-swfeozw2r5yOo=',
*      gc_sap_u       type string value 'SAPIF',
*      gc_sap_p       type string value 'Basel@2024'.
    constants gc_hfield_server_protocal_k type string value '~server_protocol' ##NO_TEXT.
    constants gc_hfield_server_protocal_v type string value 'HTTP/1.1' ##NO_TEXT.
    constants gc_hfield_content_type_k type string value 'Content-Type' ##NO_TEXT.
    constants gc_hfield_content_type_v type string value 'application/json' ##NO_TEXT.
    constants gc_hfield_content_type_vutf8 type string value 'application/json; charset=utf-8' ##NO_TEXT.
    constants gc_hfield_sap_client_k type string value 'sap-client' ##NO_TEXT.
    constants gc_hfield_request_method_k type string value '~request_method' ##NO_TEXT.
    constants gc_hfield_path_k type string value '~path' ##NO_TEXT.
    constants gc_hfield_query_string_k type string value '~query_string' ##NO_TEXT.
    constants gc_hfield_host_k type string value 'host' ##NO_TEXT.
    constants gc_hfield_server_name_k type string value '~server_name' ##NO_TEXT.
    constants gc_hfield_server_port_k type string value '~server_port' ##NO_TEXT.
    constants gc_hfield_expanded_k type string value '~uri_scheme_expanded' ##NO_TEXT.
    constants gc_hfield_location_id_k type string value 'sap-connectivity-scc-location_id' ##NO_TEXT.

    data gv_uuid type sysuuid_c32 .             "日志 ID
    data req_json type string .                 "请求报文
    data res_json type string .                 "响应报文
    data method type string .                   "请求动作：post、get、put、delete
    data path type string .                     "请求路径
    data host type string .                     "域名端口信息
    data url_expand type string .               "http / https
    data server_name type string .              "域名
    data server_port type string .              "端口
    data params type string .                   "参数
    data loction_id type string .               "子账户标识
    data:
      gt_params type hashed table of ty_list_params with unique key name ."参数列表

    class-methods initialization
      importing
                !io_server       type ref to if_http_server
      returning value(eo_result) type ref to zcl_http_extension.

    methods get_data
      changing
        !is_data type any .
    methods send_data
      importing
        !iv_code    type i default 200
        !iv_reason  type string default 'Ok'
        !is_data    type any
        !iv_key1    type any optional
        !iv_key2    type any optional
        !iv_key3    type any optional
        !iv_type    type any optional
        !iv_message type any optional .
    methods set_keys
      importing
        !iv_key1 type any
        !iv_key2 type any optional
        !iv_key3 type any optional .
    methods set_type
      importing
        !iv_type    type any
        !iv_message type any .
    methods update_type
      importing
        !iv_type    type any
        !iv_message type any .
    class-methods create_http_to_send_json
      importing
        !iv_systemid   type any
        !iv_kfqq       type any
        !iv_kfxh       type any
        !iv_json       type any optional
        !iv_file       type xstring optional
        !iv_filename   type string optional
        !iv_key1       type any optional
        !iv_key2       type any optional
        !iv_key3       type any optional
      exporting
        !ev_type       type any
        !ev_message    type any
      returning
        value(ev_json) type string .
    methods set_client_author
      changing
        !io_http_client type ref to if_http_client .
    methods set_client_header_fields
      returning
        value(et_hfields) type tihttpnvp .
  protected section.

  private section.

    data go_server type ref to if_http_server .
    data gv_flag type char1 .
    data gs_ztint_ipport type ztint_ipport .
    data gs_ztint_info type ztint_info .
    data:
      gt_ztint_para type table of ztint_para .
    data gs_ztint_log type ztint_log .
    data gv_key1 type string .
    data gv_key2 type string .
    data gv_key3 type string .
    data gv_type type char1 .
    data gv_message type string .
    data xres_json type xstring .
    data xreq_json type xstring .

    methods constructor
      importing
        !io_server type ref to if_http_server optional.

    methods create_http_client
      importing
        !iv_systemid     type any
        !iv_kfqq         type any
        !iv_kfxh         type any
      exporting
        !ev_type         type any
        !ev_message      type any
      returning
        value(eo_object) type ref to if_http_client .
    methods save_log
      importing
        !iv_flag  type char1 default ' '
        !iv_inout type char1 default '1' .
ENDCLASS.



CLASS ZCL_HTTP_EXTENSION IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Static Public Method ZCL_HTTP_EXTENSION=>INITIALIZATION
* +-------------------------------------------------------------------------------------------------+
* | [--->] IO_SERVER                      TYPE REF TO IF_HTTP_SERVER
* | [<-()] EO_RESULT                      TYPE REF TO ZCL_HTTP_EXTENSION
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method initialization.

    data: lo_http_extension type ref to zcl_http_extension.

    lo_http_extension = new zcl_http_extension( io_server ).

    eo_result = lo_http_extension.

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method ZCL_HTTP_EXTENSION->CONSTRUCTOR
* +-------------------------------------------------------------------------------------------------+
* | [--->] IO_SERVER                      TYPE REF TO IF_HTTP_SERVER(optional)
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method constructor.

    go_server = io_server.

    check go_server is not initial.

    req_json    = go_server->request->get_cdata( ).
    method      = go_server->request->get_header_field( gc_hfield_request_method_k ).
    path        = go_server->request->get_header_field( gc_hfield_path_k ).
    params      = go_server->request->get_header_field( gc_hfield_query_string_k ).
    host        = go_server->request->get_header_field( gc_hfield_host_k ).
    server_name = go_server->request->get_header_field( gc_hfield_server_name_k ).
    server_port = go_server->request->get_header_field( gc_hfield_server_port_k ).
    url_expand  = to_lower( go_server->request->get_header_field( gc_hfield_expanded_k ) ).
    loction_id  = go_server->request->get_header_field( gc_hfield_location_id_k ).

    data: lv_ipport type string.
    lv_ipport = |{ url_expand }://{ host }|.

    if loction_id is not initial.

      select single * into corresponding fields of gs_ztint_ipport
         from ztint_ipport where ipport_virtual eq lv_ipport.

      if gs_ztint_ipport is not initial.
        select single * into corresponding fields of gs_ztint_info
          from ztint_info where systemid_map eq gs_ztint_ipport-system_id and zurl_map eq path.
      endif.

    else.

      select single * into corresponding fields of gs_ztint_ipport
          from ztint_ipport where ipport eq lv_ipport.

      if gs_ztint_ipport is not initial.
        select single * into corresponding fields of gs_ztint_info
            from ztint_info where system_id eq gs_ztint_ipport-system_id and zurl eq path .
      endif.

    endif.

    save_log( ).

    if params is not initial.
      data: ls_list_params type ty_list_params.
      split params at '&' into table data(lt_list_params).
      loop at lt_list_params into data(ls_param_line).
        split ls_param_line at '=' into table data(lt_params).
        clear ls_list_params.

        read table lt_params into data(ls_param) index 1.
        if sy-subrc eq 0.
          ls_list_params-name = ls_param.
        endif.

        read table lt_params into ls_param index 2.
        if sy-subrc eq 0.
          ls_list_params-value = ls_param.
        endif.

        insert ls_list_params into table gt_params.
      endloop.
    endif.

    data: lt_fields type tihttpnvp.
    go_server->request->get_header_fields(
      changing
        fields = lt_fields ).

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_HTTP_EXTENSION->GET_DATA
* +-------------------------------------------------------------------------------------------------+
* | [<-->] IS_DATA                        TYPE        ANY
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method get_data.

    check go_server is not initial.

    /ui2/cl_json=>deserialize(
      exporting
        json        = req_json
        pretty_name = /ui2/cl_json=>pretty_mode-camel_case
      changing
        data        = is_data ).

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method ZCL_HTTP_EXTENSION->SAVE_LOG
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_FLAG                        TYPE        CHAR1 (default =' ')
* | [--->] IV_INOUT                       TYPE        CHAR1 (default ='1')
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method save_log.

    if gv_uuid is initial.

      try.
          gv_uuid = cl_system_uuid=>if_system_uuid_static~create_uuid_c32( ).
        catch cx_uuid_error .
      endtry.

      check gs_ztint_info-zlog is not initial.
      gs_ztint_log-system_id = gs_ztint_info-system_id.
      gs_ztint_log-zkfqq     = gs_ztint_info-zkfqq.
      gs_ztint_log-zkfxh     = gs_ztint_info-zkfxh.
      gs_ztint_log-guid      = gv_uuid.
      gs_ztint_log-zinout    = iv_inout.
      if xreq_json is not initial.
        gs_ztint_log-zinput    = xreq_json.
      else.
        gs_ztint_log-zinput    = cl_abap_codepage=>convert_to( req_json ).
      endif.
      gs_ztint_log-crname    = sy-uname.
      gs_ztint_log-crdate    = sy-datum.
      gs_ztint_log-crtime    = sy-uzeit.
      convert date sy-datum time sy-uzeit into time stamp gs_ztint_log-crtimestamp time zone sy-zonlo.

      modify ztint_log from gs_ztint_log.
      commit work.

    endif.

    check gs_ztint_info-zlog is not initial.

    if iv_flag eq 'X'.
      gs_ztint_log-key1  = gv_key1.
      gs_ztint_log-key2  = gv_key2.
      gs_ztint_log-key3  = gv_key3.
      gs_ztint_log-msgty = gv_type.
      gs_ztint_log-msgtx = gv_message.
      if xres_json is not initial.
        gs_ztint_log-zoutput = xres_json.
      else.
        gs_ztint_log-zoutput = cl_abap_codepage=>convert_to( res_json ).
      endif.
      gs_ztint_log-chname  = sy-uname.
      gs_ztint_log-chdate  = sy-datum.
      gs_ztint_log-chtime  = sy-uzeit.
      convert date sy-datum time sy-uzeit into time stamp gs_ztint_log-chtimestamp time zone sy-zonlo.

      modify ztint_log from gs_ztint_log.
      commit work.
    endif.

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_HTTP_EXTENSION->SET_KEYS
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_KEY1                        TYPE        ANY
* | [--->] IV_KEY2                        TYPE        ANY(optional)
* | [--->] IV_KEY3                        TYPE        ANY(optional)
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method set_keys.
    gv_key1 = iv_key1.
    gv_key2 = iv_key2.
    gv_key3 = iv_key3.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_HTTP_EXTENSION->SET_TYPE
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_TYPE                        TYPE        ANY
* | [--->] IV_MESSAGE                     TYPE        ANY
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method set_type.
    gv_type = iv_type.
    gv_message = iv_message.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_HTTP_EXTENSION->UPDATE_TYPE
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_TYPE                        TYPE        ANY
* | [--->] IV_MESSAGE                     TYPE        ANY
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method update_type.

    set_type(
      iv_type    = iv_type
      iv_message = iv_message
    ).

    save_log( iv_flag = 'X' ).
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_HTTP_EXTENSION->SEND_DATA
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_CODE                        TYPE        I (default =200)
* | [--->] IV_REASON                      TYPE        STRING (default ='Ok')
* | [--->] IS_DATA                        TYPE        ANY
* | [--->] IV_KEY1                        TYPE        ANY(optional)
* | [--->] IV_KEY2                        TYPE        ANY(optional)
* | [--->] IV_KEY3                        TYPE        ANY(optional)
* | [--->] IV_TYPE                        TYPE        ANY(optional)
* | [--->] IV_MESSAGE                     TYPE        ANY(optional)
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method send_data.

    if iv_key1 is supplied or iv_key2 is supplied or iv_key3 is supplied.
      set_keys(
        iv_key1 = iv_key1
        iv_key2 = iv_key2
        iv_key3 = iv_key3
      ).
    endif.

    if iv_type is supplied or iv_message is supplied.
      set_type(
        iv_type    = iv_type
        iv_message = iv_message
      ).
    endif.

    res_json = /ui2/cl_json=>serialize(
      exporting
        data        = is_data
        pretty_name = /ui2/cl_json=>pretty_mode-camel_case ).

    go_server->response->set_status( code = iv_code reason = iv_reason ).
    go_server->response->set_content_type( gc_hfield_content_type_v ).
    go_server->response->set_cdata( data = res_json ).
    save_log( iv_flag = 'X' ).

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method ZCL_HTTP_EXTENSION->CREATE_HTTP_CLIENT
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_SYSTEMID                    TYPE        ANY
* | [--->] IV_KFQQ                        TYPE        ANY
* | [--->] IV_KFXH                        TYPE        ANY
* | [<---] EV_TYPE                        TYPE        ANY
* | [<---] EV_MESSAGE                     TYPE        ANY
* | [<-()] EO_OBJECT                      TYPE REF TO IF_HTTP_CLIENT
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method create_http_client.

    select single * into corresponding fields of gs_ztint_ipport from ztint_ipport
        where system_id eq iv_systemid.

    select single * into corresponding fields of gs_ztint_info from ztint_info
        where system_id eq iv_systemid and zkfqq eq iv_kfqq and zkfxh eq iv_kfxh.

    select * into corresponding fields of table gt_ztint_para from ztint_para
        where system_id eq iv_systemid and zkfqq eq iv_kfqq and zkfxh eq iv_kfxh.

    if gs_ztint_ipport is initial or gs_ztint_info is initial.
      ev_type    = gc_error.
      ev_message = '接口信息不完整'.
      return.
    endif.

    data: lv_url           type string,
          lv_proxy_host    type string,
          lv_proxy_service type string,
          lv_sslid         type ssfapplssl,
          lo_client        type ref to if_http_client.

    lv_url = |{ gs_ztint_ipport-ipport }{ gs_ztint_info-zurl }|.

    case gs_ztint_ipport-system_type.
      when '1'. " sap
      when '2'. " sap 云
        lv_proxy_host    = 'proxy'.
        lv_proxy_service = '3128'.
        lv_sslid         = 'ANONYM'.
      when '3'. "文件服务器
      when '4'. "其他接口
      when others.
        ev_type = gc_error.
        ev_message = '接口类型未知'.
        return.
    endcase.

    cl_http_client=>create_by_url(
      exporting
        url                        = lv_url
        proxy_host                 = lv_proxy_host
        proxy_service              = lv_proxy_service
        ssl_id                     = lv_sslid
      importing
        client                     = lo_client
      exceptions
        argument_not_found         = 1
        plugin_not_active          = 2
        internal_error             = 3
        pse_not_found              = 4
        pse_not_distrib            = 5
        pse_errors                 = 6
        oa2c_set_token_error       = 7
        oa2c_missing_authorization = 8
        oa2c_invalid_config        = 9
        oa2c_invalid_parameters    = 10
        oa2c_invalid_scope         = 11
        oa2c_invalid_grant         = 12
        others                     = 13 ).

    if sy-subrc <> 0.
      message id sy-msgid type sy-msgty number sy-msgno
        with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 into ev_message.
      ev_type = gc_error.
      return.
    endif.

    eo_object = lo_client.

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_HTTP_EXTENSION->SET_CLIENT_AUTHOR
* +-------------------------------------------------------------------------------------------------+
* | [<-->] IO_HTTP_CLIENT                 TYPE REF TO IF_HTTP_CLIENT
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method set_client_author.

    data: lv_auth_type type i value 1,
          lv_username  type string,
          lv_password  type string.

    lv_username  = gs_ztint_ipport-username.
    lv_password  = gs_ztint_ipport-password.

    io_http_client->request->set_authorization(
      auth_type = lv_auth_type
      username  = lv_username
      password  = lv_password ).

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_HTTP_EXTENSION->SET_CLIENT_HEADER_FIELDS
* +-------------------------------------------------------------------------------------------------+
* | [<-()] ET_HFIELDS                     TYPE        TIHTTPNVP
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method set_client_header_fields.

    data: lt_header_fields type tihttpnvp.

    loop at gt_ztint_para into data(ls_ztint_para).
      if ls_ztint_para-zpara is not initial.
        lt_header_fields = value #(
        base lt_header_fields ( name = ls_ztint_para-zpara value = ls_ztint_para-zparav ) ).
      endif.
    endloop.

    et_hfields = lt_header_fields.

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Static Public Method ZCL_HTTP_EXTENSION=>CREATE_HTTP_TO_SEND_JSON
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_SYSTEMID                    TYPE        ANY
* | [--->] IV_KFQQ                        TYPE        ANY
* | [--->] IV_KFXH                        TYPE        ANY
* | [--->] IV_JSON                        TYPE        ANY(optional)
* | [--->] IV_FILE                        TYPE        XSTRING(optional)
* | [--->] IV_FILENAME                    TYPE        STRING(optional)
* | [--->] IV_KEY1                        TYPE        ANY(optional)
* | [--->] IV_KEY2                        TYPE        ANY(optional)
* | [--->] IV_KEY3                        TYPE        ANY(optional)
* | [<---] EV_TYPE                        TYPE        ANY
* | [<---] EV_MESSAGE                     TYPE        ANY
* | [<-()] EV_JSON                        TYPE        STRING
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method create_http_to_send_json.

    data(lo_http_extension) = new zcl_http_extension( ).

    data(lo_client) = lo_http_extension->create_http_client(
      exporting
        iv_systemid = iv_systemid
        iv_kfqq     = iv_kfqq
        iv_kfxh     = iv_kfxh
      importing
        ev_type     = ev_type
        ev_message  = ev_message
    ).

    check lo_client is not initial.

    lo_http_extension->set_client_author( changing io_http_client = lo_client ).

    lo_client->request->set_method( if_http_request=>co_request_method_post ).
    lo_client->request->set_version( if_http_request=>co_protocol_version_1_1 ).

    lo_client->request->set_header_fields( lo_http_extension->set_client_header_fields( ) ).

    if iv_key1 is supplied or iv_key2 is supplied or iv_key3 is supplied.
      lo_http_extension->set_keys(
        iv_key1 = iv_key1
        iv_key2 = iv_key2
        iv_key3 = iv_key3 ).
    endif.

    lo_http_extension->req_json  = iv_json.
    lo_http_extension->xreq_json = iv_file.

    lo_http_extension->save_log( iv_inout = '2' ).

    if lo_http_extension->req_json is not initial.
      lo_client->request->set_content_type( gc_hfield_content_type_v ).
      lo_client->request->set_cdata( lo_http_extension->req_json ).
    endif.

    if lo_http_extension->xreq_json is not initial.
      lo_client->request->if_http_entity~set_content_type( 'multipart/form-data' ).
      lo_client->request->if_http_entity~set_formfield_encoding( cl_http_request=>if_http_entity~co_encoding_raw ).
      data(lo_part) = lo_client->request->if_http_entity~add_multipart( ).
      data(lv_filename) = |form-data; name="file";filename="{ iv_filename }"|.

      lo_part->set_header_field(
        exporting
          name  = 'content-disposition'
          value = lv_filename ).

      lo_part->set_header_field(
        exporting
          name  = 'content-type'
          value = 'bin' ).

      data(lv_file_size) = xstrlen( lo_http_extension->xreq_json ).
      lo_part->set_data(
        exporting
          data   = lo_http_extension->xreq_json
          offset = 0
          length = lv_file_size ).
    endif.

    lo_client->send(
      exceptions
        http_communication_failure = 1
        http_invalid_state         = 2
        http_processing_failed     = 3
        http_invalid_timeout       = 4
        others                     = 5 ).
    if sy-subrc <> 0.
      ev_type = gc_error.
      lo_client->get_last_error( importing message = ev_message ).
    endif.

    lo_client->receive(
      exceptions
        http_communication_failure = 1
        http_invalid_state         = 2
        http_processing_failed     = 3
        others                     = 4 ).
    if sy-subrc <> 0.
      ev_type = gc_error.
      lo_client->get_last_error( importing message = ev_message ).
    endif.

    data: lv_code   type i.
    lo_client->response->get_status( importing code = lv_code ).

    if 400 <= lv_code or lv_code >= 500.
      lo_http_extension->xres_json = lo_client->response->get_data( ).
      ev_json =  cl_abap_codepage=>convert_from( lo_http_extension->xres_json ).
    else.
     lo_http_extension->res_json = lo_client->response->get_cdata( ).
      ev_json = lo_http_extension->res_json.
    endif.

    lo_http_extension->save_log( iv_flag = 'X' ).

    lo_client->close( ).

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method ZCL_HTTP_EXTENSION->IF_HTTP_EXTENSION~HANDLE_REQUEST
* +-------------------------------------------------------------------------------------------------+
* | [--->] SERVER                         TYPE REF TO IF_HTTP_SERVER
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method if_http_extension~handle_request.


  endmethod.
ENDCLASS.
```

# 3. 相关表

- [接口日志记录表](/Users/zhangshuailong/Desktop/notes/resource/ztyr_002/dictionary/ztint_log.html)
- [接口端口配置表](/Users/zhangshuailong/Desktop/notes/resource/ztyr_002/dictionary/ztint_ipport.html)
- [接口地址配置表](/Users/zhangshuailong/Desktop/notes/resource/ztyr_002/dictionary/ztint_info.html)
- [接口参数配置表](/Users/zhangshuailong/Desktop/notes/resource/ztyr_002/dictionary/ztint_para.html)
