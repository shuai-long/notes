~~~ABAP
class zcl_tools definition
  public
  final
  create public .

  public section.

    types:
      begin of ty_value_text,
        domvalue_l type dd07v-domvalue_l,
        ddtext     type dd07v-ddtext,
      end of ty_value_text .
    types:
      begin of ty_value_text_new,
        fdomvalue_l type dd07v-domvalue_l,
        domvalue_l  type dd07v-domvalue_l,
        ddtext      type dd07v-ddtext,
      end of ty_value_text_new .
    types:
      begin of ty_dd07v,
        domname    type dd07v-domname,
        ddlanguage type dd07v-ddlanguage,
        value_text type hashed table of ty_value_text with unique key domvalue_l,
      end of ty_dd07v .
    types:
      begin of ty_list_kostl,
        kostl type cskt-kostl,
        ltext type cskt-ltext,
      end of ty_list_kostl .
    types:
      tt_list_kostl type hashed table of ty_list_kostl with unique key kostl .
    types:
      begin of ty_kostl,
        spras type cskt-spras,
        kokrs type cskt-kokrs,
        kostl type cskt-kostl,
        ltext type cskt-ltext,
      end of ty_kostl .
    types:
      begin of ty_saknr,
        spras type skat-spras,
        ktopl type skat-ktopl,
        saknr type skat-saknr,
        txt50 type skat-txt50,
      end of ty_saknr .
    types:
      begin of ty_prctr,
        spras type cepct-spras,
        prctr type cepct-prctr,
        kokrs type cepct-kokrs,
        ltext type cepct-ltext,
      end of ty_prctr .
    types:
      begin of ty_list_prctr,
        prctr type cepct-prctr,
        ltext type cepct-ltext,
      end of ty_list_prctr .
    types:
      tt_list_prctr type hashed table of ty_list_prctr with unique key prctr .
    types:
      begin of ty_kostl_kokrs,
        kokrs type cskt-kokrs,
        kostl type cskt-kostl,
      end of ty_kostl_kokrs .
    types:
      begin of ty_prctr_kokrs,
        kokrs type cepct-kokrs,
        prctr type cepct-prctr,
        kostl type hashed table of ty_kostl_kokrs with unique key kokrs kostl,
      end of ty_prctr_kokrs .
    types:
      begin of ty_bukrs,
        bukrs type t001-bukrs,
        butxt type t001-butxt,
        prctr type hashed table of ty_prctr_kokrs with unique key kokrs prctr,
        kostl type hashed table of ty_kostl_kokrs with unique key kokrs kostl,
      end of ty_bukrs .
    types:
      begin of ty_pernr,
        pernr type pa0001-pernr,
        ename type pa0001-ename,
        orgeh type pa0001-pernr,
        plans type pa0001-plans,
      end of ty_pernr .
    types:
      begin of ty_lifnr,
        lifnr type lfa1-lifnr,
        land1 type lfa1-land1,
        name1 type lfa1-name1,
        name2 type lfa1-name2,
        name3 type lfa1-name3,
        name4 type lfa1-name4,
      end of ty_lifnr .
    types:
      begin of ty_lgart,
        lgart type lgart,
        lgtxt type lgtxt,
      end of ty_lgart .
    types:
      begin of ty_lgart_sprsl,
        sprsl      type sprsl,
        lgart_list type hashed table of ty_lgart with unique key lgart,
      end of ty_lgart_sprsl .
    types:
      begin of ty_lgart_molga,
        molga      type molga,
        lgart_list type hashed table of ty_lgart_sprsl with unique key sprsl,
      end of ty_lgart_molga .
    types:
      begin of ty_lgart_infty,
        infty      type infty,
        lgart_list type hashed table of ty_lgart_molga with unique key molga,
      end of ty_lgart_infty .
    types:
      begin of ty_txpct,
        txpct type t7cntx_rate-txpct,
        txinc type range of t7cntx_rate-txinc,
      end of ty_txpct .
    types:
      begin of ty_t7cntx_rate,
        txgrn type t7cntx_rate-txgrn,
        txpct type standard table of ty_txpct with default key,
      end of ty_t7cntx_rate .
    types:
      begin of ty_table_field_value_text,
        tabname        type tabname,
        fieldname      type fieldname,
        value_text     type hashed table of ty_value_text with unique key domvalue_l,
        value_text_new type hashed table of ty_value_text_new with unique key fdomvalue_l domvalue_l,
      end of ty_table_field_value_text .
    types:
      begin of ty_subty,
        infty type infty,
        subty type subty_591a,
        stext type sbttx,
      end of ty_subty .
    types:
      begin of ty_subty_sprsl,
        sprsl      type sprsl,
        subty_list type hashed table of ty_subty with unique key infty subty,
      end of ty_subty_sprsl .
    types:
      begin of ty_objid,
        langu type hrp1000-langu,
        otype type hrp1000-otype,
        objid type hrp1000-objid,
        stext type hrp1000-stext,
      end of ty_objid .
    types:
      begin of ty_objid_date,
        begda type d,
        objid type hashed table of ty_objid with unique key langu otype objid,
      end of ty_objid_date .

    constants gc_string1 type string value 'ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz' ##NO_TEXT.

    methods get_saknr_text
      importing
        !iv_saknr        type any
      returning
        value(ev_result) type text40 .
    methods get_lifnr_text
      importing
        !iv_lifnr       type lifnr
      returning
        value(ev_name1) type name1_gp .
    class-methods conv_posid_in
      importing
        value(iv_input)  type any
      returning
        value(ev_output) type ps_posnr .
    class-methods conv_pspid_out
      importing
        value(iv_input)  type any
      returning
        value(ev_output) type ps_pspid .
    class-methods conv_pspid_in
      importing
        value(iv_input)  type any
      returning
        value(ev_output) type ps_intnr .
    class-methods conv_spras_in
      importing
        value(iv_input)  type any
      returning
        value(ev_output) type kna1-spras .
    class-methods conv_spras_out
      importing
        value(iv_input)  type any
      returning
        value(ev_output) type kna1-spras .
    class-methods get_date_from_p0041
      importing
        !is_p0041       type p0041
        !iv_dat01       type any
      returning
        value(ev_value) type d .
    class-methods conv_month_to_eng
      importing
        !iv_month       type numc2
      returning
        value(ev_value) type string .
    class-methods select_and_save_file
      importing
        !iv_filename type any
        !iv_filetype type string default cl_gui_frontend_services=>filetype_excel
        !iv_file     type xstring optional
        !it_file     type solix_tab optional .
    class-methods save_file
      importing
        !iv_fullpath type any
        !iv_file     type xstring optional
        !it_file     type solix_tab optional .
    methods get_txpct_from_t7cntx_rate
      importing
        !iv_txgrn       type char1 default '1'
        !iv_txinc       type ptw_txinc
      returning
        value(ev_value) type ptw_txinc .
    methods constructor
      importing
        !iv_spras type spras default '1'
        !iv_kokrs type kokrs default 'BAES'
        !iv_molga type molga default '28'
        !iv_ktopl type ktopl default 'BAES' .
    methods set_log_for_table
      changing
        !is_data type any .
    methods get_domain_text
      importing
        value(iv_domname)  type domname
        value(iv_domvalue) type domvalue_l
      returning
        value(ev_ddtext)   type val_text .
    methods get_bukrs_text
      importing
        !iv_bukrs       type bukrs
      returning
        value(ev_butxt) type butxt .
    methods get_prctr_text
      importing
        !iv_prctr       type prctr
      returning
        value(ev_ltext) type ltext .
    methods get_kostl_text
      importing
        !iv_kostl       type kostl
      returning
        value(ev_ltext) type kltxt .
    methods get_pernr_name
      importing
        !iv_pernr      type pernr_d
        !iv_last       type char1 default 'X'
      returning
        value(ev_name) type emnam .
    methods get_lgart_text
      importing
        !iv_lgart        type lgart
      returning
        value(ev_result) type lgtxt .
    methods get_lgart_by_infty
      importing
        !iv_infty        type infty
      returning
        value(ev_result) type ty_lgart_sprsl .
    methods get_lgart_by_molga
      importing
        !iv_molga        type molga
      returning
        value(ev_result) type ty_lgart_molga .
    methods get_lgart_by_sprsl
      importing
        !iv_sprsl        type any
      returning
        value(ev_result) type ty_lgart_sprsl .
    methods get_string_stext
      importing
        !iv_type        type char1 optional
        !iv_string      type any
        !iv_split       type char1 default '/'
      returning
        value(ev_stext) type string .
    methods get_list_kostl_by_bukrs
      importing
        !iv_bukrs       type bukrs
      returning
        value(et_kostl) type tt_list_kostl .
    methods get_list_prctr_by_bukrs
      importing
        !iv_bukrs       type bukrs
      returning
        value(et_prctr) type tt_list_prctr .
    methods get_table_field_value_text
      importing
        !iv_tname        type any
        !iv_fname        type any
        !iv_value        type any
        !iv_fvalue       type any optional
      returning
        value(ev_result) type dd07v-ddtext
      exceptions
        sql_error .
    methods get_subty_stext
      importing
        !iv_infty        type infty
        !iv_subty        type subty_591a
      returning
        value(ev_result) type sbttx .
    methods get_objid_stext
      importing
        !iv_otype        type any default 'O'
        !iv_objid        type hrp1000-objid
        !iv_date         type d optional
      returning
        value(ev_result) type hrp1000-stext .
    methods set_spras
      importing
        !iv_spras type spras .
    methods set_kokrs
      importing
        !iv_kokrs type kokrs .
    methods set_molga
      importing
        !iv_molga type molga .
    class-methods conv_posid_out
      importing
        !iv_input        type any
      returning
        value(ev_output) type ps_posid .
    methods set_ktopl
      importing
        !iv_ktopl type ktopl .
  protected section.

  private section.

    data:
      gt_dd07v type hashed table of ty_dd07v with unique key domname ddlanguage .
    data:
      gt_bukrs type hashed table of ty_bukrs with unique key bukrs .
    data:
      gt_prctr type hashed table of ty_prctr with unique key spras prctr kokrs .
    data:
      gt_kostl type hashed table of ty_kostl with unique key spras kokrs kostl .
    data:
      gt_saknr type hashed table of ty_saknr with unique key spras ktopl saknr .
    data:
      gt_pernr_last type hashed table of ty_pernr with unique key pernr .
    data:
      gt_pernr_now type hashed table of ty_pernr with unique key pernr .
    data:
      gt_lifnr type hashed table of ty_lifnr with unique key lifnr .
    data:
      gt_table_field_value_list type hashed table of ty_table_field_value_text with unique key tabname fieldname .
    data:
      gt_t7cntx_rate type hashed table of ty_t7cntx_rate with unique key txgrn .
    data:
      gt_lgart_molga type hashed table of ty_lgart_molga with unique key molga .
    data:
      gt_lgart_infty type hashed table of ty_lgart_infty with unique key infty .
    data:
      gt_subty type hashed table of ty_subty_sprsl with unique key sprsl .
    data:
      gt_objid type hashed table of ty_objid_date with unique key begda .
    data gv_ddlanguage type ddlanguage .
    data gv_ktopl type ktopl .
    data gv_kokrs type kokrs .
    data gv_molga type molga .
    data gv_flag_bukrs type char1 .
    data gv_flag_prctr type char1 .
    data gv_flag_saknr type char1 .
    data gv_flag_kostl type char1 .
    data gv_flag_pernr type char1 .
    data gv_flag_lifnr type char1 .
    data gv_flag_append_kostl type char1 .
    data gv_flag_append_prctr type char1 .

    class-methods check_spras
      importing
        !iv_spras        type spras
      returning
        value(ev_result) type boole .
    class-methods check_kokrs
      importing
        !iv_kokrs        type kokrs
      returning
        value(ev_result) type boole .
    class-methods check_molga
      importing
        !iv_molga        type molga
      returning
        value(ev_result) type boole .
    class-methods check_ktopl
      importing
        !iv_ktopl        type ktopl
      returning
        value(ev_result) type boole .
    methods read_dd07v
      importing
        !is_dd07v        type ty_dd07v
        !iv_domvalue     type domvalue_l
      returning
        value(ev_ddtext) type val_text .
endclass.



class zcl_tools implementation.


  method check_kokrs.
    select single * into @data(ls_tka01) from tka01 where kokrs eq @iv_kokrs.
    if ls_tka01 is not initial.
      ev_result = 'X'.
    endif.
  endmethod.


  method check_spras.
    select single * into @data(ls_t002) from t002 where spras eq @iv_spras.
    if ls_t002 is not initial.
      ev_result = 'X'.
    endif.
  endmethod.


  method check_molga.
    select single * into @data(ls_t500l) from t500l where molga eq @iv_molga.
    if ls_t500l is not initial.
      ev_result = 'X'.
    endif.
  endmethod.


  method check_ktopl.
    select single * into @data(ls_t004) from t004 where ktopl eq @iv_ktopl.
    if ls_t004 is not initial.
      ev_result = 'X'.
    endif.
  endmethod.


  method constructor.

    if check_spras( iv_spras ).
      gv_ddlanguage = iv_spras.
    else.
      gv_ddlanguage = '1'.
    endif.

    if check_kokrs( iv_kokrs ).
      gv_kokrs = iv_kokrs.
    else.
      gv_kokrs = 'BAES'.
    endif.

    if check_molga( iv_molga ).
      gv_molga = iv_molga.
    else.
      gv_molga = '28'.
    endif.

    if check_ktopl( iv_ktopl ).
      gv_ktopl = iv_ktopl.
    else.
      gv_ktopl = 'BAES'.
    endif.


  endmethod.


  method set_log_for_table.

    data: ls_tablog type zsty_tablog.

    move-corresponding is_data to ls_tablog.

    if ls_tablog-crname is initial or
       ls_tablog-crdate is initial or
       ls_tablog-crtime is initial or
       ls_tablog-crtimestamp is initial.

      ls_tablog-crname = sy-uname.
      ls_tablog-crdate = sy-datum.
      ls_tablog-crtime = sy-uzeit.
      convert date sy-datum time sy-uzeit into time stamp ls_tablog-crtimestamp time zone sy-zonlo.
    endif.

    ls_tablog-crname = sy-uname.
    ls_tablog-crdate = sy-datum.
    ls_tablog-crtime = sy-uzeit.
    convert date sy-datum time sy-uzeit into time stamp ls_tablog-crtimestamp time zone sy-zonlo.

    move-corresponding ls_tablog to is_data.

  endmethod.


  method conv_posid_in.

    call function 'CONVERSION_EXIT_ABPSP_INPUT'
      exporting
        input     = iv_input
      importing
        output    = ev_output
      exceptions
        not_found = 1
        others    = 2.

  endmethod.


  method conv_posid_out.

    call function 'CONVERSION_EXIT_ABPSP_OUTPUT'
      exporting
        input     = iv_input
      importing
        output    = ev_output
      exceptions
        not_found = 1
        others    = 2.

  endmethod.


  method conv_pspid_in.

    call function 'CONVERSION_EXIT_ABPRJ_INPUT'
      exporting
        input     = iv_input
      importing
        output    = ev_output
      exceptions
        not_found = 1
        others    = 2.

  endmethod.


  method conv_spras_in.

    call function 'CONVERSION_EXIT_ISOLA_INPUT'
      exporting
        input     = iv_input
      importing
        output    = ev_output
      exceptions
        not_found = 1
        others    = 2.

  endmethod.


  method conv_spras_out.

    call function 'CONVERSION_EXIT_ISOLA_OUTPUT'
      exporting
        input     = iv_input
      importing
        output    = ev_output
      exceptions
        not_found = 1
        others    = 2.

  endmethod.


  method conv_pspid_out.

    call function 'CONVERSION_EXIT_ABPRJ_OUTPUT'
      exporting
        input     = iv_input
      importing
        output    = ev_output
      exceptions
        not_found = 1
        others    = 2.

  endmethod.


  method get_date_from_p0041.

    data: lo_message_handler type ref to if_hrpa_message_handler,
          ls_p0041           type p0041,
          lv_datar           type p0041-dar01,
          lv_date            type p0041-dat01.

    move-corresponding is_p0041 to ls_p0041.
    lv_datar = iv_dat01.

    call function 'HR_ECM_READ_IT0041_DATE_TYPE'
      exporting
        datar           = lv_datar
        p0041           = ls_p0041
        message_handler = lo_message_handler
      importing
        date            = lv_date.

    ev_value = lv_date.

  endmethod.


  method conv_month_to_eng.
    "月份文本存储表：T247
    case iv_month.
      when '01'.
        ev_value = 'Jan'.
      when '02'.
        ev_value = 'Feb'.
      when '03'.
        ev_value = 'Mar'.
      when '04'.
        ev_value = 'Apr'.
      when '05'.
        ev_value = 'May'.
      when '06'.
        ev_value = 'Jun'.
      when '07'.
        ev_value = 'Jul'.
      when '08'.
        ev_value = 'Aug'.
      when '09'.
        ev_value = 'Sep'.
      when '10'.
        ev_value = 'Oct'.
      when '11'.
        ev_value = 'Nov'.
      when '12'.
        ev_value = 'Dec'.
    endcase.
  endmethod.


  method select_and_save_file.

    data: lv_filename    type string,
          lv_path        type string,
          lv_fullpath    type string,
          lv_file_filter type string.

    lv_filename    = iv_filename.
    lv_file_filter = iv_filetype.

    call method cl_gui_frontend_services=>file_save_dialog
      exporting
        default_file_name         = lv_filename
        file_filter               = lv_file_filter
        prompt_on_overwrite       = 'X'
      changing
        filename                  = lv_filename
        path                      = lv_path
        fullpath                  = lv_fullpath
      exceptions
        cntl_error                = 1
        error_no_gui              = 2
        not_supported_by_gui      = 3
        invalid_default_file_name = 4
        others                    = 5.

    if lv_fullpath is not initial.
      save_file(
        iv_fullpath = lv_fullpath
        iv_file     = iv_file
        it_file     = it_file ).
    endif.

  endmethod.


  method save_file.

    data: lt_data_tab type solix_tab,
          lv_file     type xstring,
          lv_fullpath type string,
          lv_filesize type i.

    if iv_file is not initial.
      lv_file = iv_file.
      lt_data_tab = cl_bcs_convert=>xstring_to_solix( lv_file ).
    else.
      lt_data_tab = it_file.
      lv_file = cl_bcs_convert=>solix_to_xstring( lt_data_tab ).
    endif.

    lv_filesize = xstrlen( lv_file ).

    cl_gui_frontend_services=>gui_download(
      exporting
        bin_filesize            = lv_filesize
        filename                = iv_fullpath
        filetype                = 'BIN'
        confirm_overwrite       = 'X'
      changing
        data_tab                = lt_data_tab
      exceptions
        file_write_error        = 1
        no_batch                = 2
        gui_refuse_filetransfer = 3
        invalid_type            = 4
        no_authority            = 5
        unknown_error           = 6
        header_not_allowed      = 7
        separator_not_allowed   = 8
        filesize_not_allowed    = 9
        header_too_long         = 10
        dp_error_create         = 11
        dp_error_send           = 12
        dp_error_write          = 13
        unknown_dp_error        = 14
        access_denied           = 15
        dp_out_of_memory        = 16
        disk_full               = 17
        dp_timeout              = 18
        file_not_found          = 19
        dataprovider_exception  = 20
        control_flush_error     = 21
        not_supported_by_gui    = 22
        error_no_gui            = 23
        others                  = 24 ).

    if sy-subrc ne 0.
      message id sy-msgid type sy-msgty number sy-msgno
      with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    endif.


  endmethod.


  method get_txpct_from_t7cntx_rate.
********************************************************************** 根据薪资获取税额
    data: lv_tabix_next type sy-index,
          lv_tabix      type sy-index.

    read table gt_t7cntx_rate into data(ls_t7cntx_rate) with key txgrn = iv_txgrn.
    if sy-subrc ne 0.
      select * into table @data(lt_t7cntx_rate) from t7cntx_rate where txgrn eq @iv_txgrn.
      sort lt_t7cntx_rate by txinc descending txgrn.

      ls_t7cntx_rate-txgrn = iv_txgrn.

      do lines( lt_t7cntx_rate ) - 1 times.
        add 1 to lv_tabix.
        lv_tabix_next = lv_tabix.
        add 1 to lv_tabix_next.

        read table lt_t7cntx_rate into data(ls_t7cntx_rate_this) index lv_tabix.
        if sy-subrc eq 0.

          if lv_tabix eq 1.
            append initial line to ls_t7cntx_rate-txpct assigning field-symbol(<fs_txpct>).
            <fs_txpct>-txpct = ls_t7cntx_rate_this-txpct.
            <fs_txpct>-txinc = value #( ( sign = 'I' option = 'GE' low = ls_t7cntx_rate_this-txinc ) ).
          endif.

          append initial line to ls_t7cntx_rate-txpct assigning <fs_txpct>.
          <fs_txpct>-txinc = value #( ( sign = 'E' option = 'GE' low = ls_t7cntx_rate_this-txinc ) ).

          read table lt_t7cntx_rate into data(ls_t7cntx_rate_next) index lv_tabix_next.
          if sy-subrc eq 0.
            <fs_txpct>-txpct = ls_t7cntx_rate_next-txpct.
            <fs_txpct>-txinc = value #( base <fs_txpct>-txinc ( sign = 'E' option = 'LT' low = ls_t7cntx_rate_next-txinc ) ).
          endif.

        endif.
      enddo.

      insert ls_t7cntx_rate into table gt_t7cntx_rate.
    endif.

    loop at ls_t7cntx_rate-txpct into data(ls_txpct).
      if iv_txinc in ls_txpct-txinc.
        ev_value = ls_txpct-txpct.
        exit.
      endif.
    endloop.
  endmethod.


  method get_bukrs_text.

    if gv_flag_bukrs is initial.
      select bukrs butxt into corresponding fields of table gt_bukrs from t001.
    endif.

    read table gt_bukrs into data(ls_bukrs) with table key bukrs = iv_bukrs.
    if sy-subrc eq 0.
      ev_butxt = ls_bukrs-butxt.
    endif.

    gv_flag_bukrs = 'X'.
  endmethod.


  method get_domain_text.

    read table gt_dd07v into data(ls_dd07v) with key domname = iv_domname ddlanguage = gv_ddlanguage.
    if sy-subrc eq 0.
      ev_ddtext = read_dd07v( exporting is_dd07v = ls_dd07v iv_domvalue = iv_domvalue ).
    else.

      clear: ls_dd07v.
      ls_dd07v-domname = iv_domname.
      ls_dd07v-ddlanguage = gv_ddlanguage.
      select domvalue_l ddtext into corresponding fields of table ls_dd07v-value_text from dd07v
        where domname eq iv_domname and ddlanguage eq gv_ddlanguage .
      insert ls_dd07v into table gt_dd07v.
      ev_ddtext = read_dd07v( exporting is_dd07v = ls_dd07v iv_domvalue = iv_domvalue ).

    endif.

  endmethod.


  method get_kostl_text.
    data lv_kostl type kostl.

    if gv_flag_kostl is initial.
      select spras, kokrs, kostl, case when ltext is initial then ktext when ltext is not initial then ltext end as ltext
        from cskt where datbi eq '99991231' into corresponding fields of table @gt_kostl .
    endif.

    lv_kostl = |{ iv_kostl alpha = in }|.

    read table gt_kostl into data(ls_kostl) with key spras = gv_ddlanguage kokrs = gv_kokrs kostl = lv_kostl.
    if sy-subrc eq 0.
      ev_ltext = ls_kostl-ltext.
    endif.

    gv_flag_kostl = 'X'.
  endmethod.


  method get_lifnr_text.

    if gv_flag_lifnr is initial.
      select lifnr, land1, name1, name2, name3, name4 from lfa1 into corresponding fields of table @gt_lifnr.
    endif.

    read table gt_lifnr into data(ls_lifnr) with key lifnr = iv_lifnr.
    ev_name1 = ls_lifnr-name1.

    gv_flag_lifnr = 'X'.

  endmethod.


  method get_list_kostl_by_bukrs.

    data: ls_list_kostl  type line of tt_list_kostl,
          ls_kostl_kokrs type ty_kostl_kokrs.

    if gv_flag_append_kostl is initial.
      select kokrs, kostl, bukrs, prctr into table @data(lt_csks) from csks.

      if gv_flag_bukrs is initial.
        get_bukrs_text( '' ).
      endif.

      if gv_flag_kostl is initial.
        get_kostl_text( '' ).
      endif.

      loop at lt_csks into data(ls_csks).
        read table gt_bukrs assigning field-symbol(<fs_bukrs>) with key bukrs = ls_csks-bukrs.
        if sy-subrc eq 0.
          clear ls_kostl_kokrs.
          ls_kostl_kokrs-kokrs = ls_csks-kokrs.
          ls_kostl_kokrs-kostl = ls_csks-kostl.
          insert ls_kostl_kokrs into table <fs_bukrs>-kostl.
        endif.
      endloop.

      gv_flag_append_kostl = 'X'.
    endif.

    read table gt_bukrs into data(ls_bukrs) with key bukrs = iv_bukrs.
    if sy-subrc eq 0.
      loop at ls_bukrs-kostl into data(ls_kostl) where kokrs eq gv_kokrs.
        clear ls_list_kostl.
        ls_list_kostl-ltext = get_kostl_text( ls_kostl-kostl ).
        ls_list_kostl-kostl = ls_kostl-kostl.
        insert ls_list_kostl into table et_kostl.
      endloop.
    endif.
  endmethod.


  method get_list_prctr_by_bukrs.

    data: ls_list_prctr  type line of tt_list_prctr,
          ls_prctr_kokrs type ty_prctr_kokrs.

    if gv_flag_append_prctr is initial.
      select kokrs, prctr, bukrs into table @data(lt_csks_bukrs) from cepc_bukrs.

      if gv_flag_bukrs is initial.
        get_bukrs_text( '' ).
      endif.

      if gv_flag_prctr is initial.
        get_prctr_text( '' ).
      endif.

      loop at lt_csks_bukrs into data(ls_csks_bukrs).
        read table gt_bukrs assigning field-symbol(<fs_bukrs>) with key bukrs = ls_csks_bukrs-bukrs.
        if sy-subrc eq 0.
          clear ls_prctr_kokrs.
          ls_prctr_kokrs-kokrs = ls_csks_bukrs-kokrs.
          ls_prctr_kokrs-prctr = ls_csks_bukrs-prctr.
          insert ls_prctr_kokrs into table <fs_bukrs>-prctr.
        endif.
      endloop.

      gv_flag_append_prctr = 'X'.
    endif.

    read table gt_bukrs into data(ls_bukrs) with key bukrs = iv_bukrs.
    if sy-subrc eq 0.
      loop at ls_bukrs-prctr into data(ls_prctr) where kokrs eq gv_kokrs.
        clear ls_list_prctr.
        ls_list_prctr-ltext = get_prctr_text( ls_prctr-prctr ).
        ls_list_prctr-prctr = ls_prctr-prctr.
        insert ls_list_prctr into table et_prctr.
      endloop.
    endif.

  endmethod.


  method get_table_field_value_text.

    field-symbols:<fs_table> type any table.

    data: lv_select_field type string,
          lv_select_table type string,
          lv_select_where type string.

    define set_sql.

      if &1 is not initial.
        lv_select_field = |{ &1 } AS FDOMVALUE_L,{ &2 } AS DOMVALUE_L, { &3 } AS DDTEXT|.
      else.
        lv_select_field = |{ &2 } AS DOMVALUE_L, { &3 } AS DDTEXT|.
      endif.
      lv_select_table  = &4.
      lv_select_where  = &5.

      translate lv_select_field to upper case.
      translate lv_select_table to upper case.

    end-of-definition.

    data(lv_tname) = to_upper( iv_tname ).
    data(lv_fname) = to_upper( iv_fname ).

    read table gt_table_field_value_list into data(ls_field_line) with key tabname = lv_tname fieldname = lv_fname.
    if sy-subrc ne 0.
      clear ls_field_line.
      ls_field_line-tabname   = lv_tname.
      ls_field_line-fieldname = lv_fname.

      if iv_fvalue is not supplied.
        assign ls_field_line-value_text to <fs_table>.
      else.
        assign ls_field_line-value_text_new to <fs_table>.
      endif.


      if lv_tname eq 'PA0001' or lv_tname eq 'P0001'.

        case lv_fname.
          when 'WERKS' .
            set_sql: '' 'persa' 'name1' 't500p' ''.
          when 'BTRTL'.
            set_sql: 'werks' 'btrtl' 'btext' 't001p' ''.
          when 'PERSG'.
            set_sql: '' 'persg' 'ptext' 't501t' 'sprsl eq @gv_ddlanguage'.
          when 'PERSK'.
            set_sql: '' 'persk' 'ptext' 't503t' 'sprsl eq @gv_ddlanguage'.
        endcase.

      elseif lv_tname eq 'PA0185' or lv_tname eq 'P0185'.

        case lv_fname.
          when 'ICTYP'.
            set_sql: '' 'ictyp' 'ictxt' 't5r06' 'sprsl eq @gv_ddlanguage and molga eq @gv_molga'.
        endcase.

      elseif lv_tname eq 'PA0022' or lv_tname eq 'P0022'.

        case lv_fname.
          when 'SLART'.
            set_sql: '' 'slart' 'stext' 't517t' 'sprsl eq @gv_ddlanguage'.
        endcase.

      endif.

      try.
          if lv_select_field is not initial and lv_select_table is not initial.
            select (lv_select_field) from (lv_select_table) into corresponding fields of table @<fs_table> where (lv_select_where).
            insert ls_field_line into table gt_table_field_value_list.
          endif.
        catch cx_root into data(lo_root).
          raise sql_error.
      endtry.

    endif.


    if iv_fvalue is not supplied.
      read table ls_field_line-value_text into data(ls_value_line) with key domvalue_l = iv_value.
      if sy-subrc eq 0.
        ev_result = ls_value_line-ddtext.
      endif.
    else.
      read table ls_field_line-value_text_new into data(ls_value_line_new) with key fdomvalue_l = iv_fvalue domvalue_l = iv_value .
      if sy-subrc eq 0.
        ev_result = ls_value_line_new-ddtext.
      endif.
    endif.

  endmethod.


  method get_pernr_name.

    if gv_flag_pernr is initial.
      select pernr orgeh plans ename into corresponding fields of table gt_pernr_last from pa0001 where endda eq '99991231' .
      select pernr orgeh plans ename into corresponding fields of table gt_pernr_now from pa0001 where begda le sy-datum and endda ge sy-datum.
    endif.

    if iv_last is initial.
      data(lt_pernr) = gt_pernr_last[].
    else.
      lt_pernr = gt_pernr_now.
    endif.


    read table lt_pernr into data(ls_pernr) with key pernr = iv_pernr.
    ev_name = ls_pernr-ename.


    gv_flag_pernr = 'X'.
  endmethod.


  method get_lgart_text.

    read table gt_lgart_molga into data(ls_lgart_molga) with key molga = gv_molga.
    if sy-subrc ne 0.
      ls_lgart_molga = get_lgart_by_molga( gv_molga ).
      insert ls_lgart_molga into table gt_lgart_molga.
    endif.

    read table ls_lgart_molga-lgart_list into data(ls_lgart_sprsl) with key sprsl = gv_ddlanguage.
    if sy-subrc ne 0.
      ls_lgart_sprsl = get_lgart_by_sprsl( gv_ddlanguage ).
      insert ls_lgart_sprsl into table ls_lgart_molga-lgart_list.
      insert ls_lgart_molga into table gt_lgart_molga.
    endif.

    read table ls_lgart_sprsl-lgart_list into data(ls_lgart) with key lgart = iv_lgart.
    if sy-subrc eq 0.
      ev_result = ls_lgart-lgtxt.
    endif.

  endmethod.


  method get_prctr_text.
    data lv_prctr type prctr.

    if gv_flag_prctr is initial.
      select spras, prctr, kokrs, case when ltext is initial then ktext when ltext is not initial then ltext end as ltext
        from cepct where datbi eq '99991231' into corresponding fields of table @gt_prctr.
    endif.

    lv_prctr = |{ iv_prctr alpha = in }|.
    read table gt_prctr into data(ls_prctr) with key spras = gv_ddlanguage prctr = lv_prctr kokrs = gv_kokrs.
    ev_ltext = ls_prctr-ltext.

    gv_flag_prctr = 'X'.
  endmethod.


  method get_saknr_text.
    data: lv_saknr type saknr.

    if gv_flag_saknr is initial.
      select spras, ktopl, saknr, case when txt50 is initial then txt20 else txt50 end as txt50
        from skat into corresponding fields of table @gt_saknr.
      gv_flag_saknr = 'X'.
    endif.

    lv_saknr = |{ iv_saknr alpha = in }|.

    read table gt_saknr into data(ls_saknr) with key spras = gv_ddlanguage ktopl = gv_ktopl saknr = lv_saknr.
    ev_result = ls_saknr-txt50.


  endmethod.


  method get_string_stext.

    data: lv_bukrs type bukrs,
          lv_prctr type prctr,
          lv_kostl type kostl,
          lv_pernr type pernr_d,
          lv_objid type hrp1000-objid.

    data: lv_string type string.

    split iv_string at iv_split into table data(lt_table).

    loop at lt_table into data(ls_table).

      case iv_type.
        when 'B'.
          lv_bukrs = ls_table.
          lv_string = get_bukrs_text( lv_bukrs ).
        when 'C'.
          lv_prctr = ls_table.
          lv_string = get_prctr_text( lv_prctr ).
        when 'K'.
          lv_kostl = ls_table.
          lv_string = get_kostl_text( lv_kostl ).
        when 'P'.
          lv_pernr = ls_table.
          lv_string = get_pernr_name( lv_pernr ).
        when 'O' or 'S' .
          lv_objid = ls_table.
          lv_string = get_objid_stext( iv_otype = iv_type iv_objid = lv_objid ).
        when others.
          lv_string = ls_table.
      endcase.

      if ev_stext is initial.
        ev_stext = lv_string.
      else.
        if iv_string is not initial.
          ev_stext = |{ ev_stext }{ iv_split }{ lv_string }|.
        else.
          ev_stext = |{ ev_stext }{ iv_split }{ ls_table }|.
        endif.
      endif.

    endloop.

  endmethod.


  method read_dd07v.
    read table is_dd07v-value_text into data(ls_value_text) with key domvalue_l = iv_domvalue.
    ev_ddtext = ls_value_text-ddtext.
  endmethod.


  method set_kokrs.
    if check_kokrs( iv_kokrs ).
      gv_kokrs = iv_kokrs.
    endif.
  endmethod.


  method set_spras.
    if check_spras( iv_spras ).
      gv_ddlanguage = iv_spras.
    endif.
  endmethod.


  method set_ktopl.
    if check_ktopl( iv_ktopl ).
      gv_ktopl = iv_ktopl.
    endif.
  endmethod.


  method set_molga.
    if check_molga( iv_molga ).
      gv_molga = iv_molga.
    endif.
  endmethod.


  method get_lgart_by_infty.

    data: ls_lgart_infty type ty_lgart_infty,
          ls_lgart_molga type ty_lgart_molga,
          ls_lgart_sprsl type ty_lgart_sprsl,
          ls_lgart       type ty_lgart.

    read table gt_lgart_infty assigning field-symbol(<fs_lgart_infty>) with key infty = iv_infty.
    if sy-subrc ne 0.

      ls_lgart_infty-infty = iv_infty.
      ls_lgart_molga-molga = gv_molga.
      ls_lgart_sprsl-sprsl = gv_ddlanguage.

      select lgart into table @data(lt_t512z) from t512z where infty = @iv_infty and molga eq @gv_molga.
      loop at lt_t512z into data(ls_t512z).
        clear ls_lgart.
        ls_lgart-lgart = ls_t512z-lgart.
        ls_lgart-lgtxt = get_lgart_text( ls_t512z-lgart ).
        insert ls_lgart into table ls_lgart_sprsl-lgart_list.
      endloop.
      insert ls_lgart_sprsl into table ls_lgart_molga-lgart_list.
      insert ls_lgart_molga into table ls_lgart_infty-lgart_list.
      insert ls_lgart_infty into table gt_lgart_infty.

      ev_result = ls_lgart_sprsl.
      return.
    endif.

    read table <fs_lgart_infty>-lgart_list assigning field-symbol(<fs_lgart_molga>) with key molga = gv_molga.
    if sy-subrc ne 0.

      ls_lgart_molga-molga = gv_molga.
      ls_lgart_sprsl-sprsl = gv_ddlanguage.

      select lgart into corresponding fields of table @lt_t512z from t512z where infty = @iv_infty and molga eq @gv_molga.
      loop at lt_t512z into ls_t512z.
        clear ls_lgart.
        ls_lgart-lgart = ls_t512z-lgart.
        ls_lgart-lgtxt = get_lgart_text( ls_t512z-lgart ).
        insert ls_lgart into table ls_lgart_sprsl-lgart_list.
      endloop.

      insert ls_lgart_sprsl into table ls_lgart_molga-lgart_list.
      insert ls_lgart_molga into table <fs_lgart_infty>-lgart_list.

      ev_result = ls_lgart_sprsl.
      return.
    endif.


    read table <fs_lgart_molga>-lgart_list assigning field-symbol(<fs_lgart_sprsl>) with key sprsl = gv_ddlanguage.
    if sy-subrc ne 0.

      ls_lgart_sprsl-sprsl = gv_ddlanguage.

      select lgart into corresponding fields of table @lt_t512z from t512z where infty = @iv_infty and molga eq @gv_molga.
      loop at lt_t512z into ls_t512z.
        clear ls_lgart.
        ls_lgart-lgart = ls_t512z-lgart.
        ls_lgart-lgtxt = get_lgart_text( ls_t512z-lgart ).
        insert ls_lgart into table ls_lgart_sprsl-lgart_list.
      endloop.
      insert ls_lgart_sprsl into table <fs_lgart_molga>-lgart_list.
      ev_result = ls_lgart_sprsl.
      return.
    endif.

  endmethod.


  method get_lgart_by_molga.

    data: ls_lgart_molga type ty_lgart_molga,
          ls_lgart_sprsl type ty_lgart_sprsl,
          ls_lgart       type ty_lgart.

    ls_lgart_molga-molga = iv_molga.

    select sprsl, lgart, lgtxt from t512t
      where molga eq @iv_molga and sprsl eq @gv_ddlanguage into table @data(lt_t512t).

    loop at lt_t512t into data(ls_t512t).
      clear: ls_lgart, ls_lgart_sprsl.
      ls_lgart_sprsl-sprsl = ls_t512t-sprsl.

      ls_lgart-lgart = ls_t512t-lgart.
      ls_lgart-lgtxt = ls_t512t-lgtxt.

      read table ls_lgart_molga-lgart_list assigning field-symbol(<fs_lgart_sprsl>) with key sprsl = ls_t512t-sprsl.
      if sy-subrc eq 0.
        insert ls_lgart into table <fs_lgart_sprsl>-lgart_list.
      else.
        insert ls_lgart into table ls_lgart_sprsl-lgart_list.
        insert ls_lgart_sprsl into table ls_lgart_molga-lgart_list.
      endif.

    endloop.

    ev_result = ls_lgart_molga.

  endmethod.


  method get_lgart_by_sprsl.

    data: ls_lgart_sprsl type ty_lgart_sprsl,
          ls_lgart       type ty_lgart.

    ls_lgart_sprsl-sprsl = iv_sprsl.

    select lgart, lgtxt into table @data(lt_t512t) from t512t where sprsl eq @iv_sprsl.
    loop at lt_t512t into data(ls_t512t).
      clear ls_lgart.
      ls_lgart-lgart = ls_t512t-lgart.
      ls_lgart-lgtxt = ls_t512t-lgtxt.
      insert ls_lgart into table ls_lgart_sprsl-lgart_list.
    endloop.

    ev_result = ls_lgart_sprsl.

  endmethod.


  method get_subty_stext.
    data: ls_subty_sprsl type ty_subty_sprsl.

    read table gt_subty assigning field-symbol(<fs_subty>) with key sprsl = gv_ddlanguage.
    if sy-subrc ne 0.
      ls_subty_sprsl-sprsl = gv_ddlanguage.
      select infty, subty, stext from t591s where sprsl eq @gv_ddlanguage
        into corresponding fields of table @ls_subty_sprsl-subty_list.
      insert ls_subty_sprsl into table gt_subty.
    endif.

    read table ls_subty_sprsl-subty_list into data(ls_subty) with key infty = iv_infty subty = iv_subty.
    if sy-subrc eq 0.
      ev_result = ls_subty-stext.
    endif.

  endmethod.


  method get_objid_stext.

    data: lv_where type string.

    if iv_date is initial.
      lv_where = |ENDDA EQ '99991231'|.
    else.
      lv_where = |BEGDA LE '{ iv_date }' AND ENDDA GE '{ iv_date }'|.
    endif.

    read table gt_objid into data(ls_objid) with key begda = iv_date.
    if sy-subrc ne 0.
      select langu, otype, objid, begda, case when stext is initial then short else stext end as stext
        from hrp1000 where plvar = '01' and istat = '1' and (lv_where)
        into table @data(lt_hrp1000).

      sort lt_hrp1000 by langu otype objid begda descending.
      delete adjacent duplicates from lt_hrp1000 comparing langu otype objid.
      ls_objid-begda = iv_date.
      move-corresponding lt_hrp1000 to ls_objid-objid.
      insert ls_objid into table gt_objid.
    endif.

    read table ls_objid-objid into data(ls_objid_tmp) with key langu = gv_ddlanguage otype = iv_otype objid = iv_objid.
    ev_result = ls_objid_tmp-stext.

  endmethod.
endclass.
~~~

