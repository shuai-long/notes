[TOC]

# 附件处理 #

可以使用SAP的`BUSINESS DOCUMENT NAVIGATOR`，TCODE:`OAOR`来实现这个功能:  `OAOR`本身是一个可执行程序，通过对程序界面进行简单的修改，应该可以实现你需要的功能；

首先在Tcode：`SBDSV3`中添加一个文档CLASS，类型为OT即可；然后就通过这个CLASS管理上传和下载你的文档。

## 上传附件 ##

```abap
*&---------------------------------------------------------------------*
*& Form frm_upload_attachment
*&---------------------------------------------------------------------*
*& text 上传附件
*&---------------------------------------------------------------------*
*& -->  lv_objkey         text:对象关键字
*& -->  lv_objtype        text:对象类型,例如 'BUS1130',可在SBDSV3中添加
*&---------------------------------------------------------------------*
FORM frm_upload_attachment USING lv_objkey  TYPE swo_typeid
                                 lv_objtype TYPE swo_objtyp..

  DATA: lv_refer      TYPE REF TO cl_gos_document_service,
        ls_borident   TYPE borident,
        ls_attachment TYPE swo_typeid.

  CREATE OBJECT lv_refer.
  ls_borident-objkey  = lv_objkey.
  ls_borident-objtype = lv_objtype.
  CALL METHOD lv_refer->create_attachment
    EXPORTING
      is_object     = ls_borident
    IMPORTING
      ep_attachment = ls_attachment.
  COMMIT WORK.

ENDFORM.
```

## 查询附件 ##

```abap
*&---------------------------------------------------------------------*
*& Form frm_query_attachment
*&---------------------------------------------------------------------*
*& text 查询附件
*&---------------------------------------------------------------------*
*& -->  lv_objkey         text:对象关键字
*& -->  lv_objtype        text:对象类型,例如 'BUS1130',可在SBDSV3中添加
*&---------------------------------------------------------------------*
FORM frm_query_attachment USING lv_objkey  TYPE swo_typeid
                                lv_objtype TYPE swo_objtyp.

  DATA:l_objectid   TYPE bapiborid,
       lt_relat     TYPE TABLE OF bapirellk,
       ls_relat     LIKE LINE OF lt_relat,
       ls_folder_id LIKE soodk,
       ls_object_id LIKE soodk,
       ls_objdisp   TYPE sood2,
       lt_objcont   TYPE TABLE OF soli,
       lt_objhead   LIKE soli OCCURS 0 WITH HEADER LINE.

  l_objectid-objkey = lv_objkey.
  l_objectid-objtype = lv_objtype.

  CALL FUNCTION 'BAPI_REL_GETRELATIONS'
    EXPORTING
      objectid        = l_objectid
    TABLES
      listofrelations = lt_relat.

  SORT lt_relat BY objkey_b DESCENDING.
  LOOP AT lt_relat INTO ls_relat.
*    ls_relat-objkey_b.    "相同对象关键字的序号

    ls_folder_id-objtp = ls_relat-objkey_b(3).
    ls_folder_id-objyr = ls_relat-objkey_b+3(2).
    ls_folder_id-objno = ls_relat-objkey_b+5(12).

    ls_object_id-objtp = ls_relat-objkey_b+17(3).
    ls_object_id-objyr = ls_relat-objkey_b+20(2).
    ls_object_id-objno = ls_relat-objkey_b+22(12).

    CALL FUNCTION 'SO_OBJECT_READ'
      EXPORTING
        folder_id         = ls_folder_id
        object_id         = ls_object_id
      IMPORTING
        object_hd_display = ls_objdisp  "附件的信息
      TABLES
        objcont           = lt_objcont
        objhead           = lt_objhead
      EXCEPTIONS
        OTHERS            = 15.

*    ls_objdisp-objdes.   "附件描述
*    ls_objdisp-file_ext. "附件类型,即扩展名
*    ls_objdisp-cronam.   "附件创建人
*    ls_objdisp-crdat.    "附件创建日期
*    ls_objdisp-crtim.    "附件创建时间
  ENDLOOP.
ENDFORM.
```

## 下载附件 ##

```abap
*&---------------------------------------------------------------------*
*& Form frm_download_attachment
*&---------------------------------------------------------------------*
*& text: 下载附件
*&---------------------------------------------------------------------*
*& -->  lv_objkey_b                 text: 查询附件时拿到的相同关键字的序号
*& -->  lv_default_extension_temp   text: 查询附件时拿到的附件类型,即扩展名
*&---------------------------------------------------------------------*
FORM frm_download_attachment USING lv_objkey_b               TYPE SWO_TYPEID
                                   lv_default_extension_temp TYPE SO_FILEEXT.

  DATA:l_objectid        TYPE bapiborid,
       lt_relat          TYPE TABLE OF bapirellk WITH HEADER LINE,
       ls_folder_id      LIKE soodk,
       ls_object_id      LIKE soodk,
       ls_objdisp        TYPE sood2,
       lt_objcont        TYPE TABLE OF soli,
       lt_objhead        LIKE soli OCCURS 0 WITH HEADER LINE,
       lv_str1           TYPE char255,
       lv_filename       LIKE soli-line,
       lv_filename1      TYPE string,
       lv_path           TYPE string VALUE'C:\',
       lv_fullpath       TYPE string VALUE'C:\',
       ls_loio_object    LIKE sdokobject,
       ls_phio_object    LIKE sdokobject,
       lt_context        LIKE sdokpropty OCCURS 0 WITH HEADER LINE,
       lt_binary_content TYPE TABLE OF sdokcntbin.

  DATA: lv_len      TYPE i,
        lv_need_len TYPE i.
        
  REFRESH lt_objcont.
  ls_folder_id-objtp = lv_objkey_b(3).
  ls_folder_id-objyr = lv_objkey_b+3(2).
  ls_folder_id-objno = lv_objkey_b+5(12).

  ls_object_id-objtp = lv_objkey_b+17(3).
  ls_object_id-objyr = lv_objkey_b+20(2).
  ls_object_id-objno = lv_objkey_b+22(12).

  CALL FUNCTION 'SO_OBJECT_READ'
    EXPORTING
      folder_id         = ls_folder_id
      object_id         = ls_object_id
    IMPORTING
      object_hd_display = ls_objdisp
    TABLES
      objcont           = lt_objcont
      objhead           = lt_objhead
    EXCEPTIONS
      OTHERS            = 15.

  LOOP AT lt_objhead.
    IF sy-tabix = 1.
      SPLIT lt_objhead-line AT '=' INTO lv_str1 lv_filename.
      EXIT.
    ENDIF.
  ENDLOOP.

  CALL FUNCTION 'SO_KPRO_DATA_FROM_OBJCONT_GET'
    IMPORTING
      loio_object       = ls_loio_object
    TABLES
      objcont           = lt_objcont
      context           = lt_context
    EXCEPTIONS
      missing_kpro_data = 1
      OTHERS            = 2.

  CALL FUNCTION 'SO_LOIO_PHIO_GET'
    EXPORTING
      loio_object        = ls_loio_object
    IMPORTING
      phio_object        = ls_phio_object
    EXCEPTIONS
      kpro_inconsistency = 1
      x_error            = 2
      OTHERS             = 3.

  CALL FUNCTION 'SDOK_PHIO_LOAD_CONTENT'
    EXPORTING
      object_id           = ls_phio_object
    TABLES
      file_content_binary = lt_binary_content
    EXCEPTIONS
      not_existing        = 1
      not_authorized      = 2
      no_content          = 3
      bad_storage_type    = 4
      OTHERS              = 5.

  lv_len = ls_objdisp-objlen.

  CALL FUNCTION 'SCMS_BINARY_TO_XSTRING'
    EXPORTING
      input_length = lv_len
    TABLES
      binary_tab   = lt_binary_content
    EXCEPTIONS
      failed       = 1
      OTHERS       = 2.

  DATA:lv_default_extension  TYPE string.
  lv_filename1 = lv_filename.
  lv_default_extension = lv_default_extension_temp.


  CALL METHOD cl_gui_frontend_services=>file_save_dialog
    EXPORTING
      window_title      = '附件下载'
      default_extension = lv_default_extension
      default_file_name = lv_filename1
    CHANGING
      filename          = lv_filename1
      path              = lv_path
      fullpath          = lv_fullpath.

  IF lv_fullpath = ''.
    MESSAGE  '请选择正确的路径！' TYPE 'E'.
    STOP.
  ELSE.
    FIELD-SYMBOLS:<fs_data_tab> TYPE STANDARD TABLE.

    IF lt_binary_content[] IS NOT INITIAL.
      ASSIGN lt_binary_content TO <fs_data_tab>.
    ELSE.
      ASSIGN lt_objcont TO <fs_data_tab>.
    ENDIF.

    CALL FUNCTION 'GUI_DOWNLOAD'
      EXPORTING
        filename = lv_fullpath
        filetype = 'BIN'
      TABLES
        data_tab = <fs_data_tab>. "lt_binary_content.
  ENDIF.

ENDFORM.
```

## 删除附件 ##

```abap
*&-----------------------------------------------------------------------------*
*& Form frm_delete_attachment
*&-----------------------------------------------------------------------------*
*& text 删除附件
*&-----------------------------------------------------------------------------*
*& -->  lv_objkey_b            text: 查询附件时拿到的相同关键字的序号
*& -->  lv_objkey              text:对象关键字
*& -->  lv_objtype             text:对象类型,例如 'BUS1130',可在SBDSV3中添加
*&-----------------------------------------------------------------------------*
FORM frm_delete_attachment USING lv_objkey   TYPE swo_typeid
                                 lv_objtype  TYPE swo_objtyp
                                 lv_objkey_b TYPE swo_typeid.

  DATA: lv_refer      TYPE REF TO cl_gos_document_service,
        is_object     TYPE borident,
        is_lporb      TYPE sibflporb,
        ip_attachment TYPE borident-objkey.

  is_object-objkey   = lv_objkey.
  is_object-objtype  = lv_objtype.
  ip_attachment      = lv_objkey_b.
  CREATE OBJECT lv_refer.
  CALL METHOD lv_refer->delete_attachment
    EXPORTING
      is_object     = is_object
      is_lporb      = is_lporb
      ip_attachment = ip_attachment.

  COMMIT WORK.

ENDFORM.
```

## 完整DEMO

```abap
*&---------------------------------------------------------------------*
*& Report ZHRB_017
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
report zhrb_017.
tables: sscrfields,pm0d1,acdoca ,p1000.

include <cntn01>.

types:begin of ty_alv1,
        objid      type hrobjid, "组织编码
        stext      type stext,   "组织名称
        gjahr      type char4,   "年度
        z00hryxds  type ze_hryxds1, "预分配数
        z00hrzsxds type ze_hryxds1, "正式下达数
        z00hrtzxds type ze_hryxds1, "调整下达数
        z00hrhjs   type ze_hryxds1, "合计数
        z00hrbz    type ze_hrbz1, "备注
      end of ty_alv1.

types: begin of ty_alv,
         checkbox    type char1,
         style       type lvc_t_styl,
         z00hrbz_tmp type ze_hrbz1.
         include     type zhrt_040.
types end of ty_alv.

data:gt_alv      type table of ty_alv,
     gt_data_log type table of zhrt_040,
     gt_hrp9816  type table of p9816,
     gt_edit     type lvc_t_styl.

data:begin of gt_fj occurs 0,
       checkbox(1)  type c, "选择框
       index        type bapirellk-objkey_b, "序号
       filename(50) type c, "文件名
       file_type    type sood2-file_ext, "文件类型
       updata_name  type sy-uname, "上传人
       updata_date  type sy-datum, "上传日期
       updata_time  type sy-uzeit, "上传时间
     end of gt_fj .

data:alv_grid1      type ref to cl_gui_alv_grid, "cl_gui_alv_grid类引用
     alv_container1 type ref to cl_gui_docking_container,
     gs_toolbar     type stb_button, "功能按钮
     layout         type lvc_s_layo,
     fieldcat       type lvc_t_fcat with header line. "alv显示设置

class cl_event_handle1 definition. "事件处理类定义
  public section.

    methods handle_toolbar for event toolbar of cl_gui_alv_grid "初始化ALV工具栏对象事件，如增加按钮并设定属性
      importing
        e_object
        e_interactive.

    methods handle_user_command for event user_command of cl_gui_alv_grid    "ALV工具栏按钮的点击事件
      importing
        e_ucomm.

    methods handle_onf4 for event onf4 of cl_gui_alv_grid          "增加搜索帮助
      importing
        e_fieldname "代表用户点击了ALV的哪个字段来触发搜索帮助，
        es_row_no   "代表了当前行信息，es_row_no-row_id就是ALV中内表记录的INDEX。
        er_event_data."当前用户对ALV进行了哪些编辑的信息

endclass.

data: event_handle1 type ref to cl_event_handle1."定义类对象的引用

class cl_event_handle1 implementation."事件处理类实现部分

  method handle_toolbar.
    delete e_object->mt_toolbar where quickinfo ne '筛选'.

    gs_toolbar-function = 'SCFJ'.     "按钮的FunctionCode
    gs_toolbar-text = '上传附件'.       "按钮标签
    gs_toolbar-butn_type = '0'.     "定义按钮类型，0为标准按钮，具体取值可参考这里
    append gs_toolbar to e_object->mt_toolbar."添加按钮到工具栏中

    gs_toolbar-function = 'XZFJ'.     "按钮的FunctionCode
    gs_toolbar-text = '下载附件'.       "按钮标签
    gs_toolbar-butn_type = '0'.     "定义按钮类型，0为标准按钮，具体取值可参考这里
    append gs_toolbar to e_object->mt_toolbar."添加按钮到工具栏中

    gs_toolbar-function = 'SCFJ1'.     "按钮的FunctionCode
    gs_toolbar-text = '删除附件'.       "按钮标签
    gs_toolbar-butn_type = '0'.     "定义按钮类型，0为标准按钮，具体取值可参考这里
    append gs_toolbar to e_object->mt_toolbar."添加按钮到工具栏中

  endmethod.

  method handle_onf4.
    er_event_data->m_event_handled = 'X'. "在Method的最后，记得加上
  endmethod.

  method handle_user_command.
    data: sum type i .

    call method alv_grid1->check_changed_data. " 更改

    case e_ucomm.
      when 'SCFJ'.
        perform frm_upload_data .
      when 'XZFJ'.
        loop at gt_fj assigning field-symbol(<fs_fj>) where checkbox eq 'X'..
          perform frm_download_data using <fs_fj>-index <fs_fj>-file_type.
        endloop.
      when 'SCFJ1'.
        loop at gt_fj assigning <fs_fj> where checkbox eq 'X'..
          perform frm_delete_data using <fs_fj>-index.
        endloop.
    endcase.

    wait up to 1 seconds.
    perform frm_set_gt_fj.

    call method alv_grid1->refresh_table_display.


  endmethod.
endclass.


*----------------------------------------------------------------------*
*       CLASS lcl_event_handler DEFINITION
*----------------------------------------------------------------------*
*       类定义
*----------------------------------------------------------------------*
class lcl_event_handler definition."类定义
  public section.
    methods:
      handle_data_changed for event data_changed_finished of cl_gui_alv_grid
        importing e_modified et_good_cells.
endclass. "lcl_event_handler DEFINITION
*----------------------------------------------------------------------*
*       CLASS lcl_event_handler IMPLEMENTATION
*----------------------------------------------------------------------*
*       类实现
*----------------------------------------------------------------------*
class lcl_event_handler implementation."类实现

  method handle_data_changed.
    data:stbl type  lvc_s_stbl.
    perform handle_data_changed ."具体处理数据事件
    if e_modified = 'X'.
      stbl-row = 'X'." 基于行的稳定刷新
      stbl-col = 'X'." 基于列稳定刷新
      call method alv_grid1->refresh_table_display
        exporting
          is_stable = stbl.
    endif.
  endmethod.
endclass. "lcl_event_handler IMPLEMENTATION



**选择屏幕按钮
*SELECTION-SCREEN: FUNCTION KEY 1.

selection-screen begin of block b1 with frame title text-001.
  select-options:s_objid for p1000-objid obligatory no intervals no-extension,
                 s_gjahr for acdoca-gjahr obligatory no intervals no-extension.
  parameters:p_xdlx type char1 as listbox visible length 15 obligatory default '1'.

selection-screen end of block b1.

initialization.
  perform frm_init_data.

*结构化搜索帮助
at selection-screen on value-request for s_objid-low.
  perform frm_set_help.

start-of-selection.
  perform frm_check_objid.
  perform frm_check_xdlx.
  perform frm_get_data.

end-of-selection.
  perform frm_display_data.

*&---------------------------------------------------------------------*
*& Form frm_init_data
*&---------------------------------------------------------------------*
*& text 初始化选择屏幕
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
form frm_init_data .
  data: lt_value type vrm_values.
  lt_value[] = value #( ( key = '1' text = '预分配' )
                        ( key = '2' text = '正式下达' )
                        ( key = '3' text = '调整下达' )
  ).
  call function 'VRM_SET_VALUES'
    exporting
      id     = 'P_XDLX'
      values = lt_value.
endform.


*&---------------------------------------------------------------------*
*& Form frm_set_help
*&---------------------------------------------------------------------*
*& text 结构化搜索帮助
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
form frm_set_help .
  data : f4_objec  like objec.
  call function 'RH_OBJID_REQUEST'                       "XMKBCEK002468
    exporting
      plvar             = '01'
      otype             = 'O'
      dynpro_repid      = sy-repid
      dynpro_dynnr      = sy-dynnr
      dynpro_searkfield = 'S_OBJID-LOW'
    importing
      sel_object        = f4_objec
    exceptions
      cancelled         = 1
      wrong_condition   = 2
      nothing_found     = 3
      illegal_mode      = 4
      internal_error    = 5
      others            = 6.

  if sy-subrc = 0.
    s_objid-low = f4_objec-objid.
*    MOVE-CORRESPONDING f4_objec-realo TO s_objid-low.
  endif.
endform.

*&---------------------------------------------------------------------*
*& Form frm_check_data
*&---------------------------------------------------------------------*
*& text 权限检查
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
form frm_check_objid .
  data:lv_begda type sy-datum,
       lv_endda type sy-datum.
  lv_begda = s_gjahr-low && '0101'.
  lv_endda = s_gjahr-low && '1231'.

  call function 'RH_STRU_AUTHORITY_CHECK'
    exporting
      fcode                    = 'DISP'
      plvar                    = '01'
      otype                    = 'O'
      objid                    = s_objid-low
      with_base_ac             = 'X'
    exceptions
      no_stru_authority        = 1
      no_stru_authority_hyper  = 2
      no_stru_authority_at_all = 3
      no_base_authority        = 4
      others                   = 5.

  if sy-subrc ne 0.
    message '您没有组织单位:' && s_objid-low && '的权限' type 'S' display like 'E'.
    leave list-processing.
    stop.
  endif.
  "检查HRP9816中是否存在本年度的记录
  perform frm_read_pnnnn tables gt_hrp9816 using s_objid-low 'O' '9816' '' lv_begda lv_endda.

  if gt_hrp9816 is initial.
    message '选定的组织单位上无对应年度工资总额信息' type 'S' display like 'E'.
    leave list-processing.
    stop.
  endif.

endform.

form frm_check_xdlx.

*  IF p_xdlx EQ '1' AND NOT line_exists( gt_hrp9816[ z00hrzsxds = '' ] ).
*    MESSAGE '已存在正式下达数，请勿执行预分配' TYPE 'S' DISPLAY LIKE 'E'.
*    LEAVE LIST-PROCESSING.
*    STOP.
*  ENDIF.
*
*  IF p_xdlx EQ '3' AND line_exists( gt_hrp9816[ z00hrzsxds = '' ] ).
*    MESSAGE '未查询到正式下达数，请勿执行调整下达' TYPE 'S' DISPLAY LIKE 'E'.
*    LEAVE LIST-PROCESSING.
*    STOP.
*  ENDIF.

endform.

*&---------------------------------------------------------------------*
*& Form frm_get_data
*&---------------------------------------------------------------------*
*& text 取数
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
form frm_get_data.
  data:lv_begda   type sy-datum,
       lv_endda   type sy-datum,
       ls_alv     type ty_alv,
       lv_field   type lvc_s_styl-fieldname,
       lt_hrp9816 type table of p9816.

  lv_begda = s_gjahr-low && '0101'.
  lv_endda = s_gjahr-low && '1231'.

  data:lt_result_tab   type table of swhactor,
       lt_result_objec type table of objec,
       lt_result_struc type table of struc.

  select *
    into table @data(lt_zhrt_040)
    from zhrt_040
    where gjahr = @s_gjahr-low
    and zxdlx eq @p_xdlx.


  select *
     into table @data(lt_zhrt_043)
    from zhrt_043
    where orgeh1 eq @s_objid-low
    and z00flag eq 'X'.


  select objid,stext
    into table @data(lt_hrp1000)
    from hrp1000
    where otype = 'O'
    and begda le @sy-datum
    and endda ge @sy-datum.

  sort lt_zhrt_040 descending by objid seqnr.
  sort lt_hrp1000 by objid.

  read table gt_hrp9816 into data(ls_hrp9816) index 1.
  if sy-subrc = 0.
    clear ls_alv.
    move-corresponding ls_hrp9816 to ls_alv.
    ls_alv-zxdlx = p_xdlx.
    read table lt_hrp1000 into data(ls_hrp1000) with key objid = ls_alv-objid binary search.
    if sy-subrc = 0.
      ls_alv-stext =  ls_hrp1000-stext.
    endif.

    ls_alv-gjahr = s_gjahr-low.
    ls_alv-checkbox = 'X'.
    append ls_alv to gt_alv.

    loop at lt_zhrt_043 into data(ls_zhrt_043).
      clear ls_alv.
      perform frm_read_pnnnn tables lt_hrp9816 using ls_zhrt_043-orgeh2 'O' '9816' '' lv_begda lv_endda.
      sort lt_hrp9816 descending by seqnr.
      read table lt_hrp9816 into ls_hrp9816 index 1.
      if sy-subrc = 0.
        move-corresponding ls_hrp9816 to ls_alv.
      else.
        read table lt_zhrt_040 into data(ls_zhrt_040) with key seqnr = '000' objid = ls_zhrt_043-orgeh2.
        if sy-subrc = 0.
          move-corresponding ls_zhrt_040 to ls_alv.
        endif.
        ls_alv-objid = ls_zhrt_043-orgeh2.
      endif.
      read table lt_hrp1000 into ls_hrp1000 with key objid = ls_alv-objid binary search.
      if sy-subrc = 0.
        ls_alv-stext =  ls_hrp1000-stext.
      endif.
      ls_alv-stext = ls_zhrt_043-stext2.
      ls_alv-gjahr = s_gjahr-low.
      ls_alv-zxdlx = p_xdlx.
      append ls_alv to gt_alv.
    endloop.
  endif.

  case p_xdlx.
    when '1'.
      lv_field = 'Z00HRYXDS'.
    when '2'.
      lv_field = 'Z00HRZSXDS'.
    when '3'.
      lv_field = 'Z00HRTZXDS'.
  endcase.

  clear ls_alv.
  perform set_status_disabled using 1 lv_field.
  perform set_status_disabled using 1 'Z00HRBZ'.
  insert lines of gt_edit into table ls_alv-style.
  modify gt_alv index 1 from ls_alv transporting style.

  if gt_alv is initial.
    message '没有数据' type 'S' display like 'E'.
    leave list-processing.
    stop.
  endif.
endform.


*&---------------------------------------------------------------------*
*& Form frm_display_data
*&---------------------------------------------------------------------*
*& text 数据展示
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
form frm_display_data .
  data:lt_fcat       type lvc_t_fcat,
       ls_layout     type lvc_s_layo,
       ls_disvariant like disvariant,
       lt_events     type slis_t_event,
       lt_exclude    type slis_t_extab.

  perform frm_set_layout   changing ls_layout."样式设置
  perform frm_set_fieldcat changing lt_fcat.  "设置FIELDCAT  仅显示
  perform frm_set_event changing lt_events.  "
  perform frm_set_disvariant changing ls_disvariant.
  perform frm_alv_display  using    lt_events
  changing lt_fcat
    ls_layout
    lt_exclude
    ls_disvariant
    gt_alv."ALV显示
endform.

*&---------------------------------------------------------------------*
*& Form frm_set_layout
*&---------------------------------------------------------------------*
*& text 设置ALV样式
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
form frm_set_layout changing cs_layout type lvc_s_layo.

*<---设置列优化及行选择--->
  cs_layout-cwidth_opt = ''.
  cs_layout-sel_mode   = 'A'."A（行与列的选择，无法选择单元格）。"B（单选，不可以多选行，不可以多选单元格）。"C（多选行、列，不可以多选单元格）。"D（多选行、列，还可以多选单元格）。
  cs_layout-zebra      = abap_true.
  cs_layout-stylefname  = 'STYLE'.
  cs_layout-no_rowmark      = abap_true.

endform. " FRM_SET_LAYOUT

*&---------------------------------------------------------------------*
*& Form frm_set_fieldcat
*&---------------------------------------------------------------------*
*& text 设置fieldcat目录
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
form frm_set_fieldcat changing ct_fcat type lvc_t_fcat.

  data: ls_fcat type lvc_s_fcat.
  data: lv_yfp  type char1,
        lv_zsxd type char1,
        lv_tzxd type char1.

  define m_fcat.
    CLEAR ls_fcat.
    ls_fcat-fieldname = &1.
    ls_fcat-scrtext_m = &2.
    ls_fcat-edit = &3.
    ls_fcat-edit_mask = &4.
    ls_fcat-ref_table = &5.
    ls_fcat-txt_field = &6.
    ls_fcat-outputlen = &7.
    IF &1 EQ 'Z00HRTZXDS'.
      ls_fcat-datatype = 'CURR'.
      ls_fcat-domname = 'ZD_CURR'.
    ENDIF.
    APPEND ls_fcat TO ct_fcat.
  end-of-definition.

  case p_xdlx.
    when '1'.
      lv_yfp = 'X'.
    when '2'.
      lv_zsxd = 'X'.
    when '3'.
      lv_tzxd = 'X'.
  endcase.
  clear: ct_fcat.
  m_fcat:'OBJID'  '组织编码' '' '' '' '' '10'.
  m_fcat:'STEXT'  '组织名称' '' '' 'HRP9811' 'STEXT' '40'.
  m_fcat:'GJAHR'  '年度' '' '' '' '' '8'.
  m_fcat:'Z00HRYXDS'  '预分配数(万元)' lv_yfp '==ZLWXS' '' '' '15'.
  m_fcat:'Z00HRZSXDS'  '正式下达数(万元)' lv_zsxd '==ZLWXS' '' '' '15'.
  m_fcat:'Z00HRTZXDS'  '调整数(万元)' lv_tzxd '==ZLWXS' '' '' '15'.
  m_fcat:'Z00HRHJS'  '合计数(万元)' '' '' '' '==ZLWXS' '15'.
*  m_fcat:'Z00HRSYKYS'  '剩余可用数' '' '' 'HRP9816' 'Z00HRSYKYS' ''.
  m_fcat:'Z00HRBZ'  '备注' 'X' '' 'HRP9816' 'Z00HRBZ' '100'.
endform. " FRM_SET_FIELDCAT

*&---------------------------------------------------------------------*
*& Form frm_set_disvariant
*&---------------------------------------------------------------------*
*& text alv布局与选择屏幕挂钩
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
form frm_set_disvariant changing cs_disvariant like disvariant.
*  cs_disvariant-report = 'ZFIB_032'."程序名
*  cs_disvariant-handle = p_ywcj. "选择屏幕变量
endform.

*&---------------------------------------------------------------------*
*& Form frm_alv_display
*&---------------------------------------------------------------------*
*& text ALV展示
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
form frm_alv_display using    it_events  type slis_t_event
changing ct_fcat    type lvc_t_fcat
  cs_layout  type lvc_s_layo
  ct_exclude type slis_t_extab
  cs_disvariant like disvariant
  ct_tab     type standard table.

  call function 'REUSE_ALV_GRID_DISPLAY_LVC'
    exporting
      i_callback_program       = sy-repid
      is_layout_lvc            = cs_layout
      it_fieldcat_lvc          = ct_fcat
      i_callback_pf_status_set = 'SET_PF_STATUS'          "GUI状态设置
      i_callback_user_command  = 'ALV_USER_COMMAND'       "用户状态PAI
*     i_callback_html_top_of_page = 'HTML_TOP_OF_PAGE'       "ALV标题内容
*     i_html_height_top        = 14                       "ALV标题高度
*     it_excluding             = ct_exclude
      it_events                = it_events
*     i_default                = abap_true
*     i_save                   = 'A'
*     is_variant               = cs_disvariant
    tables
      t_outtab                 = ct_tab
    exceptions
      others                   = 0.

endform. " FRM_ALV_DISPLAY

*&---------------------------------------------------------------------*
*& Form set_pf_status
*&---------------------------------------------------------------------*
*& text GUI状态栏设置
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
form set_pf_status using pt_exclude type kkblo_t_extab.

  data:lt_exclude type kkblo_t_extab with header line.
*  lt_exclude-fcode = 'SCFJ'. APPEND lt_exclude. "需要排除的按钮
*  lt_exclude-fcode = 'XZFJ'. APPEND lt_exclude. "需要排除的按钮
*  lt_exclude-fcode = 'SCFJ1'. APPEND lt_exclude. "需要排除的按钮
  lt_exclude-fcode = 'MBXZ'. append lt_exclude. "需要排除的按钮
  lt_exclude-fcode = 'BC'. append lt_exclude. "需要排除的按钮
  set pf-status 'STANDARD' excluding lt_exclude[].

  call function 'GET_GLOBALS_FROM_SLVC_FULLSCR'
    importing
      e_grid = alv_grid1.

  call method alv_grid1->register_edit_event
    exporting
      i_event_id = cl_gui_alv_grid=>mc_evt_modified.
  if sy-subrc <> 0.
  endif.

endform.

*&---------------------------------------------------------------------*
*& Form alv_user_command
*&---------------------------------------------------------------------*
*& text  ALV按钮事件
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
form alv_user_command using r_ucomm     like sy-ucomm
      rs_selfield type slis_selfield.
  data:ref_grid type ref to cl_gui_alv_grid.
  data:ls_data_log type zhrt_040.
  data:lv_error   type char1,
       lv_message type string.
  data: answer type string.
  data:lv_file_path type rlgrap-filename.


  call function 'GET_GLOBALS_FROM_SLVC_FULLSCR'
    importing
      e_grid = ref_grid.

  call method ref_grid->check_changed_data. " 更改

  loop at gt_alv assigning field-symbol(<fs_alv>).
*    IF p_xdlx EQ '1'.
*      <fs_alv>-z00hrhjs = <fs_alv>-z00hryxds.
*    ELSE.
*      <fs_alv>-z00hrhjs = <fs_alv>-z00hrzsxds + <fs_alv>-z00hrtzxds.
*    ENDIF.

    if <fs_alv>-z00hrzsxds is initial and  <fs_alv>-z00hrtzxds is initial.
      <fs_alv>-z00hrhjs = <fs_alv>-z00hryxds.
    else.
      <fs_alv>-z00hrhjs = <fs_alv>-z00hrzsxds + <fs_alv>-z00hrtzxds.
    endif.
  endloop.


  case r_ucomm.

    when 'BC'.

      perform frm_check_data using lv_error lv_message.
      if lv_error eq 'X'.
        message '本次分配工资总额合计数大于可用总额数，请确认！' type 'E'.
      else.
        refresh gt_data_log.
        loop at gt_alv assigning <fs_alv> where checkbox ne 'X'.
          clear ls_data_log.
          move-corresponding <fs_alv> to ls_data_log.
          ls_data_log-zxdlx = p_xdlx.
          ls_data_log-seqnr = '000'.
          append ls_data_log to gt_data_log.
        endloop.
        modify zhrt_040 from table gt_data_log.
        commit work and wait.
      endif.

    when 'XD'.

      perform frm_check_data using lv_error lv_message.

      if lv_error eq 'X'.
        message lv_message type 'E'.
      else.
        perform frm_write_hrp9816.
      endif.

    when 'FJ'.
      perform frm_set_gt_fj.
      call screen 9001 starting at 10 10 ending at 50 20.

    when 'MBSC'.
      clear lv_file_path.
      perform frm_f4_help_file changing lv_file_path.
      if lv_file_path is not initial.
        data lt_alv type table of ty_alv1 with header line.
        perform frm_read_excel tables lt_alv[] using lv_file_path 1 2 8 9999 changing lt_alv.
        loop at lt_alv.
          read table gt_alv assigning <fs_alv> with key objid = lt_alv-objid.
          if sy-subrc = 0.
            if <fs_alv>-checkbox = 'X'.
              continue.
            endif.
            case p_xdlx.
              when 1.
                <fs_alv>-z00hryxds = lt_alv-z00hryxds.
              when 2.
                <fs_alv>-z00hrzsxds = lt_alv-z00hrzsxds.
              when 3.
                <fs_alv>-z00hrtzxds = lt_alv-z00hrtzxds.
            endcase.
            <fs_alv>-z00hrbz = lt_alv-z00hrbz.

            if <fs_alv>-z00hrzsxds is initial and  <fs_alv>-z00hrtzxds is initial.
              <fs_alv>-z00hrhjs = <fs_alv>-z00hryxds.
            else.
              <fs_alv>-z00hrhjs = <fs_alv>-z00hrzsxds + <fs_alv>-z00hrtzxds.
            endif.

          endif.
        endloop.
      endif.


  endcase.

  call method ref_grid->refresh_table_display. " 刷新

endform.




form frm_read_pnnnn tables pnnnn using objid
                                      otype
                                      infty
                                      subty
                                      begda
                                      endda.

  data: lv_objid type hrobjid,
        lv_begda type sy-datum,
        lv_endda type sy-datum.

  lv_objid = objid.
  lv_begda = begda.
  lv_endda = endda.

  if lv_begda is initial.
    lv_begda = '18000101'.
  endif.
  if lv_endda is initial.
    lv_endda = '99991231'.
  endif.

  refresh: pnnnn.
  call function 'RH_READ_INFTY_NNNN'
    exporting
      authority             = 'DISP'
      with_stru_auth        = 'X'
      plvar                 = '01'
      otype                 = 'O'
      objid                 = lv_objid
      infty                 = infty
      subty                 = subty
      begda                 = lv_begda
      endda                 = lv_endda
    tables
      innnn                 = pnnnn
    exceptions
      nothing_found         = 1
      wrong_condition       = 2
      infotyp_not_supported = 3
      wrong_parameters      = 4
      others                = 5.
  if sy-subrc <> 0.
* Implement suitable error handling here
  endif.

endform.

form frm_check_data using lv_error lv_message.

  data:lv_sum1 type hrp9816-z00hrhjs,
       lv_sum2 type hrp9816-z00hrhjs.
  data:begin of r_objid occurs 0 ,
         sign   type char1,
         option type char2,
         low    type char10,
         high   type char10,
       end of r_objid,
       lv_objid type char10.

  loop at gt_alv into data(ls_alv1).
    lv_objid = |*{ ls_alv1-objid }*|.
    r_objid[] = value #( base r_objid[]  sign = 'I' option = 'CP'  ( low = lv_objid )  ).
  endloop.

  select *
  into table @data(lt_hrp9817)
  from hrp9817
  where z00hrobjid in @r_objid[].

  delete lt_hrp9817 where z00hrtjny+0(4) ne s_gjahr-low.


  loop at gt_alv assigning field-symbol(<fs_alv>).
    <fs_alv>-z00hrsykys = <fs_alv>-z00hrhjs.
    loop at lt_hrp9817 into data(ls_hrp9817).
      lv_objid = |*{ <fs_alv>-objid }*|.
      if ls_hrp9817-z00hrobjid cp lv_objid.
        <fs_alv>-z00hrsykys = <fs_alv>-z00hrsykys - ls_hrp9817-z00hrzys.
        if <fs_alv>-z00hrsykys lt 0.
          lv_error = 'X'.
          lv_message = '组织单位' && <fs_alv>-objid && '的合计数小于已使用数,请确认!'.
          exit .
        endif.
      endif.
    endloop.
    if <fs_alv>-checkbox  = 'X'.
      lv_sum1 = <fs_alv>-z00hrhjs.
    else.
      lv_sum2  = lv_sum2 + <fs_alv>-z00hrhjs.
    endif.

    if lv_error = 'X'..
      exit.
    endif.
  endloop.

  if lv_error ne 'X'.
    if lv_sum1 lt lv_sum2.
      lv_error = 'X'.
      lv_message = '本次分配工资总额合计数大于可用总额数，请确认！'.
    else.
      lv_error = ''.
    endif.
  endif.
endform.


form set_status_disabled using index type sy-tabix  fieldname type lvc_s_styl-fieldname .
  data: ls_edit type lvc_s_styl.
  sy-tabix = index.
  ls_edit-fieldname = fieldname.
  ls_edit-style = cl_gui_alv_grid=>mc_style_disabled.
*  ls_edit-style = cl_gui_alv_grid=>MC_STYLE_ENABLED.
  ls_edit-style2 = space.
  ls_edit-style3 = space.
  ls_edit-style4 = space.
  ls_edit-maxlen = 23.
  insert ls_edit into table gt_edit.
endform.

form frm_write_hrp9816.
  data:lv_error   type char1,
       lv_message type char255,
       lv_begda   type sy-datum,
       lv_endda   type sy-datum.

  data:lt_data_log type table of zhrt_040,
       ls_data_log type zhrt_040.

  data:lt_hrp9816     type table of p9816,
       ls_hrp9816     type p9816,
       lt_hrp1000_beg type table of hrp1000,
       lt_hrp1000_end type table of hrp1000.

  data: lv_url   type string,
        lv_json  type string,
        req_json type string.


  lv_begda = s_gjahr-low && '0101'.
  lv_endda = s_gjahr-low && '1231'.

  select *
    into table @data(lt_hrp1000)
    from hrp1000
    for all entries in @gt_alv
    where objid eq @gt_alv-objid
    and otype eq 'O'.

  lt_hrp1000_beg[] = lt_hrp1000[].
  lt_hrp1000_end[] = lt_hrp1000[].

  sort lt_hrp1000_beg by objid begda.
  sort lt_hrp1000_end descending by objid endda.

  types:begin of ty_data_return,
          type    type char1,
          message type char255,
        end of ty_data_return.
  data:ls_data_in     type zhrs_049,
       ls_data_return type ty_data_return.

  ls_data_in-report = 'ZHRB_017'.
  loop at gt_alv into data(ls_alv) where checkbox ne 'X'.

    clear ls_data_log.
    move-corresponding ls_alv to ls_data_log.
    if ls_data_log-seqnr is not initial.
      ls_data_log-seqnr = ls_data_log-seqnr + 1.
    endif.
    append ls_data_log to lt_data_log.

    clear: ls_hrp9816.
    move-corresponding ls_alv to ls_hrp9816.
    ls_hrp9816-begda = lv_begda.
    ls_hrp9816-endda = lv_endda.
    read table lt_hrp1000_beg into data(ls_hrp1000_beg) with key objid = ls_hrp9816-objid.
    if sy-subrc = 0 and ls_hrp1000_beg-begda ge lv_begda.
      ls_hrp9816-begda = ls_hrp1000_beg-begda.
    endif.
    read table lt_hrp1000_end into data(ls_hrp1000_end) with key objid = ls_hrp9816-objid.
    if sy-subrc = 0 and ls_hrp1000_end-endda le lv_endda.
      ls_hrp9816-endda = ls_hrp1000_end-endda.
    endif.
    ls_hrp9816-mandt = sy-mandt.
    ls_hrp9816-plvar = '01'.
    ls_hrp9816-otype = 'O'.
    ls_hrp9816-infty = '9816'.
    ls_hrp9816-istat = '1'.
*    ls_hrp9816-aedtm = sy-datum.
*    ls_hrp9816-uname = sy-uname.
    ls_hrp9816-z00user = sy-uname.
    ls_hrp9816-z00hrsykys = ls_alv-z00hrsykys .
    append ls_hrp9816 to ls_data_in-item.
  endloop.


  case sy-mandt+0(1).
    when '1' or '2' or '3'.
      lv_url = 'http://10.88.26.18:8000/cctc/hrms/zhr_pay_flow?sap-client=' && sy-mandt.
    when '5'.
      lv_url =  'http://10.88.26.19:8000/cctc/hrms/zhr_pay_flow?sap-client=' && sy-mandt.
    when '7' or '8'.
      lv_url = 'http://10.88.21.49:28001/cctc/hrms/zhr_pay_flow?sap-client=' && sy-mandt.
  endcase.


  lv_json = /ui2/cl_json=>serialize( data = ls_data_in compress = abap_true ).
  call function 'Z_MM_PUSH_INTERFACE'
    exporting
      url         = lv_url
      gv_json_in  = lv_json
      call_method = 'POST'
    importing
      result      = req_json.

  /ui2/cl_json=>deserialize( exporting json        = req_json
                                       pretty_name = /ui2/cl_json=>pretty_mode-camel_case
                             changing  data        = ls_data_return ).

  if ls_data_return-type ne 'S'.
    message ls_data_return-message type 'E'.
  else.
    modify zhrt_040 from table lt_data_log.
    commit work and wait.
    message '下达成功' type 'S'.
  endif.

endform.

form frm_upload_data .


  data: lv_refer    type ref to cl_gos_document_service,
        ls_borident type borident.

  create object lv_refer.
  ls_borident-objkey = |{ s_objid-low }{ s_gjahr-low }|.
  ls_borident-objtype = 'HR0001'.

* 下载文件会乱码
*  CALL METHOD LV_REFER->CREATE_ATTACHMENT
*    EXPORTING
*      IS_OBJECT = LS_BORIDENT
**    IMPORTING
**     ep_attachment =

  data: lt_filetab       type filetable,
        lv_rc            type i,
        lv_filename      type string,
        lv_file_and_path type char200,
        lv_file          type char100,
        lv_type          type char100,
        lv_path          type char100,
        lt_bin           type solix occurs 0,
        ls_obj           type swc_object,
        ls_obja          type borident,
        ls_objb          type borident,
        ls_binrel        type gbinrel,
        lt_binatt        type standard table of brelattr,
        lv_attsize       type int4.

  call method cl_gui_frontend_services=>file_open_dialog
    exporting
      window_title            = '打开文件'
      initial_directory       = 'C:/'
    changing
      file_table              = lt_filetab
      rc                      = lv_rc
    exceptions
      file_open_dialog_failed = 1
      cntl_error              = 2
      error_no_gui            = 3
      not_supported_by_gui    = 4
      others                  = 5.


  if sy-subrc <> 0.
    message id sy-msgid type sy-msgty number sy-msgno
               with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    exit.
  else.
    read table lt_filetab into data(ls_filetab) index 1.
    lv_file_and_path = lv_filename = ls_filetab-filename.

    call function 'GUI_UPLOAD'
      exporting
        filename                = lv_filename
        filetype                = 'BIN'
      importing
        filelength              = lv_attsize
      tables
        data_tab                = lt_bin
      exceptions
        file_open_error         = 1
        file_read_error         = 2
        no_batch                = 3
        gui_refuse_filetransfer = 4
        invalid_type            = 5
        no_authority            = 6
        unknown_error           = 7
        bad_data_format         = 8
        header_not_allowed      = 9
        separator_not_allowed   = 10
        header_too_long         = 11
        unknown_dp_error        = 12
        access_denied           = 13
        dp_out_of_memory        = 14
        disk_full               = 15
        dp_timeout              = 16
        others                  = 17.

    if sy-subrc <> 0.
    else.

      call function 'STPU1_EXTRACT_FILENAME'
        exporting
          file_and_path = lv_file_and_path
        importing
          file          = lv_file
          pathname      = lv_path.

      perform frm_get_file_type using lv_file_and_path lv_type.

      swc_container      ls_cont.
      swc_create_object  ls_obj  'MESSAGE'       ''.
      swc_set_element    ls_cont 'NO_DIALOG'     'X'.
      swc_set_element    ls_cont 'DOCUMENTTITLE' lv_file.
      swc_set_table      ls_cont 'Content_Hex'   lt_bin.
      swc_set_element    ls_cont 'DOCUMENTTYPE'  lv_type.
      swc_set_element    ls_cont 'DOCUMENTSIZE'  lv_attsize.
      swc_refresh_object ls_obj.
      swc_call_method    ls_obj  'CREATE'        ls_cont.
      swc_get_object_key ls_obj  ls_objb-objkey.

      ls_objb-objtype = 'MESSAGE'.
      ls_obja-objtype = 'HR0001'.
      ls_obja-objkey = |{ s_objid-low }{ s_gjahr-low }|.

      call function 'BINARY_RELATION_CREATE_COMMIT'
        exporting
          obj_rolea      = ls_obja
          obj_roleb      = ls_objb
          relationtype   = 'ATTA'
        importing
          binrel         = ls_binrel
        tables
          binrel_attrib  = lt_binatt
        exceptions
          no_model       = 1
          internal_error = 2
          unknown        = 3
          others         = 4.
      if sy-subrc eq 0.
        message s043(sgos_msg).
      endif.
    endif.

  endif.

  commit work.


endform.

form frm_get_file_type using file_and_path type.
  data: len type i,
        pos type i.

  len = strlen( file_and_path ).
  pos = len.

  while file_and_path+pos(1) <> '.' and pos > 0.
    pos = pos - 1.
  endwhile.
  pos          = pos + 1.
  type         = file_and_path+pos.
endform.



form frm_delete_data using lv_attachment .
  data: lv_refer      type ref to cl_gos_document_service,
        is_object     type borident,
        is_lporb      type sibflporb,
        ip_attachment type borident-objkey.

  is_object-objkey = |{ s_objid-low }{ s_gjahr-low }|.
  is_object-objtype = 'HR0001'.
  ip_attachment = lv_attachment.
  create object lv_refer.
  call method lv_refer->delete_attachment
    exporting
      is_object     = is_object
      is_lporb      = is_lporb
      ip_attachment = ip_attachment.

  commit work.

endform.


form frm_download_data using lv_objkey_b lv_default_extension_temp.

  data:l_objectid        type bapiborid,
       lt_relat          type table of bapirellk with header line,
       ls_folder_id      like soodk,
       ls_object_id      like soodk,
       ls_objdisp        type sood2,
       lt_objcont        type table of soli,
       lt_objhead        like soli occurs 0 with header line,
       lv_str1           type char255,
       lv_filename       like soli-line,
       lv_filename1      type string,
       lv_path           type string value'C:\',
       lv_fullpath       type string value'C:\',
       ls_loio_object    like sdokobject,
       ls_phio_object    like sdokobject,
       lt_context        like sdokpropty occurs 0 with header line,
       lt_binary_content type table of sdokcntbin.

  data: lv_len      type i,
        lv_need_len type i.


  refresh lt_objcont.
  ls_folder_id-objtp = lv_objkey_b(3).
  ls_folder_id-objyr = lv_objkey_b+3(2).
  ls_folder_id-objno = lv_objkey_b+5(12).

  ls_object_id-objtp = lv_objkey_b+17(3).
  ls_object_id-objyr = lv_objkey_b+20(2).
  ls_object_id-objno = lv_objkey_b+22(12).

  call function 'SO_OBJECT_READ'
    exporting
      folder_id         = ls_folder_id
      object_id         = ls_object_id
    importing
      object_hd_display = ls_objdisp
    tables
      objcont           = lt_objcont
      objhead           = lt_objhead
    exceptions
      others            = 15.


  loop at lt_objhead.
    if sy-tabix = 1.
      split lt_objhead-line at '=' into lv_str1 lv_filename.
      exit.
    endif.
  endloop.

  call function 'SO_KPRO_DATA_FROM_OBJCONT_GET'
    importing
      loio_object       = ls_loio_object
    tables
      objcont           = lt_objcont
      context           = lt_context
    exceptions
      missing_kpro_data = 1
      others            = 2.

  call function 'SO_LOIO_PHIO_GET'
    exporting
      loio_object        = ls_loio_object
    importing
      phio_object        = ls_phio_object
    exceptions
      kpro_inconsistency = 1
      x_error            = 2
      others             = 3.

  call function 'SDOK_PHIO_LOAD_CONTENT'
    exporting
      object_id           = ls_phio_object
    tables
      file_content_binary = lt_binary_content
    exceptions
      not_existing        = 1
      not_authorized      = 2
      no_content          = 3
      bad_storage_type    = 4
      others              = 5.

  lv_len = ls_objdisp-objlen.

  call function 'SCMS_BINARY_TO_XSTRING'
    exporting
      input_length = lv_len
    tables
      binary_tab   = lt_binary_content
    exceptions
      failed       = 1
      others       = 2.

  data:lv_default_extension      type string.
  lv_filename1 = lv_filename.
  lv_default_extension = lv_default_extension_temp.


  call method cl_gui_frontend_services=>file_save_dialog
    exporting
      window_title      = '附件下载'
      default_extension = lv_default_extension
      default_file_name = lv_filename1
    changing
      filename          = lv_filename1
      path              = lv_path
      fullpath          = lv_fullpath.

  if lv_fullpath = ''.
    message  '请选择正确的路径！' type 'E'.
    stop.
  else.
    field-symbols:<fs_data_tab> type standard table.

    if lt_binary_content[] is not initial.
      assign lt_binary_content to <fs_data_tab>.
    else.
      assign lt_objcont to <fs_data_tab>.
    endif.

    call function 'GUI_DOWNLOAD'
      exporting
        filename = lv_fullpath
        filetype = 'BIN'
      tables
        data_tab = <fs_data_tab>. "lt_binary_content.
  endif.

endform.


form frm_set_gt_fj.
  data:l_objectid        type bapiborid,
       lt_relat          type table of bapirellk with header line,
       ls_folder_id      like soodk,
       ls_object_id      like soodk,
       ls_objdisp        type sood2,
       lt_objcont        type table of soli,
       lt_objhead        like soli occurs 0 with header line,
       lv_str1           type char255,
       lv_filename       like soli-line,
       lv_filename1      type string,
       lv_path           type string value'C:\',
       lv_fullpath       type string value'C:\',
       ls_loio_object    like sdokobject,
       ls_phio_object    like sdokobject,
       lt_context        like sdokpropty occurs 0 with header line,
       lt_binary_content type table of sdokcntbin.

  refresh gt_fj.

  l_objectid-objkey =  |{ s_objid-low }{ s_gjahr-low }|.
  l_objectid-objtype = 'HR0001'.
  call function 'BAPI_REL_GETRELATIONS'
    exporting
      objectid        = l_objectid
    tables
      listofrelations = lt_relat.

  sort lt_relat by objkey_b descending.
  loop at lt_relat .
    clear:gt_fj.
    gt_fj-index = lt_relat-objkey_b.

    ls_folder_id-objtp = lt_relat-objkey_b(3).
    ls_folder_id-objyr = lt_relat-objkey_b+3(2).
    ls_folder_id-objno = lt_relat-objkey_b+5(12).

    ls_object_id-objtp = lt_relat-objkey_b+17(3).
    ls_object_id-objyr = lt_relat-objkey_b+20(2).
    ls_object_id-objno = lt_relat-objkey_b+22(12).

    call function 'SO_OBJECT_READ'
      exporting
        folder_id         = ls_folder_id
        object_id         = ls_object_id
      importing
        object_hd_display = ls_objdisp
      tables
        objcont           = lt_objcont
        objhead           = lt_objhead
      exceptions
        others            = 15.
    gt_fj-filename = ls_objdisp-objdes.
    gt_fj-file_type = ls_objdisp-file_ext.
    gt_fj-updata_name = ls_objdisp-cronam.
    gt_fj-updata_date = ls_objdisp-crdat.
    gt_fj-updata_time = ls_objdisp-crtim.
    append gt_fj.
  endloop.
endform.

*&---------------------------------------------------------------------*
*& Module STATUS_9001 OUTPUT
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
module status_9001 output.
  data:lt_exclude type kkblo_t_extab with header line.
  lt_exclude-fcode = 'FJ'. append lt_exclude.
  lt_exclude-fcode = 'BC'. append lt_exclude.
  lt_exclude-fcode = 'XD'. append lt_exclude.
  lt_exclude-fcode = 'SCFJ'. append lt_exclude.
  lt_exclude-fcode = 'SCFJ1'. append lt_exclude.
  lt_exclude-fcode = 'XZFJ'. append lt_exclude.
  set pf-status 'STANDARD' excluding lt_exclude[].

  perform frm_create_alv.
endmodule.
*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_9001  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
module user_command_9001 input.
  case sy-ucomm.
    when '&F03' or '&F15' or '&F12'.
      leave to screen 0.
*      LEAVE SCREEN .
  endcase.
endmodule.

form frm_create_alv.
  if alv_container1 is initial."绘制第一个ALV

    create object alv_container1    "创建ALV容器对象
      exporting
*       container_name = 'CON1'
        side      = cl_gui_docking_container=>dock_at_left
        repid     = sy-repid
        dynnr     = sy-dynnr
        extension = 3000. "alv宽度

    create object alv_grid1 "创建ALV控件
      exporting
        i_parent = alv_container1.

    perform frm_set_style.

    call method alv_grid1->set_table_for_first_display
      exporting
        is_layout       = layout
      changing
        it_outtab       = gt_fj[]
        it_fieldcatalog = fieldcat[]. "如果fieldcat内表为空，则相当于没有配置，采用默认方式显示

    "为ALV按钮注册监听事件
    create object :event_handle1.
    set handler :  event_handle1->handle_toolbar       for alv_grid1,
                   event_handle1->handle_user_command  for alv_grid1,
                   event_handle1->handle_onf4          for alv_grid1."F4帮助注册事件
*                   event_handle1->handle_user_command  FOR alv_grid1,
*                   event_handle1->handle_enter  FOR alv_grid1.
*                 event_handle1->handle_hotspot_click FOR alv_grid1.


    call method alv_grid1->set_toolbar_interactive. "调用此方法才能激活工具栏上增加的自定义按钮

    call method alv_grid1->register_edit_event "注册编辑事件，否则不会触发更新事件
      exporting
        i_event_id = cl_gui_alv_grid=>mc_evt_modified.

  else.
    call method alv_grid1->refresh_table_display.
  endif.
endform.
*&---------------------------------------------------------------------*
*& Form frm_set_style
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
form frm_set_style .

  layout-cwidth_opt = ''.
  layout-sel_mode   = ''.
  layout-zebra      = abap_true.
  layout-no_rowmark      = abap_true.
  layout-no_toolbar      = abap_false.
  define set_fieldcat.
    fieldcat-fieldname = &1.
    fieldcat-reptext = &2.
    fieldcat-outputlen = &3.
    IF fieldcat-fieldname EQ 'CHECKBOX'.
       fieldcat-checkbox = 'X'.
       fieldcat-edit = 'X'.
    ENDIF.
    APPEND  fieldcat.
    CLEAR fieldcat.
  end-of-definition.


  set_fieldcat:'CHECKBOX'    '选择' '1'.
*  set_fieldcat:'INDEX'       '对象号' '34'.
  set_fieldcat:'FILENAME'    '附件名' '50'.
  set_fieldcat:'UPDATA_NAME' '上传人' '20'.
  set_fieldcat:'UPDATA_DATE' '上传日期' '10'.
  set_fieldcat:'UPDATA_TIME' '上传时间' '8'.

endform.

*&---------------------------------------------------------------------*
*&      Form  register_events
*&---------------------------------------------------------------------*
*       注册回车事件
*----------------------------------------------------------------------
form frm_register_events using e_grid type slis_data_caller_exit.

  call function 'GET_GLOBALS_FROM_SLVC_FULLSCR'
    importing
      e_grid = alv_grid1.
*
  data: gr_event_handler type ref to lcl_event_handler.
  create object gr_event_handler.
*
  call method alv_grid1->register_edit_event
    exporting
      i_event_id = cl_gui_alv_grid=>mc_evt_enter
    exceptions
      error      = 1
      others     = 2.

  set handler gr_event_handler->handle_data_changed for alv_grid1.

  if sy-subrc <> 0.
    message id sy-msgid type sy-msgty number sy-msgno
    with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  endif.
endform. "register_events


form handle_data_changed .

  loop at gt_alv assigning field-symbol(<fs_alv>) where checkbox ne 'X'.
    if <fs_alv>-z00hrzsxds is initial and  <fs_alv>-z00hrtzxds is initial.
      <fs_alv>-z00hrhjs = <fs_alv>-z00hryxds.
    else.
      <fs_alv>-z00hrhjs = <fs_alv>-z00hrzsxds + <fs_alv>-z00hrtzxds.
    endif.
  endloop.

endform.
*&---------------------------------------------------------------------*
*& Form frm_set_event
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      <-- LT_EVENTS
*&---------------------------------------------------------------------*
form frm_set_event  changing lt_events type slis_t_event.
  data: ls_events type slis_alv_event.
  ls_events-name = 'CALLER_EXIT'.
  ls_events-form = 'FRM_REGISTER_EVENTS'.
  append ls_events to lt_events.
endform.

*&---------------------------------------------------------------------*
*& Form frm_f4_help_file
*&---------------------------------------------------------------------*
*& text  选择上传路径
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
form frm_f4_help_file changing o_fname type rlgrap-filename.
  data: l_filetab type filetable,
        l_waftab  like line of l_filetab,
        l_rc      type i.
  call method cl_gui_frontend_services=>file_open_dialog
    exporting
      window_title            = '打开文件'
      initial_directory       = 'C:/'
    changing
      file_table              = l_filetab
      rc                      = l_rc
    exceptions
      file_open_dialog_failed = 1
      cntl_error              = 2
      error_no_gui            = 3
      not_supported_by_gui    = 4
      others                  = 5.
  if sy-subrc <> 0.
    message id sy-msgid type sy-msgty number sy-msgno
    with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    exit.
  else.
    read table l_filetab into l_waftab index 1.
    o_fname = l_waftab-filename.
    clear: l_filetab,
    l_waftab.
  endif.
endform.

form frm_read_excel tables l_tab
                     using pv_file type rlgrap-filename
                           lv_i_begin_col type i
                           lv_i_begin_row type i
                           lv_i_end_col type i
                           lv_i_end_row type i
                    changing l_wa type any.

  refresh l_tab.

  data: lt_excel type table of alsmex_tabline with header line.
  field-symbols <lfs_value> type any.

  call function 'ALSM_EXCEL_TO_INTERNAL_TABLE'
    exporting
      filename                = pv_file
      i_begin_col             = lv_i_begin_col
      i_begin_row             = lv_i_begin_row
      i_end_col               = lv_i_end_col
      i_end_row               = lv_i_end_row
    tables
      intern                  = lt_excel
    exceptions
      inconsistent_parameters = 1
      upload_ole              = 2
      others                  = 3.
  if sy-subrc <> 0.
    message s001(00) with '读取 Excel 文件失败' display like 'E'.
    stop.
  endif.

  loop at lt_excel.
    assign component lt_excel-col of structure l_wa to <lfs_value>.
    if sy-subrc = 0 .
      replace all occurrences  of  regex ',' in lt_excel-value with ''.
      <lfs_value> = lt_excel-value.
    endif.
    at end of row.
      append l_wa to l_tab.
      clear l_wa.
    endat.
  endloop.
endform.

```

