# 搜索帮助

## 引用现有搜索帮助 ##

```abap
SELECT-OPTIONS: p_mandt FOR ska1-mandt MATCHCODE OBJECT H_T000."SE11创建搜索帮助并使用关键字引用
```

## 根据某个表创建搜索帮助 ##

```abap
TABLES:t000."声明程序使用的表

SELECT-OPTIONS: p_mandt FOR t000-mandt.

INITIALIZATION.
  SELECT mandt, mtext INTO TABLE @DATA(gt_help_data) FROM t000.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_mandt-low." 字段下限
  PERFORM frm_f4_help TABLES gt_help_data  USING 'MANDT' 'P_MANDT-LOW'.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_mandt-high."字段上限
  PERFORM frm_f4_help TABLES gt_help_data  USING 'MANDT' 'P_MANDT-HIGH'.

FORM frm_f4_help TABLES gt_help_data
                 USING  VALUE(pv_retfield)
                        VALUE(pv_dynprofield).
  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield       = pv_retfield     "内表对应的字段
      dynpprog       = sy-repid        " 定义系统变量自动获取程序名称
      dynpnr          = sy-dynnr       "定义系统变量自动获取程序编号
      dynprofield    = pv_dynprofield  " 屏幕接收值得字段
      value_org       = 'S'
    TABLES
      value_tab       = gt_help_data     "传入搜索帮助值的内表
    EXCEPTIONS
      parameter_error = 1
      no_values_found = 2
      OTHERS          = 3.
ENDFORM. "frm_f4_help
```

## CBFORM定义

```abap
FORM FRM_GET_STEXT TABLES  RECORD_TAB  STRUCTURE SEAHLPRES
                   CHANGING SHLP        TYPE SHLP_DESCR_T
                            CALLCONTROL LIKE DDSHF4CTRL.
                            
  shlp-intdescr-dialogtype = 'C'.         "设置显示模式
  callcontrol-maxrecords = '99999999'.    "最大显示数量

  "增加返回字段
  DATA: INTERFACE LIKE LINE OF SHLP-INTERFACE.
  READ TABLE SHLP-INTERFACE INTO INTERFACE INDEX 1.
  INTERFACE-SHLPFIELD+4(1) = '2'.
  INTERFACE-VALFIELD = 'P0021-Z00HRRYXLMS'.
  APPEND INTERFACE TO SHLP-INTERFACE.
endform.
```

## 返回一行多个字段

```abap
DATA: LT_ZMMS_INV_TKPR LIKE TABLE OF LS_ZMMS_INV_TKPR,
      LT_RETURN        TYPE TABLE OF DDSHRETVAL,
      LT_FIELD_MAPPING TYPE TABLE OF DSELC.
      
LT_FIELD_MAPPING = VALUE #( ( FLDNAME = 'F0001' DYFLDNAME = 'ZMMS_INV_TKPR-ZDJBH' )
                            ( FLDNAME = 'F0002' DYFLDNAME = 'ZMMS_INV_TKPR-ZITMSN' ) ).
                            
CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
  EXPORTING
    RETFIELD        = 'ZDJBH'
    DYNPPROG        = SY-REPID
    DYNPNR          = SY-DYNNR
    DYNPROFIELD     = 'ZMMS_INV_TKPR-ZDJBH'
    VALUE_ORG       = 'S'
  TABLES
    VALUE_TAB       = LT_ZMMS_INV_TKPR
    RETURN_TAB      = LT_RETURN
    DYNPFLD_MAPPING = LT_FIELD_MAPPING
  EXCEPTIONS
    PARAMETER_ERROR = 1
    NO_VALUES_FOUND = 2
    OTHERS          = 3.                  
```

## DIALOG POV

```abap
PROCESS ON VALUE-REQUEST.    
  FIELD p9088-persk MODULE f4_persk_values.
```

## OOALV 搜索帮助

1. fieldcat 中为字段设置搜索帮助

   ```abap
   ls_fcat-f4availabl = 'X'.
   ```

2. 定义事件并实现

   ```abap
     methods handle_onf4 for event onf4 of cl_gui_alv_grid
       importing
         e_fieldname
         es_row_no
         er_event_data
         et_bad_cells
         e_display.
         
     method handle_onf4.
   
       data: lt_return    type table of ddshretval.
   
       field-symbols: <fs_value_list> type table,
                      <fs_modi>       type lvc_t_modi.
   
       read table gt_alv into data(ls_alv) index es_row_no-row_id.
   
       case e_fieldname.
         when 'ZMN'.
           select v~matnr as value, maktx, maktg from mara as v left outer join makt as t
             on v~matnr eq t~matnr and spras eq @sy-langu
             into table @data(lt_makt) order by v~matnr.
           assign lt_makt to <fs_value_list>.
       endcase.
       check <fs_value_list> is assigned.
       call function 'F4IF_INT_TABLE_VALUE_REQUEST'
         exporting
           retfield        = 'VALUE'
           value_org       = 'S'
         tables
           value_tab       = <fs_value_list> "lt_vrm_values
           return_tab      = lt_return
         exceptions
           parameter_error = 1
           no_values_found = 2
           others          = 3.
   
       if sy-subrc eq 0 and e_display is initial.
         read table lt_return into data(ls_return) index 1.
         if sy-subrc eq 0.
   
           data(ls_value) = value lvc_s_modi(
               row_id    = es_row_no-row_id
               fieldname = e_fieldname
               value     = ls_return-fieldval
           ).
   
           assign er_event_data->m_data->* to <fs_modi>.
           append ls_value to <fs_modi>.
         endif.
       endif.
   
       er_event_data->m_event_handled = 'X'.
     endmethod.
   ```

3. 注册事件

   ```abap
   set handler handle_onf4 for go_alv_grid.
   ```

4. 注册搜索帮助字段并注册

   ```abap
   data: lt_f4 type lvc_t_f4.
   
   lt_f4 = value #(register = 'X' chngeafter = 'X' ( fieldname = to_upper( fieldname1 ) )
   
   go_alv_grid->register_f4_for_fields( lt_f4 ).
   ```

# 文件搜索帮助 #

```abap
call function 'F4_FILENAME'
  importing
    file_name = p_file.
```

```abap
PARAMETERS p_file LIKE rlgrap-filename ."文件路径

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_file.
  PERFORM frm_f4_help_file CHANGING p_file.

FORM frm_f4_help_file CHANGING o_fname TYPE rlgrap-filename.
  DATA: l_filetab TYPE filetable,
        l_waftab  LIKE LINE OF l_filetab,
        l_rc      TYPE i.
  CALL METHOD cl_gui_frontend_services=>file_open_dialog
    EXPORTING
      window_title            = '打开文件'
      initial_directory       = 'C:/'
    CHANGING
      file_table              = l_filetab
      rc                      = l_rc
    EXCEPTIONS
      file_open_dialog_failed = 1
      cntl_error              = 2
      error_no_gui            = 3
      not_supported_by_gui    = 4
      OTHERS                  = 5.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    EXIT.
  ELSE.
    READ TABLE l_filetab INTO l_waftab INDEX 1.
    o_fname = l_waftab-filename.
    CLEAR: l_filetab,
           l_waftab.
  ENDIF.
ENDFORM.
```

# HR相关搜索帮助

## HR 结构化搜索帮助

```ABAP
PARAMETERS p_objid TYPE hrobjid OBLIGATORY.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_objid.
  PERFORM frm_f4_help_objid USING 'O' 'P_OBJID' CHANGING p_objid.

FORM frm_f4_help_objid USING otype field_name
                      CHANGING field.

  DATA: f4_loc_objec LIKE objec.

  CALL FUNCTION 'RH_OBJID_REQUEST'
    EXPORTING
      plvar             = '01'
      otype             = otype
      seark_begda       = sy-datum
      seark_endda       = sy-datum
      dynpro_repid      = sy-repid
      dynpro_dynnr      = sy-dynnr
      dynpro_searkfield = field_name
      set_mode          = ' '
    IMPORTING
      sel_object        = f4_loc_objec
    EXCEPTIONS
      cancelled         = 1
      wrong_condition   = 2
      nothing_found     = 3
      illegal_mode      = 4
      internal_error    = 5
      OTHERS            = 6.

  IF sy-subrc = 0.
    field = f4_loc_objec-objid.
  ENDIF.

ENDFORM.
```

## HR 评估文档搜索帮助

```abap
    data: lt_selected  type hap_t_templates,
          lt_templates type hap_t_templates,
          ls_return    type bal_s_msg.

    select * into corresponding fields of table lt_selected
      from hrp1000 where plvar eq '01' and otype eq 'VA' and objid in s_tempid.

    call function 'HRHAP_POPUP_F4_TEMPLATE'
      exporting
        authority_check    = '03'
        plan_version       = '01'
        multiple_selection = 'X' "多选
        t_selected         = lt_selected
      importing
        t_templates        = lt_templates
        s_return           = ls_return.

    clear: s_tempid, s_tempid[].
    loop at lt_templates into data(ls_templates).
      if sy-tabix eq 1.
        s_tempid = value #( sign = 'I' option = 'EQ' low = ls_templates-objid ).
      endif.

      s_tempid[] = value #( base s_tempid[] sign = 'I' option = 'EQ' ( low = ls_templates-objid ) ).
    endloop.
```

# 日期相关搜索帮助 #

```abap
PARAMETERS:p_kmonth TYPE kmonth DEFAULT sy-datum+0(6).

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_kmonth.
  PERFORM frm_f4_help_month USING p_kmonth.

FORM frm_f4_help_month USING actual_month.

  DATA: selected_month LIKE isellist-month,
        return_code    TYPE sy-subrc.

  CALL FUNCTION 'POPUP_TO_SELECT_MONTH'
    EXPORTING
      actual_month               = sy-datum+0(6)   "传入年份
      factory_calendar           = ' '            "工厂日历 ID
      holiday_calendar           = ' '            "假日日历 ID
      language                   = sy-langu       "当前系统语言
    IMPORTING
      selected_month             = selected_month "返回用户输入月份
      return_code                = return_code    "返回 sy-subrc
    EXCEPTIONS
      factory_calendar_not_found = 1
      holiday_calendar_not_found = 2
      month_not_found            = 3
      OTHERS                     = 4.

  IF return_code = 0.
    actual_month = selected_month.
  ENDIF.

ENDFORM.
```

# 请求号相关搜索帮助

## 请求号搜索帮助

```abap
data: lv_username             type syst_uname,
      lv_trkorr_pattern       type trkorr,
      lv_trfunctions          type c,
      lv_trstatus             type c,
      lv_from_date            type as4date,
      lv_to_date              type as4date,
      lv_client               type syst_mandt,
      lv_project              type trkorr_p,
      lv_title                type trwbo_title,
      lv_via_selection_screen type c value 'X',
      lv_complete_requests    type c value 'X',
      lt_exclude_requests     type trkorrs,
      lv_selected_request     type trkorr,
      ls_selected_request     type trwbo_request_header.

lv_username = sy-uname.

call function 'TR_F4_REQUESTS'
  exporting
    iv_username             = lv_username
    iv_trkorr_pattern       = lv_trkorr_pattern
    iv_trfunctions          = lv_trfunctions
    iv_trstatus             = lv_trstatus
    iv_from_date            = lv_from_date
    iv_to_date              = lv_to_date
    iv_client               = lv_client
    iv_project              = lv_project
    iv_title                = lv_title
    iv_via_selection_screen = lv_via_selection_screen
    iv_complete_requests    = lv_complete_requests
    it_exclude_requests     = lt_exclude_requests
  importing
    ev_selected_request     = lv_selected_request
    es_selected_request     = ls_selected_request.
```

## 请求号搜索帮助(带创建)

```abap
data: lv_suppress_dialog    type trparflag value space,
      lv_request_types      type c,
      lv_cli_dep            type trparflag value space,
      lv_request            type trkorr value space,
      lt_e071               type tr_objects,
      lt_e071k              type tr_keys,
      lv_lock_objects       type trparflag value space,
      lv_title              type c,
      lv_start_column       type syst_cucol value 3,
      lv_start_row          type syst_curow value 7,
      lv_with_error_log     type c value 'X',
      lv_no_owner_check     type c value space,
      lv_foreign_request(3) type c value '   ',
      lt_e071k_str          type e071k_strtyp,
      lt_obj_entries        type cts_obj_entries,
      ls_request            type trwbo_request_header.

call function 'TR_REQUEST_CHOICE'
  exporting
    iv_suppress_dialog   = lv_suppress_dialog
    iv_request_types     = lv_request_types
    iv_cli_dep           = lv_cli_dep
    iv_request           = lv_request
    it_e071              = lt_e071
    it_e071k             = lt_e071k
    iv_lock_objects      = lv_lock_objects
    iv_title             = lv_title
    iv_start_column      = lv_start_column
    iv_start_row         = lv_start_row
    iv_with_error_log    = lv_with_error_log
    iv_no_owner_check    = lv_no_owner_check
    iv_foreign_request   = lv_foreign_request
    it_e071k_str         = lt_e071k_str
    it_obj_entries       = lt_obj_entries
  importing
    es_request           = ls_request
  exceptions
    invalid_request      = 1
    invalid_request_type = 2
    user_not_owner       = 3
    no_objects_appended  = 4
    enqueue_error        = 5
    cancelled_by_user    = 6
    recursive_call       = 7
    others               = 8.
if sy-subrc <> 0.
* message id sy-msgid type sy-msgty number sy-msgno
*   with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
endif.
```

# SE11 创建搜索帮助去重

在此处添加函数,函数具体代码如下:<img src="https://picture-bj.oss-cn-beijing.aliyuncs.com/pciture/image-20220901175632107.png" alt="image-20220901175632107" style="zoom:50%;" />

```abap
FUNCTION zeh_lxsecond.
*"----------------------------------------------------------------------
*"*"本地接口：
*"  TABLES
*"      SHLP_TAB TYPE  SHLP_DESCT OPTIONAL
*"      RECORD_TAB STRUCTURE  SEAHLPRES OPTIONAL
*"  CHANGING
*"     VALUE(SHLP) TYPE  SHLP_DESCR
*"     VALUE(CALLCONTROL) LIKE  DDSHF4CTRL STRUCTURE  DDSHF4CTRL
*"----------------------------------------------------------------------


  IF callcontrol-step = 'DISP'.
    SORT record_tab.
    DELETE ADJACENT DUPLICATES FROM record_tab COMPARING ALL FIELDS.
  ENDIF.

ENDFUNCTION.
```







